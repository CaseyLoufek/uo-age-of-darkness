[ITEMDEF 0fbb]
//Tongs
DEFNAME=i_tongs
RESOURCES=2 i_ingot_iron
TYPE=T_WEAPON_MACE_SMITH
FLIP=1
WEIGHT=2
RESOURCES=3 i_ingot_iron
SKILLMAKE=BLACKSMITHING 54.1
CATEGORY=Items by Professions
SUBSECTION=Blacksmiths
DESCRIPTION=Tongs
DUPELIST=0fbc
TEVENTS=T_PROPRIEDADES

ON=@Create
	HITPOINTS=50

[ITEMDEF 0fb4]
//Sledge Hammer
DEFNAME=i_hammer_sledge
TYPE=T_WEAPON_MACE_SMITH
FLIP=1
RESOURCES=2 i_ingot_iron, 1 i_log
SKILLMAKE=BLACKSMITHING 54.1
WEIGHT=9
CATEGORY=Items by Professions
SUBSECTION=Blacksmiths
DESCRIPTION=Sledge Hammer
DUPELIST=0fb5
TEVENTS=T_PROPRIEDADES

ON=@Create
	HITPOINTS=50


[typedef t_forge]

ON=@TARGON_ITEM
SRC.MESSAGE=Use blacksmithing tools to forge and smelt.
RETURN 1

ON=@TARGON_CHAR
SRC.MESSAGE=Use blacksmithing tools to forge and smelt.
RETURN 1

ON=@TARGON_GROUND
SRC.MESSAGE=Use blacksmithing tools to forge and smelt.
RETURN 1

[TYPEDEF t_weapon_mace_smith]
ON=@DCLICK
IF (<SRC.ACTION>!=-1) || (<SRC.CTAG0.CRAFTING>)
	SRC.SYSMESSAGE You must wait to perform another action.
	RETURN 1
ENDIF
IF !(<f_skmenu_blacksmithing_cancraft>)
	SRC.SYSMESSAGE You must be near an anvil and a forge to smith items.
	RETURN 1
ENDIF
DIALOG d_skillmenu_blacksmithing
EQUIP
RETURN 1

[TYPEDEF t_ingot]
ON=@DCLICK
RETURN 1


[typedef t_ore]
on=@dclick
if <cont> != <src.findlayer.21>
src.sysmessage @,,2 1042010
elif <src.mining> >= <eval <skillmake.1.value>-100>
	if <src.isneartype t_forge 2>
		if <src.skillcheck mining <eval <skillmake.1.value>/10>>
			serv.newitem <tdata1>
			new.amount <eval (((5*(<src.mining>-<skillmake.1.value>)))/100)>
			if <new.amount> < 1
				new.amount = 1
			elif  <new.amount> > <amount>
				new.amount = <amount>
			endif
			if (((<src.mining>-<skillmake.1.value>)) > 1000)
				local.bonus = <new.amount>*(<src.mining>-<skillmake.1.value>)/1000
			endif
			src.consume <new.amount> <baseid>
			new.amount += <local.bonus>
			new.bounce
		endif
	else
	src.sysmessage <def.smelt_noforge>
	endif
else
src.sysmessage <def.smelt_noskill>
endif
return 1

////****************************************************************************
//SPHERE by : Menasoft ©1997-2004
//www.sphereserver.com
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
//****************************************************************************
// FILE LAST UPDATED: Friday, August 18, 2006
//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//OSI-style blacksmithing skillmenu, crafted by Gygrazok
//I've made this system using runuo scripts and uo stratics as references, and it's almost identical to the osi craft menu
//(probably there's some little difference in some message).
//At the moment, enhance functions is not enabled, since without having a complete magic item system it's completely useless
//
//*How this works:
//items created with colored ingots are simply modified at creation setting name, color and relevant tag0s to modify their properties
//(every material has a corresponding function which is called on every item made with that material).
//This means that you cannot directly create (for example with .add or with newitem) an item made with colored
//ingots. Of course you could create a function that creates the item and then applies on it the material-function...
//Note: to determine whether an item should use subres (ingots) or subres2 (dragon scales), this system checks for the
//_first_ resource specified inside the RESOURCES of the item: if it is i_dragon_scale, it uses subres2, otherwise it uses subres.
//
//*How to add new items to the menu:
//the craftable items are determined by defnames, so to add an item you have to add a defname like this:
//skmenu_blacksmithing_X_Y i_your_item
//where X is a number representing the category in which the item fits, and Y a number that internally identifies the item inside that category
//if you see the defnames included below you'll understand immediately how this works.
//
//*How to add new resources:
//add two new defname like this:
//skmenu_blacksmithing_subres_X i_your_material
//skmenu_blacksmithing_subres_X_skill skill_required_to_craft_item_with_this_material
//(X is a number that identifies the material)
//And then add a function named f_material_i_your_material, it will automatically be called upon the crafted item with that material
//see the provided examples to see how it works (material functions are at the bottom of the file)
//Note that blacksmithing has subres and subres2: subres2 are dragon scales, and they are used only with dragon scale armor,
//if you are going to use this system as a base for other skillmenus, you'll probably have to remove all the parts which refers to subres2
//
//*New trigger:
//@MakeItem: fires on the tool used to craft the item. It fires just after the item is created, but before any
//material application or anything. Var.argn1 holds the exceptional chance (so you can modify this to make it so
//certain hammers can increase the exceptional chance), var.argn2 holds the material number (for
//example, for an item made in iron this would be 0, dull copper would be 1 and so on). Example, put this in the script of a sledge hammer:
//ON=@MakeItem
//IF (<VAR.ARGN2><8)
//	VAR.ARGN2 += 1
//ENDIF
//With this simple script you can make it so this hammer creates items with an "upgraded" material (ie, items made
//in iron will come out made of dull copper, items made in dull copper will come out made of shadow and so on)
//
//As a final note, this system is meant to be used with my Aos-style Combat System and with armors and weapons modified to suit OSI,
//although it's not difficult to adapt it to "standard" combat system.

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//DEFNAMES
//this sections includes defnames used to determine the craftable items and the crafting resources
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

[DEFNAME def_skmenu_blacksmithing]
skmenu_blacksmithing_flags 07 //01=markoption 02=resmelt 04=repair 08=enhance
skmenu_blacksmithing_subres_0 i_ingot_iron
skmenu_blacksmithing_subres_1 i_ingot_rusty
skmenu_blacksmithing_subres_2 i_ingot_old_copper
skmenu_blacksmithing_subres_3 i_ingot_dull_copper
skmenu_blacksmithing_subres_4 i_ingot_copper
skmenu_blacksmithing_subres_5 i_ingot_bronze
skmenu_blacksmithing_subres_6 i_ingot_corroded_bronze
skmenu_blacksmithing_subres_7 i_ingot_shadow
skmenu_blacksmithing_subres_8 i_ingot_silver
skmenu_blacksmithing_subres_9 i_ingot_gold
skmenu_blacksmithing_subres_10 i_ingot_rose
skmenu_blacksmithing_subres_11 i_ingot_agapite
skmenu_blacksmithing_subres_12 i_ingot_verite
skmenu_blacksmithing_subres_13 i_ingot_valorite
skmenu_blacksmithing_subres_14 i_ingot_bloodrock
skmenu_blacksmithing_subres_15 i_ingot_mytheril
skmenu_blacksmithing_subres_16 i_ingot_blackrock

////

skmenu_blacksmithing_0_name RINGMAILS
skmenu_blacksmithing_0_0 i_ringmail_gloves
skmenu_blacksmithing_0_1 i_ringmail_leggings
skmenu_blacksmithing_0_2 i_ringmail_sleeves
skmenu_blacksmithing_0_3 i_ringmail_tunic

skmenu_blacksmithing_1_name CHAINMAIL
skmenu_blacksmithing_1_0 i_chainmail_coif
skmenu_blacksmithing_1_1 i_chainmail_leggings
skmenu_blacksmithing_1_2 i_chainmail_tunic

skmenu_blacksmithing_2_name PLATEMAILS
skmenu_blacksmithing_2_0 i_platemail_arms
skmenu_blacksmithing_2_1 i_platemail_gloves
skmenu_blacksmithing_2_2 i_platemail_gorget
skmenu_blacksmithing_2_3 i_platemail_leggings
skmenu_blacksmithing_2_4 i_platemail_chest
skmenu_blacksmithing_2_5 i_armor_female_plate
//SE armor
skmenu_blacksmithing_2_6 i_platemail_mempo
skmenu_blacksmithing_2_7 i_platemail_do
skmenu_blacksmithing_2_8 i_platemail_hiro_sode
skmenu_blacksmithing_2_9 i_platemail_suneate
skmenu_blacksmithing_2_10 i_platemail_haidate

skmenu_blacksmithing_3_name HELMETS
skmenu_blacksmithing_3_0 i_bascinet
skmenu_blacksmithing_3_1 i_helm_closed
skmenu_blacksmithing_3_2 i_helm_open
skmenu_blacksmithing_3_3 i_helm_nose
skmenu_blacksmithing_3_3 i_helm_winged
skmenu_blacksmithing_3_3 i_helm_orc
skmenu_blacksmithing_3_4 i_platemail_helm
//SE helmets
skmenu_blacksmithing_3_5 i_chainmail_hatsuburi
skmenu_blacksmithing_3_6 i_platemail_hatsuburi
skmenu_blacksmithing_3_7 i_platemail_jingasa_heavy
skmenu_blacksmithing_3_8 i_platemail_jingasa_light
skmenu_blacksmithing_3_9 i_platemail_jingasa_small
skmenu_blacksmithing_3_10 i_platemail_kabuto_decorative
skmenu_blacksmithing_3_11 i_platemail_kabuto_battle
skmenu_blacksmithing_3_12 i_platemail_kabuto_standard
//ML circlets
skmenu_blacksmithing_3_13 i_circlet
skmenu_blacksmithing_3_14 i_circlet_royal
skmenu_blacksmithing_3_15 i_circlet_gemmed

skmenu_blacksmithing_4_name SHIELDS
skmenu_blacksmithing_4_0 i_shield_buckler
skmenu_blacksmithing_4_1 i_shield_round_bronze
skmenu_blacksmithing_4_2 i_shield_heater
skmenu_blacksmithing_4_3 i_shield_round_metal
skmenu_blacksmithing_4_4 i_shield_kite_metal
skmenu_blacksmithing_4_5 i_shield_kite_wood

skmenu_blacksmithing_5_name BLADED
skmenu_blacksmithing_5_0 i_bone_harvester
skmenu_blacksmithing_5_1 i_sword_broad
skmenu_blacksmithing_5_2 i_crescent_blade
skmenu_blacksmithing_5_3 i_cutlass
skmenu_blacksmithing_5_4 i_dagger
skmenu_blacksmithing_5_5 i_katana
skmenu_blacksmithing_5_6 i_kryss
skmenu_blacksmithing_5_7 i_sword_long
skmenu_blacksmithing_5_8 i_scimitar
skmenu_blacksmithing_5_9 i_sword_viking
skmenu_blacksmithing_5_10 i_sword_long
skmenu_blacksmithing_5_11 i_no_dachi
skmenu_blacksmithing_5_12 i_wakizashi
skmenu_blacksmithing_5_13 i_lajatang
skmenu_blacksmithing_5_14 i_daisho
skmenu_blacksmithing_5_15 i_tekagi
skmenu_blacksmithing_5_16 i_kama
skmenu_blacksmithing_5_17 i_sai
skmenu_blacksmithing_5_18 i_sword_assassin_spike
skmenu_blacksmithing_5_19 i_elven_machete
skmenu_blacksmithing_5_20 i_elven_spellblade
skmenu_blacksmithing_5_21 i_sword_leafblade
skmenu_blacksmithing_5_22 i_radiant_scimitar
skmenu_blacksmithing_5_23 i_rune_blade
skmenu_blacksmithing_5_24 i_sword_warcleaver

skmenu_blacksmithing_6_name AXES
skmenu_blacksmithing_6_0 i_axe
skmenu_blacksmithing_6_1 i_axe_battle
skmenu_blacksmithing_6_2 i_axe_double
skmenu_blacksmithing_6_3 i_axe_exec
skmenu_blacksmithing_6_4 i_axe_battle_large
skmenu_blacksmithing_6_5 i_axe_two_hand
skmenu_blacksmithing_6_6 i_axe_war
skmenu_blacksmithing_6_7 i_ornate_axe
skmenu_blacksmithing_6_8 i_pickaxe

skmenu_blacksmithing_7_name POLE ARMS
skmenu_blacksmithing_7_0 i_bardiche
skmenu_blacksmithing_7_1 i_staff_bladed
skmenu_blacksmithing_7_2 i_staff_bladed_double
skmenu_blacksmithing_7_3 i_halberd
skmenu_blacksmithing_7_4 i_lance
skmenu_blacksmithing_7_5 i_pike
skmenu_blacksmithing_7_6 i_spear_short
skmenu_blacksmithing_7_7 i_scythe
skmenu_blacksmithing_7_8 i_spear
skmenu_blacksmithing_7_9 i_war_fork
skmenu_blacksmithing_7_10 i_pitchfork

skmenu_blacksmithing_8_name BASHING
skmenu_blacksmithing_8_0 i_hammer_pick
skmenu_blacksmithing_8_1 i_mace
skmenu_blacksmithing_8_2 i_maul
skmenu_blacksmithing_8_3 i_scepter
skmenu_blacksmithing_8_4 i_mace_war
skmenu_blacksmithing_8_5 i_hammer_war
skmenu_blacksmithing_8_6 i_hammer_smith
skmenu_blacksmithing_8_7 i_hammer_sledge
skmenu_blacksmithing_8_8 i_tessen
//ML maces
skmenu_blacksmithing_8_9 i_diamond_mace

//skmenu_blacksmithing_9_name DRAGON ARMOR
//skmenu_blacksmithing_9_0 i_dragon_gloves
//skmenu_blacksmithing_9_1 i_dragon_helm
//skmenu_blacksmithing_9_2 i_dragon_leggings
//skmenu_blacksmithing_9_3 i_dragon_sleeves
//skmenu_blacksmithing_9_4 i_dragon_chest




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//MAIN FUNCTIONS
//Here there are the "core" functions that control the crafting procedure. 
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

[FUNCTION f_skmenu_blacksmithing_cancraft]
//this must be edited out for different skillmenus (because, for example, bowcraft doesn't need anvil & forge)
IF (<HITPOINTS><=0)
	SRC.Ctag0.SKMENU_BLACKSMITHING_NOTICE You have worn out your tool!
	RETURN 0
ELSEIF (<CONT.TOPOBJ.UID>!=<SRC.UID>)
	SRC.Ctag0.SKMENU_BLACKSMITHING_NOTICE The tool must be on your person to use.
	RETURN 0
ENDIF
IF (<SRC.ISNEARTYPE t_anvil, 2>) && (<SRC.ISNEARTYPE t_forge, 2>)
	RETURN 1
ELSE
	SRC.Ctag0.SKMENU_BLACKSMITHING_NOTICE You must be near an anvil and a forge to smith items.
	RETURN 0
ENDIF

//This is the main craft function
[FUNCTION f_skmenu_blacksmithing_makeitem]
//preliminary checks
IF (<SRC.ACTION>!=-1) || (<SRC.FINDID.i_blacksmithing_craft>) || (<SRC.RESTEST 1 i_tinkering_craft>) || (<SRC.RESTEST 1 i_bowcraft_craft>)
	SRC.SYSMESSAGE You must wait to perform another action
	RETURN 1
ENDIF
IF !(<f_skmenu_blacksmithing_cancraft>)
	DIALOG d_skillmenu_blacksmithing
	RETURN 1
ELSEIF !(<SRC.CANMAKESKILL <ARGS>>)
	SRC.Ctag0.SKMENU_BLACKSMITHING_NOTICE You don't have the required skills to attempt this item.
	DIALOG d_skillmenu_blacksmithing
	RETURN 1
ENDIF

//check resources
	IF (<SRC.Ctag0.SKMENU_SUBRES>)
		LOCAL.RESOURCES=<f_resource_replace <ARGS>,<SRC.CTAG0.SKMENU_SUBRES_BASE>,<SRC.CTAG0.SKMENU_SUBRES>>
	ELSE
		LOCAL.RESOURCES=<SERV.ITEMDEF.<ARGS>.RESOURCES>
	ENDIF

IF !(<SRC.RESTEST <LOCAL.RESOURCES>>)
	SRC.Ctag0.SKMENU_BLACKSMITHING_NOTICE You don't have the resources required to make that item.
	DIALOG d_skillmenu_blacksmithing
	RETURN 1
ENDIF
SRC.CONSUME <LOCAL.RESOURCES>

//update the "last ten" list
IF (<ARGS>!=<SRC.Ctag0.SKMENU_BLACKSMITHING_LASTTEN_0>) 
	FOR 9 1
		TRY SRC.Ctag0.SKMENU_BLACKSMITHING_LASTTEN_<dLOCAL._FOR> = <SRC.Ctag0.SKMENU_BLACKSMITHING_LASTTEN_<EVAL <LOCAL._FOR>-1>>
	ENDFOR
	SRC.Ctag0.SKMENU_BLACKSMITHING_LASTTEN_0 <ARGS>
ENDIF

//start crafting
SERV.NEWITEM i_blacksmithing_craft
NEW.CONT=<SRC.UID>
NEW.TAG.SUBRES=<SRC.Ctag0.SKMENU_SUBRES>
NEW.LINK=<UID> //linked to the tool
NEW.TIMER=1
NEW.tag0.ITEM=<ARGS>
NEW.TAG.SUBRESUSED=<TAG.SUBRESUSED>
SRC.ANIM 11
SRC.EVENTS +e_blacksmithing_crafting

//This is the memory item used for the crafting
[ITEMDEF i_blacksmithing_craft]
ID=02007
NAME=Blacksmithing craft memory
TYPE=t_eq_script

ON=@CREATE
MOREX={2 4}

ON=@TIMER	
CONT.SFX 02a
CONT.ANIM 11
MOREX -= 1
IF (<MOREX>)
	TIMER 1
	RETURN 1
ENDIF
LOCAL.CHANCE=<CONT.f_skmenu_successchance <tag0.ITEM>, <LINK.UID>>
IF (RAND(1000)<<LOCAL.CHANCE>) //success! we can create the item
	SERV.NEWITEM <tag0.ITEM> 
	LOCAL.EXCEP=<CONT.f_skmenu_exceptionalchance <tag0.ITEM>, <LINK.UID>>
	VAR.ARGN1=<LOCAL.EXCEP>
	VAR.ARGN2=<UID> //Blacksmithing crafting memory link, use to find/change material (tag.subres)
	LINK.TRIGGER=@MakeItem
	LOCAL.EXCEP=<VAR.ARGN1>
	NEW.CONT=<CONT.FINDLAYER.21.UID>
	CONT.SOUND 057
	IF <TAG0.SUBRESUSED>
		TRY NEW.f_material_<TAG.SUBRES>
	ENDIF
	IF (RAND(1000)<<LOCAL.EXCEP>)
		NEW.QUALITY=201
		IF (<CONT.Ctag0.SKMENU_BLACKSMITHING_MARKOPTION>==0)
			NEW.tag0.CRAFTEDBY=<CONT.name>
		ELSEIF (<CONT.Ctag0.SKMENU_BLACKSMITHING_MARKOPTION>==2)
			CONT.Ctag0.SKMENU_BLACKSMITHING_TOOL=<LINK.UID>
			TRYSRC <CONT.UID> NEW.DIALOG d_skillmenu_blacksmithing_querymakersmark
			LOCAL.NODIALOG=1
		ENDIF
		IF (<NEW.HITPOINTS>)
			NEW.HITPOINTS += <eval ((<new.hitpoints>) * (<cont.armslore>+<cont.skillmod armslore> + 250))/4000>
		ELSEIF (<NEW.TAG0.HITS>)
			NEW.TAG0.MAXHITS += <eval ((<new.TAG0.MAXHITS>) * (<cont.armslore>+<cont.skillmod armslore> + 250))/4000>
			NEW.TAG0.HITS = NEW.TAG0.MAXHITS
		ENDIF

		new.tag0.modvalue += <EVAL ((<CONT.ARMSLORE>+<cont.skillmod armslore>)*<NEW.VALUE>)/400>
		IF (<NEW.ISARMOR>) || (<NEW.TYPE>==t_shield)
			new.modar += <eval ((<new.armor.hi>+<new.modar>) * (<cont.armslore>+<cont.skillmod armslore> + 250))/4000>
			new.resfire += <eval ((<new.resfire>) * (<cont.armslore>+<cont.skillmod armslore> + 250))/4000>
			new.rescold += <eval ((<new.rescold>) * (<cont.armslore>+<cont.skillmod armslore> + 250))/4000>
			new.respoison += <eval ((<new.respoison>) * (<cont.armslore>+<cont.skillmod armslore> + 250))/4000>
			new.resenergy += <eval ((<new.resenergy>) * (<cont.armslore>+<cont.skillmod armslore> + 250))/4000>
			CONT.SKILLGAIN armslore <eval <SERV.ITEMDEF.<TAG.ITEM>.SKILLMAKE.0.VAL>>>
		ELSEIF (<NEW.ISWEAPON>)	
			NEW.tag0.DAMINCREASE += <EVAL (<CONT.ARMSLORE>+<cont.skillmod armslore>+250)/40>
			CONT.SKILLGAIN armslore <eval <SERV.ITEMDEF.<TAG.ITEM>.SKILLMAKE.0.VAL>>>
		ENDIF
		NEW.NAME=exceptional <NEW.NAME>
	ELSE
		NEW.QUALITY=<EVAL (<LOCAL.CHANCE>+<LOCAL.EXCEP>)/10> //arbitrary formula for quality calculation
	ENDIF
	local.exp = (<eval (rand(<NEW.VALUE>+<NEW.tag0.MODVALUE>)+rand(<NEW.VALUE>+<NEW.tag0.MODVALUE>))/4>)
	cont.Trigger @ExpChange,<def.TAT_AS_ARGN>,<eval <local.exp>>,1,1
	NEW.RESENDTOOLTIP
	LINK.HITS -= 1 //damage the tool
	IF (<LINK.HITS><=0)
		LINK.REMOVE
		CONT.SYSMESSAGE You have worn out your tool!
		IF (<NEW.QUALITY>>200) && !(<CONT.Ctag0.SKMENU_BLACKSMITHING_MARKOPTION>)
			CONT.SYSMESSAGE You create an exceptional quality item and affix your maker's mark.
		ELSEIF (<NEW.QUALITY>>200)
			CONT.SYSMESSAGE You create an exceptional quality item.
		ELSE
			CONT.SYSMESSAGE You create the item.
		ENDIF
		LOCAL.NODIALOG=1
	ELSE
		LINK.RESENDTOOLTIP
		IF (<NEW.QUALITY>>200) && !(<CONT.Ctag0.SKMENU_BLACKSMITHING_MARKOPTION>)
			CONT.Ctag0.SKMENU_BLACKSMITHING_NOTICE You create an exceptional quality item and affix your maker's mark.
		ELSEIF (<NEW.QUALITY>>200)
			CONT.Ctag0.SKMENU_BLACKSMITHING_NOTICE You create an exceptional quality item.
		ELSE
			CONT.Ctag0.SKMENU_BLACKSMITHING_NOTICE You create the item.
		ENDIF
	ENDIF
	IF (<LOCAL.CHANCE><1000) //gain skill only if the crafting was not trivial
		CONT.SKILLGAIN blacksmithing <EVAL 10-(<LOCAL.CHANCE>/100)>
	ENDIF
ELSE
	LINK.HITS -= 1
	IF (<LINK.HITS><=0)
		LINK.REMOVE
		CONT.SYSMESSAGE You have worn out your tool!
		CONT.SYSMESSAGE You failed to create the item, and some of your materials are lost.
		LOCAL.NODIALOG=1
	ELSE
		LINK.RESENDTOOLTIP
		CONT.Ctag0.SKMENU_BLACKSMITHING_NOTICE You failed to create the item, and some of your materials are lost. //actually _all_ materials are lost, since they are consumed when the crafting starts
	ENDIF

	IF <cont.skillcheck MINING <eval <SERV.ITEMDEF.<TAG.ITEM>.SKILLMAKE.0.VAL>>>
		LOCAL.I=1
		WHILE (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL> > 0)
			IF (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>==<CONT.CTAG0.SKMENU_SUBRES_BASE>)
				NEWITEM <CONT.CTAG0.SKMENU_SUBRES>
				NEW.AMOUNT=(<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL>/2)
				trysrc <cont> new.bounce
			ELSE
				NEWITEM <SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>
				NEW.AMOUNT=(<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL>/2)
				trysrc <cont> new.bounce
			ENDIF
			LOCAL.I += 1
		ENDWHILE
	ELSE
		LOCAL.I=1
		WHILE (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL> > 0)
			IF (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>==<CONT.CTAG0.SKMENU_SUBRES_BASE>)
				NEWITEM <CONT.CTAG0.SKMENU_SUBRES>
				NEW.AMOUNT=(<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL>/3)
				trysrc <cont> new.bounce
			ELSE
				NEWITEM <SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>
				NEW.AMOUNT=(<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL>/3)
				trysrc <cont> new.bounce
			ENDIF
			LOCAL.I += 1
		ENDWHILE
	ENDIF

	IF (<CONT.BLACKSMITHING><30.0) //skill gain on failure only under 30.0 blacksmithing
		CONT.SKILLGAIN blacksmithing 1
	ENDIF
ENDIF
IF !(<LOCAL.NODIALOG>)
	TRYSRC <CONT.UID> LINK.DIALOG d_skillmenu_blacksmithing
ENDIF
REMOVE
RETURN 1

ON=@UNEQUIP
SRC.EVENTS -e_blacksmithing_crafting

[EVENTS e_blacksmithing_crafting]
ON=@SKILLSTART
SRC.SYSMESSAGE You must wait to perform another action
ACTION -1
RETURN 1




[FUNCTION f_skmenu_blacksmithing_smelt]
IF !(<SRC.ISNEARTYPE t_forge, 2>)
	SRC.SYSMESSAGE You must be near a forge to smelt items.
	RETURN 1
ENDIF
IF !(<ARGO.UID>)
	TARGETF f_skmenu_blacksmithing_smelt <UID>
	SRC.SYSMESSAGE Target an item to recycle
ELSE
	OBJ=<ARGO.UID>
	IF !(<OBJ.ISITEM>)
		RETURN 1
	ELSEIF (<OBJ.CONT.TOPOBJ>!=<SRC.UID>)
		RETURN 1
	ENDIF
	LOCAL.I=1
	WHILE (<OBJ.RESOURCES.<LOCAL.I>.VAL>)
		IF (<OBJ.RESOURCES.<LOCAL.I>.KEY>==i_ingot_iron)
			LOCAL.AMOUNT=<OBJ.RESOURCES.<LOCAL.I>.VAL>
		ENDIF
		LOCAL.I += 1
	ENDWHILE
	OBJ=<ARGN1>
	IF !(<LOCAL.AMOUNT>)
		SRC.Ctag0.SKMENU_BLACKSMITHING_NOTICE=You can't melt that down into ingots.
		TRY OBJ.DIALOG d_skillmenu_blacksmithing
		RETURN 1
	ENDIF
	LOCAL.AMOUNT /= 2
	IF (<LOCAL.AMOUNT><1)
		LOCAL.AMOUNT=1
	ENDIF
	SRC.SOUND 02a
	SRC.SOUND 0240
	OBJ=<ARGO.UID>
	OBJ.REMOVE
	IF !(<OBJ.tag0.BASEMATERIAL>==i_ingot_iron)
		SERV.NEWITEM i_ingot_iron
	ELSE
		SERV.NEWITEM <OBJ.tag0.MATERIAL>
	ENDIF
	NEW.AMOUNT=<LOCAL.AMOUNT>
	NEW.BOUNCE
	SRC.Ctag0.SKMENU_BLACKSMITHING_NOTICE=You melt the item down into ingots.
	OBJ=<ARGN1>
	TRY OBJ.DIALOG d_skillmenu_blacksmithing
ENDIF

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//MENU
//This is the biggest part of the script, the skillmenu. It's organized in subfunctions which draw specific part of the dialog
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

[DIALOG d_skillmenu_blacksmithing] //argn1=page
40,40
SRC.DIALOGCLOSE d_skillmenu_blacksmithing
page 0

resizepic 0 0 5054 530 437
gumppictiled 10 10 510 22 2624 
gumppictiled 10 292 150 45 2624
gumppictiled 165 292 355 45 2624
gumppictiled 10 342 510 85 2624
gumppictiled 10 37 200 250 2624
gumppictiled 215 37 305 250 2624
checkertrans 10 10 510 417

dhtmlgump 10 12 510 20 0 0 <htmlFormatTitle BLACKSMITHY MENU>
dhtmlgump 10 37 200 22 0 0 <htmlFormatTitle CATEGORIES>
dhtmlgump 215 37 305 22 0 0 <htmlFormatTitle SELECTIONS>
dhtmlgump 10 302 150 25 0 0 <htmlFormatTitle NOTICES>

button 15 402 4017 4019 1 0 0
dhtmlgump 50 405 150 18 0 0 <htmlFormat EXIT>

button 270 402 4005 4007 1 0 <buttonID 6 2>
dhtmlgump 305 405 150 18 0 0 <htmlFormat MAKE LAST>

IF (<DEF.skmenu_blacksmithing_flags>&01)
	button 270 362 4005 4007 1 0 <buttonID 6 6>
	DOSWITCH (<SRC.Ctag0.SKMENU_BLACKSMITHING_MARKOPTION>)
	dhtmlgump 305 365 150 18 0 0 <htmlFormat MARK ITEM>
	dhtmlgump 305 365 150 18 0 0 <htmlFormat DO NOT MARK>
	dhtmlgump 305 365 150 18 0 0 <htmlFormat PROMPT FOR MARK>
	ENDDO
ENDIF
IF (<DEF.skmenu_blacksmithing_flags>&02)
	button 15 342 4005 4007 1 0 <buttonID 6 1>
	dhtmlgump 50 345 150 18 0 0 <htmlFormat SMELT ITEM>
ENDIF
IF (<DEF.skmenu_blacksmithing_flags>&04)
	button 270 342 4005 4007 1 0 <buttonID 6 5>
	dhtmlgump 305 345 150 18 0 0 <htmlFormat REPAIR ITEM>
ENDIF
IF (<DEF.skmenu_blacksmithing_flags>&08)
	button 270 382 4005 4007 1 0 <buttonId 6 8>
	dhtmlgump 305 385 150 18 0 0 <htmlFormat ENHANCE ITEM>
ENDIF
IF !(<ISEMPTY <SRC.Ctag0.SKMENU_BLACKSMITHING_NOTICE>>)
	dhtmlgump 170 295 350 40 0 0 <htmlFormat <SRC.Ctag.SKMENU_BLACKSMITHING_NOTICE>>
	SRC.Ctag0.SKMENU_BLACKSMITHING_NOTICE
ENDIF
//colored ingots
IF (<DEF0.skmenu_blacksmithing_subres_0>)
	button 15 362 4005 4007 1 0 <buttonId 6 0>
	dhtmlgump 50 365 250 18 0 0 <htmlFormat <SERV.ITEMDEF.<SRC.Ctag0.SKMENU_SUBRES>.NAME>>
ENDIF
//dragon scales

f_skmenu_blacksmithing_categories
IF (0<ARGS>==1)
	f_skmenu_blacksmithing_subres
ELSE
	f_skmenu_blacksmithing_items <SRC.Ctag0.SKMENU_BLACKSMITHING_LASTCATEGORY>
ENDIF

[DIALOG d_skillmenu_blacksmithing BUTTON]
ON=1 0ffffffff
ARGN1 -= 1
LOCAL.TYPE=<ARGN1>%8
LOCAL.INDEX=<ARGN1>/8
IF (<LOCAL.TYPE>==0) // show group
	SRC.Ctag0.SKMENU_BLACKSMITHING_LASTCATEGORY=<LOCAL.INDEX>
	DIALOG d_skillmenu_blacksmithing
ELSEIF (<LOCAL.TYPE>==1) // create item
	f_skmenu_blacksmithing_makeitem <DEF.skmenu_blacksmithing_<SRC.dCtag0.SKMENU_BLACKSMITHING_LASTCATEGORY>_<dLOCAL.INDEX>>
ELSEIF (<LOCAL.TYPE>==2) // item details
	DIALOG d_skillmenu_blacksmithing_details 0, <LOCAL.INDEX>
ELSEIF (<LOCAL.TYPE>==3) // create item (last ten)
	f_skmenu_blacksmithing_makeitem <SRC.Ctag0.SKMENU_BLACKSMITHING_LASTTEN_<dLOCAL.INDEX>>
ELSEIF (<LOCAL.TYPE>==4) // item details (last ten) [this is redundant]
	DIALOG d_skillmenu_blacksmithing_details 0, <LOCAL.INDEX>
ELSEIF (<LOCAL.TYPE>==5) //resource selected
	IF (<DEF.skmenu_blacksmithing_subres_<dLOCAL.INDEX>>)
		LOCAL.NUMBER = <DEF.SKMENU_SUBRES_<DEF.skmenu_blacksmithing_subres_<dLOCAL.INDEX>>_SKILL>
		IF (<LOCAL.NUMBER> > <SRC.blacksmithing>)
			SRC.CTAG.SKMENU_blacksmithing_NOTICE You cannot work this strange and unusual material.
			DIALOG d_skillmenu_blacksmithing 0, 1
			RETURN 1
		ENDIF
		SRC.CTAG0.SKMENU_SUBRES=<DEF.skmenu_blacksmithing_subres_<dLOCAL.INDEX>>
		SRC.CTAG0.SKMENU_SUBRES_SKILL=<LOCAL.NUMBER>
		SRC.CTAG0.SKMENU_SUBRES_BASE=<DEF.skmenu_blacksmithing_subres_0>
		SRC.CTAG.SKMENU_blacksmithing_NOTICE Material: <serv.itemdef.<SRC.CTAG0.SKMENU_SUBRES>.name>
		DIALOG d_skillmenu_blacksmithing
	ENDIF
ELSEIF (<LOCAL.TYPE>==6) //misc
	IF (<LOCAL.INDEX>==0) //resource selection
		IF (<DEF0.skmenu_blacksmithing_subres_0>)
			DIALOG d_skillmenu_blacksmithing 0, 1
		ENDIF
	ELSEIF (<LOCAL.INDEX>==1) //resmelt
		IF (<DEF0.skmenu_blacksmithing_flags>&02)
			f_skmenu_blacksmithing_smelt
		ENDIF
	ELSEIF (<LOCAL.INDEX>==2) //make last
		IF (<SRC.Ctag0.SKMENU_BLACKSMITHING_LASTTEN_0>)
			f_skmenu_blacksmithing_makeitem <SRC.Ctag0.SKMENU_BLACKSMITHING_LASTTEN_0>
		ELSE
			SRC.Ctag0.SKMENU_BLACKSMITHING_NOTICE You haven't made anything yet.
			DIALOG d_skillmenu_blacksmithing
		ENDIF
	ELSEIF (<LOCAL.INDEX>==3) //last 10 items made
		SRC.Ctag0.SKMENU_BLACKSMITHING_LASTCATEGORY=-1
		DIALOG d_skillmenu_blacksmithing
	ELSEIF (<LOCAL.INDEX>==4) // Toggle use resource hue
		SRC.Ctag0.SKMENU_BLACKSMITHING_DONOTCOLOR ^= 1
		DIALOG d_skillmenu_blacksmithing 0, 1
	ELSEIF (<LOCAL.INDEX>==5) //repair
		IF (<DEF0.skmenu_blacksmithing_flags>&04)
			SRC.REPAIRITEM
		ENDIF
	ELSEIF (<LOCAL.INDEX>==6) //toggle mark option
		IF (<DEF0.skmenu_blacksmithing_flags>&01)
			DOSWITCH <SRC.Ctag0.SKMENU_BLACKSMITHING_MARKOPTION>
			SRC.Ctag0.SKMENU_BLACKSMITHING_MARKOPTION = 1
			SRC.Ctag0.SKMENU_BLACKSMITHING_MARKOPTION = 2
			SRC.Ctag0.SKMENU_BLACKSMITHING_MARKOPTION = 0
			ENDDO
		ENDIF
		DIALOG d_skillmenu_blacksmithing
	ELSEIF (<LOCAL.INDEX>==8) //enhance
		IF (<DEF0.skmenu_blacksmithing_flags>&08)
			//f_skmenu_blacksmithing_enhance
		ENDIF
	ENDIF
ENDIF

[FUNCTION f_skmenu_blacksmithing_categories]
button 15 60 4005 4007 1 0 <buttonId 6 3>
dhtmlgump 50 63 150 18 0 0 <htmlFormat LAST TEN>
WHILE (<DEF0.skmenu_blacksmithing_<dLOCAL.I>_0>)
	button 15 <EVAL 80+(<LOCAL.I>*20)> 4005 4007 1 0 <buttonId 0 <LOCAL.I>>
	dhtmlgump 50 <EVAL 83+(<LOCAL.I>*20)> 150 18 0 0 <htmlFormat <DEF.skmenu_blacksmithing_<dLOCAL.I>_name>>
	LOCAL.I += 1
ENDWHILE

[FUNCTION f_skmenu_blacksmithing_items] //argn1=selected category
IF (<ARGN1>==-1)
	WHILE (<SRC.Ctag0.SKMENU_BLACKSMITHING_LASTTEN_<dLOCAL.I>>)
		LOCAL.INDEX=<LOCAL.I>%10
		LOCAL.ITEM=<SRC.Ctag0.SKMENU_BLACKSMITHING_LASTTEN_<dLOCAL.I>>
		IF (<LOCAL.INDEX>==0)
			IF (<LOCAL.I>>0)
				button 370 260 4005 4007 0 <EVAL (<LOCAL.I>/10)+1> 0
				dhtmlgump 405 263 100 18 0 0 <htmlFormat NEXT PAGE>
			ENDIF
			page <EVAL (<LOCAL.I>/10)+1>
			IF (<LOCAL.I>>0)
				button 220 260 4014 4016 0 <EVAL (<LOCAL.I>/10)> 0
				dhtmlgump 255 263 100 18 0 0 <htmlFormat PREV PAGE>
			ENDIF
		ENDIF
		button 220 <EVAL 60+(<LOCAL.INDEX>*20)> 4005 4007 1 0 <buttonId 3 <LOCAL.I>>
		dhtmlgump 255 <EVAL 63+(<LOCAL.INDEX>*20)> 220 18 0 0 <htmlFormat <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>
		button 480 <EVAL 60+(<LOCAL.INDEX>*20)> 4011 4012 1 0 <buttonId 4 <LOCAL.I>>
		LOCAL.I += 1
	ENDWHILE
ELSE
	WHILE (<DEF0.skmenu_blacksmithing_<dARGN1>_<dLOCAL.I>>)
		LOCAL.INDEX=<LOCAL.I>%10
		LOCAL.ITEM=<DEF.skmenu_blacksmithing_<dARGN1>_<dLOCAL.I>>
		IF (<LOCAL.INDEX>==0)
			IF (<LOCAL.I>>0)
				button 370 260 4005 4007 0 <EVAL (<LOCAL.I>/10)+1> 0
				dhtmlgump 405 263 100 18 0 0 <htmlFormat NEXT PAGE>
			ENDIF
			page <EVAL (<LOCAL.I>/10)+1>
			IF (<LOCAL.I>>0)
				button 220 260 4014 4016 0 <EVAL (<LOCAL.I>/10)> 0
				dhtmlgump 255 263 100 18 0 0 <htmlFormat PREV PAGE>
			ENDIF
		ENDIF
		button 220 <EVAL 60+(<LOCAL.INDEX>*20)> 4005 4007 1 0 <buttonId 1 <LOCAL.I>>
		dhtmlgump 255 <EVAL 63+(<LOCAL.INDEX>*20)> 220 18 0 0 <htmlFormat <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>
		button 480 <EVAL 60+(<LOCAL.INDEX>*20)> 4011 4012 1 0 <buttonId 2 <LOCAL.I>>
		LOCAL.I += 1
	ENDFOR
ENDIF

[FUNCTION f_skmenu_blacksmithing_subres]
WHILE (<DEF0.skmenu_blacksmithing_subres_<dLOCAL.I>>)
	LOCAL.INDEX=<LOCAL.I>%10
	IF (<LOCAL.INDEX>==0)
		IF (<LOCAL.I>>0)
			button 485 260 4005 4007 0 <EVAL (<LOCAL.I>/10)+1> 0
		ENDIF
		page <EVAL (<LOCAL.I>/10)+1>
		IF (<LOCAL.I>>0)
			button 455 260 4014 4016 0 <EVAL (<LOCAL.I>/10)> 0
		ENDIF
		button 220 260 4005 4007 1 0 <buttonID 6 4>
		dhtmlgump 255 263 200 18 0 0 <htmlFormat <QVAL <SRC.Ctag0.SKMENU_BLACKSMITHING_DONOTCOLOR>?USE MATERIAL COLOR:DO NOT COLOR>>
	ENDIF
	button 220 <EVAL 60+(<LOCAL.INDEX>*20)> 4005 4007 1 0 <buttonID 5 <LOCAL.I>>
	dhtmlgump 255 <EVAL 63+(<LOCAL.INDEX>*20)> 250 18 0 0 <htmlFormat <SERV.ITEMDEF.<DEF0.skmenu_blacksmithing_subres_<dLOCAL.I>>.NAME>>
	LOCAL.I += 1
ENDWHILE



[DIALOG d_skillmenu_blacksmithing_details]
40,40
SRC.DIALOGCLOSE d_skillmenu_blacksmithing
SRC.DIALOGCLOSE d_skillmenu_blacksmithing_details
page 0

resizepic 0 0 5054 530 417
gumppictiled 10 10 510 22 2624 
gumppictiled 10 37 150 148 2624
gumppictiled 165 37 355 90 2624
gumppictiled 10 190 155 22 2624
gumppictiled 10 217 150 53 2624
gumppictiled 165 132 355 80 2624
gumppictiled 10 275 155 22 2624
gumppictiled 10 302 150 53 2624
gumppictiled 165 217 355 80 2624
gumppictiled 10 360 155 22 2624
gumppictiled 165 302 355 80 2624
gumppictiled 10 387 510 22 2624
checkertrans 10 10 510 399

dhtmlgump 10 12 510 20 0 0 <htmlFormatTitle BLACKSMITHY MENU>
dhtmlgump 170 40 150 20 0 0 <htmlFormatTitle ITEM>
dhtmlgump 10 192 150 22 0 0 <htmlFormatTitle SKILLS>
dhtmlgump 10 277 150 22 0 0 <htmlFormatTitle MATERIALS>
dhtmlgump 10 362 150 22 0 0 <htmlFormatTitle OTHER>

button 15 387 4014 4016 1 0 0
dhtmlgump 50 390 150 18 0 0 <htmlFormat BACK>

button 270 387 4005 4007 1 0 <EVAL <ARGS>+1>
dhtmlgump 305 390 150 18 0 0 <htmlFormat MAKE NOW>
IF (<SRC.Ctag0.SKMENU_BLACKSMITHING_LASTCATEGORY>==-1) // pick item from "last ten" list
	LOCAL.ITEM=<SRC.Ctag0.SKMENU_BLACKSMITHING_LASTTEN_<dARGS>>
ELSE
	LOCAL.ITEM=<DEF0.skmenu_blacksmithing_<SRC.dCtag0.SKMENU_BLACKSMITHING_LASTCATEGORY>_<dARGS>>
ENDIF
dhtmlgump 330 40 180 18 0 0 <htmlFormat <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>
dorigin - 282 
IF (<SERV.ITEMDEF.<LOCAL.ITEM>.CAN>&CAN_I_REPLICATE)
	dhtmlgump 170 *20 310 18 <htmlFormat Make as many as possible at once>
ENDIF

tilepic 20 50 <EVAL ID(<SERV.ITEMDEF.<LOCAL.ITEM>.DISPID>)>
dhtmlgump 170 *20 310 18 0 0 <HtmlFormat This item may hold it's maker mark>

f_skmenu_details_drawskills <LOCAL.ITEM>
f_skmenu_details_drawresources <LOCAL.ITEM>

[DIALOG d_skillmenu_blacksmithing_details BUTTON]
ON=0 //back
DIALOG d_skillmenu_blacksmithing

ON=1 0ffffffff //make
ARGN1 -= 1
IF (<SRC.Ctag0.SKMENU_BLACKSMITHING_LASTCATEGORY>==-1) // pick item from "last ten" list
	LOCAL.ITEM=<SRC.Ctag0.SKMENU_BLACKSMITHING_LASTTEN_<dARGN1>>
ELSE
	LOCAL.ITEM=<DEF0.skmenu_blacksmithing_<SRC.dCtag0.SKMENU_BLACKSMITHING_LASTCATEGORY>_<dARGN1>>
ENDIF
f_skmenu_blacksmithing_makeitem <LOCAL.ITEM>


[DIALOG d_skillmenu_blacksmithing_querymakersmark]
40,40
resizepic 0 0 5054 220 170
resizepic 10 10 3000 200 150
dhtmlgump 20 20 180 80 0 0 <HtmlFormat Do you wish to place your maker's mark on this item?>
dhtmlgump 55 100 140 25 0 0 <HtmlFormat CONTINUE>
button 20 100 4005 4007 1 0 1
dhtmlgump 55 125 140 25 0 0 <HtmlFormat CANCEL>
button 20 125 4005 4007 1 0 0

[DIALOG d_skillmenu_blacksmithing_querymakersmark BUTTON]
ON=0
SRC.SYSMESSAGE Cancelled mark.
IF (<UID.<SRC.Ctag0.SKMENU_BLACKSMITHING_TOOL>>) //make sure the tool still exists
	SRC.Ctag0.SKMENU_BLACKSMITHING_NOTICE=You create an exceptional quality item
	TRY UID.<SRC.Ctag0.SKMENU_BLACKSMITHING_TOOL>.DIALOG d_skillmenu_blacksmithing
ENDIF

ON=1
SRC.SYSMESSAGE You mark the item.
tag0.CRAFTEDBY <SRC.UID>
RESENDTOOLTIP
IF (<UID.<SRC.Ctag0.SKMENU_BLACKSMITHING_TOOL>>) //make sure the tool still exists
	SRC.Ctag0.SKMENU_BLACKSMITHING_NOTICE=You create an exceptional quality item and affix your maker's mark.
	TRY UID.<SRC.Ctag0.SKMENU_BLACKSMITHING_TOOL>.DIALOG d_skillmenu_blacksmithing
ENDIF


