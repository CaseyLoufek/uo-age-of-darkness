[ITEMDEF 014fb]
//DEFNAME2=LOCKPICK
DEFNAME=i_lockpick
TYPE=T_LOCKPICK
FLIP=1
WEIGHT=.5
VALUE=28
CATEGORY=Items by Professions
SUBSECTION=Thief
DESCRIPTION=Lockpick
DUPELIST=014fc
SKILLMAKE=TINKERING 58.5
RESOURCES=3 i_ingot_iron

[ITEMDEF 014fd]
DEFNAME=i_lockpick_set
//DEFNAME2=LOCKPICKS
TYPE=T_LOCKPICK
FLIP=1
RESOURCES=9 i_ingot_iron
WEIGHT=1.5
VALUE=90
NAME=lockpick set%s
CATEGORY=Items by Professions
SUBSECTION=Thief
DESCRIPTION=Lockpicks (Set)
DUPELIST=014fe
SKILLMAKE=TINKERING 79.8

[ITEMDEF 01eb1]
DEFNAME=i_barrel_staves
RESOURCES=5 i_BOARD
skillmake=5.0 tinkering
WEIGHT=2.5
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Barrel Staves
DUPELIST=01eb2,01eb3,01eb4

[ITEMDEF 01004]
DEFNAME=i_barrel_tap
RESOURCES=2 i_ingot_iron
SKILLMAKE=TINKERING 52.0
WEIGHT=1
CATEGORY=Items by Professions
SUBSECTION=BarKeep
DESCRIPTION=Barrel Tap

[ITEMDEF 010e1]
DEFNAME=i_barrel_hoops
RESOURCES=4 i_ingot_iron
SKILLMAKE=TINKERING 12.0
WEIGHT=4
CATEGORY=Items by Professions
SUBSECTION=BarKeep
DESCRIPTION=Barrel Hoops
DUPELIST=010e2

[ITEMDEF 01db8]
//barrel lid
DEFNAME=i_barrel_lid
RESOURCES=3 i_BOARD
skillmake=5.0 tinkering
CATEGORY=Decoration - Furniture
SUBSECTION=Broken Furniture
DESCRIPTION=Barrel Lid
WEIGHT=2

[ITEMDEF 0104b]
DEFNAME=i_clock
RESOURCES=i_CLOCK_FRAME,i_CLOCK_PARTS
WEIGHT=2
TYPE=T_CLOCK
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Clock
DUPELIST=0104c
SKILLMAKE=TINKERING 40.7

[ITEMDEF 0104d]
DEFNAME=i_clock_frame
RESOURCES=6 i_board
WEIGHT=2
//TYPE=T_CLOCK
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Clock Frame
DUPELIST=0104e
SKILLMAKE=TINKERING 20.4

[ITEMDEF 0104f]
DEFNAME=i_clock_parts
RESOURCES=2 i_axel_and_gears, 1 i_gears, 1 i_spring, 2 i_ingot_iron
WEIGHT=1
//TYPE=T_CLOCK
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Clock Parts
DUPELIST=01050
SKILLMAKE=TINKERING 35.5

[ITEMDEF 01051]
DEFNAME=i_axel_and_gears
RESOURCES=i_axel, i_gears
WEIGHT=1
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Axle with Gears
DUPELIST=01052
SKILLMAKE=TINKERING 30.4

[ITEMDEF 01053]
//gears
DEFNAME=i_gears
RESOURCES=1 i_ingot_iron
WEIGHT=1
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Gears
DUPELIST=01054
SKILLMAKE=TINKERING 16.7

[ITEMDEF 01055]
DEFNAME=i_hinge
RESOURCES=1 i_ingot_iron
WEIGHT=1
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Hinge
DUPELIST=01056
SKILLMAKE=TINKERING 14.8

[ITEMDEF 01057]
DEFNAME=i_sextant
TYPE=T_SEXTANT
RESOURCES=i_sextant_parts, i_gears
WEIGHT=2
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Sextant
DUPELIST=01058
SKILLMAKE=TINKERING 23.7

[ITEMDEF 01059]
DEFNAME=i_sextant_parts
RESOURCES=i_hinge, 2 i_ingot_iron
WEIGHT=2
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Sextant Parts
DUPELIST=0105a
SKILLMAKE=TINKERING 21.0

[ITEMDEF 0105b]
DEFNAME=i_axel
RESOURCES=1 i_ingot_iron
WEIGHT=1
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Axle
DUPELIST=0105c
SKILLMAKE=TINKERING 10.4

[ITEMDEF 0105d]
DEFNAME=i_springs
RESOURCES=1 i_ingot_iron
WEIGHT=1
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Springs
DUPELIST=0105e
SKILLMAKE=TINKERING 33.7

[ITEMDEF 0100e]
DEFNAME=i_key_copper
RESOURCES=3 i_ingot_iron
WEIGHT=1
TYPE=T_KEY
CATEGORY=Provisions - Miscellaneous
SUBSECTION=Keys
DESCRIPTION=Copper Key
SKILLMAKE=TINKERING 43.0

[ITEMDEF 0100f]
DEFNAME=i_key_gold
RESOURCES=2 i_ingot_iron, 1 i_ingot_gold
WEIGHT=1
TYPE=T_KEY
CATEGORY=Provisions - Miscellaneous
SUBSECTION=Keys
DESCRIPTION=Gold Key
SKILLMAKE=TINKERING 1.0

[ITEMDEF 01010]
// DEFNAME2=IRON_KEY
DEFNAME=i_key_iron
RESOURCES=3 i_ingot_iron
WEIGHT=1
TYPE=T_KEY
CATEGORY=Provisions - Miscellaneous
SUBSECTION=Keys
DESCRIPTION=Iron Key
SKILLMAKE=TINKERING 1.0

[ITEMDEF 01011]
DEFNAME=i_key_ring
TYPE=T_KEYRING
TDATA2=1
WEIGHT=1
CATEGORY=Provisions - Miscellaneous
SUBSECTION=Keys
DESCRIPTION=Key Ring
RESOURCES=1 i_ingot_iron
SKILLMAKE=TINKERING 6.0

ON=@DCLICK
	SRC.MESSAGE Drop keys on an empty key ring

[ITEMDEF 01012]
// DEFNAME2=MAGIC_Key
DEFNAME=i_key_magic
WEIGHT=1
TYPE=T_KEY
CATEGORY=Provisions - Miscellaneous
SUBSECTION=Keys
DESCRIPTION=Magic Key
SKILLMAKE=TINKERING 50.0, MAGERY 50.0
RESOURCES=1 i_ingot_gold, 1 i_ingot_silver, 1 i_ingot_copper

ON=@Create
	ATTR=attr_magic|attr_newbie

[ITEMDEF 01013]
// DEFNAME2=RUSTY_KEY
DEFNAME=i_key_rusty
RESOURCES=1 i_INGOT_RUSTY
SKILLMAKE=TINKERING 1.0
WEIGHT=1
TYPE=T_KEY
CATEGORY=Provisions - Miscellaneous
SUBSECTION=Keys
DESCRIPTION=Rusty Iron Key


[ITEMDEF 01769]
// Key ring with keys on it
DEFNAME=i_key_ring_1
TYPE=T_KEYRING
TDATA2=1
RESOURCES=1 i_ingot_iron
SKILLMAKE=TINKERING 6.0
WEIGHT=1
CATEGORY=Provisions - Miscellaneous
SUBSECTION=Keys
DESCRIPTION=Key Ring w/Keys
DUPELIST=0176a,0176b

[ITEMDEF 01c18]
DEFNAME=i_oil_flask
VALUE=3
WEIGHT=1
CATEGORY=Provisions - Miscellaneous
SUBSECTION=Etc.
DESCRIPTION=Oil Flask

[ITEMDEF 01c19]
//exploding, lit oil flask animation
DEFNAME=i_fx_oil_flask
CATEGORY=Animations
SUBSECTION=Oil Flask
DESCRIPTION=@
DUPELIST=01c1a,01c1b,01c1c,01c1d,01c1e,01c1f

[ITEMDEF 01c1a]
//lit oil flask
DUPEITEM=01c19

[ITEMDEF 01c1b]
//lit oil flask
DUPEITEM=01c19

[ITEMDEF 01c1c]
//dimly lit oil flask, frame from the animation
DUPEITEM=01c19

[ITEMDEF 01c1d]
//breaking oil flask, frame from the animation
DUPEITEM=01c19

[ITEMDEF 01c1e]
//exploded oil flask, frame from the animation
DUPEITEM=01c19

[ITEMDEF 01c1f]
//firey exploded oil flask, frame from the animation
DUPEITEM=01c19


[ITEMDEF 01eb6]
DEFNAME=i_toolkit_empty
RESOURCES=2 i_hide,4 i_board
SKILLMAKE=TINKERING 80.0
WEIGHT=3
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Empty Tool Kit

[ITEMDEF 01eb7]
DEFNAME=i_toolcase_empty
RESOURCES=2 i_hide,4 i_board
SKILLMAKE=TINKERING 80.0
WEIGHT=3
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Empty Tool Case

[ITEMDEF 01eb8]
DEFNAME=i_toolkit
RESOURCES=i_TOOLKIT_EMPTY, 4 i_ingot_iron,4 i_board
SKILLMAKE=TINKERING 50.0
WEIGHT=6
TYPE=t_carpentry
DUPELIST=01eb9
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Tool Kit

[ITEMDEF 01eb9]
DUPEITEM=01eb8

[ITEMDEF 01eba]
DEFNAME=i_toolcase
RESOURCES=i_toolcase_empty,4 i_ingot_iron,4 i_board
SKILLMAKE=TINKERING 50.0
WEIGHT=6
TYPE=t_carpentry
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Tool Case
DUPELIST=01ebb

[ITEMDEF 01ebb]
DUPEITEM=01eba

[ITEMDEF 01ebc]
DEFNAME=i_tinker_tools
TYPE=T_TINKER_TOOLS
NAME=tinker's tools
WEIGHT=2
CATEGORY=Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Tinker's Tools
SKILLMAKE=TINKERING 35.0
RESOURCES=4 i_ingot_iron

ON=@CREATE
	HITS=50

ON=@DCLICK
IF (<SRC.RESTEST 1 i_tinkering_craft>) || (<SRC.RESTEST 1 i_blacksmithing_craft>)
	SRC.SYSMESSAGE You must wait to perform another action.
	RETURN 1
ENDIF

DIALOG d_skillmenu_tinkering
RETURN 1

[DEFNAME def_skmenu_tinkering]
skmenu_tinkering_flags 01|04 //01=markoption 02=resmelt 04=repair 08=enhance
skmenu_tinkering_subres_0 i_board

skmenu_tinkering_0_name PARTS
skmenu_tinkering_0_0 i_axel
skmenu_tinkering_0_1 i_gears
skmenu_tinkering_0_2 i_hinge
skmenu_tinkering_0_3 i_springs
skmenu_tinkering_0_4 i_SEXTANT_PARTS
skmenu_tinkering_0_5 i_axel_and_gears
skmenu_tinkering_0_6 i_CLOCK_PARTS
skmenu_tinkering_0_7 i_clock_frame
skmenu_tinkering_0_8 i_barrel_hoops
skmenu_tinkering_0_9 i_barrel_tap
skmenu_tinkering_0_10 i_barrel_staves
skmenu_tinkering_0_11 i_barrel_lid
skmenu_tinkering_0_12 i_wire_iron
skmenu_tinkering_0_13 i_wire_copper
skmenu_tinkering_0_14 i_wire_silver
skmenu_tinkering_0_15 i_wire_gold
skmenu_tinkering_0_16 i_beads
skmenu_tinkering_0_17 i_rope
skmenu_tinkering_0_18 i_nails
skmenu_tinkering_0_19 i_toolkit_empty
skmenu_tinkering_0_20 i_toolcase_empty

skmenu_tinkering_1_name TOOLS
skmenu_tinkering_1_0 i_shovel
skmenu_tinkering_1_1 i_saw
skmenu_tinkering_1_2 i_saw_dovetail
skmenu_tinkering_1_3 i_plane_jointing
skmenu_tinkering_1_4 i_plane_moulding
skmenu_tinkering_1_5 i_plane_smoothing
skmenu_tinkering_1_6 i_froe
skmenu_tinkering_1_7 i_scorp
skmenu_tinkering_1_8 i_inshave
skmenu_tinkering_1_9 i_knife_draw
skmenu_tinkering_1_10 i_hammer
skmenu_tinkering_1_11 i_scissors
skmenu_tinkering_1_12 i_sewing_kit
skmenu_tinkering_1_13 i_tinker_tools
skmenu_tinkering_1_14 i_arrow_fletching
skmenu_tinkering_1_15 i_tongs
skmenu_tinkering_1_16 i_FISHING_POLE
skmenu_tinkering_1_17 i_lockpick
skmenu_tinkering_1_18 i_lockpick_set
skmenu_tinkering_1_19 i_mortar_pestle

skmenu_tinkering_2_name UTENSILS
skmenu_tinkering_2_0 i_fork
skmenu_tinkering_2_1 i_spoon
skmenu_tinkering_2_2 i_knife
skmenu_tinkering_2_3 i_plate
skmenu_tinkering_2_4 i_tray
skmenu_tinkering_2_5 i_kettle
skmenu_tinkering_2_6 i_frypan
skmenu_tinkering_2_7 i_pot
skmenu_tinkering_2_8 i_skull_mug
skmenu_tinkering_2_9 i_mug_ceramic
skmenu_tinkering_2_10 i_goblet
skmenu_tinkering_2_11 i_mug_pewter
skmenu_tinkering_2_12 i_goblet_2
skmenu_tinkering_2_13 i_spittoon
skmenu_tinkering_2_14 i_flour_sifter
skmenu_tinkering_2_15 i_bowl_wood
skmenu_tinkering_2_16 i_bowl_wood_lg
skmenu_tinkering_2_17 i_bowl_pewter

skmenu_tinkering_3_name JEWELRY
skmenu_tinkering_3_0 i_elven_glasses
skmenu_tinkering_3_1 i_necklace_metal_beads
skmenu_tinkering_3_2 i_necklace_gold
skmenu_tinkering_3_3 i_necklace_gold_beads
skmenu_tinkering_3_4 i_earrings_gold
skmenu_tinkering_3_5 i_ring_gold
skmenu_tinkering_3_6 i_bracelet_silver
skmenu_tinkering_3_7 i_necklace_silver
skmenu_tinkering_3_8 i_earrings_silver
skmenu_tinkering_3_9 i_ring_silver
skmenu_tinkering_3_10 i_necklace_silver_beads
skmenu_tinkering_3_11 i_ankh_pendant

skmenu_tinkering_4_name TRAPS
skmenu_tinkering_4_0 i_trap_explosion

skmenu_tinkering_5_name INSTRUMENTS
skmenu_tinkering_5_0 i_tambourine
skmenu_tinkering_5_1 i_drum
skmenu_tinkering_5_2 i_lute
skmenu_tinkering_5_3 i_lyre
skmenu_tinkering_5_4 i_HARP_LARGE

skmenu_tinkering_6_name OTHER
skmenu_tinkering_6_0 i_clock
skmenu_tinkering_6_1 i_sextant
skmenu_tinkering_6_2 i_spyglass
skmenu_tinkering_6_3 i_abacus
skmenu_tinkering_6_4 i_candle
skmenu_tinkering_6_5 i_lantern
skmenu_tinkering_6_6 i_powder_keg
skmenu_tinkering_6_7 i_keg_potion
skmenu_tinkering_6_8 i_dice_cup
skmenu_tinkering_6_9 i_game_board
skmenu_tinkering_6_10 i_backgammon_game



//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//MAIN FUNCTIONS
//Here there are the "core" functions that control the crafting procedure. 
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

//This is the main craft function
[FUNCTION f_skmenu_tinkering_makeitem]
//preliminary checks
IF (<SRC.ACTION>!=-1) || (<SRC.FINDID.i_tinkering_craft>)
	SRC.SYSMESSAGE You must wait to perform another action
	RETURN 1
ENDIF
IF !(<SRC.CANMAKESKILL <ARGS>>)
	SRC.CTAG0.SKMENU_tinkering_NOTICE You don't have the required skills to attempt this item.
	DIALOG d_skillmenu_tinkering
	RETURN 1
ENDIF

//check resources
//check resources
	IF (<SRC.Ctag0.SKMENU_SUBRES_SKILL>)
		LOCAL.RESOURCES=<f_resource_replace <ARGS>,<SRC.dCTAG0.SKMENU_SUBRES_BASE>,<SRC.dCTAG0.SKMENU_SUBRES>>
	ELSE
		LOCAL.RESOURCES=<SERV.ITEMDEF.<ARGS>.RESOURCES>
	ENDIF

IF !(<SRC.RESTEST <LOCAL.RESOURCES>>)
	SRC.Ctag0.SKMENU_TINKERING_NOTICE You don't have the resources required to make that item.
	DIALOG d_skillmenu_TINKERING
	RETURN 1
ENDIF
SRC.CONSUME <LOCAL.RESOURCES>

//update the "last ten" list
IF (<ARGS>!=<SRC.CTAG0.SKMENU_tinkering_LASTTEN_0>) 
	FOR 9 1
		TRY SRC.CTAG0.SKMENU_tinkering_LASTTEN_<dLOCAL._FOR> = <SRC.CTAG.SKMENU_tinkering_LASTTEN_<EVAL <LOCAL._FOR>-1>>
	ENDFOR
	SRC.CTAG0.SKMENU_tinkering_LASTTEN_0 <ARGS>
ENDIF

//start crafting
SERV.NEWITEM i_tinkering_craft
NEW.CONT=<SRC.UID>
NEW.MORE=<SRC.CTAG0.SKMENU_SUBRES>
NEW.LINK=<UID> //linked to the tool
NEW.TIMER=1
NEW.TAG.ITEM=<ARGS>
SRC.ANIM 33
SRC.EVENTS +e_tinkering_crafting

//This is the memory item used for the crafting
[ITEMDEF i_tinkering_craft]
ID=02007
NAME=tinkering craft memory
TYPE=t_eq_script

ON=@CREATE
MOREX={2 4}

ON=@TIMER	
CONT.SFX 055
CONT.ANIM 33
MOREX -= 1
IF (<MOREX>)
	TIMER 1
	RETURN 1
ENDIF
LOCAL.CHANCE=<CONT.f_skmenu_tinkering_successchance <TAG.ITEM>, <LINK.UID>>
IF (RAND(1000)<<LOCAL.CHANCE>) //success! we can create the item
	SERV.NEWITEM <TAG.ITEM> 
	LOCAL.EXCEP=<CONT.f_skmenu_tinkering_exceptionalchance <TAG.ITEM>, <LINK.UID>>
	VAR.ARGN1=<LOCAL.EXCEP>
	VAR.ARGN2=<MORE>
	LINK.TRIGGER=@MakeItem
	LOCAL.EXCEP=<VAR.ARGN1>
	MORE=<VAR.ARGN2>
	NEW.CONT=<CONT.FINDLAYER.21.UID>
	CONT.SOUND 057
	NEW.TAG.OVERRIDE.RANGE=<RANGE>
	NEW.f_skmenu_tinkering_material <MORE>
	IF (RAND(1000)<<LOCAL.EXCEP>)
		NEW.QUALITY=201
		IF (<CONT.CTAG0.SKMENU_tinkering_MARKOPTION>==0)
			NEW.TAG0.CRAFTEDBY=<CONT.UID>
		ELSEIF (<CONT.CTAG0.SKMENU_tinkering_MARKOPTION>==2)
			CONT.CTAG0.SKMENU_tinkering_TOOL=<LINK.UID>
			TRYSRC <CONT.UID> NEW.DIALOG d_skillmenu_tinkering_querymakersmark
			LOCAL.NODIALOG=1
		ENDIF
		IF (<NEW.ISWEAPON>)
			NEW.NAME Exceptional <NEW.NAME>
			NEW.HITPOINTS += <EVAL <CONT.ARMSLORE>/50>+5
			NEW.TAG0.DAMINCREASE += <EVAL <CONT.ARMSLORE>/50>+5 //exceptional weapons have +20% damage
			new.tag0.modvalue += <EVAL <CONT.ARMSLORE>/50>+5
		ENDIF
	ELSE
		NEW.QUALITY=<EVAL (<LOCAL.CHANCE>+<LOCAL.EXCEP>)/10> //arbitrary formula for quality calculation
	ENDIF
	var.exp = (<eval rand(<NEW.VALUE>+<NEW.tag0.MODVALUE>)/((<cont.level>+1)/2)>)
	expeval <eval <var.exp>>
	cont.sysmessage You have gained <var.expmsg> experience.
	CONT.EXP += <eval <var.exp>>
	NEW.RESENDTOOLTIP
	LINK.HITS -= 1 //damage the tool
	IF (<LINK.HITS><=0)
		LINK.REMOVE
		CONT.SYSMESSAGE You have worn out your tool!
		IF (<NEW.QUALITY>>200) && !(<CONT.CTAG0.SKMENU_tinkering_MARKOPTION>)
			CONT.SYSMESSAGE You create an exceptional quality item and affix your maker's mark.
		ELSEIF (<NEW.QUALITY>>200)
			CONT.SYSMESSAGE You create an exceptional quality item.
		ELSE
			CONT.SYSMESSAGE You create the item.
		ENDIF
		LOCAL.NODIALOG=1
	ELSE
		LINK.RESENDTOOLTIP
		IF (<NEW.QUALITY>>200) && !(<CONT.CTAG0.SKMENU_tinkering_MARKOPTION>)
			CONT.CTAG0.SKMENU_tinkering_NOTICE You create an exceptional quality item and affix your maker's mark.
		ELSEIF (<NEW.QUALITY>>200)
			CONT.CTAG0.SKMENU_tinkering_NOTICE You create an exceptional quality item.
		ELSE
			CONT.CTAG0.SKMENU_tinkering_NOTICE You create the item.
		ENDIF
	ENDIF
	IF (<LOCAL.CHANCE><1000) //gain skill only if the crafting was not trivial
		CONT.SKILLGAIN tinkering <EVAL 10-(<LOCAL.CHANCE>/100)>
	ENDIF
ELSE
	LINK.HITS -= 1
	IF (<LINK.HITS><=0)
		LINK.REMOVE
		CONT.SYSMESSAGE You have worn out your tool!
		CONT.SYSMESSAGE You failed to create the item, and some of your materials are lost.
		LOCAL.NODIALOG=1
	ELSE
		LINK.RESENDTOOLTIP
		CONT.CTAG0.SKMENU_tinkering_NOTICE You failed to create the item, and some of your materials are lost. //actually _all_ materials are lost, since they are consumed when the crafting starts
	ENDIF
	IF (<CONT.tinkering><30.0) //skill gain on failure only under 30.0 tinkering
		CONT.SKILLGAIN tinkering 1
	ENDIF
ENDIF
IF !(<LOCAL.NODIALOG>)
	TRYSRC <CONT.UID> LINK.DIALOG d_skillmenu_tinkering
ENDIF
REMOVE
RETURN 1

ON=@UNEQUIP
SRC.EVENTS -e_tinkering_crafting

[EVENTS e_tinkering_crafting]
ON=@SKILLSTART
SRC.SYSMESSAGE You must wait to perform another action
ACTION -1
RETURN 1

[FUNCTION f_skmenu_tinkering_material]
TRY f_material_<ARGN1>

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//MENU
//This is the biggest part of the script, the skillmenu. It's organized in subfunctions which draw specific part of the dialog
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

[DIALOG d_skillmenu_tinkering] //argn1=page
40,40
SRC.DIALOGCLOSE d_skillmenu_tinkering
page 0

resizepic 0 0 5054 530 437
gumppictiled 10 10 510 22 2624 
gumppictiled 10 292 150 45 2624
gumppictiled 165 292 355 45 2624
gumppictiled 10 342 510 85 2624
gumppictiled 10 37 200 250 2624
gumppictiled 215 37 305 250 2624
checkertrans 10 10 510 417

dhtmlgump 10 12 510 20 0 0 <htmlFormatTitle TINKERING MENU>
dhtmlgump 10 37 200 22 0 0 <htmlFormatTitle CATEGORIES>
dhtmlgump 215 37 305 22 0 0 <htmlFormatTitle SELECTIONS>
dhtmlgump 10 302 150 25 0 0 <htmlFormatTitle NOTICES>

button 15 402 4017 4019 1 0 0
dhtmlgump 50 405 150 18 0 0 <htmlFormat EXIT>

button 270 402 4005 4007 1 0 <buttonID 6 2>
dhtmlgump 305 405 150 18 0 0 <htmlFormat MAKE LAST>

IF (<DEF.skmenu_tinkering_flags>&01)
	button 270 362 4005 4007 1 0 <buttonID 6 6>
	DOSWITCH (<SRC.CTAG0.SKMENU_tinkering_MARKOPTION>)
	dhtmlgump 305 365 150 18 0 0 <htmlFormat MARK ITEM>
	dhtmlgump 305 365 150 18 0 0 <htmlFormat DO NOT MARK>
	dhtmlgump 305 365 150 18 0 0 <htmlFormat PROMPT FOR MARK>
	ENDDO
ENDIF
IF (<DEF.skmenu_tinkering_flags>&02)
	button 15 342 4005 4007 1 0 <buttonID 6 1>
	dhtmlgump 50 345 150 18 0 0 <htmlFormat SMELT ITEM>
ENDIF
IF (<DEF.skmenu_tinkering_flags>&04)
	button 270 342 4005 4007 1 0 <buttonID 6 5>
	dhtmlgump 305 345 150 18 0 0 <htmlFormat REPAIR ITEM>
ENDIF
IF (<DEF.skmenu_tinkering_flags>&08)
	button 270 382 4005 4007 1 0 <buttonId 6 8>
	dhtmlgump 305 385 150 18 0 0 <htmlFormat ENHANCE ITEM>
ENDIF
IF !(<ISEMPTY <SRC.CTAG.SKMENU_tinkering_NOTICE>>)
	dhtmlgump 170 295 350 40 0 0 <htmlFormat <SRC.CTAG.SKMENU_tinkering_NOTICE>>
	SRC.CTAG.SKMENU_tinkering_NOTICE
ENDIF
//colored logs
IF (<DEF0.skmenu_tinkering_subres_0>)
	button 15 362 4005 4007 1 0 <buttonId 6 0>
	dhtmlgump 50 365 250 18 0 0 <htmlFormat <SERV.ITEMDEF.<SRC.CTAG.SKMENU_SUBRES>.NAME>>
ENDIF

f_skmenu_tinkering_categories
IF (0<ARGS>==1)
	f_skmenu_tinkering_subres
ELSE
	f_skmenu_tinkering_items <SRC.CTAG0.SKMENU_tinkering_LASTCATEGORY>
ENDIF

[DIALOG d_skillmenu_tinkering BUTTON]
ON=1 0ffffffff
ARGN1 -= 1
LOCAL.TYPE=<ARGN1>%8
LOCAL.INDEX=<ARGN1>/8
IF (<LOCAL.TYPE>==0) // show group
	SRC.CTAG0.SKMENU_tinkering_LASTCATEGORY=<LOCAL.INDEX>
	DIALOG d_skillmenu_tinkering
ELSEIF (<LOCAL.TYPE>==1) // create item
	f_skmenu_tinkering_makeitem <DEF.skmenu_tinkering_<SRC.dCTAG0.SKMENU_tinkering_LASTCATEGORY>_<dLOCAL.INDEX>>
ELSEIF (<LOCAL.TYPE>==2) // item details
	DIALOG d_skillmenu_tinkering_details 0, <LOCAL.INDEX>
ELSEIF (<LOCAL.TYPE>==3) // create item (last ten)
	f_skmenu_tinkering_makeitem <SRC.CTAG0.SKMENU_tinkering_LASTTEN_<dLOCAL.INDEX>>
ELSEIF (<LOCAL.TYPE>==4) // item details (last ten) [this is redundant]
	DIALOG d_skillmenu_tinkering_details 0, <LOCAL.INDEX>
ELSEIF (<LOCAL.TYPE>==5) //resource selected
	IF (<DEF.skmenu_tinkering_subres_<dLOCAL.INDEX>>)
		LOCAL.NUMBER = <DEF.SKMENU_SUBRES_<DEF.skmenu_tinkering_subres_<dLOCAL.INDEX>>_SKILL>
		IF (<LOCAL.NUMBER> > <SRC.tinkering>)
			SRC.CTAG.SKMENU_tinkering_NOTICE You cannot work this strange and unusual material.
			DIALOG d_skillmenu_tinkering 0, 1
			RETURN 1
		ENDIF
		SRC.CTAG0.SKMENU_SUBRES=<DEF.skmenu_tinkering_subres_<dLOCAL.INDEX>>
		SRC.CTAG0.SKMENU_SUBRES_SKILL=<LOCAL.NUMBER>
		SRC.CTAG0.SKMENU_SUBRES_BASE=<DEF.skmenu_tinkering_subres_0>
		SRC.CTAG.SKMENU_tinkering_NOTICE Material: <serv.itemdef.<SRC.CTAG0.SKMENU_SUBRES>.name>
		DIALOG d_skillmenu_tinkering
	ENDIF
ELSEIF (<LOCAL.TYPE>==6) //misc
	IF (<LOCAL.INDEX>==0) //resource selection
		IF (<DEF0.skmenu_tinkering_subres_0>)
			DIALOG d_skillmenu_tinkering 0, 1
		ENDIF
	ELSEIF (<LOCAL.INDEX>==1) //resmelt
		IF (<DEF0.skmenu_tinkering_flags>&02)
			f_skmenu_tinkering_smelt
		ENDIF
	ELSEIF (<LOCAL.INDEX>==2) //make last
		IF (<SRC.CTAG0.SKMENU_tinkering_LASTTEN_0>)
			f_skmenu_tinkering_makeitem <SRC.CTAG0.SKMENU_tinkering_LASTTEN_0>
		ELSE
			SRC.CTAG.SKMENU_tinkering_NOTICE You haven't made anything yet.
			DIALOG d_skillmenu_tinkering
		ENDIF
	ELSEIF (<LOCAL.INDEX>==3) //last 10 items made
		SRC.CTAG0.SKMENU_tinkering_LASTCATEGORY=-1
		DIALOG d_skillmenu_tinkering
	ELSEIF (<LOCAL.INDEX>==4) // Toggle use resource hue
		IF (<DEF0.skmenu_tinkering_flags>&010)
		SRC.CTAG0.SKMENU_tinkering_DONOTCOLOR ^= 1
		DIALOG d_skillmenu_tinkering 0, 1
		ENDIF
	ELSEIF (<LOCAL.INDEX>==5) //repair
		IF (<DEF0.skmenu_tinkering_flags>&04)
			SRC.RepairItem
		ENDIF
	ELSEIF (<LOCAL.INDEX>==6) //toggle mark option
		IF (<DEF0.skmenu_tinkering_flags>&01)
			DOSWITCH <SRC.CTAG0.SKMENU_tinkering_MARKOPTION>
			SRC.CTAG.SKMENU_tinkering_MARKOPTION = 1
			SRC.CTAG.SKMENU_tinkering_MARKOPTION = 2
			SRC.CTAG.SKMENU_tinkering_MARKOPTION = 0
			ENDDO
		ENDIF
		DIALOG d_skillmenu_tinkering
	ELSEIF (<LOCAL.INDEX>==8) //enhance
		IF (<DEF0.skmenu_tinkering_flags>&08)
			f_skmenu_tinkering_enhance
		ENDIF
	ELSEIF (<LOCAL.INDEX>==9) // Toggle use resource hue (subres2 page)
		IF (<DEF0.skmenu_tinkering_flags>&010)
		SRC.CTAG0.SKMENU_tinkering_DONOTCOLOR ^= 1
		DIALOG d_skillmenu_tinkering 0, 2
		ENDIF
	ENDIF
ENDIF

[FUNCTION f_skmenu_tinkering_categories]
button 15 60 4005 4007 1 0 <buttonId 6 3>
dhtmlgump 50 63 150 18 0 0 <htmlFormat LAST TEN>
WHILE (<DEF0.skmenu_tinkering_<dLOCAL.I>_0>)
	button 15 <EVAL 80+(<LOCAL.I>*20)> 4005 4007 1 0 <buttonId 0 <LOCAL.I>>
	dhtmlgump 50 <EVAL 83+(<LOCAL.I>*20)> 150 18 0 0 <htmlFormat <DEF.skmenu_tinkering_<dLOCAL.I>_name>>
	LOCAL.I += 1
ENDWHILE

[FUNCTION f_skmenu_tinkering_items] //argn1=selected category
IF (<ARGN1>==-1)
	WHILE (<SRC.CTAG0.SKMENU_tinkering_LASTTEN_<dLOCAL.I>>)
		LOCAL.INDEX=<LOCAL.I>%10
		LOCAL.ITEM=<SRC.CTAG0.SKMENU_tinkering_LASTTEN_<dLOCAL.I>>
		IF (<LOCAL.INDEX>==0)
			IF (<LOCAL.I>>0)
				button 370 260 4005 4007 0 <EVAL (<LOCAL.I>/10)+1> 0
				dhtmlgump 405 263 100 18 0 0 <htmlFormat NEXT PAGE>
			ENDIF
			page <EVAL (<LOCAL.I>/10)+1>
			IF (<LOCAL.I>>0)
				button 220 260 4014 4016 0 <EVAL (<LOCAL.I>/10)> 0
				dhtmlgump 255 263 100 18 0 0 <htmlFormat PREV PAGE>
			ENDIF
		ENDIF
		button 220 <EVAL 60+(<LOCAL.INDEX>*20)> 4005 4007 1 0 <buttonId 3 <LOCAL.I>>
		dhtmlgump 255 <EVAL 63+(<LOCAL.INDEX>*20)> 220 18 0 0 <htmlFormat <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>
		button 480 <EVAL 60+(<LOCAL.INDEX>*20)> 4011 4012 1 0 <buttonId 4 <LOCAL.I>>
		LOCAL.I += 1
	ENDWHILE
ELSE
	WHILE (<DEF0.skmenu_tinkering_<dARGN1>_<dLOCAL.I>>)
		LOCAL.INDEX=<LOCAL.I>%10
		LOCAL.ITEM=<DEF.skmenu_tinkering_<dARGN1>_<dLOCAL.I>>
		IF (<LOCAL.INDEX>==0)
			IF (<LOCAL.I>>0)
				button 370 260 4005 4007 0 <EVAL (<LOCAL.I>/10)+1> 0
				dhtmlgump 405 263 100 18 0 0 <htmlFormat NEXT PAGE>
			ENDIF
			page <EVAL (<LOCAL.I>/10)+1>
			IF (<LOCAL.I>>0)
				button 220 260 4014 4016 0 <EVAL (<LOCAL.I>/10)> 0
				dhtmlgump 255 263 100 18 0 0 <htmlFormat PREV PAGE>
			ENDIF
		ENDIF
		button 220 <EVAL 60+(<LOCAL.INDEX>*20)> 4005 4007 1 0 <buttonId 1 <LOCAL.I>>
		dhtmlgump 255 <EVAL 63+(<LOCAL.INDEX>*20)> 220 18 0 0 <htmlFormat <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>
		button 480 <EVAL 60+(<LOCAL.INDEX>*20)> 4011 4012 1 0 <buttonId 2 <LOCAL.I>>
		LOCAL.I += 1
	ENDFOR
ENDIF

[FUNCTION f_skmenu_tinkering_subres]
WHILE (<DEF0.skmenu_tinkering_subres_<dLOCAL.I>>)
	LOCAL.INDEX=<LOCAL.I>%10
	IF (<LOCAL.INDEX>==0)
		IF (<LOCAL.I>>0)
			button 485 260 4005 4007 0 <EVAL (<LOCAL.I>/10)+1> 0
		ENDIF
		page <EVAL (<LOCAL.I>/10)+1>
		IF (<LOCAL.I>>0)
			button 455 260 4014 4016 0 <EVAL (<LOCAL.I>/10)> 0
		ENDIF
//		button 220 260 4005 4007 1 0 <buttonID 6 4>
//		dhtmlgump 255 263 200 18 0 0 <htmlFormat <QVAL <SRC.CTAG0.SKMENU_tinkering_DONOTCOLOR>?USE MATERIAL COLOR:DO NOT COLOR>>
	ENDIF
	button 220 <EVAL 60+(<LOCAL.INDEX>*20)> 4005 4007 1 0 <buttonID 5 <LOCAL.I>>
	dhtmlgump 255 <EVAL 63+(<LOCAL.INDEX>*20)> 250 18 0 0 <htmlFormat <SERV.ITEMDEF.<DEF0.skmenu_tinkering_subres_<dLOCAL.I>>.NAME>>
	LOCAL.I += 1
ENDWHILE


[DIALOG d_skillmenu_tinkering_details]
40,40
SRC.DIALOGCLOSE d_skillmenu_tinkering
SRC.DIALOGCLOSE d_skillmenu_tinkering_details
page 0

resizepic 0 0 5054 530 417
gumppictiled 10 10 510 22 2624 
gumppictiled 10 37 150 148 2624
gumppictiled 165 37 355 90 2624
gumppictiled 10 190 155 22 2624
gumppictiled 10 217 150 53 2624
gumppictiled 165 132 355 80 2624
gumppictiled 10 275 155 22 2624
gumppictiled 10 302 150 53 2624
gumppictiled 165 217 355 80 2624
gumppictiled 10 360 155 22 2624
gumppictiled 165 302 355 80 2624
gumppictiled 10 387 510 22 2624
checkertrans 10 10 510 399

dhtmlgump 10 12 510 20 0 0 <htmlFormatTitle tinkering MENU>
dhtmlgump 170 40 150 20 0 0 <htmlFormatTitle ITEM>
dhtmlgump 10 192 150 22 0 0 <htmlFormatTitle SKILLS>
dhtmlgump 10 277 150 22 0 0 <htmlFormatTitle MATERIALS>
dhtmlgump 10 362 150 22 0 0 <htmlFormatTitle OTHER>

button 15 387 4014 4016 1 0 0
dhtmlgump 50 390 150 18 0 0 <htmlFormat BACK>

button 270 387 4005 4007 1 0 <EVAL <ARGS>+1>
dhtmlgump 305 390 150 18 0 0 <htmlFormat MAKE NOW>
IF (<SRC.CTAG0.SKMENU_tinkering_LASTCATEGORY>==-1) // pick item from "last ten" list
	LOCAL.ITEM=<SRC.CTAG.SKMENU_tinkering_LASTTEN_<dARGS>>
ELSE
	LOCAL.ITEM=<DEF0.skmenu_tinkering_<SRC.dCTAG0.SKMENU_tinkering_LASTCATEGORY>_<dARGS>>
ENDIF
dhtmlgump 330 40 180 18 0 0 <htmlFormat <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>
dorigin - 282 
IF (<SERV.ITEMDEF.<LOCAL.ITEM>.CAN>&CAN_I_REPLICATE)
	dhtmlgump 170 *20 310 18 <htmlFormat Make as many as possible at once>
ENDIF

tilepic 20 50 <EVAL ID(<SERV.ITEMDEF.<LOCAL.ITEM>.DISPID>)>
dhtmlgump 170 *20 310 18 0 0 <HtmlFormat This item may hold it's maker mark>

f_skmenu_details_drawskills <LOCAL.ITEM>
f_skmenu_details_drawresources <LOCAL.ITEM>

[DIALOG d_skillmenu_tinkering_details BUTTON]
ON=0 //back
DIALOG d_skillmenu_tinkering

ON=1 0ffffffff //make
ARGN1 -= 1
IF (<SRC.CTAG0.SKMENU_tinkering_LASTCATEGORY>==-1) // pick item from "last ten" list
	LOCAL.ITEM=<SRC.CTAG.SKMENU_tinkering_LASTTEN_<dARGN1>>
ELSE
	LOCAL.ITEM=<DEF0.skmenu_tinkering_<SRC.dCTAG0.SKMENU_tinkering_LASTCATEGORY>_<dARGN1>>
ENDIF
f_skmenu_tinkering_makeitem <LOCAL.ITEM>



[DIALOG d_skillmenu_tinkering_querymakersmark]
40,40
resizepic 0 0 5054 220 170
resizepic 10 10 3000 200 150
dhtmlgump 20 20 180 80 0 0 <HtmlFormat Do you wish to place your maker's mark on this item?>
dhtmlgump 55 100 140 25 0 0 <HtmlFormat CONTINUE>
button 20 100 4005 4007 1 0 1
dhtmlgump 55 125 140 25 0 0 <HtmlFormat CANCEL>
button 20 125 4005 4007 1 0 0

[DIALOG d_skillmenu_tinkering_querymakersmark BUTTON]
ON=0
SRC.SYSMESSAGE Cancelled mark.
IF (<UID.<SRC.CTAG0.SKMENU_tinkering_TOOL>>) //make sure the tool still exists
	SRC.CTAG0.SKMENU_tinkering_NOTICE=You create an exceptional quality item
	TRY UID.<SRC.CTAG0.SKMENU_tinkering_TOOL>.DIALOG d_skillmenu_tinkering
ENDIF

ON=1
SRC.SYSMESSAGE You mark the item.
TAG.CRAFTEDBY <SRC.UID>
RESENDTOOLTIP
IF (<UID.<SRC.CTAG0.SKMENU_tinkering_TOOL>>) //make sure the tool still exists
	SRC.CTAG0.SKMENU_tinkering_NOTICE=You create an exceptional quality item and affix your maker's mark.
	TRY UID.<SRC.CTAG0.SKMENU_tinkering_TOOL>.DIALOG d_skillmenu_tinkering
ENDIF

[EOF]