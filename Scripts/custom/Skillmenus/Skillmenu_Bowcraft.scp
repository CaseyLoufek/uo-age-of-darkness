[ITEMDEF 01bd4]
//Shaft listed as "shafts"
//DEFNAME=i_ARROW_SHAFTS
DEFNAME=i_arrow_shaft
TYPE=T_SHAFT
RESOURCES=i_board
REPLICATE=1
SKILLMAKE=1.8 BOWCRAFT
WEIGHT=.1
VALUE=3
CATEGORY=Items by Professions
SUBSECTION=Archers/Bowyers
DESCRIPTION=Shaft
DUPELIST=01bd5,01bd6

ON=@dclick
	return 1

[TYPEDEF T_feather]

ON=@dclick
	return 1


[ITEMDEF 0f3f]
//Arrow
DEFNAME=i_arrow
NAME=arrow%s
TYPE=T_WEAPON_ARROW
FLIP=1
RESOURCES=1 i_arrow_shaft, 1 i_feather
REPLICATE=1
SKILLMAKE=11.3 BOWCRAFT
WEIGHT=0.1
VALUE=5
DUPELIST=0f40,0f41,0f42
CATEGORY=Items by Professions
SUBSECTION=Archers/Bowyers
DESCRIPTION=Arrow

ON=@DCLICK
SRC.MESSAGE Target a bow to use these arrows with it...
TARGET
RETURN 1

ON=@TARGON_ITEM
IF (<SRC.TARG.TYPE>==T_weapon_bow)
	IF !(<SRC.TARG.TOPOBJ.UID>==<SRC.UID>)
		SRC.SYSMESSAGE "You must have the bow in your backpack."
		RETURN 1 
	ENDIF
	SRC.TARG.BOUNCE
	SRC.TARG.EVENTS -e_poison_arrow
	SRC.TARG.EVENTS -e_fire_arrow
	SRC.TARG.EVENTS -e_sting_arrow
	SRC.TARG.TAG0.OVERRIDE.AMMOTYPE=
	SRC.TARG.TAG0.OVERRIDE.AMMOANIM=
	SRC.TARG.TAG0.OVERRIDE.AMMOANIMHUE=
	SRC.SYSMESSAGE You ready normal arrows for use with the bow.
	RETURN 1
ENDIF

[ITEMDEF 01bfb]
// Crossbow Bolt
//DEFNAME2=BOLTS
DEFNAME=i_xbolt
TYPE=T_WEAPON_BOLT
FLIP=1
REPLICATE=1
VALUE=5
RESOURCES=1 i_arrow_shaft, 1 i_feather
SKILLMAKE=12.8 BOWCRAFT
WEIGHT=0.1
CATEGORY=Items by Professions
SUBSECTION=Archers/Bowyers
DESCRIPTION=Crossbow Bolt
DUPELIST=01bfc,01bfd,01bfe

ON=@DCLICK
SRC.MESSAGE Target a crossbow to use these bolts with it...
TARGET
RETURN 1

ON=@TARGON_ITEM
IF (<SRC.TARG.TYPE>==T_weapon_xbow)
	IF !(<SRC.TARG.TOPOBJ.UID>==<SRC.UID>)
		SRC.SYSMESSAGE "You must have the crossbow in your backpack."
		RETURN 1 
	ENDIF
	SRC.TARG.BOUNCE
	SRC.TARG.EVENTS -e_ice_bolt
	SRC.TARG.EVENTS -e_thunder_bolt
	SRC.TARG.TAG0.OVERRIDE.AMMOTYPE=
	SRC.TARG.TAG0.OVERRIDE.AMMOANIM=
	SRC.TARG.TAG.OVERRIDE.AMMOANIMHUE=
	SRC.SYSMESSAGE You ready normal bolts for use with the crossbow.
	RETURN 1
ENDIF

[ITEMDEF i_fletching_tools]
ID=i_arrow_fletching
RESOURCES=4 i_FEATHER, 4 i_board
SKILLMAKE=TINKERING 10.1
VALUE=43
FLIP=1
WEIGHT=.1
CATEGORY=Items by Professions
SUBSECTION=Archers/Bowyers
DESCRIPTION=Arrow Fletching Equip
DUPELIST=01023
TEVENTS=T_PROPRIEDADES

ON=@CREATE
	TAG.HITS=50
	TAG.MAXHITS=50

ON=@DCLICK
IF (<SRC.ACTION>!=-1) || (<SRC.CTAG0.CRAFTING>)
	SRC.SYSMESSAGE You must wait to perform another action.
	RETURN 1
ENDIF
IF !(<f_skmenu_Bowcraft_cancraft>)
	RETURN 1
ENDIF
DIALOG d_skillmenu_bowcraft
RETURN 1


	
on=@destroy

serv.log I have been removed from the server.

[DEFNAME def_skmenu_Bowcraft]
skmenu_Bowcraft_flags 01|04 //01=markoption 02=resmelt 04=repair 08=enhance
skmenu_Bowcraft_subres_0 i_board
skmenu_Bowcraft_subres_1 i_board_willow
skmenu_Bowcraft_subres_2 i_board_cyprus
skmenu_Bowcraft_subres_3 i_board_cedar
skmenu_Bowcraft_subres_4 i_board_oak
skmenu_Bowcraft_subres_5 i_board_yew
skmenu_Bowcraft_subres_6 i_board_walnut
skmenu_Bowcraft_subres_7 i_board_mahogany
skmenu_Bowcraft_subres_8 i_board_heartwood
skmenu_Bowcraft_subres_9 i_board_bloodwood
skmenu_Bowcraft_subres_10 i_board_frostwood
skmenu_Bowcraft_subres_11 i_board_sunwood

skmenu_Bowcraft_0_name BOWS
skmenu_Bowcraft_0_0 i_bow
skmenu_Bowcraft_0_1 i_bow_composite
skmenu_Bowcraft_0_2 i_elven_composite_longbow
skmenu_Bowcraft_0_3 i_magical_shortbow
skmenu_Bowcraft_0_4 i_yumi

skmenu_Bowcraft_1_name CROSSBOWS
skmenu_Bowcraft_1_0 i_crossbow
skmenu_Bowcraft_1_1 i_crossbow_heavy
skmenu_Bowcraft_1_2 i_crossbow_repeating

skmenu_Bowcraft_2_name OTHER
skmenu_Bowcraft_2_0 i_fukiya

skmenu_Bowcraft_3_name AMMUNITION
skmenu_Bowcraft_3_0 i_arrow_shaft
skmenu_Bowcraft_3_1 i_arrow
skmenu_Bowcraft_3_2 i_xbolt



//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//MAIN FUNCTIONS
//Here there are the "core" functions that control the crafting procedure. 
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

[FUNCTION f_skmenu_Bowcraft_cancraft]
//this must be edited out for different skillmenus (because, for example, bowcraft doesn't need anvil & forge)
IF (<TAG0.HITS><=0)
	SRC.Ctag0.SKMENU_Bowcraft_NOTICE You have worn out your tool!
	RETURN 0
ELSEIF (<CONT.TOPOBJ.UID>!=<SRC.UID>)
	SRC.Ctag0.SKMENU_Bowcraft_NOTICE The tool must be on your person to use.
	RETURN 0
ENDIF
RETURN 1

//This is the main craft function
[FUNCTION f_skmenu_bowcraft_makeitem]
//preliminary checks
IF (<SRC.ACTION>!=-1) || (<SRC.CTAG0.CRAFTING>)
	SRC.SYSMESSAGE You must wait to perform another action
	RETURN 1
ENDIF
IF !(<f_skmenu_bowcraft_cancraft>)
	DIALOG d_skillmenu_bowcraft
	RETURN 1
ELSEIF !(<SRC.CANMAKESKILL <ARGS>>)
	SRC.Ctag0.SKMENU_bowcraft_NOTICE You don't have the required skills to attempt this item.
	DIALOG d_skillmenu_bowcraft
	RETURN 1
ENDIF

//check resources
	IF (<SRC.Ctag0.SKMENU_SUBRES>)
		LOCAL.RESOURCES=<f_resource_replace <ARGS>,<SRC.CTAG0.SKMENU_SUBRES_BASE>,<SRC.CTAG0.SKMENU_SUBRES>>
	ELSE
		LOCAL.RESOURCES=<SERV.ITEMDEF.<ARGS>.RESOURCES>
	ENDIF

IF !(<SRC.RESTEST <LOCAL.RESOURCES>>)
	SRC.Ctag0.SKMENU_bowcraft_NOTICE You don't have the resources required to make that item.
	DIALOG d_skillmenu_bowcraft
	RETURN 1
ENDIF
SRC.CONSUME <LOCAL.RESOURCES>

//update the "last ten" list
IF (<ARGS>!=<SRC.Ctag0.SKMENU_bowcraft_LASTTEN_0>) 
	FOR 9 1
		TRY SRC.Ctag0.SKMENU_bowcraft_LASTTEN_<dLOCAL._FOR> = <SRC.Ctag0.SKMENU_bowcraft_LASTTEN_<EVAL <LOCAL._FOR>-1>>
	ENDFOR
	SRC.Ctag0.SKMENU_bowcraft_LASTTEN_0 <ARGS>
ENDIF

//start crafting
SERV.NEWITEM i_bowcraft_craft
NEW.CONT=<SRC.UID>
NEW.TAG.SUBRES=<SRC.Ctag0.SKMENU_SUBRES>
SRC.CTAG0.CRAFTING=1
NEW.LINK=<UID> //linked to the tool
NEW.TIMER=1
NEW.tag0.ITEM=<ARGS>
NEW.TAG.SUBRESUSED=<TAG.SUBRESUSED>
NEW.TAG.RESOURCES=<LOCAL.RESOURCES>
SRC.ANIM 11
SRC.EVENTS +e_bowcraft_crafting

//This is the memory item used for the crafting
[ITEMDEF i_bowcraft_craft]
ID=02007
NAME=bowcraft craft memory
TYPE=t_eq_script

ON=@CREATE
MOREX={2 4}

ON=@TIMER	
CONT.SFX 05ab
CONT.SALUTE
MOREX -= 1
IF (<MOREX>)
	TIMER 1
	RETURN 1
ENDIF

IF (<SERV.ITEMDEF.<TAG0.ITEM>.CAN>&CAN_I_REPLICATE)

LOCAL.FIRSTFREE=0
WHILE ((<CONT.RESTEST <TAG.RESOURCES>>) || (<LOCAL.FIRSTFREE>==0))//&& (<LINK.TAG0.HITS><=0)
	IF (<LOCAL.FIRSTFREE>==0)
		LOCAL.FIRSTFREE=1
	ELSE
		CONT.CONSUME <TAG.RESOURCES>
	ENDIF
	LOCAL.CHANCE=<CONT.f_skmenu_successchance <tag0.ITEM>, <LINK.UID>>
	IF (RAND(1000)<<LOCAL.CHANCE>) //success! we can create the item
		SERV.NEWITEM <tag0.ITEM> 
		LOCAL.EXCEP=<CONT.f_skmenu_exceptionalchance <tag0.ITEM>, <LINK.UID>>
		VAR.ARGN1=<LOCAL.EXCEP>
		VAR.ARGN2=<UID> //bowcraft crafting memory link, use to find/change material (tag.subres)
		LINK.TRIGGER=@MakeItem
		LOCAL.EXCEP=<VAR.ARGN1>
		NEW.CONT=<CONT.FINDLAYER.21.UID>
		CONT.SOUND 057
		IF <TAG0.SUBRESUSED>
			TRY NEW.f_material_<TAG.SUBRES>
		ENDIF
		IF (RAND(1000)<<LOCAL.EXCEP>)
			NEW.QUALITY=201
			IF (<CONT.Ctag0.SKMENU_bowcraft_MARKOPTION>==0)
				NEW.tag0.CRAFTEDBY=<CONT.name>
			ELSEIF (<CONT.Ctag0.SKMENU_bowcraft_MARKOPTION>==2)
				CONT.Ctag0.SKMENU_bowcraft_TOOL=<LINK.UID>
				TRYSRC <CONT.UID> NEW.DIALOG d_skillmenu_bowcraft_querymakersmark
				LOCAL.NODIALOG=1
			ENDIF
			IF (<NEW.ISARMOR>) || (<NEW.TYPE>==t_shield) || (<NEW.TYPE>==t_clothing)
				NEW.HITPOINTS += <EVAL (<CONT.ARMSLORE>+<CONT.SKILLMOD Armslore>)/50>+5
				local.one = <eval (<new.armor.hi> * ((<CONT.ARMSLORE>+<CONT.SKILLMOD Armslore>) + 250))>
				local.two = <eval (<local.one> / 5000)>
				new.modar += <dlocal.two>
				new.tag0.modvalue += <EVAL (<CONT.ARMSLORE>+<CONT.SKILLMOD Armslore>)/50>+5
				CONT.SKILLGAIN armslore <eval <SERV.ITEMDEF.<TAG.ITEM>.SKILLMAKE.0.VAL>>>
			ELSEIF (<NEW.ISWEAPON>)	
				NEW.HITPOINTS += <EVAL (<CONT.ARMSLORE>+<CONT.SKILLMOD Armslore>)/50>+5
				NEW.tag0.DAMINCREASE += <EVAL (<CONT.ARMSLORE>+<CONT.SKILLMOD Armslore>)/50>+5
				new.tag0.modvalue += <EVAL (<CONT.ARMSLORE>+<CONT.SKILLMOD Armslore>)/50>+5
				CONT.SKILLGAIN armslore <eval <SERV.ITEMDEF.<TAG.ITEM>.SKILLMAKE.0.VAL>>>
			ENDIF
			NEW.NAME=exceptional <NEW.NAME>
		ELSE
			NEW.QUALITY=<EVAL (<LOCAL.CHANCE>+<LOCAL.EXCEP>)/10> //arbitrary formula for quality calculation
		ENDIF
		local.exp = (<eval (rand(<NEW.VALUE>+<NEW.tag0.MODVALUE>)+rand(<NEW.VALUE>+<NEW.tag0.MODVALUE>))/4>)
		cont.Trigger @ExpChange,<def.TAT_AS_ARGN>,<local.exp>,1,1
		NEW.RESENDTOOLTIP
		IF (<LOCAL.CHANCE><1000) //gain skill only if the crafting was not trivial
			CONT.SKILLGAIN bowcraft <eval <SERV.ITEMDEF.<TAG.ITEM>.SKILLMAKE.0.VAL>>>
		ENDIF
	ELSE
		IF <cont.skillcheck LUMBERJACKING <eval <SERV.ITEMDEF.<TAG.ITEM>.SKILLMAKE.0.VAL>>>
			LOCAL.I=1
			WHILE (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL> > 0)
				IF (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>==<CONT.CTAG0.SKMENU_SUBRES_BASE>)
					NEWITEM <CONT.CTAG0.SKMENU_SUBRES>
					NEW.AMOUNT=(<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL>/2)
					IF !(<NEW.AMOUNT>==0)
						trysrc <cont> new.bounce
					ELSE
						NEW.REMOVE
					ENDIF
				ELSE
					NEWITEM <SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>
					NEW.AMOUNT=(<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL>/2)
					IF !(<NEW.AMOUNT>==0)
						trysrc <cont> new.bounce
					ELSE
						NEW.REMOVE
					ENDIF
				ENDIF
				LOCAL.I += 1
			ENDWHILE
		ELSE
			LOCAL.I=1
			WHILE (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL> > 0)
				IF (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>==<CONT.CTAG0.SKMENU_SUBRES_BASE>)
					NEWITEM <CONT.CTAG0.SKMENU_SUBRES>
					NEW.AMOUNT=(<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL>/3)
					IF !(<NEW.AMOUNT>==0)
						trysrc <cont> new.bounce
					ELSE
						NEW.REMOVE
					ENDIF
				ELSE
					NEWITEM <SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>
					NEW.AMOUNT=(<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL>/3)
					IF !(<NEW.AMOUNT>==0)
						trysrc <cont> new.bounce
					ELSE
						NEW.REMOVE
					ENDIF
				ENDIF
				LOCAL.I += 1
			ENDWHILE
		ENDIF

		IF (<CONT.bowcraft><30.0) //skill gain on failure only under 30.0 bowcraft
			CONT.SKILLGAIN bowcraft 1
		ENDIF
	ENDIF
ENDWHILE
LINK.TAG0.HITS -= 1 //damage the tool
IF (<LINK.TAG0.HITS><=0)
	LINK.REMOVE
	CONT.SYSMESSAGE You have worn out your tool!
	CONT.SYSMESSAGE You have created as many of this item as your resources allow.
	LOCAL.NODIALOG=1
ELSE
	LINK.RESENDTOOLTIP
	CONT.Ctag0.SKMENU_bowcraft_NOTICE You have created as many of this item as your resources allow.
ENDIF

ELSE

LOCAL.CHANCE=<CONT.f_skmenu_successchance <tag0.ITEM>, <LINK.UID>>
IF (RAND(1000)<<LOCAL.CHANCE>) //success! we can create the item
	SERV.NEWITEM <tag0.ITEM> 
	LOCAL.EXCEP=<CONT.f_skmenu_exceptionalchance <tag0.ITEM>, <LINK.UID>>
	VAR.ARGN1=<LOCAL.EXCEP>
	VAR.ARGN2=<UID> //bowcraft crafting memory link, use to find/change material (tag.subres)
	LINK.TRIGGER=@MakeItem
	LOCAL.EXCEP=<VAR.ARGN1>
	NEW.CONT=<CONT.FINDLAYER.21.UID>
	CONT.SOUND 057
	IF <TAG0.SUBRESUSED>
		TRY NEW.f_material_<TAG.SUBRES>
	ENDIF
	IF (RAND(1000)<<LOCAL.EXCEP>)
		NEW.QUALITY=201
		IF (<CONT.Ctag0.SKMENU_bowcraft_MARKOPTION>==0)
			NEW.tag0.CRAFTEDBY=<CONT.name>
		ELSEIF (<CONT.Ctag0.SKMENU_bowcraft_MARKOPTION>==2)
			CONT.Ctag0.SKMENU_bowcraft_TOOL=<LINK.UID>
			TRYSRC <CONT.UID> NEW.DIALOG d_skillmenu_bowcraft_querymakersmark
			LOCAL.NODIALOG=1
		ENDIF
		NEW.HITPOINTS += <eval ((<new.hitpoints>) * (<cont.armslore>+<cont.skillmod armslore> + 250))/4000>
		new.tag0.modvalue += <EVAL ((<CONT.ARMSLORE>+<cont.skillmod armslore>)*<NEW.VALUE>)/400>
		IF (<NEW.ISARMOR>) || (<NEW.TYPE>==t_shield)
			new.modar += <eval ((<new.armor.hi>+<new.modar>) * (<cont.armslore>+<cont.skillmod armslore> + 250))/4000>
			new.resfire += <eval ((<new.resfire>) * (<cont.armslore>+<cont.skillmod armslore> + 250))/4000>
			new.rescold += <eval ((<new.rescold>) * (<cont.armslore>+<cont.skillmod armslore> + 250))/4000>
			new.respoison += <eval ((<new.respoison>) * (<cont.armslore>+<cont.skillmod armslore> + 250))/4000>
			new.resenergy += <eval ((<new.resenergy>) * (<cont.armslore>+<cont.skillmod armslore> + 250))/4000>
			CONT.SKILLGAIN armslore <eval <SERV.ITEMDEF.<TAG.ITEM>.SKILLMAKE.0.VAL>>>
		ELSEIF (<NEW.ISWEAPON>)	
			NEW.tag0.DAMINCREASE += <EVAL (<CONT.ARMSLORE>+<cont.skillmod armslore>+250)/40>
			CONT.SKILLGAIN armslore <eval <SERV.ITEMDEF.<TAG.ITEM>.SKILLMAKE.0.VAL>>>
		ENDIF
		NEW.NAME=exceptional <NEW.NAME>
	ELSE
		NEW.QUALITY=<EVAL (<LOCAL.CHANCE>+<LOCAL.EXCEP>)/10> //arbitrary formula for quality calculation
	ENDIF
	var.exp = (<eval rand(<NEW.VALUE>+<NEW.tag0.MODVALUE>)/((<cont.level>+1)/2)>)
	expeval <eval <var.exp>>
	cont.sysmessage You have gained <var.expmsg> experience.
	CONT.EXP += <eval <var.exp>>
	NEW.RESENDTOOLTIP
	LINK.TAG0.HITS -= 1 //damage the tool
	IF (<LINK.TAG0.HITS><=0)
		LINK.REMOVE
		CONT.SYSMESSAGE You have worn out your tool!
		IF (<NEW.QUALITY>>200) && !(<CONT.Ctag0.SKMENU_bowcraft_MARKOPTION>)
			CONT.SYSMESSAGE You create an exceptional quality item and affix your maker's mark.
		ELSEIF (<NEW.QUALITY>>200)
			CONT.SYSMESSAGE You create an exceptional quality item.
		ELSE
			CONT.SYSMESSAGE You create the item.
		ENDIF
		LOCAL.NODIALOG=1
	ELSE
		LINK.RESENDTOOLTIP
		IF (<NEW.QUALITY>>200) && !(<CONT.Ctag0.SKMENU_bowcraft_MARKOPTION>)
			CONT.Ctag0.SKMENU_bowcraft_NOTICE You create an exceptional quality item and affix your maker's mark.
		ELSEIF (<NEW.QUALITY>>200)
			CONT.Ctag0.SKMENU_bowcraft_NOTICE You create an exceptional quality item.
		ELSE
			CONT.Ctag0.SKMENU_bowcraft_NOTICE You create the item.
		ENDIF
	ENDIF
	IF (<LOCAL.CHANCE><1000) //gain skill only if the crafting was not trivial
		CONT.SKILLGAIN bowcraft <eval <SERV.ITEMDEF.<TAG.ITEM>.SKILLMAKE.0.VAL>>>
	ENDIF
ELSE
	LINK.TAG0.HITS -= 1
	IF (<LINK.TAG0.HITS><=0)
		LINK.REMOVE
		CONT.SYSMESSAGE You have worn out your tool!
		CONT.SYSMESSAGE You failed to create the item, and some of your materials are lost.
		LOCAL.NODIALOG=1
	ELSE
		LINK.RESENDTOOLTIP
		CONT.Ctag0.SKMENU_bowcraft_NOTICE You failed to create the item, and some of your materials are lost. //actually _all_ materials are lost, since they are consumed when the crafting starts
	ENDIF

	IF <cont.skillcheck LUMBERJACKING <eval <SERV.ITEMDEF.<TAG.ITEM>.SKILLMAKE.0.VAL>>>
		LOCAL.I=1
		WHILE (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL> > 0)
			IF (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>==<CONT.CTAG0.SKMENU_SUBRES_BASE>)
				NEWITEM <CONT.CTAG0.SKMENU_SUBRES>
				NEW.AMOUNT=(<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL>/2)
					IF !(<NEW.AMOUNT>==0)
						trysrc <cont> new.bounce
					ELSE
						NEW.REMOVE
					ENDIF
			ELSE
				NEWITEM <SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>
				NEW.AMOUNT=(<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL>/2)
					IF !(<NEW.AMOUNT>==0)
						trysrc <cont> new.bounce
					ELSE
						NEW.REMOVE
					ENDIF
			ENDIF
			LOCAL.I += 1
		ENDWHILE
	ELSE
		LOCAL.I=1
		WHILE (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL> > 0)
			IF (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>==<CONT.CTAG0.SKMENU_SUBRES_BASE>)
				NEWITEM <CONT.CTAG0.SKMENU_SUBRES>
				NEW.AMOUNT=(<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL>/3)
					IF !(<NEW.AMOUNT>==0)
						trysrc <cont> new.bounce
					ELSE
						NEW.REMOVE
					ENDIF
			ELSE
				NEWITEM <SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>
				NEW.AMOUNT=(<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL>/3)
					IF !(<NEW.AMOUNT>==0)
						trysrc <cont> new.bounce
					ELSE
						NEW.REMOVE
					ENDIF
			ENDIF
			LOCAL.I += 1
		ENDWHILE
	ENDIF

	IF (<CONT.bowcraft><30.0) //skill gain on failure only under 30.0 bowcraft
		CONT.SKILLGAIN bowcraft 1
	ENDIF
ENDIF

ENDIF

IF !(<LOCAL.NODIALOG>)
	TRYSRC <CONT.UID> LINK.DIALOG d_skillmenu_bowcraft
ENDIF
CONT.CTAG.CRAFTING=
REMOVE
RETURN 1

ON=@UNEQUIP
SRC.EVENTS -e_bowcraft_crafting

[EVENTS e_bowcraft_crafting]
ON=@SKILLSTART
SRC.SYSMESSAGE You must wait to perform another action
ACTION -1
RETURN 1




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//MENU
//This is the biggest part of the script, the skillmenu. It's organized in subfunctions which draw specific part of the dialog
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

[DIALOG d_skillmenu_bowcraft] //argn1=page
40,40
SRC.DIALOGCLOSE d_skillmenu_bowcraft
page 0

resizepic 0 0 5054 530 437
gumppictiled 10 10 510 22 2624 
gumppictiled 10 292 150 45 2624
gumppictiled 165 292 355 45 2624
gumppictiled 10 342 510 85 2624
gumppictiled 10 37 200 250 2624
gumppictiled 215 37 305 250 2624
checkertrans 10 10 510 417

dhtmlgump 10 12 510 20 0 0 <htmlFormatTitle BOWCRAFT MENU>
dhtmlgump 10 37 200 22 0 0 <htmlFormatTitle CATEGORIES>
dhtmlgump 215 37 305 22 0 0 <htmlFormatTitle SELECTIONS>
dhtmlgump 10 302 150 25 0 0 <htmlFormatTitle NOTICES>

button 15 402 4017 4019 1 0 0
dhtmlgump 50 405 150 18 0 0 <htmlFormat EXIT>

button 270 402 4005 4007 1 0 <buttonID 6 2>
dhtmlgump 305 405 150 18 0 0 <htmlFormat MAKE LAST>

IF (<DEF.skmenu_bowcraft_flags>&01)
	button 270 362 4005 4007 1 0 <buttonID 6 6>
	DOSWITCH (<SRC.Ctag0.SKMENU_bowcraft_MARKOPTION>)
	dhtmlgump 305 365 150 18 0 0 <htmlFormat MARK ITEM>
	dhtmlgump 305 365 150 18 0 0 <htmlFormat DO NOT MARK>
	dhtmlgump 305 365 150 18 0 0 <htmlFormat PROMPT FOR MARK>
	ENDDO
ENDIF
IF (<DEF.skmenu_bowcraft_flags>&02)
	button 15 342 4005 4007 1 0 <buttonID 6 1>
	dhtmlgump 50 345 150 18 0 0 <htmlFormat SMELT ITEM>
ENDIF
IF (<DEF.skmenu_bowcraft_flags>&04)
	button 270 342 4005 4007 1 0 <buttonID 6 5>
	dhtmlgump 305 345 150 18 0 0 <htmlFormat REPAIR ITEM>
ENDIF
IF (<DEF.skmenu_bowcraft_flags>&08)
	button 270 382 4005 4007 1 0 <buttonId 6 8>
	dhtmlgump 305 385 150 18 0 0 <htmlFormat ENHANCE ITEM>
ENDIF
IF !(<ISEMPTY <SRC.Ctag0.SKMENU_bowcraft_NOTICE>>)
	dhtmlgump 170 295 350 40 0 0 <htmlFormat <SRC.Ctag.SKMENU_bowcraft_NOTICE>>
	SRC.Ctag0.SKMENU_bowcraft_NOTICE
ENDIF
//colored ingots
IF (<DEF0.skmenu_bowcraft_subres_0>)
	button 15 362 4005 4007 1 0 <buttonId 6 0>
	dhtmlgump 50 365 250 18 0 0 <htmlFormat <SERV.ITEMDEF.<SRC.Ctag0.SKMENU_SUBRES>.NAME>>
ENDIF
//dragon scales

f_skmenu_bowcraft_categories
IF (0<ARGS>==1)
	f_skmenu_bowcraft_subres
ELSE
	f_skmenu_bowcraft_items <SRC.Ctag0.SKMENU_bowcraft_LASTCATEGORY>
ENDIF

[DIALOG d_skillmenu_bowcraft BUTTON]
ON=1 0ffffffff
ARGN1 -= 1
LOCAL.TYPE=<ARGN1>%8
LOCAL.INDEX=<ARGN1>/8
IF (<LOCAL.TYPE>==0) // show group
	SRC.Ctag0.SKMENU_bowcraft_LASTCATEGORY=<LOCAL.INDEX>
	DIALOG d_skillmenu_bowcraft
ELSEIF (<LOCAL.TYPE>==1) // create item
	f_skmenu_bowcraft_makeitem <DEF.skmenu_bowcraft_<SRC.dCtag0.SKMENU_bowcraft_LASTCATEGORY>_<dLOCAL.INDEX>>
ELSEIF (<LOCAL.TYPE>==2) // item details
	DIALOG d_skillmenu_bowcraft_details 0, <LOCAL.INDEX>
ELSEIF (<LOCAL.TYPE>==3) // create item (last ten)
	f_skmenu_bowcraft_makeitem <SRC.Ctag0.SKMENU_bowcraft_LASTTEN_<dLOCAL.INDEX>>
ELSEIF (<LOCAL.TYPE>==4) // item details (last ten) [this is redundant]
	DIALOG d_skillmenu_bowcraft_details 0, <LOCAL.INDEX>
ELSEIF (<LOCAL.TYPE>==5) //resource selected
	IF (<DEF.skmenu_bowcraft_subres_<dLOCAL.INDEX>>)
		LOCAL.NUMBER = <DEF.SKMENU_SUBRES_<DEF.skmenu_bowcraft_subres_<dLOCAL.INDEX>>_SKILL>
		IF (<LOCAL.NUMBER> > <SRC.bowcraft>)
			SRC.CTAG.SKMENU_bowcraft_NOTICE You cannot work this strange and unusual material.
			DIALOG d_skillmenu_bowcraft 0, 1
			RETURN 1
		ENDIF
		SRC.CTAG0.SKMENU_SUBRES=<DEF.skmenu_bowcraft_subres_<dLOCAL.INDEX>>
		SRC.CTAG0.SKMENU_SUBRES_SKILL=<LOCAL.NUMBER>
		SRC.CTAG0.SKMENU_SUBRES_BASE=<DEF.skmenu_bowcraft_subres_0>
		SRC.CTAG.SKMENU_bowcraft_NOTICE Material: <serv.itemdef.<SRC.CTAG0.SKMENU_SUBRES>.name>
		DIALOG d_skillmenu_bowcraft
	ENDIF
ELSEIF (<LOCAL.TYPE>==6) //misc
	IF (<LOCAL.INDEX>==0) //resource selection
		IF (<DEF0.skmenu_bowcraft_subres_0>)
			DIALOG d_skillmenu_bowcraft 0, 1
		ENDIF
	ELSEIF (<LOCAL.INDEX>==1) //resmelt
		IF (<DEF0.skmenu_bowcraft_flags>&02)
			f_skmenu_bowcraft_smelt
		ENDIF
	ELSEIF (<LOCAL.INDEX>==2) //make last
		IF (<SRC.Ctag0.SKMENU_bowcraft_LASTTEN_0>)
			f_skmenu_bowcraft_makeitem <SRC.Ctag0.SKMENU_bowcraft_LASTTEN_0>
		ELSE
			SRC.Ctag0.SKMENU_bowcraft_NOTICE You haven't made anything yet.
			DIALOG d_skillmenu_bowcraft
		ENDIF
	ELSEIF (<LOCAL.INDEX>==3) //last 10 items made
		SRC.Ctag0.SKMENU_bowcraft_LASTCATEGORY=-1
		DIALOG d_skillmenu_bowcraft
	ELSEIF (<LOCAL.INDEX>==4) // Toggle use resource hue
		SRC.Ctag0.SKMENU_bowcraft_DONOTCOLOR ^= 1
		DIALOG d_skillmenu_bowcraft 0, 1
	ELSEIF (<LOCAL.INDEX>==5) //repair
		IF (<DEF0.skmenu_bowcraft_flags>&04)
			SRC.REPAIRITEM
		ENDIF
	ELSEIF (<LOCAL.INDEX>==6) //toggle mark option
		IF (<DEF0.skmenu_bowcraft_flags>&01)
			DOSWITCH <SRC.Ctag0.SKMENU_bowcraft_MARKOPTION>
			SRC.Ctag0.SKMENU_bowcraft_MARKOPTION = 1
			SRC.Ctag0.SKMENU_bowcraft_MARKOPTION = 2
			SRC.Ctag0.SKMENU_bowcraft_MARKOPTION = 0
			ENDDO
		ENDIF
		DIALOG d_skillmenu_bowcraft
	ELSEIF (<LOCAL.INDEX>==8) //enhance
		IF (<DEF0.skmenu_bowcraft_flags>&08)
			//f_skmenu_bowcraft_enhance
		ENDIF
	ENDIF
ENDIF

[FUNCTION f_skmenu_bowcraft_categories]
button 15 60 4005 4007 1 0 <buttonId 6 3>
dhtmlgump 50 63 150 18 0 0 <htmlFormat LAST TEN>
WHILE (<DEF0.skmenu_bowcraft_<dLOCAL.I>_0>)
	button 15 <EVAL 80+(<LOCAL.I>*20)> 4005 4007 1 0 <buttonId 0 <LOCAL.I>>
	dhtmlgump 50 <EVAL 83+(<LOCAL.I>*20)> 150 18 0 0 <htmlFormat <DEF.skmenu_bowcraft_<dLOCAL.I>_name>>
	LOCAL.I += 1
ENDWHILE

[FUNCTION f_skmenu_bowcraft_items] //argn1=selected category
IF (<ARGN1>==-1)
	WHILE (<SRC.Ctag0.SKMENU_bowcraft_LASTTEN_<dLOCAL.I>>)
		LOCAL.INDEX=<LOCAL.I>%10
		LOCAL.ITEM=<SRC.Ctag0.SKMENU_bowcraft_LASTTEN_<dLOCAL.I>>
		IF (<LOCAL.INDEX>==0)
			IF (<LOCAL.I>>0)
				button 370 260 4005 4007 0 <EVAL (<LOCAL.I>/10)+1> 0
				dhtmlgump 405 263 100 18 0 0 <htmlFormat NEXT PAGE>
			ENDIF
			page <EVAL (<LOCAL.I>/10)+1>
			IF (<LOCAL.I>>0)
				button 220 260 4014 4016 0 <EVAL (<LOCAL.I>/10)> 0
				dhtmlgump 255 263 100 18 0 0 <htmlFormat PREV PAGE>
			ENDIF
		ENDIF
		button 220 <EVAL 60+(<LOCAL.INDEX>*20)> 4005 4007 1 0 <buttonId 3 <LOCAL.I>>
		dhtmlgump 255 <EVAL 63+(<LOCAL.INDEX>*20)> 220 18 0 0 <htmlFormat <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>
		button 480 <EVAL 60+(<LOCAL.INDEX>*20)> 4011 4012 1 0 <buttonId 4 <LOCAL.I>>
		LOCAL.I += 1
	ENDWHILE
ELSE
	WHILE (<DEF0.skmenu_bowcraft_<dARGN1>_<dLOCAL.I>>)
		LOCAL.INDEX=<LOCAL.I>%10
		LOCAL.ITEM=<DEF.skmenu_bowcraft_<dARGN1>_<dLOCAL.I>>
		IF (<LOCAL.INDEX>==0)
			IF (<LOCAL.I>>0)
				button 370 260 4005 4007 0 <EVAL (<LOCAL.I>/10)+1> 0
				dhtmlgump 405 263 100 18 0 0 <htmlFormat NEXT PAGE>
			ENDIF
			page <EVAL (<LOCAL.I>/10)+1>
			IF (<LOCAL.I>>0)
				button 220 260 4014 4016 0 <EVAL (<LOCAL.I>/10)> 0
				dhtmlgump 255 263 100 18 0 0 <htmlFormat PREV PAGE>
			ENDIF
		ENDIF
		button 220 <EVAL 60+(<LOCAL.INDEX>*20)> 4005 4007 1 0 <buttonId 1 <LOCAL.I>>
		dhtmlgump 255 <EVAL 63+(<LOCAL.INDEX>*20)> 220 18 0 0 <htmlFormat <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>
		button 480 <EVAL 60+(<LOCAL.INDEX>*20)> 4011 4012 1 0 <buttonId 2 <LOCAL.I>>
		LOCAL.I += 1
	ENDFOR
ENDIF

[FUNCTION f_skmenu_bowcraft_subres]
WHILE (<DEF0.skmenu_bowcraft_subres_<dLOCAL.I>>)
	LOCAL.INDEX=<LOCAL.I>%10
	IF (<LOCAL.INDEX>==0)
		IF (<LOCAL.I>>0)
			button 485 260 4005 4007 0 <EVAL (<LOCAL.I>/10)+1> 0
		ENDIF
		page <EVAL (<LOCAL.I>/10)+1>
		IF (<LOCAL.I>>0)
			button 455 260 4014 4016 0 <EVAL (<LOCAL.I>/10)> 0
		ENDIF
		button 220 260 4005 4007 1 0 <buttonID 6 4>
		dhtmlgump 255 263 200 18 0 0 <htmlFormat <QVAL <SRC.Ctag0.SKMENU_bowcraft_DONOTCOLOR>?USE MATERIAL COLOR:DO NOT COLOR>>
	ENDIF
	button 220 <EVAL 60+(<LOCAL.INDEX>*20)> 4005 4007 1 0 <buttonID 5 <LOCAL.I>>
	dhtmlgump 255 <EVAL 63+(<LOCAL.INDEX>*20)> 250 18 0 0 <htmlFormat <SERV.ITEMDEF.<DEF0.skmenu_bowcraft_subres_<dLOCAL.I>>.NAME>>
	LOCAL.I += 1
ENDWHILE



[DIALOG d_skillmenu_bowcraft_details]
40,40
SRC.DIALOGCLOSE d_skillmenu_bowcraft
SRC.DIALOGCLOSE d_skillmenu_bowcraft_details
page 0

resizepic 0 0 5054 530 417
gumppictiled 10 10 510 22 2624 
gumppictiled 10 37 150 148 2624
gumppictiled 165 37 355 90 2624
gumppictiled 10 190 155 22 2624
gumppictiled 10 217 150 53 2624
gumppictiled 165 132 355 80 2624
gumppictiled 10 275 155 22 2624
gumppictiled 10 302 150 53 2624
gumppictiled 165 217 355 80 2624
gumppictiled 10 360 155 22 2624
gumppictiled 165 302 355 80 2624
gumppictiled 10 387 510 22 2624
checkertrans 10 10 510 399

dhtmlgump 10 12 510 20 0 0 <htmlFormatTitle BOWCRAFT MENU>
dhtmlgump 170 40 150 20 0 0 <htmlFormatTitle ITEM>
dhtmlgump 10 192 150 22 0 0 <htmlFormatTitle SKILLS>
dhtmlgump 10 277 150 22 0 0 <htmlFormatTitle MATERIALS>
dhtmlgump 10 362 150 22 0 0 <htmlFormatTitle OTHER>

button 15 387 4014 4016 1 0 0
dhtmlgump 50 390 150 18 0 0 <htmlFormat BACK>

button 270 387 4005 4007 1 0 <EVAL <ARGS>+1>
dhtmlgump 305 390 150 18 0 0 <htmlFormat MAKE NOW>
IF (<SRC.Ctag0.SKMENU_bowcraft_LASTCATEGORY>==-1) // pick item from "last ten" list
	LOCAL.ITEM=<SRC.Ctag0.SKMENU_bowcraft_LASTTEN_<dARGS>>
ELSE
	LOCAL.ITEM=<DEF0.skmenu_bowcraft_<SRC.dCtag0.SKMENU_bowcraft_LASTCATEGORY>_<dARGS>>
ENDIF
dhtmlgump 330 40 180 18 0 0 <htmlFormat <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>
dorigin - 282 
IF (<SERV.ITEMDEF.<LOCAL.ITEM>.CAN>&CAN_I_REPLICATE)
	dhtmlgump 170 *20 310 18 <htmlFormat Make as many as possible at once>
ENDIF

tilepic 20 50 <EVAL ID(<SERV.ITEMDEF.<LOCAL.ITEM>.DISPID>)>
dhtmlgump 170 *20 310 18 0 0 <htmlFormat This item may hold it's maker mark>

f_skmenu_details_drawskills <LOCAL.ITEM>
f_skmenu_details_drawresources <LOCAL.ITEM>

[DIALOG d_skillmenu_bowcraft_details BUTTON]
ON=0 //back
DIALOG d_skillmenu_bowcraft

ON=1 0ffffffff //make
ARGN1 -= 1
IF (<SRC.Ctag0.SKMENU_bowcraft_LASTCATEGORY>==-1) // pick item from "last ten" list
	LOCAL.ITEM=<SRC.Ctag0.SKMENU_bowcraft_LASTTEN_<dARGN1>>
ELSE
	LOCAL.ITEM=<DEF0.skmenu_bowcraft_<SRC.dCtag0.SKMENU_bowcraft_LASTCATEGORY>_<dARGN1>>
ENDIF
f_skmenu_bowcraft_makeitem <LOCAL.ITEM>


[DIALOG d_skillmenu_bowcraft_querymakersmark]
40,40
resizepic 0 0 5054 220 170
resizepic 10 10 3000 200 150
dhtmlgump 20 20 180 80 0 0 <HtmlFormat Do you wish to place your maker's mark on this item?>
dhtmlgump 55 100 140 25 0 0 <HtmlFormat CONTINUE>
button 20 100 4005 4007 1 0 1
dhtmlgump 55 125 140 25 0 0 <HtmlFormat CANCEL>
button 20 125 4005 4007 1 0 0

[DIALOG d_skillmenu_bowcraft_querymakersmark BUTTON]
ON=0
SRC.SYSMESSAGE Cancelled mark.
IF (<UID.<SRC.Ctag0.SKMENU_bowcraft_TOOL>>) //make sure the tool still exists
	SRC.Ctag0.SKMENU_bowcraft_NOTICE=You create an exceptional quality item
	TRY UID.<SRC.Ctag0.SKMENU_bowcraft_TOOL>.DIALOG d_skillmenu_bowcraft
ENDIF

ON=1
SRC.SYSMESSAGE You mark the item.
tag0.CRAFTEDBY <SRC.UID>
RESENDTOOLTIP
IF (<UID.<SRC.Ctag0.SKMENU_bowcraft_TOOL>>) //make sure the tool still exists
	SRC.Ctag0.SKMENU_bowcraft_NOTICE=You create an exceptional quality item and affix your maker's mark.
	TRY UID.<SRC.Ctag0.SKMENU_bowcraft_TOOL>.DIALOG d_skillmenu_bowcraft
ENDIF
[EOF]