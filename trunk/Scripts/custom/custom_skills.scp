//****************************************************************************
//SPHERE by : Menasoft ©1997-2006
//www.sphereserver.net
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
//****************************************************************************
// FILE LAST UPDATED: 27-Jul-2006

VERSION=0.56b

// Generic advance rates for stats
[ADVANCE]
STR=10000,4000,600
INT=10000,4000,600
DEX=10000,4000,600


[COMMENT SKILL x]
ADV_RATE=10.0,200.0,800.0 // 10.0 successes at 0% to gain .1, 800.0 successes at 100% to gain .1 skill, 200.0 successes at 50% to gain .1, 
VALUES=0,100	// The amount of value an item created with the skill gets at skill levels.

STAT_STR=80		// You will tend toward these stat vals if you use this skill a lot.
BONUS_STATS=10	// What percent of the skill success is based on stat levels. = 10%
BONUS_STR=80 // % of each stat toward success at skill

[SKILL 0]
DEFNAME=SKILL_ALCHEMY
KEY=Alchemy
TITLE=Alchemist

DELAY=5.0,2.0
EFFECT=0.0,100.0
ADV_RATE=1.7,40.0,100.0
VALUES=1,20,100

BONUS_STATS=50
BONUS_STR=0
BONUS_DEX=20
BONUS_INT=80

STAT_STR=500
STAT_INT=500
STAT_DEX=500


[SKILL 1]
DEFNAME=SKILL_ANATOMY
KEY=Anatomy
TITLE=Scholar
PROMPT_MSG=@,,2 500321
DELAY=3.5,1.0
ADV_RATE=1.0,30.0,75.0


BONUS_STATS=50
BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100

STAT_STR=500
STAT_INT=500
STAT_DEX=500

ON=@Fail
SRC.MESSAGE @,,2 1042915

[SKILL 2]
DEFNAME=SKILL_ANIMAL_LORE
KEY=AnimalLore
TITLE=Scholar
PROMPT_MSG=@,,2 500328
DELAY=3.5,1.0
ADV_RATE=1.0,30.0,75.0

STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=50

ON=@Success
IF (<SRC.TARG.ISMYPET>) || (<SRC.ANIMALLORE> > 999)
	src.dialog d_alore
	return
ENDIF

ON=@Fail
SRC.MESSAGE @,,2 500334

[SKILL 3]
DEFNAME=SKILL_APPRAISE
KEY=ItemID
TITLE=Merchant
PROMPT_MSG=@,,2 500343
DELAY=3.5,1.0
ADV_RATE=1.0,30.0,75.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=50

ON=@Success
	IF !(<SRC.TARG.TAG0.MAGICPOTENCY> > (<SRC.ITEMID>*5))
		SRC.TARG.F_ID
	ELSE
		SRC.SYSMESSAGE You can't begin to understand this item.
	ENDIF
	RETURN 1

ON=@Start
	if (<SRC.TARG.ATTR>&(attr_identified))
		SRC.SYSMESSAGE That item is already identified.
		return 1
	ENDIF

ON=@Fail
SRC.MESSAGE You fail to identify the item.
SKILLGAIN ItemID 30

[SKILL 4]
DEFNAME=SKILL_ARMSLORE
KEY=ArmsLore
TITLE=Arms%s
PROMPT_MSG=@,,2 500906
DELAY=3.5,1.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=50
ADV_RATE=1.0,30.0,75.0

ON=@Success
	SRC.TARG.F_ID
	RETURN 1

ON=@Fail
	SRC.SYSMESSAGE You are uncertain about this item 

ON=@Abort
	SRC.SYSMESSAGE You stop trying to evaluate the item.

[SKILL 5]
DEFNAME=SKILL_Parrying
KEY=Parrying
TITLE=Shieldfighter
ADV_RATE=2.5,50.0,125.0
EFFECT=0.0,90.0

STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=40
BONUS_DEX=60
BONUS_INT=0
BONUS_STATS=50

[SKILL 6]
DEFNAME=SKILL_Begging
KEY=Begging
TITLE=Beggar
PROMPT_MSG=@,,2 500397
DELAY=4.0,2.0
ADV_RATE=1.0,30.0,75.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=0
BONUS_DEX=20
BONUS_INT=80
BONUS_STATS=50

ON=@Start
IF (!(<SRC.TARG.NPC>) && !(<SRC.TARG.TAG0.CHARITY>)) || (<SRC.TARG.RESTEST 1 i_beg_timer>)
	SRC.SYSMESSAGE They seem unwilling to donate at the moment.
	return 1
ENDIF

ON=@Success
FORCONT <src.targ> 10
	IF (<TYPE> == t_food) || (<TYPE> == t_fruit) || (<TYPE> == t_coin) || (<TYPE> == t_note)
		IF ((<VALUE>+<TAG0.MODVALUE>) < (<SRC.BEGGING>/10))
			var.take = <eval ((<SRC.BEGGING>/10) / (<VALUE>+<TAG0.MODVALUE>))>
			var.take = <eval rand(<eval <var.take>>)>+1
			dupe
			IF (<var.take> >= <amount>)
				var.take=<amount>
				remove
				new.amount=<var.take>
	   			new.bounce
			ELSE
				amount=(<amount> - <var.take>)
				new.amount=<var.take>
	   			new.bounce
			ENDIF
			src.targ.sysmessage You give <src.name> <eval <var.take>> <name> out of the kindness of your heart.
			src.sysmessage <src.targ.name> kindly gives you <eval <var.take>> <name>.
			newitem i_beg_timer
			new.LINK=<src.targ>
			trysrc <src.targ> new.equip
			new.timer = 5*60
			var.take=<var.take>*(<VALUE>+<TAG0.MODVALUE>)
			src.targ.karma += <var.take>
			IF (<SRC.TARG.TAG0.CHARITY>)
				F_Virtue_Exp_Gain_Compassion=<var.take>
			ENDIF
			return 1
		ENDIF
	ENDIF
	newitem i_beg_timer
	new.LINK=<src.targ>
	trysrc <src.targ> new.equip
	new.timer = 5*60
ENDFOR
SRC.SYSMESSAGE <src.targ.name> has nothing to give you.
SRC.TARG.SYSMESSAGE <src.name> begs from you but you have nothing to give.

ON=@Fail
	SRC.SYSMESSAGE You fail utterly in your attempt to grovel.
	IF !(<R4>)
		newitem i_beg_timer
		new.LINK=<src.targ>
		trysrc <src.targ> new.equip
		new.timer = 5*60
	ENDIF
	SRC.SKILLGAIN BEGGING 30

ON=@Abort
	SRC.SYSMESSAGE You stop attempting to grovel.



[itemdef i_beg_timer]
ID=i_memory
name=Beg Timer
TYPE=t_eq_script

on=@create
ATTR=attr_invis|attr_decay

on=@equip
timer = <tag.timer>

on=@timer
remove
return 1


[SKILL 7]
DEFNAME=SKILL_Blacksmith
KEY=Blacksmithing
TITLE=Blacksmith
PROMPT_MSG=
DELAY=5.0,2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=70
BONUS_DEX=20
BONUS_INT=10
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0
VALUES=1,20,100


[SKILL 8]
DEFNAME=Skill_Bowcraft
KEY=Bowcraft
TITLE=Bowyer
PROMPT_MSG=
DELAY=5.0,2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=10
BONUS_DEX=70
BONUS_INT=20
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0
VALUES=1,30,200

[SKILL 9]
DEFNAME=SKILL_PEACEMAKING
KEY=Peacemaking
TITLE=Bard
PROMPT_MSG=
DELAY=2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=0
BONUS_DEX=50
BONUS_INT=50
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

ON=@Start
IF (<FINDTYPE.t_musical>)
TARGETF NEW_PEACEMAKING
sysmessageloc -1,1049525
RETURN 1
ENDIF

[SKILL 10]
DEFNAME=Skill_Camping
KEY=Camping
TITLE=Camper
PROMPT_MSG=
DELAY=8.0,1.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=10
BONUS_DEX=30
BONUS_INT=60
BONUS_STATS=50
ADV_RATE=1.0,30.0,75.0

ON=@Fail
	SRC.MESSAGE @,,2 501696

[SKILL 11]
DEFNAME=Skill_Carpentry
KEY=Carpentry
TITLE=Carpenter
PROMPT_MSG=
DELAY=5.0,2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=60
BONUS_DEX=20
BONUS_INT=20
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0
VALUES=1,20,80


[SKILL 12]
DEFNAME=SKILL_CARTOGRAPHY
KEY=Cartography
TITLE=Cartographer
PROMPT_MSG=
DELAY=5.0,2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=0
BONUS_DEX=20
BONUS_INT=80
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0
VALUES=1,20,80


[SKILL 13]
DEFNAME=Skill_Cooking
KEY=Cooking
TITLE=Chef
PROMPT_MSG=What would you like to cook?
DELAY=5.0,2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=0
BONUS_DEX=40
BONUS_INT=60
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0
VALUES=1,20,80

ON=@Fail
	SRC.MESSAGE You ruin the food.



[SKILL 14]
DEFNAME=Skill_DetectHidden
KEY=DetectingHidden
TITLE=Detective
PROMPT_MSG=
DELAY=3.0,2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=20
BONUS_INT=80
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0

ON=@Fail
	SRC.MESSAGE @,,2 500817

ON=@Success
FORCHARS <EVAL (<TRACKING>/100)+16>
	IF (<FLAGS>&statf_hidden)
		IF (<SRC.DetectingHidden> >= <HIDING>)
		FLAGS &= ~statf_hidden
		sysmessage <SRC.name> found you!
		SRC.sysmessage You find <name>.
			If (<detectinghidden> == 0)
			detectinghidden=1
			EndIf
		skillgain DetectingHidden <EVAL <DetectingHidden>/10>
		UPDATE
		ENDIF
	ELIF (<FLAGS>&statf_invisible)
		IF (<SRC.TRACKING> >= <MAGERY>) && (<SRC.DetectingHidden> >= <HIDING>)
			IF (<FINDID.i_rune_INVISIBILITY>)
			FINDID.i_rune_INVISIBILITY.TIMER
			ELSE
			FLAGS &= ~statf_invisible
			ENDIF
		sysmessage <SRC.name> found you!
		SRC.sysmessage You find <name>.
		skillgain DetectingHidden <EVAL <DetectingHidden>/10>
		ENDIF
	ENDIF
ENDFOR
SRC.SKILLGAIN DetectingHidden <EVAL <SRC.DetectingHidden>/10>
RETURN 1

[SKILL 15]
DEFNAME=Skill_Discordance
KEY=Discordance
TITLE=Bard
PROMPT_MSG=@,,2 500873
DELAY=2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=50
BONUS_INT=50
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

ON=@Fail
SRC.MESSAGE @,,2 500883

ON=@Start
IF (<FLAGS>&statf_freeze) || (<FLAGS>&statf_dead)
SYSMESSAGE @946,3,1 You can't do much in your current state.
RETURN 1
ENDIF
IF (<FINDTYPE.t_musical>)
	IF (!(<CTAG0.FIXMUSIC>))
		IF (<ACT> != <UID>)
			IF (<ACT.TAG0.ENTICED>)
				SYSMESSAGE @,,2 500879
			ELSE
				LOCAL.ARMORMODIFIER=<EVAL (<DISCORDANCE>/65)>
				LOCAL.REALDIFF=<EVAL (<ACT.BARDDIFF <UID>>-10)>
				FLOAT.CHANCE=<FLOATVAL (50+(2*((((<DISCORDANCE>/2)+(<MUSICIANSHIP>/2)+10))-<dLOCAL.REALDIFF>)))/100>
				IF (<FLOAT.CHANCE> < <FLOATVAL 0.01>)
					FLOAT.CHANCE=<FLOATVAL 0.01>
				ENDIF
				IF (<FLOAT.CHANCE> > <RANDOMDOUBLE>)
					IF (<EVAL <ACT.AC>-<LOCAL.ARMORMODIFIER>> >= 1)
						ACT.MODAR -= <LOCAL.ARMORMODIFIER>
					ENDIF
					LOCAL.STATMODIFIER=<EVAL (<DISCORDANCE>/45)>
					IF (<ACT.BARDDIFF> == 160)
						LOCAL.STATMODIFIER /= 2
					ENDIF
					SERV.NEWITEM i_enticement_armor_fix,1,<ACT.UID>
					NEW.MORE1=<LOCAL.ARMORMODIFIER>
					IF (<EVAL <ACT.OSTR>-((<ACT.OSTR>*<LOCAL.STATMODIFIER>)/100)> > 15)
						NEW.TAG.STRCHANGED=<EVAL (<ACT.OSTR>*<LOCAL.STATMODIFIER>)/100>
						ACT.MODSTR -= <NEW.TAG.STRCHANGED>
					ENDIF
					IF (<EVAL <ACT.ODEX>-((<ACT.ODEX>*<LOCAL.STATMODIFIER>)/100)> > 15)
						NEW.TAG.DEXCHANGED=<EVAL ((<ACT.ODEX>*<LOCAL.STATMODIFIER>)/100)>
						ACT.MODDEX -= <NEW.TAG.DEXCHANGED>
					ENDIF
					IF (<EVAL <ACT.OINT>-((<ACT.OINT>*<LOCAL.STATMODIFIER>)/100)> > 15)
						NEW.TAG.INTCHANGED=<EVAL (<ACT.OINT>*<LOCAL.STATMODIFIER>)/100>
						ACT.MODINT -= <NEW.TAG.INTCHANGED>
					ENDIF
					NEW.TIMER=<EVAL <DISCORDANCE>/12>
					ACT.TIMERF <EVAL <DISCORDANCE>/12>,TAG.ENTICED=
					ACT.TAG.ENTICED=1
					SKILLGAIN DISCORDANCE <EVAL (<DISCORDANCE>/10)+1>
					CTAG.FIXMUSIC=1
					TIMERF 5,CTAG.FIXMUSIC
					SYSMESSAGE @,,2 1049539
					ACT.EFFECT 3,i_fx_sparkle,16,30
					ACT.SOUND 1420
				ELSE
					CTAG.FIXMUSIC=1
					TIMERF 5,CTAG.FIXMUSIC
					MESSAGE @,,2 501599
					SKILLGAIN DISCORDANCE <EVAL (<DISCORDANCE>/10)+1>
				ENDIF
			ENDIF

		ELSE
		SYSMESSAGELOC -1,500880
		ENDIF
	ELSE
	SYSMESSAGE @946,3,1 You must wait before enticing another creature!
	ENDIF
ELSE
SYSMESSAGE @946,3,1 You must have an instrument with you.
ENDIF
RETURN 1

[ITEMDEF i_enticement_armor_fix]
ID=i_memory
NAME=Enticement
TYPE=T_EQ_SCRIPT

ON=@Timer
CONT.MODAR += <dMORE1>
CONT.MODSTR += <TAG0.STRCHANGED>
CONT.MODDEX += <TAG0.DEXCHANGED>
CONT.MODINT += <TAG0.INTCHANGED>
CONT.EFFECT 3,i_fx_sparkle,16,30
REMOVE
RETURN 1

[SKILL 16]
DEFNAME=Skill_EvalInt
KEY=EvaluatingIntel
TITLE=Scholar
PROMPT_MSG=@,,2 500906
DELAY=3.0,2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=50
ADV_RATE=1.0,30.0,75.0

ON=@Fail
	SRC.MESSAGE @,,2 500911


[SKILL 17]
DEFNAME=Skill_Healing
KEY=Healing
TITLE=Healer
PROMPT_MSG=Who will you use the bandages on?
DELAY=3.0,1.0
EFFECT=2.0,20.0
ADV_RATE=2.5,50.0,125.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STATS=50
BONUS_STR=0
BONUS_DEX=30
BONUS_INT=70

ON=@Success
    If <Act.f_bandage_cure> && <Def0.PoisonSys_NoHealBandage>
        Return 1
    Endif

ON=@Fail
    SRC.SYSMESSAGE You apply the bandages, but they barely help.

ON=@Abort
    SRC.SYSMESSAGE You stop applying the bandages.

[SKILL 18]
DEFNAME=Skill_Fishing
KEY=Fishing
TITLE=Fisher%s
PROMPT_MSG=@,,2 500974
DELAY=3.0,2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=10
BONUS_DEX=70
BONUS_INT=20
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0
VALUES=1,10,50

ON=@Fail
	SRC.MESSAGE @,,2 503171

[SKILL 19]
DEFNAME=Skill_Forensics
KEY=Forensics
TITLE=Scholar
PROMPT_MSG=What would you like to examine?
DELAY=3.5,1.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=50
ADV_RATE=1.0,30.0,75.0

ON=@Fail
SRC.sysmessageloc -1,1055048


[SKILL 20]
DEFNAME=Skill_Herding
KEY=Herding
TITLE=Farmer
PROMPT_MSG=
DELAY=2.0,0.5
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=60
BONUS_DEX=10
BONUS_INT=30
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0

ON=@Fail
	SRC.SYSMESSAGE You don't seem to be able to persuade that to move.

ON=@Abort
	SRC.SYSMESSAGE You give up trying to persuade that to move.



[SKILL 21]
DEFNAME=Skill_Hiding
KEY=Hiding
TITLE=Rogue
PROMPT_MSG=
DELAY=2.0
STAT_STR=20
STAT_INT=60
STAT_DEX=90
BONUS_STR=0
BONUS_DEX=80
BONUS_INT=20
BONUS_STATS=25
ADV_RATE=10000.0

on=@Wait
if (<src.tag0.skilldone_hiding>==1)
  SRC.SYSMESSAGE You can't use hiding again yet.
  return 1
else
  src.tag.skilldone_hiding = 1
  src.timerf 3 f_clear_skilldone hiding
endif
f_start_hiding
return 1


[function f_start_hiding]
if (<action> == skill_magery) || (<action> == skill_necromancy) ||  (<action> == skill_chivalry) || (<action> == skill_spellweaving) || (<action> == skill_mysticism) // do bushido and ninjitsu count as spell casting?
	sysmessage <def.scp.hiding_spellactive>
	return 1
endif
local.range = <eval (18 - (<hiding> / 100))>
forchars <dlocal.range>
	if (<memoryfindtype.memory_war_targ.link> == <src>) && (<canseelos <src>>)
		local.combat = 1
	endif
endfor
if <local.combat>
	sysmessage <def.scp.hiding_activecombat>
	return 1
endif
if !<r0,<eval ((<hiding> / 100) - 1)>> // we might need a better success chance formula
	local.fail = 1
else
	skillgain hiding <eval (<hiding> / 10)>
endif
if <local.fail>
	skillgain hiding <eval (<hiding> / 7) +1>
	sysmessage <def.scp.hiding_fail>
else
	sysmessage <def.scp.hiding_success>
	f_hide
endif

[function f_hide]
addbuff 1012,1075655,1075656,0,0
removefromview
flags |= statf_hidden
events +e_skill_hiding
update

[function f_reveal]
if <flags>&statf_hidden
sysmessage <def.scp.hiding_reveal>
endif
flags &= ~statf_hidden
events -e_skill_hiding
events -e_skill_stealth
dspeech -spk_hidden
tag.stepstealth =
removebuff 1012
update

[events e_skill_hiding]
on=@stepstealth
if <flags>&statf_onhorse
	f_reveal
endif
f_start_stealth
argn1 = 0
events -e_skill_hiding

on=@skillstart
if !((<argn1> == detectinghidden) || (<argn1> == itemid) || (<argn1> == anatomy) || (<argn1> == armslore) || (<argn1> == animallore) || (<argn1> == evaluatingintel) || (<argn1> == forensics) || (<argn1> == poisoning) || (<argn1> == tracking) || (<argn1> == snooping) || (<argn1> == stealing)) 
f_reveal
endif

on=@skillwait
if !((<argn1> == detectinghidden) || (<argn1> == itemid) || (<argn1> == anatomy) || (<argn1> == armslore) || (<argn1> == animallore) || (<argn1> == evaluatingintel) || (<argn1> == forensics) || (<argn1> == poisoning) || (<argn1> == tracking) || (<argn1> == snooping) || (<argn1> == stealing)) 
f_reveal
endif

on=@personalspace
src.sysmessage <def.scp.hiding_shoveinvis>
return 0

on=@spelleffect
if <argn1> == s_reveal
	local.divisor = <eval (<hiding> + <stealth>)>
	if <local.divisor> > 0
		local.chance = <eval ((50 * (<src.magery> + <src.detectinghidden>)) / <local.divisor>)>
	else
		local.chance = 1000
	endif
	if <local.chance> > <r1000>
		f_reveal
	else
		return 1
	endif
endif

[speech spk_hidden]
on=*
f_reveal

[SKILL 47]
DEFNAME=SKILL_STEALTH
KEY=Stealth
TITLE=Rogue
PROMPT_MSG=
DELAY=2.0
STAT_STR=20
STAT_INT=60
STAT_DEX=90
BONUS_STR=0
BONUS_DEX=80
BONUS_INT=20
BONUS_STATS=25
ADV_RATE=10000.0

ON=@Wait
f_start_stealth
return 1

[function f_start_stealth]
if !<flags>&statf_hidden
	sysmessage <def.scp.stealth_nothidden>
	f_reveal
else
	//if <f_armor_rating> >= 42
	//	sysmessage <def.scp.stealth_armorcheck>
	//	f_reveal
	//else
		if <r1,1000> > <r1,(<stealth>+<skillmod stealth>)> // we might need a better success chance formula
			sysmessage <def.scp.stealth_fail>
			skillgain stealth <eval (<stealth> / 7) + 1>
			f_reveal
		else
			sysmessage <def.scp.stealth_success>
			skillgain stealth <eval (<stealth> / 10)>
			f_stealth
		endif
	//endif
endif

[function f_stealth]
addbuff 1012,1075655,1075656,0,0
tag.stepstealth = <eval (<stealth>+<skillmod stealth>) / 5.0>
events +e_skill_stealth
flags |= statf_hidden
dspeech +spk_hidden
update

[events e_skill_stealth]
on=@stepstealth
if <flags>&statf_onhorse
	f_reveal
endif
tag0.stepstealth -= <qval (<flags>&statf_fly) ? 2:1>
if <tag0.stepstealth> <= 0
	f_start_stealth
endif
argn1 = 0

on=@skillstart
if !((<argn1> == detectinghidden) || (<argn1> == itemid) || (<argn1> == anatomy) || (<argn1> == armslore) || (<argn1> == animallore) || (<argn1> == evaluatingintel) || (<argn1> == forensics) || (<argn1> == poisoning) || (<argn1> == tracking) || (<argn1> == snooping) || (<argn1> == stealing)) 
f_reveal
endif

on=@skillwait
if !((<argn1> == detectinghidden) || (<argn1> == itemid) || (<argn1> == anatomy) || (<argn1> == armslore) || (<argn1> == animallore) || (<argn1> == evaluatingintel) || (<argn1> == forensics) || (<argn1> == poisoning) || (<argn1> == tracking) || (<argn1> == snooping) || (<argn1> == stealing)) 
f_reveal
endif

on=@personalspace
src.sysmessage <def.scp.hiding_shoveinvis>
return 1

on=@spelleffect
if <argn1> == s_reveal
local.divisor = <eval (<hiding> + <stealth>)>
	if <local.divisor> > 0
	local.chance = <eval ((50 * (<src.magery> + <src.detectinghidden>)) / <local.divisor>)>
	else
	local.chance = 1000
	endif
	if <local.chance> > <r1000>
	f_reveal
	else
	return 1
	endif
endif

[SKILL 22]
DEFNAME=SKILL_PROVOCATION
KEY=Provocation
TITLE=Bard
PROMPT_MSG=@,,2 501587
DELAY=3.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=50
BONUS_INT=50
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

ON=@Fail
SRC.sysmessageloc -1,501599

ON=@Select
SYSMESSAGELOC -1,501587
TARGETFW F_PROVOCATION
RETURN 1

[SKILL 23]
DEFNAME=Skill_Inscription
KEY=Inscription
TITLE=Scribe
PROMPT_MSG=
DELAY=5.0,2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=20
BONUS_INT=80
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0
VALUES=1,30,200


[SKILL 24]
DEFNAME=Skill_LockPick
KEY=LockPicking
TITLE=Locksmith
PROMPT_MSG=@,,2 502068
DELAY=3.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=70
BONUS_INT=30
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

ON=@Success
	IF !(<RESTEST 1 i_lockpick_set>) && !(<RESTEST 1 i_lockpick>)
		SRC.MESSAGE You do not have the means to open this.
		RETURN 1
	ENDIF

ON=@Fail
	SRC.SYSMESSAGE You are unable to pick the lock.
	IF (<RESTEST 1 i_lockpick_set>)
		IF !<skillcheck LockPicking 300> || !(<R10>)
			SRC.CONSUME=1 i_lockpick_set
			SRC.MESSAGE You break a lockpick set.
		ENDIF
	ELIF (<RESTEST 1 i_lockpick>)
		IF !<skillcheck LockPicking 400> || !(<R3>)
			SRC.CONSUME=1 i_lockpick
			SRC.MESSAGE You break a lockpick.
		ENDIF
	ENDIF
	SKILLGAIN LockPicking 30
	RETURN


[SKILL 25]
DEFNAME=Skill_Magery
KEY=Magery
TITLE=Mage
PROMPT_MSG=
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0
VALUES=1,20,80
//DELAY=1.0 // based on skill and difficulty.

ON=@Fail
	SRC.SYSMESSAGE The spell fizzles.

ON=@Abort
	SRC.SYSMESSAGE You stop casting the spell.

[SKILL 26]
DEFNAME=Skill_MagicResist
KEY=MagicResistance
TITLE=Resistor
PROMPT_MSG=
EFFECT=0.0,90.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=50
BONUS_DEX=0
BONUS_INT=50
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

ON=@Start
RETURN 1

ON=@Success
RETURN 1

ON=@PreStart
RETURN 1

ON=@UseQuick
RETURN 1

[SKILL 27]
DEFNAME=Skill_Tactics
KEY=Tactics
TITLE=Warrior
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=20
BONUS_DEX=50
BONUS_INT=30
BONUS_STATS=50
ADV_RATE=5.0,100.0,250.0

[SKILL 28]
DEFNAME=Skill_Snooping
KEY=Snooping
TITLE=Pickpocket
PROMPT_MSG=
DELAY=2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=0
BONUS_DEX=80
BONUS_INT=20
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

ON=@Fail
SRC.sysmessageloc -1,500210
SKILLGAIN SNOOPING <EVAL <SNOOPING>/10>

ON=@Abort
SRC.sysmessageloc -1,500210

ON=@PreStart
IF (<region.flags>&region_flag_safe)
sysmessageloc -1,500209
return 1
ENDIF


[SKILL 29]
DEFNAME=Skill_Musicianship
KEY=Musicianship
TITLE=Bard
PROMPT_MSG=
DELAY=2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=0
BONUS_DEX=50
BONUS_INT=50
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

ON=@PreStart
IF (<CTAG0.FIXMUSIC>)
sysmessageloc -1,500119
RETURN 1
ENDIF

ON=@Success
CTAG.FIXMUSIC=1
TIMERF 4,CTAG.FIXMUSIC=

[SKILL 30]
DEFNAME=SKILL_POISONING
KEY=Poisoning
TITLE=Assassin
FLAGS = 01|020
PROMPT_MSG=To what do you wish to apply the poison?
DELAY=2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=0
BONUS_DEX=30
BONUS_INT=70
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0
VALUES=1,30,200

ON=@Select
If !<Ctag0.Skill.Poisoning>
Src.Sysmessage @,,2 <Def0.PoisonCliloc_PoisonWhat>
Src.Targetf f_skill_poison
Else
Src.Sysmessage @,,2 <Def0.PoisonCliloc_YouMustWait>
Endif
Return 1

ON=@Fail
SRC.SYSMESSAGE @,,2 <Def0.PoisonCliloc_FailToApply>

ON=@Abort
//SRC.SYSMESSAGE You decide not to poison anything for now.

[Function f_skill_poison]
If <Argo.TopObj>!=<Uid>
Sysmessage @,,2 <Def0.PoisonCliloc_MustBeInPack>
Return 1
Endif
If !<Argo.IsItem>
Sysmessage @,,2 <Def0.PoisonCliloc_CantWorkOnThat>
Return 1
Endif
If ((!<Def0.PoisonWep_<Argo.Type>>) && (!<Argo.Tag0.Override.Poisonable>))
Sysmessage @,,2 <Def0.PoisonCliloc_NotPoisonable>
Return 1
Endif
If ((<Argo.TwoHands>) && (!<Def0.PoisonSys_WeaponPoisonTwoHand>) && (!<Argo.Tag0.Override.Poisonable>))
Sysmessage @,,2 <Def0.PoisonCliloc_NotPoisonableTwoHands>
Return 1
Endif
Sysmessage @,,2 <Def0.PoisonCliloc_WhatPoisonUse>
Targetf f_skill_poison_2 <Argo.Uid>

[Function f_skill_poison_2]
Ref1 = <Argv[0]>
If <Argo.TopObj>!=<Uid>
Sysmessage @,,2 <Def0.PoisonCliloc_MustBeInPack>
Return 1
Endif
If <Ref1.TopObj>!=<Uid>
Sysmessage @,,2 <Def0.PoisonCliloc_TargetTooFar>
Return 1
Endif
If !<Argo.IsItem>
Sysmessage @,,2 <Def0.PoisonCliloc_CantWorkOnThat>
Return 1
Endif
If <Argo.Type> != t_potion
Sysmessage @,,2 <Def0.PoisonCliloc_NotPoisonPot>
Return 1
Endif
If <Argo.More1> != s_poison
Sysmessage @,,2 <Def0.PoisonCliloc_NotPoisonPot>
Return 1
Endif
If <Argo.Tag0.Poison>
Local.PoisonLevel = <Argo.Tag0.Poison>
Else
Local.PoisonLevel = <Qval <Argo.More2> < 500 ? 0 : <Qval <Argo.More2> < 650 ? 1 : <Qval <Argo.More2> < 800 ? 2 : <Qval <Argo.More2> < 950 ? 3 : 4>>>>
Endif
If <Argo.Amount> > 1
Argo.Amount -= 1
Argo.Fix
Else
Argo.Remove
Endif
Serv.Newitem = i_bottle_empty
New.Bounce
Ctag.Skill.Poisoning = 1
Anim 33
Sound 583
Local.X
For 2 <Eval {2 3}>
Timerf <Local._For>,Anim 33
Timerf <Local._For>,Sound 583
Local.X = <Local._For>
Endfor
Timerf <Local.X>,f_skill_poison_apply <Ref1.Uid>,<Local.PoisonLevel>


[Function f_skill_poison_apply]
Ref1 = <Argv[0]>
Ctag.Skill.Poisoning =
Local.Chance = <Eval {(<Poisoning> / 2) <Poisoning>}>
If <Local.Chance> > <Def0.PoisonType_<Eval <Argv[1]>>_ApplicationDifficulty>
Sysmessage @,,2 <Def0.PoisonCliloc_YouApplyPoison>
Ref1.Tag.Poison.Level = <Argv[1]>
Ref1.Tag.Poison.Charges = <Def0.PoisonType_<Eval <Argv[1]>>_ApplicationCharges>
Ref1.Events +t_weapon_poisoned
Ref1.ResendToolTip
If <Findlayer.<Ref1.Layer>.Uid>==<Ref1.Uid>
Events +e_weapon_poisoned
Endif
If <Poisoning> < <Def0.PoisonType_<Eval <Argv[1]>>_ApplicationDifficulty> + 350
Skillgain Poisoning,1
Endif
Elif <Local.Chance> < <Def0.PoisonType_<Eval <Argv[1]>>_ApplicationDifficulty> - (<Def0.PoisonType_<Eval <Argv[1]>>_ApplicationDifficulty> / 3)
Sysmessage @,,2 <Def0.PoisonCliloc_GraveMistake>
New_Poison <Qval <Argv[1]> ? (<Argv[1]> +- 1) : 0>
Else
Sysmessage @,,2 <Def0.PoisonCliloc_FailApplyPoison>
If <Poisoning> < 150
Skillgain Poisoning,1
Endif
Endif

[Typedef t_weapon_poisoned]

ON=@Equip
Src.Events +e_weapon_poisoned

ON=@UnEquip
Src.Events -e_weapon_poisoned

[Events e_weapon_poisoned]

On=@Hit
If Rand(11) < <Eval 2 + (<Poisoning>/200)>
Src.New_Poison <Weapon.Tag0.Poison.Level>,<Uid>
Sysmessage @,,2 <Def0.PoisonCliloc_BloodMixPoison>
Weapon.More1l -= <Eval {1 2}>
If <Weapon.More1l> < 0
Weapon.More1l = 0
Endif
Endif
Weapon.Tag.Poison.Charges = <Eval <Weapon.Tag0.Poison.Charges> +- 1>
Weapon.ResendToolTip
If <Weapon.Tag0.Poison.Charges> <= 0
Weapon.Tag.Poison.Level =
Weapon.Tag.Poison.Charges =
Weapon.Events -t_weapon_poisoned
Events -e_weapon_poisoned
Endif


[SKILL 31]
DEFNAME=Skill_Archery
KEY=Archery
TITLE=Archer
PROMPT_MSG=
DELAY=2.0,0.1
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=00
BONUS_DEX=80
BONUS_INT=20
BONUS_STATS=50
ADV_RATE=5.0,100.0,250.0

[SKILL 32]
DEFNAME=SKILL_SPIRITSPEAK
KEY=SpiritSpeak
TITLE=Medium
PROMPT_MSG=
FLAGS=
DELAY=2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500

BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

ON=@Wait
IF !(<ACTION> == -1)
	aCTION = -1
endif

ON=@Fail
SRC.MESSAGE @,,2 502443

ON=@PreStart
IF (<HITS> >= <MAXHITS>)
	SYSMESSAGE @55 You don't need to heal.
	RETURN 1
ENDIF

ON=@Success
IF (<HITS> >= <MAXHITS>)
SYSMESSAGE @55 You don't need to heal.
RETURN 1
ENDIF
skillgain SpiritSpeak <SpiritSpeak>
FORITEMS 3
IF (<TYPE>==t_corpse)
IF (<CANSEELOS>)
IF !(<MOREY>)
LOCAL.MAXDAMHEALED=<EVAL (<SRC.SPIRITSPEAK>/20)+4>
LOCAL.MINDAMHEALED=<EVAL (<LOCAL.MAXDAMHEALED>/2)+1>
IF (<LOCAL.MINDAMHEALED> < <LOCAL.MAXDAMHEALED>)
SRC.HITS += <R<LOCAL.MINDAMHEALED>,<EVAL <LOCAL.MAXDAMHEALED>+1>>
ELSE
SRC.HITS += <LOCAL.MAXDAMHEALED>
ENDIF
SRC.SOUND 586
SRC.SAY @751 Ahn Mih Sah Ko
SRC.ANIM 17
MOREY=1
COLOR=0481
IF (<SRC.HITS> > <SRC.MAXHITS>)
SRC.HITS=<SRC.MAXHITS>
ENDIF
SRC.SYSMESSAGE You channel energy from a nearby corpse to heal your wounds.
SRC.EFFECT 3,i_fx_bless_effect,1,20
RETURN 1
ENDIF
ENDIF
ENDIF
ENDFOR
IF (<SRC.MANA> >= 10)
LOCAL.MAXDAMHEALED=<EVAL (<SRC.SPIRITSPEAK>/100)+4>
LOCAL.MINDAMHEALED=<EVAL (<LOCAL.MAXDAMHEALED>/2)+1>
SRC.HITS += <R<LOCAL.MINDAMHEALED>,<EVAL <LOCAL.MAXDAMHEALED>+1>>
IF (<SRC.HITS> > <SRC.MAXHITS>)
SRC.HITS=<SRC.MAXHITS>
ENDIF
SRC.SYSMESSAGE You channel your own spiritual energy to heal your wounds.
SRC.MANA -= 10
SRC.EFFECT 3,i_fx_bless_effect,1,20
ENDIF

//made by ClouD_BR

[SKILL 33]
DEFNAME=Skill_Stealing
KEY=Stealing
TITLE=Thief
PROMPT_MSG=@,,2 502698
DELAY=2.5
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=80
BONUS_INT=20
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

ON=@Fail
	SRC.sysmessageloc -1,502723

ON=@Abort
	SRC.sysmessageloc -1,502723

[SKILL 34]
DEFNAME=Skill_Tailoring
KEY=Tailoring
TITLE=Tailor
PROMPT_MSG=
DELAY=5.0,2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=70
BONUS_INT=30
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0
VALUES=1,20,80

ON=@SUCCESS
	SRC.CTAG0.CLOTH_COLOR=<ACT.COLOR>

ON=@Fail
	SRC.SYSMESSAGE Tailoring failed. Some of the cloth is ruined.

ON=@Abort
	SRC.SYSMESSAGE You decide not to sew anymore for now.


[SKILL 35]
DEFNAME=Skill_Taming
KEY=Taming
TITLE=Animal Tamer
PROMPT_MSG=@,,2 502789
DELAY=2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=20
BONUS_INT=80
BONUS_STATS=50
ADV_RATE=1.0,30.0,75.0

on=@wait
sysmessage @,,1 Tame which animal?
targetf f_start_taming
return 1


[function f_start_taming]
if <argo.uid> == <uid>
	sysmessage <def.scp.taming_tameself>
	return 1
elif <argo.isplayer>
	sysmessage <def.scp.taming_player>
	return 1
elif !<argo.ischar>
	sysmessage <def.scp.taming_notchar>
	return 1
elif !<argo.tag0.taming>
	argo.message <def.scp.taming_notameable>
	return 1
elif <argo.owner>
	argo.message <def.scp.taming_alreadytamed>
	return 1
elif (<argo.tag0.taming> > <taming>)
	argo.message <def.scp.taming_nochance>
	return 1
elif <argo.tag0.beingtamed>
	argo.message <def.scp.taming_ontame>
	return 1
elif <argo.can_anger_on_tame> && !<argo.isevent.e_peacemaked>
	if <r1,100> <= 95
	argo.message <def.scp.taming_getanger>
	argo.face <uid>
	argo.attack <uid>
	return 1
	endif
endif
if (<src.targ.tag0.slots_needed>)
    local.slots = <src.targ.tag.slots_needed>
else
    local.slots = 1
endif
IF ((<SRC.CURFOLLOWER>+<local.slots>) > <SRC.MAXFOLLOWER>)
	SRC.SYSMESSAGE You have too many followers.
	RETURN 1
ENDIF
message <def.scp.taming_started>
forplayers
	if <uid> != <src.uid>
	trysrc <uid> uid.<src.uid>.message <def.scp.taming_he_started>
	endif
endfor
tag.totame = <argo.uid>
argo.tag.beingtamed = 1
serv.newitem i_tamefix
new.link = <argo.uid>
new.tag.starttime = <serv.time>
equip <new>
new.more1 = {4 5}
return 1


[itemdef i_tamefix]
id=i_memory
type=t_eq_script
name=tamefix

on=@equip
timer=3

on=@timer
if <more1>
	if <cont.distance <link>> > 3
		link.message <def.scp.taming_toofaraway>
		cont.tamingfail <link>
		remove
		return 1
	elif <cont.flags>&statf_dead
		link.message <def.scp.taming_youdead>
		cont.tamingfail <link>
		remove
		return 1
	elif !(<cont.canseelos <link>>)
		link.message <def.scp.taming_cantseelos>
		cont.tamingfail <link>
		remove
		return 1
	elif <link.attacker>
		if <eval (<serv.time> - (<link.attacker.last.elapsed> * 10))> > <tag0.starttime>
		link.message <def.scp.taming_isangry>
		cont.tamingfail <link>
		remove
		return 1
		endif
	endif
cont.say @,,2 { {502790 502793} 1 {1005608 1005613} 1 {1010593 1010596} 1 }
cont.face <link>
more1 --
timer = {2 3}
cont.skillgain animallore <eval (<link.tag.taming> / 10)>
cont.SKILLGAIN taming <eval (<link.tag.taming> / 10)>
else
	if <r> <= <cont.tamingsuccesschance <link>>
		link.removeallmemories
		link.makemypet <cont>,<dtag0.paralyzed>
	else
		cont.tamingfail <link>
		cont.sysmessage <def.scp.taming_failed>
	endif
remove
endif
return 1

[function removeallmemories]
while (<findid.i_memory>)
	findid(i_memory).remove
endwhile

[function makemypet]
ref1=<argv[0]>
serv.newitem i_memory
new.attr=04
new.more1=04
new.more2=<serv.time>
new.morep=<p>
new.color=memory_ipet
new.timer=-1
new.link=<ref1>
new.cont=<uid>
flags |= statf_pet
tag.beingtamed=
home = -1,-1
if !(<tag0.owner.<ref1>>)
	ref1.sysmessage @,,2 502799 // It seems to accept you as master.
	ref1.skillgain animallore <eval (<tag.taming> / 10)>
	ref1.SKILLGAIN taming <eval (<tag.taming> / 10)>
else
	ref1.sysmessage @,,2 502797 // That wasn't even challenging.
endif
if !(<tag0.owner.<ref1>>)
	tag.owner.<ref1> = 1
endif
if (<tag0.slots_needed>)
    local.slots = <tag.slots_needed>
else
    local.slots = 1
endif
ref1.CURFOLLOWER += <local.slots>
EMOTE loose some wild instincts
STR -= (<obj.STR>/10)
DEX -= (<obj.DEX>/10)
INT -= (<obj.INT>/10)
EXP -= (<obj.EXP>/2)
IF (<LEVEL> > 0)
	  TAG.NEXTLEVEL -= (<TAG.NEXTLEVEL>/2)
	  LEVEL -= 1
ENDIF
for 0 57
	<dlocal._for> -= (<I.<dlocal._for>>/10)
endfor
ref1.tag.totame=
update

[function tamingfail]
ref1=<args>
ref1.tag.beingtamed=
tag.totame=
skillgain animallore <eval (<ref1.tag.taming> / 20)>
SKILLGAIN taming <eval (<ref1.tag.taming> / 20)>

[function can_anger_on_tame]
//if (<baseid> == c_m_hiryu) || (<baseid> == c_m_nightmare) || (<baseid> == c_armored_swamp_dragon) || (<baseid> == c_swamp_dragon) || (<body> == c_dragon_red) || (<body> == c_dragon_green) || (<body> == c_dragon_small_black) || (<body> == c_dragon_small_red)
return 0
//else
//return 0
//endif

[function tamingsuccesschance]
ref1=<args>
doswitch <ref1.tag0.owner.<uid>>
	local.successchance=<eval (((<taming> - <ref1.tag.taming>) + 1) * 2)>
	local.successchance=1000
enddo
return <dlocal.successchance>



[SKILL 36]
DEFNAME=skill_tasteid
KEY=TasteID
TITLE=Food Taster
PROMPT_MSG=@,,2 502807
DELAY=3.5,1.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=20
BONUS_DEX=0
BONUS_INT=80
BONUS_STATS=50
ADV_RATE=1.0,30.0,75.0

ON=@SUCCESS
IF (<TARG.TYPE>==t_potion)
	LOCAL.POTENCY=Extremely Weak
	IF (<TARG.MORE2> > 110.0)
		LOCAL.POTENCY=Supremely Potent
	ELSEIF (<TARG.MORE2> > 95.0)
		LOCAL.POTENCY=Extremely Potent
	ELSEIF (<TARG.MORE2> > 80.0)
		LOCAL.POTENCY=Greater
	ELSEIF (<TARG.MORE2> > 65.0)
		LOCAL.POTENCY=Moderate
	ELSEIF (<TARG.MORE2> > 50.0)
		LOCAL.POTENCY=Lesser
	ENDIF
	TARG.NAME=<LOCAL.POTENCY> <SERV.SPELL.<TARG.MORE1>.NAME> potion
	TARG.FIX
ENDIF
SRC.MESSAGE It tastes like <TARG.NAME>.
RETURN


ON=@Fail
	SRC.MESSAGE You cannot identify the substance.
	SKILLGAIN TasteID 30
	return

ON=@Abort
	SRC.MESSAGE You stop trying to identify the substance.
	return

[SKILL 37]
DEFNAME=Skill_Tinkering
KEY=Tinkering
TITLE=Tinker
PROMPT_MSG=
DELAY=5.0,2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=30
BONUS_INT=70
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0
VALUES=1,20,80

[SKILL 38]
DEFNAME=Skill_Tracking
KEY=Tracking
TITLE=Ranger
PROMPT_MSG=
DELAY=3.0,1.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=20
BONUS_INT=80
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0

ON=@Select
if (<src.tag0.skilldone_tracking>==1)
  SRC.SYSMESSAGE You can't use tracking again yet.
  return 1
else
  src.tag.skilldone_tracking = 1
  src.timerf 3 f_clear_skilldone tracking
endif

IF (<DEF.USENEWTRACKING>)
SRC.CTAG.TRACKINGCHARS
	FOR <SRC.CTAG0.TRACKEDPLAYERS>
	TRY SRC.CTAG.TRACKEDPLAYERS.<EVAL <LOCAL._FOR>>
	ENDFOR
SRC.CTAG.TRACKEDPLAYERS
	FOR <SRC.CTAG0.TRACKEDMONSTERS>
	TRY SRC.CTAG.TRACKEDMONSTERS.<EVAL <LOCAL._FOR>>
	ENDFOR
SRC.CTAG.TRACKEDMONSTERS
	FOR <SRC.CTAG0.TRACKEDHUMANS>
	TRY SRC.CTAG.TRACKEDHUMANS.<EVAL <LOCAL._FOR>>
	ENDFOR
SRC.CTAG.TRACKEDHUMANS
	FOR <SRC.CTAG0.TRACKEDANIMALS>
	TRY SRC.CTAG.TRACKEDANIMALS.<EVAL <LOCAL._FOR>>
	ENDFOR
SRC.CTAG.TRACKEDANIMALS
	FORCHARS <EVAL (((<TRACKING>+<SKILLMOD Tracking>)*3)/100)+15>
		IF (<UID> != <SRC>)
			IF !(<ISGM>)
				If (<src.tracking> == 0)
				src.tracking=1
				EndIf
				IF (<TRACKING> <= <SRC.TRACKING>)
				SRC.CTAG0.TRACKINGCHARS ++
					IF (<ISPLAYER>)
					SRC.CTAG0.TRACKEDPLAYERS ++
					TRY SRC.CTAG.TRACKEDPLAYERS.<EVAL <SRC.CTAG0.TRACKEDPLAYERS>>=<UID>
					ELIF (<NPC> == brain_animal)
					SRC.CTAG0.TRACKEDANIMALS ++
					TRY SRC.CTAG.TRACKEDANIMALS.<EVAL <SRC.CTAG0.TRACKEDANIMALS>>=<UID>
					ELIF (<BODY> != c_man) && (<BODY> != c_woman) && (<BODY> != c_elf_male) && (<BODY> != c_elf_female)
					SRC.CTAG0.TRACKEDMONSTERS ++
					TRY SRC.CTAG.TRACKEDMONSTERS.<EVAL <SRC.CTAG0.TRACKEDMONSTERS>>=<UID>
					ELSE
					SRC.CTAG0.TRACKEDHUMANS ++
					TRY SRC.CTAG.TRACKEDHUMANS.<EVAL <SRC.CTAG0.TRACKEDHUMANS>>=<UID>
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDFOR
SRC.DIALOG d_NEW_TRACKING
RETURN 1
ENDIF

ON=@Fail
	SRC.SYSMESSAGE Tracking failed.

ON=@Abort
	SRC.SYSMESSAGE You stop tracking.


[SKILL 39]
DEFNAME=Skill_Vet
KEY=Veterinary
TITLE=Veterinarian
PROMPT_MSG=What animal would you like to heal?
DELAY=2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=30
BONUS_INT=70
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

ON=@Success
    If <Act.f_veterinary_bandage_cure> && <Def0.PoisonSys_NoVetHealBandage>
        Return 1
    Endif

ON=@Fail
    SRC.SYSMESSAGE You apply the bandages, but they barely help.

ON=@Abort
    SRC.SYSMESSAGE You stop applying the bandages.

	
[SKILL 40]
DEFNAME=Skill_Swordsmanship
KEY=Swordsmanship
TITLE=Swords%s
PROMPT_MSG=
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=50
BONUS_DEX=50
BONUS_INT=0
BONUS_STATS=50
ADV_RATE=5.0,100.0,250.0

[SKILL 41]
DEFNAME=Skill_Macefighting
KEY=Macefighting
TITLE=Macefighter
PROMPT_MSG=
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=70
BONUS_DEX=30
BONUS_INT=0
BONUS_STATS=50
ADV_RATE=5.0,100.0,250.0

[SKILL 42]
DEFNAME=Skill_Fencing
KEY=Fencing
TITLE=Fencer
PROMPT_MSG=
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=70
BONUS_INT=30
BONUS_STATS=50
ADV_RATE=5.0,100.0,250.0

[SKILL 43]
DEFNAME=Skill_Wrestling
KEY=Wrestling
TITLE=Wrestler
PROMPT_MSG=
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=50
BONUS_DEX=40
BONUS_INT=10
BONUS_STATS=50
ADV_RATE=5.0,100.0,250.0

[SKILL 44]
DEFNAME=SKILL_LUMBERJACK
KEY=Lumberjacking
TITLE=Lumberjack
PROMPT_MSG=
DELAY=2.0,1.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=70
BONUS_DEX=30
BONUS_INT=0
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0
VALUES=1,10,50

ON=@Fail
	SRC.SYSMESSAGE You hack at the tree for a while, but fail to produce any useable wood.

ON=@Abort
	SRC.SYSMESSAGE You decide not to chop wood for now.

[SKILL 45]
DEFNAME=SKILL_MINING
KEY=Mining
TITLE=Miner
PROMPT_MSG=@,,2 501858
DELAY=2.0,1.
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=80
BONUS_DEX=20
BONUS_INT=0
BONUS_STATS=50
ADV_RATE=1.7,40.0,100.0
VALUES=1,10,80
FLAGS=skf_nomindist

ON=@Fail
	SRC.SYSMESSAGE You loosen some rocks but fail to find any useable ore.

ON=@Abort
	SRC.SYSMESSAGE You decide not to mine for now.

[SKILL 46]
DEFNAME=SKILL_MEDITATION
KEY=Meditation
TITLE=Stoic
DELAY=3.5,1.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

ON=@Select
IF (<SRC.FINDLAYER(6).TYPE>==t_armor) || (<SRC.FINDLAYER(13).TYPE>==t_armor) || (<SRC.FINDLAYER(24).TYPE>==t_armor) || (<SRC.FINDLAYER(19).TYPE>==t_armor) || (<SRC.FINDLAYER(10).TYPE>==t_armor) || (<SRC.FINDLAYER(7).TYPE>==t_armor)
  SRC.SYSMESSAGE You cannot meditate in heavy armor.
  RETURN 1
ENDIF

ON=@Fail
	SRC.SYSMESSAGE You lose your concentration.

ON=@Abort
	SRC.SYSMESSAGE You stop concentrating.

[SKILL 48]
DEFNAME=SKILL_REMOVE_TRAP
KEY=RemoveTrap
TITLE=Trapsmith
PROMPT_MSG=Select the trap to disarm
DELAY=7.0,3.0,2.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=70
BONUS_INT=30
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

ON=@Fail
	SRC.SYSMESSAGE You fail to disarm the trap.

ON=@Abort
	SRC.SYSMESSAGE You give up trying to disarm the trap.

[SKILL 49]
DEFNAME=SKILL_Necromancy
KEY=Necromancy
TITLE=Evoker
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0
VALUES=1,10,80

ON=@Fail
	SRC.SYSMESSAGE The spell fizzles.

ON=@Abort
	SRC.SYSMESSAGE You stop casting the spell.
	
[SKILL 50]
DEFNAME=SKILL_Focus
KEY=Focus
TITLE=Stoic
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=20
BONUS_DEX=40
BONUS_INT=40
BONUS_STATS=50
ADV_RATE=100.0,400.0,800.0

[SKILL 51]
DEFNAME=SKILL_CHIVALRY
KEY=Chivalry
TITLE=Priest
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=20
BONUS_DEX=00
BONUS_INT=80
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

[SKILL 52]
DEFNAME=SKILL_BUSHIDO
KEY=Bushido
TITLE=Polefighter
FLAGS=skf_fight
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=40
BONUS_DEX=40
BONUS_INT=20
BONUS_STATS=50
ADV_RATE=5.0,100.0,250.0

[SKILL 53]
DEFNAME=SKILL_NINJITSU
KEY=Ninjitsu
TITLE=Axe%s
FLAGS=skf_fight
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=70
BONUS_DEX=30
BONUS_INT=0
BONUS_STATS=50
ADV_RATE=5.0,100.0,250.0

[SKILL 54]
DEFNAME=SKILL_SPELLWEAVING
KEY=Spellweaving
TITLE=Arcanist
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0
VALUES=1,10,80

[SKILL 55]
DEFNAME=SKILL_MYSTICISM
KEY=Mysticism
TITLE=Mystic
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0

[SKILL 56]
DEFNAME=SKILL_IMBUING
KEY=Imbuing
TITLE=Artificer
DELAY=3.0
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=0
BONUS_DEX=20
BONUS_INT=80
BONUS_STATS=50
ADV_RATE=2.5,50.0,125.0
VALUES=1,30,200

[SKILL 57]
DEFNAME=SKILL_THROWING
KEY=Throwing
TITLE=Bladeweaver
STAT_STR=500
STAT_INT=500
STAT_DEX=500
BONUS_STR=20
BONUS_DEX=60
BONUS_INT=20
BONUS_STATS=50
ADV_RATE=5.0,100.0,250.0
FLAGS=skf_ranged|skf_fight

[SKILLCLASS 0]
// undeclared class.
// max skills for players of this skill class. 
// might want this to be all 50.0 to make people declare a class?
DEFNAME=Class_undeclared
NAME=undeclared
// EVENTS=e_ClassUndeclared
STATSUM=300
SKILLSUM=10000.0
STR=100
INT=100
DEX=100
AnimalLore=100.0
Alchemy=100.0
Anatomy=100.0
ItemId=100.0
ArmsLore=100.0
Parrying=100.0
Begging=100.0
Blacksmithing=100.0
Bowcraft=100.0
Peacemaking=100.0
Camping=100.0
Carpentry=100.0
Cartography=100.0
Cooking=100.0
DetectingHidden=100.0
Discordance=100.0
EvaluatingIntel=100.0
Healing=100.0
Fishing=100.0
Forensics=100.0
Herding=100.0
Hiding=100.0
Provocation=100.0
Inscription=100.0
LockPicking=100.0
Magery=100.0
MagicResistance=100.0
Tactics=100.0
Snooping=100.0
Musicianship=100.0
Poisoning=100.0
Archery=100.0
SpiritSpeak=100.0
Stealing=100.0
Tailoring=100.0
Taming=100.0
TasteId=100.0
Tinkering=100.0
Tracking=100.0
Veterinary=100.0
Swordsmanship=100.0
Macefighting=100.0
Fencing=100.0
Wrestling=100.0
Lumberjacking=100.0
Mining=100.0
Meditation=100.0
Stealth=100.0
RemoveTrap=100.0
Necromancy=100.0
Focus=100.0
Chivalry=100.0
Bushido=100.0
Ninjitsu=100.0
Spellweaving=100.0

[FUNCTION BARDDIFF]
FLOAT.BARDDIFF=<FLOATVAL ((<MAXHITS>*1.6)+<MAXSTAM>+<MAXMANA>+(<SKILLTOTAL>/10))>
IF (<MAGERY>)
FLOAT.BARDDIFF=<FLOATVAL <FLOAT.BARDDIFF>+100>
ENDIF	
IF (<NPC> == <DEF.brain_dragon>)
FLOAT.BARDDIFF=<FLOATVAL <FLOAT.BARDDIFF>+100>
ENDIF
IF (<TAG0.LIFELEECH>)
FLOAT.BARDDIFF=<FLOATVAL <FLOAT.BARDDIFF>+100>
ENDIF
FLOAT.BARDDIFF=<FLOATVAL <FLOAT.BARDDIFF>+<POISONING>>
IF (<EVAL (<SKILLTOTAL>/10)+<STR>+<DEX>+<INT>> <= 700)
LOCAL.FINALDIFF=<EVAL <FEVAL <FLOAT.BARDDIFF>>/10>
ELSE
FLOAT.BARDDIFF=<FLOATVAL ((<FLOAT.BARDDIFF>-700)*0.275)+700>
LOCAL.FINALDIFF=<EVAL <FEVAL <FLOAT.BARDDIFF>>/10>
ENDIF
IF (<LOCAL.FINALDIFF> > 160)
LOCAL.FINALDIFF=160
ENDIF
IF (!(<ISEMPTY <ARGS>>))
REF1=<ARGS>
	IF (<REF1.MUSICIANSHIP> > 100.0)
	LOCAL.FINALDIFF -= <EVAL ((<REF1.MUSICIANSHIP>/10)-100)/2>
	ENDIF
LOCAL.FINALDIFF -= 17
ENDIF
IF (<LOCAL.FINALDIFF> < 1)
LOCAL.FINALDIFF=1
ENDIF
RETURN <dLOCAL.FINALDIFF>


[FUNCTION RANDOMDOUBLE]
IF (<ARGN1>)
LOCAL.VAL=<EVAL <ARGN1>*1000000>
ELSE
LOCAL.VAL=1000000
ENDIF
FLOAT.RANDOM=<FLOATVAL <R<dLOCAL.VAL>>/1000000>
RETURN <FLOAT.RANDOM>

[DEFNAME Skill_Messages]

scp.skill_youaredead		"@,,2 500949"	// You can't do that when you're dead.
scp.skill_mustwait		"@,,2 500118"	// You must wait a few moments to use another skill.
scp.hiding_fail			"@,,2 501241"		// You fail to hide.
scp.hiding_abort		"@,,2 501241"		// You fail to hide.
scp.hiding_spellactive		"@,,2 501238"		// You are busy doing something else and cannot hide.
scp.hiding_activecombat		"@022,,2 501237"	// You can't seem to hide right now.
scp.hiding_success		"@01f4,,2 501240"	// You have hidden yourself well.
scp.hiding_fail			"@022,,2 501241"	// You fail to hide.
scp.hiding_reveal		"@,,2 501242"		// You are no longer hidden.
scp.hiding_shoveinvis		"@,,2 1019041"		// You shove something invisible out of the way.


scp.stealth_nothidden		"@,,2 502725" // You must hide first
scp.stealth_reqhiding		"@,,2 502726" // You are not hidden well enough.  Become better at hiding.
scp.stealth_armorcheck		"@,,2 502727" // You could not hope to move quietly wearing this much armor.
scp.stealth_success		"@,,2 502730" // You begin to move quietly.
scp.stealth_fail		"@,,2 502731" // You fail in your attempt to move unnoticed.

scp.taming_tameself		"@,,1 You are your own master."
scp.taming_notchar		"@,,2 502801"	// You can't tame that!
scp.taming_notameable		"@,,2 1049655"	// That creature cannot be tamed.
scp.taming_alreadytamed		"@,,2 502804"	// That animal looks tame already.
scp.taming_onlymale		"@,,2 1049653"	// That creature can only be tamed by males.
scp.taming_onlyfemale		"@,,2 1049652"	// That creature can only be tamed by females.
scp.taming_onlyelf		"@03b2,,2 502801"	// You can't tame that!
scp.taming_nochance		"@,,2 502806"	// You have no chance of taming this creature.
scp.taming_manyfollowers	"@,,2 1049611"	// You have too many followers to tame that creature.
scp.taming_manyowners		"@,,2 1005615"	// This animal has had too many owners and is too upset for you to tame.
scp.taming_mustsubdue		"@,,2 1054025"	// You must subdue this creature before you can tame it!
scp.taming_ontame		"@,,2 502802"	// Someone else is already taming this.
scp.taming_getanger		"@,,2 502805"	// You seem to anger the beast!
scp.taming_started		"@059,,2 1010597"	// *You start to tame the creature.*
scp.taming_hestarted		"@059,,2 1010598"	// *begins taming a creature.*
scp.taming_failed		"@,,2 502798"	// You fail to tame the creature.
scp.taming_toofaraway		"@,,2 502795"	// You are too far away to continue taming.
scp.taming_abort		"You give up trying to tame the creature."
scp.taming_youdead		"@,,2 502796"	// You are dead, and cannot continue taming.
scp.taming_cantseelos		"@,,2 1049654"	// You do not have a clear path to the animal you are taming, and must cease your attempt.
scp.taming_isangry		"@,,2 502794"	// The animal is too angry to continue taming.
scp.taming_player		"@,,2 502801"	// You can't tame that!
scp.taming_subdued		"@,,2 1080057"	// * The creature has been beaten into subjugation! *


[EOF]
