//*****************************************************************
//
//	Monster Events
//
//*****************************************************************


[FUNCTION f_equip_mine]
SERV.NEWITEM=i_timer_mine
EQUIP=<NEW.UID>


[ITEMDEF i_timer_mine]
ID=i_memory
TYPE=t_eq_script
LAYER=layer_special
NAME=Memoria del Iron Beetle

ON=@EQUIP
IF !(<CONT.FLAGS>&statf_war)
	SRC.npcmine
ENDIF

ON=@TIMER
IF !(<CONT.FLAGS>&statf_war)
	CONT.npcmine
ENDIF
RETURN 1

[FUNCTION npcmine]
REF1 = <FINDTYPE.t_weapon_mace_pick.UID>
FINDID.I_TIMER_mine.TIMER={30 60}
ACTARG3 = t_rock
ACTPRV = <REF1>
ACTP = <P>
ACTION = 45
RETURN


[EVENTS e_swarm]
on=@Attack
	OBJ=<UID>
	LOCAL.TARGET=<SRC>
	FORCHARS 24
		IF (STRMATCH(*<obj.body>*,<serv.chardef(<body>).desires>)) && !(<FLAGS>&statf_war) )
			TIMERF 1, TRYSRC <LOCAL.TARGET> ATTACK
		ENDIF
	ENDFOR


[EVENTS e_undead_spawns]

ON=@NPCActFollow
IF !(<R15>)
SERV.NEWITEM i_unholy_bone
NEW.P=<P>
NEW.TIMER=20
ENDIF

[ITEMDEF i_unholy_bone]
ID=i_reag_daemon_bone
NAME=Unholy Bone
TYPE=T_NORMAL

ON=@Create
ATTR=ATTR_MOVE_NEVER
COLOR=038b

ON=@Timer
SERV.NEWNPC { c_skeleton 3 c_skeleton_mage 1 c_skeleton_knight 1 c_patchwork_skeleton 4 }
NEW.P=<P>
NEW.UPDATE
NEW.REMOVETIMER <EVAL 60*5>
REMOVE
RETURN 1

ON=@Damage
REMOVE

ON=@Step
IF (<SRC.ISPLAYER>)
REMOVE
ENDIF

[EVENTS e_food_steal]
ON=@Hit
	IF !(<R4>)
		IF (<FOOD> >= <MAXFOOD>) //Second stomach for enemies' food
			FOOD=(<MAXFOOD>-1)
		ENDIF
		IF (<SRC.FINDTYPE.T_FOOD>)
			OBJ=<SRC.FINDTYPE.T_FOOD>
			TRYSRC <UID> OBJ.USEITEM
		ELSEIF (<SRC.FINDTYPE.T_FRUIT>)
			OBJ=<SRC.FINDTYPE.T_FRUIT>
			TRYSRC <UID> OBJ.USEITEM
		ENDIF
	endif


[EVENTS e_evil_mount]
on=@userdclick
	if (src.karma > -2000)
		src.sysmessage Only the corrupt can ride this creature.
		return 1
	endif

[EVENTS e_good_mount]
on=@userdclick
	if (src.karma < 2000)
		src.sysmessage Only the pure can ride this creature.
		return 1
	endif


[EVENTS e_death_rain_gold]
ON=@DEATHCORPSE
 RAIN_PLATINUM {1 5}
 RAIN_GOLD {5 25}
 RAIN_SILVER {50 250}
 RAIN_COPPER {500 2500}
 RAIN_PLATINUM {1 5}
 RAIN_GOLD {5 25}
 RAIN_SILVER {50 250}
 RAIN_COPPER {500 2500}
 RAIN_PLATINUM {1 5}
 RAIN_GOLD {5 25}
 RAIN_SILVER {50 250}
 RAIN_COPPER {500 2500}
 RAIN_PLATINUM {1 5}
 RAIN_GOLD {5 25}
 RAIN_SILVER {50 250}
 RAIN_COPPER {500 2500}
 RAIN_PLATINUM {1 5}
 RAIN_GOLD {5 25}
 RAIN_SILVER {50 250}
 RAIN_COPPER {500 2500}

[FUNCTION RAIN_PLATINUM]
  NEWITEM=i_platinum_coin,<ARGS>
  NEW.MOVENEAR <src>,5
  IF !(<new.CanSeeLOS>)
	NEW.REMOVE
	RAIN_PLATINUM
	RETURN 1
  ENDIF  
  NEW.SOUND 55
  NEW.EFFECT 2 01ec6 0 16 0 0 0
  NEW.attr=<NEW.ATTR>|attr_decay 
  NEW.TIMER=600 //this should probably be called from .ini but I'm lazy now

[FUNCTION RAIN_GOLD]
  NEWITEM=i_gold_coin,<ARGS>
  NEW.MOVENEAR <src>,5
  IF !(<new.CanSeeLOS>)
	NEW.REMOVE
	RAIN_GOLD
	RETURN 1
  ENDIF  
  NEW.SOUND 55
  NEW.EFFECT 2 01ec6 0 16 0 0 0
  NEW.attr=<NEW.ATTR>|attr_decay 
  NEW.TIMER=600 //this should probably be called from .ini but I'm lazy now

[FUNCTION RAIN_SILVER]
  NEWITEM=i_silver_coin,<ARGS>
  NEW.MOVENEAR <src>,5
  IF !(<new.CanSeeLOS>)
	NEW.REMOVE
	RAIN_SILVER
	RETURN 1
  ENDIF  
  NEW.SOUND 55
  NEW.EFFECT 2 01ec6 0 16 0 0 0
  NEW.attr=<NEW.ATTR>|attr_decay 
  NEW.TIMER=600 //this should probably be called from .ini but I'm lazy now

[FUNCTION RAIN_COPPER]
  NEWITEM=i_copper_coin,<ARGS>
  NEW.MOVENEAR <src>,5
  IF !(<new.CanSeeLOS>)
	NEW.REMOVE
	RAIN_COPPER
	RETURN 1
  ENDIF  
  NEW.SOUND 55
  NEW.EFFECT 2 01ec6 0 16 0 0 0
  NEW.attr=<NEW.ATTR>|attr_decay 
  NEW.TIMER=600 //this should probably be called from .ini but I'm lazy now

[EVENTS e_combat_move_intel]
ON=@NPCActFight
IF !<TAG0.WeaponSkillEnabled> && !(<FINDID.i_combatmove_timer>) && !<R6> 
	IF !<Weapon>
		LOCAL.COMSKILL=WRESTLING
	ELSE
		LOCAL.COMSKILL=<Weapon.skill>
	ENDIF
	IF (<I.<LOCAL.COMSKILL>> >= 900)
		DORAND 3
			TAG.WeaponSkillEnabled=<QVAL <Weapon> ? <Weapon.tag0.Manuever3> : <serv.chardef.<ID>.Tag0.Manuever3>>
			TAG.WeaponSkillEnabled=<QVAL <Weapon> ? <Weapon.tag0.Manuever2> : <serv.chardef.<ID>.Tag0.Manuever2>>
			TAG.WeaponSkillEnabled=<QVAL <Weapon> ? <Weapon.tag0.Manuever1> : <serv.chardef.<ID>.Tag0.Manuever1>>
		ENDDO
	ELIF (<I.<LOCAL.COMSKILL>> >= 700)
		DORAND 2
			TAG.WeaponSkillEnabled=<QVAL <Weapon> ? <Weapon.tag0.Manuever2> : <serv.chardef.<ID>.Tag0.Manuever2>>
			TAG.WeaponSkillEnabled=<QVAL <Weapon> ? <Weapon.tag0.Manuever1> : <serv.chardef.<ID>.Tag0.Manuever1>>
		ENDDO
	ELIF (<I.<LOCAL.COMSKILL>> >= 500)
		TAG.WeaponSkillEnabled=<QVAL <Weapon> ? <Weapon.tag0.Manuever1> : <serv.chardef.<ID>.Tag0.Manuever1>>
	ENDIF
ENDIF

[EVENTS e_spawn_minion]
//Put a TAG.SPAWN equal to the id of the creature to spawn
//Otherwise it defaults to spawning one of its own kind
ON=@NPCActFight
IF !(<R10>) && (<STAM> >= 30) && (<HITS> >= 30)
	SERV.NEWNPC <QVAL <TAG0.SPAWN> ? <TAG.SPAWN> : <ID>>
	NEW.P=<P>
	NEW.HOME=<NEW.P>
	NEW.HOMEDIST=10
	STAM -= 30
	HITS -= 15
	EMOTE spawn a <NEW.NAME>
ENDIF

[EVENTS e_dividing]
on=@gethit
	//	say <Hval <argn2>>
     if (rand(2) == 1) && (<ARGN2> & dam_slash) && (<food> > 5)
          serv.newnpc=<ID>
          new.p=<p>
          new.home=<p>
          new.homedist=10 // don't run off
          new.hits=(<hits> / 2)
          new.food=(<food> / 2) //cannot divide indefinettely with stoping to eat
          food=(<food>/2) //cannot divide indefinettely with stoping to eat
          new.fix
          emote "divide"
          return 1
     endif

[EVENTS e_corpse_eater]
ON=@ItemStep
IF (<ACT.TYPE> == t_corpse) && !(<ACT.MOREY>)
	SPELLEFFECT S_GREATER_HEAL,1200,<UID>
	ACT.MOREY=1
	ACT.COLOR=0481
	EMOTE drain the corpse to heal
ENDIF

[EVENTS e_mind_whip] 

on=@NPCActFight
     if (rand(5) == 1) && (<mana> > 18)
		FOR 3
			DORAND 5
				src.spelleffect s_Hallucination,1000
				src.spelleffect s_feeblemind,1000
				src.spelleffect s_mana_drain,1000
				src.spelleffect s_pain_spike,1000
				src.spelleffect s_paralyze,1000
			enddo
		ENDFOR
		mana -= 18
		src.emote reel from a mind whip
	endif

[EVENTS e_acidic] 
ON=@HIT
     if (rand(4) == 1)
	     Local.Location=0
	     Src.Damage_Loc_<eval <Local.Location>>
	     Src.Damage_Loc_<eval <Local.Location>>
	     Src.Damage_Loc_<eval <Local.Location>>
	     SRC.SYSMESSAGE You are splashed with acid from <NAME>'s attack.
     endif

ON=@GETHIT
IF <SRC.WEAPON> && (rand(5) == 1)
  SRC.WEAPON.MORE1L -= 1
  SRC.SYSMESSAGE Your weapon takes acid damage from hitting <NAME>. 
   IF (<SRC.WEAPON.MORE1L> <= 0) 
    SRC.WEAPON.REMOVE 
    SRC.MESSAGE Your weapon melts in your grasp. 
    SRC.SYSMESSAGE You weapon was destroyed by the the acid damage from hitting <NAME>. 
   ENDIF 
  UPDATE 
  RETURN 0 
ENDIF

[ITEMDEF i_spawn_egg]
DEFNAME=i_spawn_egg
NAME=egg
ID=010dc
CATEGORY=Miscellaneous
SUBSECTION=Misc
DESCRIPTION=Spawning Egg

ON=@Timer
   f_egg_gestate <AMOUNT> //Call gestate loop <AMOUNT> times
   IF ( (<EVAL (0<LINK.TAG.NPCAI.LEIC.DieOnHatch>)>) && !(<EVAL (0<LINK.TAG.NPCAI.LEIC.TotalQuantity>)>) && (0<LINK.HITS>) ) //If I am out of eggs and I die on hatching all my eggs
     LINK.HITS=0  //Suicide (Die on hatch)
   ENDIF
 REMOVE
 RETURN 0

ON=@DClick
 IF ( ((<DISTANCE>)<(2)) ) //If SRC is close enough to egg
   SRC.EMOTE destroy <QVAL (<AMOUNT>-1) ? some eggs : an egg> //Emote action
   SOUND snd_MONSTER_SLIME1 //Play squishy noise
   SRC.ANIM 20FB  //Display single hand attack animation
   REMOVE   //Remove the spawning egg
 ELSE
   SRC.SYSMESSAGE You are too far away to destroy this egg.
 ENDIF
 RETURN 1

[FUNCTION f_egg_gestate]
 IF ( (<ARGN>)<(1) ) //If loop has run its course
   RETURN 0  //Exit loop
 ENDIF
 IF !( (<SECTOR.COMPLEXITY>)>((<SERV.MAXCOMPLEXITY>)*(1)) ) //If sector is not too busy
   VAR.GESTATE.OldACT=<LINK.ACT> //Temporary ACT storage
   LINK.NEWNPC { <LINK.TAG.NPCAI.LEIC.Type1> <EVAL <LINK.TAG.NPCAI.LEIC.Weight1>> <LINK.TAG.NPCAI.LEIC.Type2> <EVAL <LINK.TAG.NPCAI.LEIC.Weight2>> <LINK.TAG.NPCAI.LEIC.Type3> <EVAL <LINK.TAG.NPCAI.LEIC.Weight3>> <LINK.TAG.NPCAI.LEIC.Type4> <EVAL <LINK.TAG.NPCAI.LEIC.Weight4>> <LINK.TAG.NPCAI.LEIC.Type5> <EVAL <LINK.TAG.NPCAI.LEIC.Weight5>> } //Generate an NPC
   //LINK.ACT.FLAGS=<LINK.ACT.FLAGS>|010000000
   LINK.ACT.P=<P> //Set new NPC's position to that of the egg
   LINK.ACT.UPDATE //Update the new NPC
   LINK.ACT=<VAR.GESTATE.OldACT> //Restore the old ACT
 ENDIF
 f_egg_gestate <EVAL (<ARGN>-1)> //Recurse
 RETURN 0

[EVENTS e_layeggsincorpse]
ON=@ItemStep
 IF ( (<ACT.BASEID>==i_corpse) && !((<FLAGS>)&(statf_war)) && (((<EVAL <TAG.NPCAI.LEIC.TotalQuantity>>)>(0)) || ((<EVAL <TAG.NPCAI.LEIC.TotalQuantity>>)==(-1))) ) && !(<ACT.MOREY>)
   ACT.MOREY=1
   ACT.COLOR=0481
   NEWITEM i_spawn_egg   //Create a spawning egg
   ACT.DISPID=<TAG.NPCAI.LEIC.Graphic> //Set its graphic
   ACT.COLOR=<TAG.NPCAI.LEIC.Color> //Set its color
   ACT.LINK=<UID>   //Set its link to be the egg layer
   ACT.AMOUNT=<EVAL {1 <TAG.NPCAI.LEIC.Amount>}> //Set its amount
   IF !( (<TAG.NPCAI.LEIC.TotalQuantity>)==(-1) ) //If I have a limited number of eggs
     TAG.NPCAI.LEIC.TotalQuantity=<EVAL (<TAG.NPCAI.LEIC.TotalQuantity>-<ACT.AMOUNT>)> //Update my number of eggs left
     IF ( (<TAG.NPCAI.LEIC.Amount>)>(<TAG.NPCAI.LEIC.TotalQuantity>) ) //If the amount laid is greater than the amount I have left
       TAG.NPCAI.LEIC.Amount=<TAG.NPCAI.LEIC.TotalQuantity> //Change the amount laid
     ENDIF
   ENDIF
   TAG.NPCAI.EggsLaid=<EVAL ((0<TAG.NPCAI.EggsLaid>)+(<ACT.AMOUNT>))> //Add amount of eggs laid to total amount laid
   IF !( (0<TAG.NPCAI.InvisibleEggLaying>) )
     IF ( (<ACT.AMOUNT>)>1 ) //If there is more than one egg
       ACT.NAME=eggs  //Set name to be plural
       EMOTE <TAG.NPCAI.LEIC.Message> eggs
     ELSE   //If there is only one egg
       EMOTE <TAG.NPCAI.LEIC.Message> an egg
     ENDIF
   ENDIF
   ACT.ATTR=08052
   IF ( (0<TAG.NPCAI.InvisibleEggLaying>) )
     ACT.ATTR=<ACT.ATTR>|080 //Make egg invisible
   ENDIF
   ACT.P=<P>   //Set egg's position to egg layers position
   ACT.TIMER={<EVAL (<TAG.NPCAI.LEIC.GestationTime>/2)> <EVAL ((<TAG.NPCAI.LEIC.GestationTime>/2)*3)>} //Set egg timer around average gestation time
 ENDIF
 RETURN 1

[eof]
