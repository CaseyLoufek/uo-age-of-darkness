
//
// Contains string descriptions for each gesture (used as an EMOTE if Gesture_Display is 1).
//
[DEFNAME Gesture_Descriptions]
Gesture_Display 0
Gesture_06  "Yawn"
Gesture_015 "Faint"
Gesture_020 "Bow"
Gesture_021 "Salute"
Gesture_064 "Applaud"
Gesture_066 "Argue"
Gesture_068 "Blow a Kiss"
Gesture_069 "Bow Formally"
Gesture_06B "Cover ears"
Gesture_06C "Curtsey"
Gesture_06D "Do a Jig"
Gesture_06E "Folk Dance"
Gesture_06F "Dance"
Gesture_070 "Tribal Dance"
Gesture_071 "Fold Arms"
Gesture_072 "is Impatient"
Gesture_073 "Give a Lecture"
Gesture_074 "Nod"
Gesture_075 "Point"
Gesture_077 "Greet with a Salute"
Gesture_079 "Shake Head"
Gesture_07B "is Victorious"
Gesture_07C "Celebrate"
Gesture_07D "Wave"
Gesture_07E "Wave with Two Hands"
Gesture_07F "Wave Long Distance"
Gesture_080 "is Confused"

//
// Function to handle packet 191 (0xBF)
// Used to process Gestures.
// Usage: Add 'PACKET191=F_Packet191' to Sphere.INI
//
[FUNCTION F_Packet191]
IF (<LOCAL.2> != 09) || (<LOCAL.4> != 0e)
  RETURN 0
ENDIF
OBJ = <LOCAL.CHAR>
IF (<ISEMPTY <DEF.GESTURE_<LOCAL.8>>>)
  SERV.LOG @Unknown Gesture: <LOCAL.8>
  RETURN 0
ENDIF
IF (<DEF.GESTURE_DISPLAY>)
  OBJ.EMOTE <DEF.GESTURE_<LOCAL.8>>
ENDIF
OBJ.F_PERFORM_GESTURE <LOCAL.CHAR> <LOCAL.8>
RETURN 1


//
// Performs an animation on the specified character.
// Usage: F_Perform_Gesture <CharacterUid> <GestureID>
//
[FUNCTION F_Perform_Gesture]
FORCLIENTS 18
  SENDPACKET 06e D<ARGN1> W<ARGN2> 00 00 W01 00 00 01F
ENDFOR