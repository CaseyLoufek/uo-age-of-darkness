[ITEMDEF 0e9b]
//Mortar & Pestle
DEFNAME=i_mortar_pestle
TYPE=T_weapon_mace_pick
RESOURCES=3 i_ingot_iron
WEIGHT=1
CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Magical Items
DESCRIPTION=Mortar and Pestle
SKILLMAKE=TINKERING 23.3,t_tinker_tools

ON=@CREATE
	HITPOINTS=50

ON=@DCLICK
IF (<SRC.ACTION>!=-1) || (<SRC.CTAG0.CRAFTING>)
	SRC.SYSMESSAGE You must wait to perform another action.
	RETURN 1
ENDIF
DIALOG d_skillmenu_Alchemy
RETURN 1

[DEFNAME def_skmenu_Alchemy]
skmenu_Alchemy_flags 01 //01=markoption 02=resmelt 04=repair 08=enhance

skmenu_Alchemy_0_name ENCHANCEMENT
skmenu_Alchemy_0_0 i_potion_Agility
skmenu_Alchemy_0_1 i_potion_AgilityGreat
skmenu_Alchemy_0_2 i_potion_Nightsight
skmenu_Alchemy_0_3 i_potion_Strength
skmenu_Alchemy_0_4 i_potion_StrengthGreat
skmenu_Alchemy_0_5 i_potion_Clever
skmenu_Alchemy_0_6 i_potion_CleverGreat
skmenu_Alchemy_0_7 i_potion_StoneskinLess
skmenu_Alchemy_0_8 i_potion_Stoneskin
skmenu_Alchemy_0_9 i_potion_StoneskinGreat

skmenu_Alchemy_1_name RESTORATIVE
skmenu_Alchemy_1_0 i_potion_HealLess
skmenu_Alchemy_1_1 i_potion_Heal
skmenu_Alchemy_1_2 i_potion_HealGreat
skmenu_Alchemy_1_3 i_potion_Restore
skmenu_Alchemy_1_4 i_potion_RestoreGreat
skmenu_Alchemy_1_5 i_potion_Mana
skmenu_Alchemy_1_6 i_potion_ManaTotal
skmenu_Alchemy_1_7 i_potion_Refresh
skmenu_Alchemy_1_8 i_potion_RefreshTotal
skmenu_Alchemy_1_9 i_potion_CureLess
skmenu_Alchemy_1_10 i_potion_Cure
skmenu_Alchemy_1_11 i_potion_CureGreat

skmenu_Alchemy_2_name OFFENSIVE
skmenu_Alchemy_2_0 i_potion_ExplosionLess
skmenu_Alchemy_2_1 i_potion_Explosion
skmenu_Alchemy_2_2 i_potion_ExplosionGreat
skmenu_Alchemy_2_3 i_potion_PoisonLess
skmenu_Alchemy_2_4 i_potion_Poison
skmenu_Alchemy_2_5 i_potion_PoisonGreat
skmenu_Alchemy_2_6 i_potion_PoisonDeadly

skmenu_Alchemy_3_name MISC
skmenu_Alchemy_3_0 i_potion_Invisibility
skmenu_Alchemy_3_1 i_potion_Incognito
skmenu_Alchemy_3_2 i_potion_Shrink

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//MAIN FUNCTIONS
//Here there are the "core" functions that control the crafting procedure. 
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

//This is the main craft function
[FUNCTION f_skmenu_Alchemy_makeitem]
//preliminary checks
IF (<SRC.ACTION>!=-1) || (<SRC.FINDID.i_Alchemy_craft>)
	SRC.SYSMESSAGE You must wait to perform another action
	RETURN 1
ENDIF
IF !(<SRC.CANMAKESKILL <ARGS>>)
	SRC.CTAG0.SKMENU_Alchemy_NOTICE You don't have the required skills to attempt this item.
	DIALOG d_skillmenu_Alchemy
	RETURN 1
ENDIF

//check resources
LOCAL.RESOURCES=<f_resource_replace <ARGS>,i_dough,<DEF.skmenu_Alchemy_subres_<SRC.dCTAG0.SKMENU_Alchemy_SUBRES>>>

IF !(<SRC.RESTEST <LOCAL.RESOURCES>>)
	SRC.CTAG0.SKMENU_Alchemy_NOTICE You don't have the resources to make that.
	DIALOG d_skillmenu_Alchemy
	RETURN 1
ENDIF
SRC.CONSUME <LOCAL.RESOURCES>

//update the "last ten" list
IF (<ARGS>!=<SRC.CTAG0.SKMENU_Alchemy_LASTTEN_0>) 
	FOR 9 1
		TRY SRC.CTAG0.SKMENU_Alchemy_LASTTEN_<dLOCAL._FOR> = <SRC.CTAG.SKMENU_Alchemy_LASTTEN_<EVAL <LOCAL._FOR>-1>>
	ENDFOR
	SRC.CTAG0.SKMENU_Alchemy_LASTTEN_0 <ARGS>
ENDIF

//start crafting
SERV.NEWITEM i_Alchemy_craft
NEW.CONT=<SRC.UID>
NEW.MORE=<SRC.CTAG0.SKMENU_Alchemy_SUBRES>
NEW.LINK=<UID> //linked to the tool
NEW.TIMER=1
NEW.TAG.ITEM=<ARGS>
SRC.ANIM 33
SRC.EVENTS +e_Alchemy_crafting

//This is the memory item used for the crafting
[ITEMDEF i_Alchemy_craft]
ID=02007
NAME=Alchemy craft memory
TYPE=t_eq_script

ON=@CREATE
MOREX={2 4}

ON=@TIMER	
CONT.SFX 055
CONT.ANIM 33
MOREX -= 1
IF (<MOREX>)
	TIMER 1
	RETURN 1
ENDIF
LOCAL.CHANCE=<CONT.f_skmenu_Alchemy_successchance <TAG.ITEM>, <LINK.UID>>
IF (RAND(1000)<<LOCAL.CHANCE>) //success! we can create the item
	SERV.NEWITEM <TAG.ITEM> 
	LOCAL.EXCEP=<CONT.f_skmenu_Alchemy_exceptionalchance <TAG.ITEM>, <LINK.UID>>
	VAR.ARGN1=<LOCAL.EXCEP>
	VAR.ARGN2=<MORE>
	LINK.TRIGGER=@MakeItem
	LOCAL.EXCEP=<VAR.ARGN1>
	MORE=<VAR.ARGN2>
	IF (RAND(1000)<<LOCAL.EXCEP>)
		NEW.QUALITY=201
		IF (<CONT.CTAG0.SKMENU_Alchemy_MARKOPTION>==0)
				NEW.TAG0.CRAFTEDBY=<CONT.NAME>
				LOCAL.POTENCY=Extremely Weak
				IF (<MORE2> > 110.0)
					LOCAL.POTENCY=Supremely Potent
				ELSEIF (<MORE2> > 95.0)
					LOCAL.POTENCY=Extremely Potent
				ELSEIF (<MORE2> > 80.0)
					LOCAL.POTENCY=Greater
				ELSEIF (<MORE2> > 65.0)
					LOCAL.POTENCY=Moderate
				ELSEIF (<MORE2> > 50.0)
					LOCAL.POTENCY=Lesser
				ENDIF
			NAME=<LOCAL.POTENCY> <SERV.SPELL.<MORE1>.NAME> potion%s

		ELSEIF (<CONT.CTAG0.SKMENU_Alchemy_MARKOPTION>==2)
			CONT.CTAG0.SKMENU_Alchemy_TOOL=<LINK.UID>
			TRYSRC <CONT.UID> NEW.DIALOG d_skillmenu_Alchemy_querymakersmark
			LOCAL.NODIALOG=1
		ENDIF
		new.more2 += (<new.more2>*<CONT.TASTEID>/4000)+5
	ELSE
		NEW.QUALITY=<EVAL (<LOCAL.CHANCE>+<LOCAL.EXCEP>)/10> //arbitrary formula for quality calculation
	ENDIF
	trysrc <cont> new.bounce
	CONT.SOUND 057
	var.exp = (<eval rand(<NEW.VALUE>+<NEW.tag0.MODVALUE>)/2>)
	expeval <eval <var.exp>>
	cont.sysmessage You have gained <var.expmsg> experience.
	CONT.EXP += <eval <var.exp>>
	NEW.RESENDTOOLTIP
	LINK.HITS -= 1 //damage the tool
	IF (<LINK.HITS><=0)
		LINK.REMOVE
		CONT.SYSMESSAGE You have worn out your tool!
		IF (<NEW.QUALITY>>200) && !(<CONT.CTAG0.SKMENU_Alchemy_MARKOPTION>)
			CONT.SYSMESSAGE You create an exceptional quality item and affix your maker's mark.
		ELSEIF (<NEW.QUALITY>>200)
			CONT.SYSMESSAGE You create an exceptional quality item.
		ELSE
			CONT.SYSMESSAGE You create the item.
		ENDIF
		LOCAL.NODIALOG=1
	ELSE
		LINK.RESENDTOOLTIP
		IF (<NEW.QUALITY>>200) && !(<CONT.CTAG0.SKMENU_Alchemy_MARKOPTION>)
			CONT.CTAG0.SKMENU_Alchemy_NOTICE You create an exceptional quality item and affix your maker's mark.
		ELSEIF (<NEW.QUALITY>>200)
			CONT.CTAG0.SKMENU_Alchemy_NOTICE You create an exceptional quality item.
		ELSE
			CONT.CTAG0.SKMENU_Alchemy_NOTICE You create the item.
		ENDIF
	ENDIF
	IF (<LOCAL.CHANCE><1000) //gain skill only if the crafting was not trivial
		CONT.SKILLGAIN Alchemy <EVAL 10-(<LOCAL.CHANCE>/100)>
	ENDIF
ELSE
	LINK.HITS -= 1
	IF (<LINK.HITS><=0)
		LINK.REMOVE
		CONT.SYSMESSAGE You have worn out your tool!
		CONT.SYSMESSAGE You failed to create the item, and some of your materials are lost.
		LOCAL.NODIALOG=1
	ELSE
		LINK.RESENDTOOLTIP
		CONT.CTAG0.SKMENU_Alchemy_NOTICE You failed to make the potion, and some ingredients are lost.
	ENDIF
		LOCAL.I=1
		WHILE (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL> > 0)
			IF (<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>==i_bottle_empty)
				NEWITEM i_bottle_empty
				trysrc <cont> new.bounce
			ELSE
				NEWITEM <SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.KEY>
				NEW.AMOUNT=(<SERV.ITEMDEF.<TAG.ITEM>.RESOURCES.<LOCAL.I>.VAL>/2)
					IF !(<NEW.AMOUNT>==0)
						trysrc <cont> new.bounce
					ELSE
						NEW.REMOVE
					ENDIF
			ENDIF
			LOCAL.I += 1
		ENDWHILE
	IF (<CONT.ALCHEMY><30.0) //skill gain on failure only under 30.0 Alchemy
		CONT.SKILLGAIN ALCHEMY 1
	ENDIF
ENDIF
IF !(<LOCAL.NODIALOG>)
	TRYSRC <CONT.UID> LINK.DIALOG d_skillmenu_Alchemy
ENDIF
REMOVE
RETURN 1

ON=@UNEQUIP
SRC.EVENTS -e_Alchemy_crafting

[EVENTS e_Alchemy_crafting]
ON=@SKILLSTART
SRC.SYSMESSAGE You must wait to perform another action
ACTION -1
RETURN 1


[FUNCTION f_skmenu_Alchemy_successchance]
//argv[0] = item to craft
//argv[1] = the tool used in the craft (so that one can easily make magical tools that increase success chance or something like that)
IF !(<CANMAKESKILL <ARGV[0]>>)
	RETURN 0
ENDIF
//the skill checked for the success chance is only the first in the skillmake list
//I think this is a bit different from osi
LOCAL.CHANCE=<EVAL (<ALCHEMY>-<SERV.ITEMDEF.<ARGV[0]>.SKILLMAKE.1.VAL>)*2>
LOCAL.CHANCE -= <EVAL <DEF.skmenu_Alchemy_subres_<dCtag0.SKMENU_Alchemy_SUBRES>_skill>>
IF (<LOCAL.CHANCE>>1000)
	LOCAL.CHANCE=1000
ELSEIF (<LOCAL.CHANCE><0)
	LOCAL.CHANCE=0
ENDIF
RETURN <LOCAL.CHANCE>

[FUNCTION f_skmenu_Alchemy_exceptionalchance]
//argv[0] = item to craft
//argv[1] = the tool used in the craft
LOCAL.OFFSET=600-((<ALCHEMY>-950)*3)
IF (<LOCAL.OFFSET><450)
	LOCAL.OFFSET=450
ELSEIF (<LOCAL.OFFSET>>600)
	LOCAL.OFFSET=600
ENDIF
LOCAL.CHANCE=<f_skmenu_Alchemy_successchance <ARGV[0]>,<ARGV[1]>>-<LOCAL.OFFSET>
IF (<LOCAL.CHANCE>>1000)
	LOCAL.CHANCE=1000
ELSEIF (<LOCAL.CHANCE><0)
	LOCAL.CHANCE=0
ENDIF
RETURN <LOCAL.CHANCE>

[FUNCTION f_skmenu_Alchemy_material]
	TRY f_material_<DEF.skmenu_Alchemy_subres_<dARGN1>>

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//MENU
//This is the biggest part of the script, the skillmenu. It's organized in subfunctions which draw specific part of the dialog
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

[DIALOG d_skillmenu_Alchemy] //argn1=page
40,40
SRC.DIALOGCLOSE d_skillmenu_Alchemy
page 0

resizepic 0 0 5054 530 437
gumppictiled 10 10 510 22 2624 
gumppictiled 10 292 150 45 2624
gumppictiled 165 292 355 45 2624
gumppictiled 10 342 510 85 2624
gumppictiled 10 37 200 250 2624
gumppictiled 215 37 305 250 2624
checkertrans 10 10 510 417

dhtmlgump 10 12 510 20 0 0 <htmlFormatTitle ALCHEMY MENU>
dhtmlgump 10 37 200 22 0 0 <htmlFormatTitle CATEGORIES>
dhtmlgump 215 37 305 22 0 0 <htmlFormatTitle SELECTIONS>
dhtmlgump 10 302 150 25 0 0 <htmlFormatTitle NOTICES>

button 15 402 4017 4019 1 0 0
dhtmlgump 50 405 150 18 0 0 <htmlFormat EXIT>

button 270 402 4005 4007 1 0 <buttonID 6 2>
dhtmlgump 305 405 150 18 0 0 <htmlFormat MAKE LAST>

IF (<DEF.skmenu_Alchemy_flags>&01)
	button 270 362 4005 4007 1 0 <buttonID 6 6>
	DOSWITCH (<SRC.CTAG0.SKMENU_Alchemy_MARKOPTION>)
	dhtmlgump 305 365 150 18 0 0 <htmlFormat MARK ITEM>
	dhtmlgump 305 365 150 18 0 0 <htmlFormat DO NOT MARK>
	dhtmlgump 305 365 150 18 0 0 <htmlFormat PROMPT FOR MARK>
	ENDDO
ENDIF
IF (<DEF.skmenu_Alchemy_flags>&02)
	button 15 342 4005 4007 1 0 <buttonID 6 1>
	dhtmlgump 50 345 150 18 0 0 <htmlFormat SMELT ITEM>
ENDIF
IF (<DEF.skmenu_Alchemy_flags>&04)
	button 270 342 4005 4007 1 0 <buttonID 6 5>
	dhtmlgump 305 345 150 18 0 0 <htmlFormat REPAIR ITEM>
ENDIF
IF (<DEF.skmenu_Alchemy_flags>&08)
	button 270 382 4005 4007 1 0 <buttonId 6 8>
	dhtmlgump 305 385 150 18 0 0 <htmlFormat ENHANCE ITEM>
ENDIF
IF !(<ISEMPTY <SRC.CTAG.SKMENU_Alchemy_NOTICE>>)
	dhtmlgump 170 295 350 40 0 0 <htmlFormat <SRC.CTAG.SKMENU_Alchemy_NOTICE>>
	SRC.CTAG.SKMENU_Alchemy_NOTICE
ENDIF
//colored logs
IF (<DEF0.skmenu_Alchemy_subres_0>)
	button 15 362 4005 4007 1 0 <buttonId 6 0>
	dhtmlgump 50 365 250 18 0 0 <htmlFormat <SERV.ITEMDEF.<DEF.skmenu_Alchemy_subres_<SRC.dCTAG0.SKMENU_Alchemy_SUBRES>>.NAME>>
ENDIF

f_skmenu_Alchemy_categories
IF (0<ARGS>==1)
	f_skmenu_Alchemy_subres
ELSE
	f_skmenu_Alchemy_items <SRC.CTAG0.SKMENU_Alchemy_LASTCATEGORY>
ENDIF

[DIALOG d_skillmenu_Alchemy BUTTON]
ON=1 0ffffffff
ARGN1 -= 1
LOCAL.TYPE=<ARGN1>%8
LOCAL.INDEX=<ARGN1>/8
IF (<LOCAL.TYPE>==0) // show group
	SRC.CTAG0.SKMENU_Alchemy_LASTCATEGORY=<LOCAL.INDEX>
	DIALOG d_skillmenu_Alchemy
ELSEIF (<LOCAL.TYPE>==1) // create item
	f_skmenu_Alchemy_makeitem <DEF.skmenu_Alchemy_<SRC.dCTAG0.SKMENU_Alchemy_LASTCATEGORY>_<dLOCAL.INDEX>>
ELSEIF (<LOCAL.TYPE>==2) // item details
	DIALOG d_skillmenu_Alchemy_details 0, <LOCAL.INDEX>
ELSEIF (<LOCAL.TYPE>==3) // create item (last ten)
	f_skmenu_Alchemy_makeitem <SRC.CTAG0.SKMENU_Alchemy_LASTTEN_<dLOCAL.INDEX>>
ELSEIF (<LOCAL.TYPE>==4) // item details (last ten) [this is redundant]
	DIALOG d_skillmenu_Alchemy_details 0, <LOCAL.INDEX>
ELSEIF (<LOCAL.TYPE>==5) //resource selected
	IF (<DEF.skmenu_Alchemy_subres_<dLOCAL.INDEX>>)
		IF (<DEF.skmenu_Alchemy_subres_<dLOCAL.INDEX>_skill>><SRC.ALCHEMY>)
			SRC.CTAG.SKMENU_Alchemy_NOTICE You cannot work this strange and unusual ingredient.
			DIALOG d_skillmenu_Alchemy 0, 1
			RETURN 1
		ENDIF
		SRC.CTAG0.SKMENU_Alchemy_SUBRES=<LOCAL.INDEX>
		SRC.CTAG.SKMENU_Alchemy_NOTICE Material: <SERV.ITEMDEF.<DEF.skmenu_Alchemy_subres_<dLOCAL.INDEX>>.NAME>
		DIALOG d_skillmenu_Alchemy
	ENDIF
ELSEIF (<LOCAL.TYPE>==6) //misc
	IF (<LOCAL.INDEX>==0) //resource selection
		IF (<DEF0.skmenu_Alchemy_subres_0>)
			DIALOG d_skillmenu_Alchemy 0, 1
		ENDIF
	ELSEIF (<LOCAL.INDEX>==1) //resmelt
		IF (<DEF0.skmenu_Alchemy_flags>&02)
			f_skmenu_Alchemy_smelt
		ENDIF
	ELSEIF (<LOCAL.INDEX>==2) //make last
		IF (<SRC.CTAG0.SKMENU_Alchemy_LASTTEN_0>)
			f_skmenu_Alchemy_makeitem <SRC.CTAG0.SKMENU_Alchemy_LASTTEN_0>
		ELSE
			SRC.CTAG.SKMENU_Alchemy_NOTICE You haven't made anything yet.
			DIALOG d_skillmenu_Alchemy
		ENDIF
	ELSEIF (<LOCAL.INDEX>==3) //last 10 items made
		SRC.CTAG0.SKMENU_Alchemy_LASTCATEGORY=-1
		DIALOG d_skillmenu_Alchemy
	ELSEIF (<LOCAL.INDEX>==4) // Toggle use resource hue
		IF (<DEF0.skmenu_Alchemy_flags>&010)
		SRC.CTAG0.SKMENU_Alchemy_DONOTCOLOR ^= 1
		DIALOG d_skillmenu_Alchemy 0, 1
		ENDIF
	ELSEIF (<LOCAL.INDEX>==5) //repair
		IF (<DEF0.skmenu_Alchemy_flags>&04)
			SRC.REPAIR
		ENDIF
	ELSEIF (<LOCAL.INDEX>==6) //toggle mark option
		IF (<DEF0.skmenu_Alchemy_flags>&01)
			DOSWITCH <SRC.CTAG0.SKMENU_Alchemy_MARKOPTION>
			SRC.CTAG.SKMENU_Alchemy_MARKOPTION = 1
			SRC.CTAG.SKMENU_Alchemy_MARKOPTION = 2
			SRC.CTAG.SKMENU_Alchemy_MARKOPTION = 0
			ENDDO
		ENDIF
		DIALOG d_skillmenu_Alchemy
	ELSEIF (<LOCAL.INDEX>==8) //enhance
		IF (<DEF0.skmenu_Alchemy_flags>&08)
			f_skmenu_Alchemy_enhance
		ENDIF
	ELSEIF (<LOCAL.INDEX>==9) // Toggle use resource hue (subres2 page)
		IF (<DEF0.skmenu_Alchemy_flags>&010)
		SRC.CTAG0.SKMENU_Alchemy_DONOTCOLOR ^= 1
		DIALOG d_skillmenu_Alchemy 0, 2
		ENDIF
	ENDIF
ENDIF

[FUNCTION f_skmenu_Alchemy_categories]
button 15 60 4005 4007 1 0 <buttonId 6 3>
dhtmlgump 50 63 150 18 0 0 <htmlFormat LAST TEN>
WHILE (<DEF0.skmenu_Alchemy_<dLOCAL.I>_0>)
	button 15 <EVAL 80+(<LOCAL.I>*20)> 4005 4007 1 0 <buttonId 0 <LOCAL.I>>
	dhtmlgump 50 <EVAL 83+(<LOCAL.I>*20)> 150 18 0 0 <htmlFormat <DEF.skmenu_Alchemy_<dLOCAL.I>_name>>
	LOCAL.I += 1
ENDWHILE

[FUNCTION f_skmenu_Alchemy_items] //argn1=selected category
IF (<ARGN1>==-1)
	WHILE (<SRC.CTAG0.SKMENU_Alchemy_LASTTEN_<dLOCAL.I>>)
		LOCAL.INDEX=<LOCAL.I>%10
		LOCAL.ITEM=<SRC.CTAG0.SKMENU_Alchemy_LASTTEN_<dLOCAL.I>>
		IF (<LOCAL.INDEX>==0)
			IF (<LOCAL.I>>0)
				button 370 260 4005 4007 0 <EVAL (<LOCAL.I>/10)+1> 0
				dhtmlgump 405 263 100 18 0 0 <htmlFormat NEXT PAGE>
			ENDIF
			page <EVAL (<LOCAL.I>/10)+1>
			IF (<LOCAL.I>>0)
				button 220 260 4014 4016 0 <EVAL (<LOCAL.I>/10)> 0
				dhtmlgump 255 263 100 18 0 0 <htmlFormat PREV PAGE>
			ENDIF
		ENDIF
		button 220 <EVAL 60+(<LOCAL.INDEX>*20)> 4005 4007 1 0 <buttonId 3 <LOCAL.I>>
		dhtmlgump 255 <EVAL 63+(<LOCAL.INDEX>*20)> 220 18 0 0 <htmlFormat <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>
		button 480 <EVAL 60+(<LOCAL.INDEX>*20)> 4011 4012 1 0 <buttonId 4 <LOCAL.I>>
		LOCAL.I += 1
	ENDWHILE
ELSE
	WHILE (<DEF0.skmenu_Alchemy_<dARGN1>_<dLOCAL.I>>)
		LOCAL.INDEX=<LOCAL.I>%10
		LOCAL.ITEM=<DEF.skmenu_Alchemy_<dARGN1>_<dLOCAL.I>>
		IF (<LOCAL.INDEX>==0)
			IF (<LOCAL.I>>0)
				button 370 260 4005 4007 0 <EVAL (<LOCAL.I>/10)+1> 0
				dhtmlgump 405 263 100 18 0 0 <htmlFormat NEXT PAGE>
			ENDIF
			page <EVAL (<LOCAL.I>/10)+1>
			IF (<LOCAL.I>>0)
				button 220 260 4014 4016 0 <EVAL (<LOCAL.I>/10)> 0
				dhtmlgump 255 263 100 18 0 0 <htmlFormat PREV PAGE>
			ENDIF
		ENDIF
		button 220 <EVAL 60+(<LOCAL.INDEX>*20)> 4005 4007 1 0 <buttonId 1 <LOCAL.I>>
		dhtmlgump 255 <EVAL 63+(<LOCAL.INDEX>*20)> 220 18 0 0 <htmlFormat <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>
		button 480 <EVAL 60+(<LOCAL.INDEX>*20)> 4011 4012 1 0 <buttonId 2 <LOCAL.I>>
		LOCAL.I += 1
	ENDFOR
ENDIF

[FUNCTION f_skmenu_Alchemy_subres]
WHILE (<DEF0.skmenu_Alchemy_subres_<dLOCAL.I>>)
	LOCAL.INDEX=<LOCAL.I>%10
	IF (<LOCAL.INDEX>==0)
		IF (<LOCAL.I>>0)
			button 485 260 4005 4007 0 <EVAL (<LOCAL.I>/10)+1> 0
		ENDIF
		page <EVAL (<LOCAL.I>/10)+1>
		IF (<LOCAL.I>>0)
			button 455 260 4014 4016 0 <EVAL (<LOCAL.I>/10)> 0
		ENDIF
//		button 220 260 4005 4007 1 0 <buttonID 6 4>
//		dhtmlgump 255 263 200 18 0 0 <htmlFormat <QVAL <SRC.CTAG0.SKMENU_Alchemy_DONOTCOLOR>?USE MATERIAL COLOR:DO NOT COLOR>>
	ENDIF
	button 220 <EVAL 60+(<LOCAL.INDEX>*20)> 4005 4007 1 0 <buttonID 5 <LOCAL.I>>
	dhtmlgump 255 <EVAL 63+(<LOCAL.INDEX>*20)> 250 18 0 0 <htmlFormat <SERV.ITEMDEF.<DEF0.skmenu_Alchemy_subres_<dLOCAL.I>>.NAME>>
	LOCAL.I += 1
ENDWHILE


[DIALOG d_skillmenu_Alchemy_details]
40,40
SRC.DIALOGCLOSE d_skillmenu_Alchemy
SRC.DIALOGCLOSE d_skillmenu_Alchemy_details
page 0

resizepic 0 0 5054 530 417
gumppictiled 10 10 510 22 2624 
gumppictiled 10 37 150 148 2624
gumppictiled 165 37 355 90 2624
gumppictiled 10 190 155 22 2624
gumppictiled 10 217 150 53 2624
gumppictiled 165 132 355 80 2624
gumppictiled 10 275 155 22 2624
gumppictiled 10 302 150 53 2624
gumppictiled 165 217 355 80 2624
gumppictiled 10 360 155 22 2624
gumppictiled 165 302 355 80 2624
gumppictiled 10 387 510 22 2624
checkertrans 10 10 510 399

dhtmlgump 10 12 510 20 0 0 <htmlFormatTitle Alchemy MENU>
dhtmlgump 170 40 150 20 0 0 <htmlFormatTitle ITEM>
dhtmlgump 10 192 150 22 0 0 <htmlFormatTitle SKILLS>
dhtmlgump 10 277 150 22 0 0 <htmlFormatTitle MATERIALS>
dhtmlgump 10 362 150 22 0 0 <htmlFormatTitle OTHER>

button 15 387 4014 4016 1 0 0
dhtmlgump 50 390 150 18 0 0 <htmlFormat BACK>

button 270 387 4005 4007 1 0 <EVAL <ARGS>+1>
dhtmlgump 305 390 150 18 0 0 <htmlFormat MAKE NOW>
IF (<SRC.CTAG0.SKMENU_Alchemy_LASTCATEGORY>==-1) // pick item from "last ten" list
	LOCAL.ITEM=<SRC.CTAG.SKMENU_Alchemy_LASTTEN_<dARGS>>
ELSE
	LOCAL.ITEM=<DEF0.skmenu_Alchemy_<SRC.dCTAG0.SKMENU_Alchemy_LASTCATEGORY>_<dARGS>>
ENDIF
dhtmlgump 330 40 180 18 0 0 <htmlFormat <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>
dorigin - 282 
IF (<SERV.ITEMDEF.<LOCAL.ITEM>.CAN>&CAN_I_REPLICATE)
	dhtmlgump 170 *20 310 18 <htmlFormat Make as many as possible at once>
ENDIF

tilepic 20 50 <EVAL ID(<SERV.ITEMDEF.<LOCAL.ITEM>.DISPID>)>
dhtmlgump 170 *20 310 18 0 0 <HtmlFormat This item may hold it's maker mark>

f_skmenu_Alchemy_details_drawskills <LOCAL.ITEM>
f_skmenu_Alchemy_details_drawresources <LOCAL.ITEM>

[DIALOG d_skillmenu_Alchemy_details BUTTON]
ON=0 //back
DIALOG d_skillmenu_Alchemy

ON=1 0ffffffff //make
ARGN1 -= 1
IF (<SRC.CTAG0.SKMENU_Alchemy_LASTCATEGORY>==-1) // pick item from "last ten" list
	LOCAL.ITEM=<SRC.CTAG.SKMENU_Alchemy_LASTTEN_<dARGN1>>
ELSE
	LOCAL.ITEM=<DEF0.skmenu_Alchemy_<SRC.dCTAG0.SKMENU_Alchemy_LASTCATEGORY>_<dARGN1>>
ENDIF
f_skmenu_Alchemy_makeitem <LOCAL.ITEM>


[FUNCTION f_skmenu_Alchemy_details_drawskills]
LOCAL.I=1
WHILE (<SERV.ITEMDEF.<ARGS>.SKILLMAKE.<LOCAL.I>.VAL>)
	dhtmlgump 170 <EVAL 112+(<LOCAL.I>*20)> 200 18 0 0 <HtmlFormat <SERV.SKILL.<SERV.ITEMDEF.<ARGS>.SKILLMAKE.<LOCAL.I>.KEY>.NAME>>
	dhtmlgump 430 <EVAL 112+(<LOCAL.I>*20)> 90 18 0 0 <HtmlFormat <FVAL <SERV.ITEMDEF.<ARGS>.SKILLMAKE.<LOCAL.I>.VAL>>>
	LOCAL.I += 1
ENDWHILE
dhtmlgump 170 80 250 18 0 0 <HtmlFormat Success Chance:>
dhtmlgump 430 80 90 18 0 0 <HtmlFormat <FVAL <SRC.f_skmenu_Alchemy_successchance <ARGS>, <UID>>>%>
dhtmlgump 170 100 250 18 0 0 <HtmlFormat Exceptional Chance:>
dhtmlgump 430 100 90 18 0 0 <HtmlFormat <FVAL <SRC.f_skmenu_Alchemy_exceptionalchance <ARGS>, <UID>>>%>

[FUNCTION f_skmenu_Alchemy_details_drawresources]
LOCAL.I=1
WHILE (<SERV.ITEMDEF.<ARGS>.RESOURCES.<LOCAL.I>.VAL>)
	IF (<SERV.ITEMDEF.<ARGS>.RESOURCES.<LOCAL.I>.KEY>==i_log) && (<SRC.CTAG0.SKMENU_Alchemy_SUBRES>)
		dhtmlgump 170 <EVAL 199+(<LOCAL.I>*20)> 310 18 0 0 <HtmlFormat <SERV.ITEMDEF.<DEF.skmenu_Alchemy_subres_<SRC.dCTAG0.SKMENU_Alchemy_SUBRES>>.NAME>>
		IF !(<SRC.CTAG0.SKMENU_Alchemy_DONOTCOLOR>)
			dhtmlgump 170 *20 310 18 0 0 <HtmlFormat * The item retains the color of this material>
			dhtmlgump 500 <EVAL 199+(<LOCAL.I>*20)> 18 18 0 0 <HtmlFormat *>
		ENDIF
	ELSE
		dhtmlgump 170 <EVAL 199+(<LOCAL.I>*20)> 310 18 0 0 <HtmlFormat <SERV.ITEMDEF.<SERV.ITEMDEF.<ARGS>.RESOURCES.<LOCAL.I>.KEY>.NAME>>
	ENDIF
	dhtmlgump 430 <EVAL 199+(<LOCAL.I>*20)> 310 18 0 0 <HtmlFormat <SERV.ITEMDEF.<ARGS>.RESOURCES.<LOCAL.I>.VAL>>
	LOCAL.I += 1
ENDWHILE


[DIALOG d_skillmenu_Alchemy_querymakersmark]
40,40
resizepic 0 0 5054 220 170
resizepic 10 10 3000 200 150
dhtmlgump 20 20 180 80 0 0 <HtmlFormat Do you wish to place your maker's mark on this item?>
dhtmlgump 55 100 140 25 0 0 <HtmlFormat CONTINUE>
button 20 100 4005 4007 1 0 1
dhtmlgump 55 125 140 25 0 0 <HtmlFormat CANCEL>
button 20 125 4005 4007 1 0 0

[DIALOG d_skillmenu_Alchemy_querymakersmark BUTTON]
ON=0
SRC.SYSMESSAGE Cancelled mark.
IF (<UID.<SRC.CTAG0.SKMENU_Alchemy_TOOL>>) //make sure the tool still exists
	SRC.CTAG0.SKMENU_Alchemy_NOTICE=You create an exceptional quality item
	TRY UID.<SRC.CTAG0.SKMENU_Alchemy_TOOL>.DIALOG d_skillmenu_Alchemy
ENDIF

ON=1
SRC.SYSMESSAGE You mark the item.
TAG.CRAFTEDBY <SRC.UID>
RESENDTOOLTIP
IF (<UID.<SRC.CTAG0.SKMENU_Alchemy_TOOL>>) //make sure the tool still exists
	SRC.CTAG0.SKMENU_Alchemy_NOTICE=You create an exceptional quality item and affix your maker's mark.
	TRY UID.<SRC.CTAG0.SKMENU_Alchemy_TOOL>.DIALOG d_skillmenu_Alchemy
ENDIF



[EOF]