//*****************************************************************
//**   NPC Weapon Switcher
//**        By Lerkur
//**  Last Edited 10/01/02
//**  
//**  This script contains:
//**   -f_equip_melee_weapon function
//**   -f_equip_ranged_weapon function
//**   -e_npc_weapon_switcher events
//*****************************************************************

[EVENTS e_combat_medusa]
ON=@GetHit
   IF (RAND(2)==1)   //switch from archery to melee when necessary (but not always perfectly)
      IF (<FINDLAYER(2).TYPE>==t_weapon_bow)||(<FINDLAYER(2).TYPE>==t_weapon_xbow)
         IF (<DISTANCE><2)
            FINDLAYER.2.CONT=<FINDLAYER.21.UID>  // put bow in pack
            IF (<FINDTYPE.t_weapon_fence>)
               FINDTYPE.t_weapon_fence.CONT=<UID>
            ELSEIF (<FINDTYPE.t_weapon_axe>)
               FINDTYPE.t_weapon_axe.CONT=<UID>
            ELSEIF (<FINDTYPE.t_weapon_mace_sharp>)
               FINDTYPE.t_weapon_mace_sharp.CONT=<UID>
            ELSEIF (<FINDTYPE.t_weapon_mace_pick>)
               FINDTYPE.t_weapon_mace_pick.CONT=<UID>
            ELSEIF (<FINDTYPE.t_weapon_mace_smith>)
               FINDTYPE.t_weapon_mace_smith.CONT=<UID>
            ELSEIF (<FINDTYPE.t_weapon_sword>)
               FINDTYPE.t_weapon_sword.CONT=<UID>
            ENDIF
         ENDIF
      ELSEIF !(<FINDLAYER(2).TYPE>==t_weapon_bow)
         IF (<DISTANCE>>1)
            FINDLAYER.1.CONT=<FINDLAYER.21.UID>  // put sword in pack
            IF (<FINDTYPE.t_weapon_xbow>)
               FINDTYPE.t_weapon_xbow.CONT=<UID>
//               SERV.NEWITEM i_xbolt,5,<FINDLAYER.21.UID>
            ELSE
               FINDTYPE.t_weapon_bow.CONT=<UID>
//               SERV.NEWITEM i_arrow,5,<FINDLAYER.21.UID>
            ENDIF
         ENDIF
      ENDIF
   ENDIF

[EVENTS e_intel_char]
ON=@CharAttack
 IF !(<FLAGS>&statf_war)
	ATTACK
 ENDIF
return

ON=@ItemStep
//field check
return

[EVENTS e_intel_char_looting]

ON=@NPCLookAtItem
 IF !(<FLAGS>&statf_war)
	 IF (<ARGO.TYPE> == t_corpse)
		 IF (<ARGO.TAG0.LOOTED.<UID>> < 5 )
			 IF (<ISEMPTY <tag.home>>)
				TAG.HOME=<HOME>
				TAG.HOMEDIST=<HOMEDIST>
			 ENDIF
			 HOME=<ARGO.P>
			 HOMEDIST=1
			 return 1
		 ELIF (<HOME> == <ARGO.P>)
			 HOME=<TAG.HOME>
			 HOMEDIST=<TAG.HOMEDIST>
		 ENDIF
	 ENDIF	
 ENDIF	
return

ON=@ItemStep
IF !(<FLAGS>&statf_conjured)
	IF (<ACT.TYPE> == t_corpse) || (<ACT.TYPE> == t_container)
	    IF (<ACT.FCOUNT> > 0)
		  local.item=<r<ACT.FCOUNT>>
		  obj=<ACT.FINDCONT.<local.item>.uid>
		  IF (STRMATCH(*<obj.type>*,"<serv.chardef(<id>).desires>")) || (STRMATCH(*<obj.id>*,"<serv.chardef(<id>).desires>"))
			    IF !(<ACT.FINDCONT.<local.item>.ATTR> & attr_newbie)
				    TRYSRC <UID> ACT.FINDCONT.<local.item>.BOUNCE
				    ACT.TAG0.LOOTED.<UID> -= 1
			    ELSE
				    ACT.TAG0.LOOTED.<UID> += 1
			    ENDIF
	          ELIF (<ACT.FINDCONT.<local.item>.ATTR> & attr_magic)
			    IF !(<ACT.FINDCONT.<local.item>.ATTR> & attr_newbie)
				    TRYSRC <UID> ACT.FINDCONT.<local.item>.BOUNCE
				    ACT.TAG0.LOOTED.<UID> -= 1
			    ELSE
				    ACT.TAG0.LOOTED.<UID> += 1
			    ENDIF
		  ELSE
			    ACT.TAG0.LOOTED.<UID> += 1
		  ENDIF
	    ELSE
			ACT.TAG0.LOOTED.<UID> = 6
	    ENDIF
        ENDIF
ENDIF

ON=@EnvironChange
     SRC.EQUIPARMOR
     SRC.EQUIPWEAPON



[EVENTS e_npc_weapon_switcher]

ON=@NPCActFight
   IF ( <DISTANCE> > 2 )
     IF ( (<FINDTYPE(t_weapon_bow)>) || (<FINDTYPE(t_weapon_xbow)>) )
       IF !( (<FINDLAYER(2).TYPE> == t_weapon_bow) || (<FINDLAYER(2).TYPE> == t_weapon_xbow) )
         f_equip_ranged_weapon
       ENDIF
     ENDIF
   ELIF ( <DISTANCE> < 2 )
     IF ( (<FINDLAYER(2).TYPE>==t_weapon_bow) || (<FINDLAYER(2).TYPE>==t_weapon_xbow) )
       f_equip_melee_weapon
     ENDIF
   ENDIF
 RETURN 0




[EVENTS e_npc_intel_potion_user]

ON=@GETHIT
   LOCAL.UID = 0
   IF (<FINDID(i_rune_poison)>) || (<FINDID(i_memory_poison_new)>)
      LOCAL.UID=<FINDID(i_potion_CureGreat)>
      IF (<LOCAL.UID>==0)
	LOCAL.UID=<FINDID(i_potion_Cure)>
      ENDIF
      IF (<LOCAL.UID>==0)
	LOCAL.UID=<FINDID(i_potion_CureLess)>
      ENDIF
      //We found one, use it
      IF (<LOCAL.UID>!=0)
         TRYSRC <UID> USEITEM <LOCAL.UID>
	 RETURN
      ENDIF
   ENDIF
   IF (<HITS> <= (<MAXHITS>/3))
      //Check for pots
      LOCAL.UID=<FINDID(i_potion_HealGreat)>
      IF (<LOCAL.UID>==0)
	LOCAL.UID=<FINDID(i_potion_Heal)>
      ENDIF
      IF (<LOCAL.UID>==0)
	LOCAL.UID=<FINDID(i_potion_HealLess)>
      ENDIF
      //We found one, use it
      IF (<LOCAL.UID>!=0)
         TRYSRC <UID> USEITEM <LOCAL.UID>
	 RETURN
      ENDIF
   ENDIF
RETURN

ON=@EnvironChange
   LOCAL.UID = 0
   IF (<FINDID(i_rune_poison)>) || (<FINDID(i_memory_poison_new)>)
      LOCAL.UID=<FINDID(i_potion_CureGreat)>
      IF (<LOCAL.UID>==0)
	LOCAL.UID=<FINDID(i_potion_Cure)>
      ENDIF
      IF (<LOCAL.UID>==0)
	LOCAL.UID=<FINDID(i_potion_CureLess)>
      ENDIF
      //We found one, use it
      IF (<LOCAL.UID>!=0)
         TRYSRC <UID> USEITEM <LOCAL.UID>
	 RETURN
      ENDIF
   ENDIF
RETURN 0

[function f_bandage]
IF !(<FINDID(i_new_bandageheal)>) && !(<FINDID(i_new_bandagevet)>) 
   IF (<FINDID(i_rune_poison)>) || (<FINDID(i_memory_poison_new)>) || (<HITS> <= (<MAXHITS>/2))
	If <Def0.HealSys_UseHealSys> && <Restest 1 i_bandage>
		Ref1 = <Uid>
		Consume i_new_bandageheal
		Consume i_new_bandagevet
		Local.HealItem=<Ref1.GetBandageItem>
		If <Serv.Itemdef.<Local.HealItem>>
			Serv.Newitem = <Local.HealItem>
			New.Link = <Ref1.Uid>
			Equip <New.Uid>
			Consume	i_bandage
		Endif
		Return 1
	Endif
   ENDIF
ENDIF

[EVENTS e_npc_intel_bandage_user]

ON=@GETHIT
	f_bandage

ON=@EnvironChange
	f_bandage

[FUNCTION f_equip_melee_weapon]
 VAR.EquipMeleeWeapon.OldArchery=<ARCHERY>
 ARCHERY=0
 TRYSRC <UID> FINDLAYER(1).BOUNCE
 TRYSRC <UID> FINDLAYER(2).BOUNCE
 EQUIPWEAPON
 EQUIPARMOR
 ARCHERY=<VAR.EquipMeleeWeapon.OldArchery>
 RETURN 0

[FUNCTION f_equip_ranged_weapon]
 VAR.EquipMeleeWeapon.OldArchery=<ARCHERY>
 ARCHERY=2000
 TRYSRC <UID> FINDLAYER(1).BOUNCE
 TRYSRC <UID> FINDLAYER(2).BOUNCE
 EQUIPWEAPON
 EQUIPARMOR
 ARCHERY=<VAR.EquipMeleeWeapon.OldArchery>
 RETURN 0


[EOF]
