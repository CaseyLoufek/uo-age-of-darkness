//****************************************************************************
// SPHERE by : Menasoft ©1997-2006
// www.sphereserver.net
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
//****************************************************************************
//Necromancer Items script, by Exer
//from The Darkside Shards (www.tdsuo.com)
//Necromancer Items script, by Exer
//from The Darkside Shards (www.tdsuo.com)
// FILE LAST UPDATED: Sunday, march 12th, 2006
// By: Ghost of OSIR ////////

[TYPEDEF T_SHRINE]

ON=@DCLICK
IF !(<SRC.FLAGS> & statf_dead)
	MENU MENU_TITHING
RETURN 1

ON=@DROPON_SELF
 IF (<ARGO.type> == t_coin) || (<ARGO.type> == t_note)
  LOCAL.TITHING = <ARGN>*<ARGO.VALUE>
  SRC.TITHING += <LOCAL.TITHING>
  SRC.CONSUME <ARGN> <ARGO>
  SRC.SMSG Your have gained <LOCAL.TITHING> tithing points.
  RETURN 0
 ENDIF
 
[MENU MENU_TITHING]
How much do you wish to tithe?

ON=0 Tithe 5c
src.f_tithe 5

ON=0 Tithe 10c
src.f_tithe 10

ON=0 Tithe 50c
src.f_tithe 50

ON=0 Tithe 100c
src.f_tithe 100

ON=0 Tithe 500c
src.f_tithe 500

ON=0 Tithe 1000c
src.f_tithe 1000

[FUNCTION f_tithe]
IF (<SRC.CURRENCY> >= <eval <args>>)
    SRC.TITHING=<SRC.TITHING>+<eval <args>>
    SRC.CONSUMECURRENCY <eval <args>>
    SRC.MESSAGE You tithe <args>c.
    SRc.SFX 037
ELSE
 SRC.MESSAGE You do not have enough.
ENDIF

[FUNCTION f_tithing_check]
IF !(<SRC.NPC>)
	IF (<SRC.TITHING> < <args>)
		SRC.MESSAGE You do not have enough tithing points.
		RETURN 1
	ENDIF
ENDIF

[ITEMDEF 02252]
name=Holy Book
defname=i_spellbook_holy
TYPE=t_spellbook
WEIGHT=3
LAYER=1
VALUE=400
TEVENTS=T_PROPRIEDADES

ON=@Create
more1=01
ATTR=attr_magic

CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Cult of Truth/Paladin
DESCRIPTION=Holy Spellbook

on=@dclick
sdialog d_book_holy
return 1

ON=@DROPON_SELF
IF !(<TOPOBJ.UID>==<SRC.UID>)
	SRC.SYSMESSAGE The spellbook must be in your possesion to add scrolls.
	RETURN 0
ENDIF
IF (<ARGO.TYPE>==t_scroll)
  IF (strmatch(*HOLY*,<serv.itemdef(<SERV.SPELL(<ARGO.MOREX>).RUNE_ITEM>).tag.school>))
    SRC.TAG.CASTING=<SERV.SPELL(<ARGO.MOREX>).defname>
    SRC.TAG.SPELLBOOK=<UID>
    IF !(<SRC.INBOOK>)
		var.index=1
		FOR <var.index> 1 16
			IF (<ISEMPTY <TAG.SPELL_<eval <var.index>>>>)
				TAG.SPELL_<eval <var.index>>=<SERV.SPELL(<ARGO.MOREX>).defname>
				SRC.SYSMESSAGE You transfer the scroll into the spellbook.
				ARGO.REMOVE
				RETURN 1
			ENDIF
			var.index += 1
		ENDFOR
		SRC.SYSMESSAGE There is no more room in that spellbook.
		RETURN 1
    ENDIF
    SRC.SYSMESSAGE That spell is already in that spellbook.
    RETURN 1
  ENDIF
  SRC.SYSMESSAGE That is not a holy spell.
  RETURN 1
ENDIF
SRC.SYSMESSAGE That is not a scroll.
RETURN 1


/////////////////////////////////////
// Dialog Holy //
/////////////////////////////////////

[DIALOG d_book_Holy]
0,0
gumppic 0 0 11009
page 1
button 324 8 2236 2236 0 2 0 // Next Page

IF !(<tag0.spell_1>==0) 
button 55 35 2118 2117 1 0 1 // Spell 1
button 70 35 2087 2087 0 2 0 // Spell 1
dhtmlgump 75 35 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_1>).NAME>
ENDIF
IF !(<tag0.spell_2>==0) 
button 55 55 2118 2117 1 0 2 // Spell 2
button 70 55 2087 2087 0 2 0 // Spell 2
dhtmlgump 75 55 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_2>).NAME>
ENDIF
IF !(<tag0.spell_3>==0) 
button 55 75 2118 2117 1 0 3 // Spell 3
button 70 75 2087 2087 0 3 0 // Spell 3
dhtmlgump 75 75 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_3>).NAME>
ENDIF
IF !(<tag0.spell_4>==0) 
button 55 95 2118 2117 1 0 4 // Spell 4
button 70 95 2087 2087 0 3 0 // Spell 4
dhtmlgump 75 95 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_4>).NAME>
ENDIF
IF !(<tag0.spell_5>==0) 
button 55 115 2118 2117 1 0 5 // Spell 5
button 70 115 2087 2087 0 4 0 // Spell 5
dhtmlgump 75 115 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_5>).NAME>
ENDIF
IF !(<tag0.spell_6>==0) 
button 55 135 2118 2117 1 0 6 // Spell 6
button 70 135 2087 2087 0 4 0 // Spell 6
dhtmlgump 75 135 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_6>).NAME>
ENDIF
IF !(<tag0.spell_7>==0) 
button 55 155 2118 2117 1 0 7 // Spell 7
button 70 155 2087 2087 0 5 0 // Spell 7
dhtmlgump 75 155 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_7>).NAME>
ENDIF
IF !(<tag0.spell_8>==0) 
button 55 175 2118 2117 1 0 8 // Spell 8
button 70 175 2087 2087 0 5 0 // Spell 8
dhtmlgump 75 175 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_8>).NAME>
ENDIF
IF !(<tag0.spell_9>==0) 
button 220 35 2118 2117 1 0 9 // Spell 9
button 235 35 2087 2087 0 6 0 // Spell 9
dhtmlgump 240 35 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_9>).NAME>
ENDIF
IF !(<tag0.spell_10>==0) 
button 220 55 2118 2117 1 0 10 // Spell 10
button 235 55 2087 2087 0 6 0 // Spell 10
dhtmlgump 240 55 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_10>).NAME>
ENDIF
IF !(<tag0.spell_11>==0) 
button 220 75 2118 2117 1 0 11 // Spell 11
button 235 75 2087 2087 0 7 0 // Spell 11
dhtmlgump 240 75 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_11>).NAME>
ENDIF
IF !(<tag0.spell_12>==0) 
button 220 95 2118 2117 1 0 12 // Spell 12
button 235 95 2087 2087 0 7 0 // Spell 12
dhtmlgump 240 95 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_12>).NAME>
ENDIF
IF !(<tag0.spell_13>==0) 
button 220 115 2118 2117 1 0 13 // Spell 13
button 235 115 2087 2087 0 8 0 // Spell 13
dhtmlgump 240 115 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_13>).NAME>
ENDIF
IF !(<tag0.spell_14>==0) 
button 220 135 2118 2117 1 0 14 // Spell 14
button 235 135 2087 2087 0 8 0 // Spell 14
dhtmlgump 240 135 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_14>).NAME>
ENDIF
IF !(<tag0.spell_15>==0) 
button 220 155 2118 2117 1 0 15 // Spell 15
button 235 155 2087 2087 0 9 0 // Spell 15
dhtmlgump 240 155 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_15>).NAME>
ENDIF
IF !(<tag0.spell_16>==0) 
button 220 175 2118 2117 1 0 16 // Spell 16
button 235 175 2087 2087 0 9 0 // Spell 16
dhtmlgump 240 175 150 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_16>).NAME>
ENDIF

dhtmlgump 85 10 150 20 0 0 <def.big> <def.BFONT_DBLUE> Index
dhtmlgump 245 10 150 20 0 0 <def.big> <def.BFONT_DBLUE> Index


LOCAL.SPELLPLACE=1
FOR 8

page <eval ((<LOCAL.SPELLPLACE>/2)+2)>
button 53 8 2235 2235 0 <eval ((<LOCAL.SPELLPLACE>/2)+1> 0 // Prev page
IF !(<eval ((<LOCAL.SPELLPLACE>/2)+2)>==9)
button 324 8 2236 2236 0 <eval ((<LOCAL.SPELLPLACE>/2)+3)> 0 // Next Page
ENDIF


IF !(<tag0.spell_<EVAL <LOCAL.SPELLPLACE>>>==0) 
button 60 35 20480 20480 1 0 <EVAL <LOCAL.SPELLPLACE>>
gumppic 60 35 <Eval <serv.itemdef(<SERV.SPELL(<tag0.spell_<EVAL <LOCAL.SPELLPLACE>>>).RUNE_ITEM>).tag.icon>>
dhtmlgump 60 10 140 20 0 0 <def.center> <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_<EVAL <LOCAL.SPELLPLACE>>>).NAME>
dhtmlgump 110 30 80 40 0 0 <def.small><def.BFONT_DBLUE><serv.itemdef(<SERV.SPELL(<tag0.spell_<eval <LOCAL.SPELLPLACE>>>).RUNE_ITEM>).tag.chant> 
dhtmlgump 55 80 140 20 0 0 <def.BFONT_DBLUE> Tithing:
dhtmlgump 100 80 140 20 0 0 <def.BFONT_BLUE> <Eval <serv.itemdef(<SERV.SPELL(<tag0.spell_<EVAL <LOCAL.SPELLPLACE>>>).RUNE_ITEM>).tag.tithing>>
dhtmlgump 55 100 140 20 0 0 <def.BFONT_DBLUE> Mana:
dhtmlgump 90 100 140 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_<eval <LOCAL.SPELLPLACE>>>).MANAUSE>
ENDIF

IF !(<tag0.spell_<eval <LOCAL.SPELLPLACE>+1>>==0) 
button 220 35 20480 20480 1 0 <eval <LOCAL.SPELLPLACE>+1>
gumppic 220 35 <serv.itemdef(<SERV.SPELL(<tag0.spell_<eval <LOCAL.SPELLPLACE>+1>>).RUNE_ITEM>).tag.icon> 
dhtmlgump 190 10 140 20 0 0 <def.center> <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_<eval <LOCAL.SPELLPLACE>+1>>).NAME>
dhtmlgump 270 30 80 40 0 0 <def.small><def.BFONT_DBLUE><serv.itemdef(<SERV.SPELL(<tag0.spell_<eval <LOCAL.SPELLPLACE>+1>>).RUNE_ITEM>).tag.chant> 
dhtmlgump 215 80 140 20 0 0 <def.BFONT_DBLUE> Tithing:
dhtmlgump 260 80 140 20 0 0 <def.BFONT_BLUE> <Eval <serv.itemdef(<SERV.SPELL(<tag0.spell_<EVAL <LOCAL.SPELLPLACE>+1>>).RUNE_ITEM>).tag.tithing>>
dhtmlgump 215 100 140 20 0 0 <def.BFONT_DBLUE> Mana:
dhtmlgump 250 100 140 20 0 0 <def.BFONT_BLUE> <SERV.SPELL(<tag0.spell_<eval <LOCAL.SPELLPLACE>+1>>).MANAUSE>
ENDIF

LOCAL.SPELLPLACE += 2

ENDFOR

[DIALOG d_book_holy TEXT]
Index

[DIALOG d_book_holy BUTTON]
ON=0
// Prev Page
ON=1
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_1>
sdialog d_book_holy
ON=2
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_2>
sdialog d_book_holy
ON=3
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_3>
sdialog d_book_holy
ON=4
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_4>
sdialog d_book_holy
ON=5
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_5>
sdialog d_book_holy
ON=6
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_6>
sdialog d_book_holy
ON=7
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_7>
sdialog d_book_holy
ON=8
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_8>
sdialog d_book_holy
ON=9
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_9>
sdialog d_book_holy
ON=10
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_10>
sdialog d_book_holy
ON=11
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_11>
sdialog d_book_holy
ON=12
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_12>
sdialog d_book_holy
ON=13
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_13>
sdialog d_book_holy
ON=14
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_14>
sdialog d_book_holy
ON=15
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_15>
sdialog d_book_holy
ON=16
SRC.TAG.SPELLBOOK=<UID>
SRC.CAST <tag0.spell_16>
sdialog d_book_holy

[ITEMDEF 0227c]
DEFNAME=i_scroll_paladin
TYPE=t_scroll
WEIGHT=0.5

[ITEMDEF i_scroll_cleanse_by_fire]
ID=i_scroll_paladin
NAME=Cleanse by Fire
TYPE=t_scroll
RESOURCES=i_scroll_blank
SKILLMAKE=INSCRIPTION 25.0, CHIVALRY 15.0


ON=@CREATE
ATTR=ATTR_MAGIC
MOREX=s_cleanse_by_fire
MOREY={50 80}


CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Paladin
DESCRIPTION=Cleanse by Fire


[ITEMDEF i_scroll_close_wounds]
ID=i_scroll_paladin
NAME=Close Wounds
TYPE=t_scroll
RESOURCES=i_scroll_blank
SKILLMAKE=INSCRIPTION 20.0, CHIVALRY 10.0

ON=@Create
ATTR=ATTR_MAGIC
MOREX=s_close_wounds
MOREY={50 80}


CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Paladin
DESCRIPTION=Close Wounds

[ITEMDEF i_scroll_consecrate_weapon]
ID=i_scroll_paladin
NAME=Consecrate Weapon
TYPE=t_scroll
RESOURCES=i_scroll_blank
SKILLMAKE=INSCRIPTION 35.0, CHIVALRY 25.0

ON=@Create
ATTR=ATTR_MAGIC
MOREX=s_consecrate_weapon
MOREY={50 80}

CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Paladin
DESCRIPTION=Consecrate Weapon


[ITEMDEF i_scroll_dispel_evil]
ID=i_scroll_paladin
NAME=Dispel Evil
TYPE=t_scroll
RESOURCES=i_scroll_blank
SKILLMAKE=INSCRIPTION 55.0, CHIVALRY 45.0

ON=@Create
ATTR=ATTR_MAGIC
MOREX=s_Dispel_evil
MOREY={50 80}

CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Paladin
DESCRIPTION=Dispel Evil

[ITEMDEF i_scroll_divine_fury]
ID=i_scroll_paladin
NAME=Divine Fury
TYPE=t_scroll
RESOURCES=i_scroll_blank
SKILLMAKE=INSCRIPTION 45.0, CHIVALRY 35.0

ON=@Create
ATTR=ATTR_MAGIC
MOREX=s_Divine_fury
MOREY={50 80}

[ITEMDEF i_scroll_enemy_of_one]
ID=i_scroll_paladin
NAME=Enemy of One
TYPE=t_scroll
RESOURCES=i_scroll_blank
SKILLMAKE=INSCRIPTION 55.0, CHIVALRY 45.0

ON=@Create
ATTR=ATTR_MAGIC
MOREX=s_enemy_of_one
MOREY={50 80}

CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Paladin
DESCRIPTION=Enemy of One


CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Paladin
DESCRIPTION=Divine Fury


[ITEMDEF i_scroll_holy_light]
ID=i_scroll_paladin
NAME=Holy Light
TYPE=t_scroll
RESOURCES=i_scroll_blank
SKILLMAKE=INSCRIPTION 75.0, CHIVALRY 65.0

ON=@Create
ATTR=ATTR_MAGIC
MOREX=s_Holy_light
MOREY={50 80}

CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Paladin
DESCRIPTION=Holy Light



[ITEMDEF i_scroll_noble_sacrifice]
ID=i_scroll_paladin
NAME=Noble Sacrifice
TYPE=t_scroll
RESOURCES=i_scroll_blank
SKILLMAKE=INSCRIPTION 85.0, CHIVALRY 75.0

ON=@Create
ATTR=ATTR_MAGIC
MOREX=s_noble_sacrifice
MOREY={50 80}

CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Paladin
DESCRIPTION=Noble Sacrifice

[ITEMDEF i_scroll_remove_curse]
ID=i_scroll_paladin
NAME=Remove Curse
TYPE=t_scroll
RESOURCES=i_scroll_blank
SKILLMAKE=INSCRIPTION 25.0, CHIVALRY 15.0

ON=@Create
ATTR=ATTR_MAGIC
MOREX=s_remove_curse
MOREY={50 80}

CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Paladin
DESCRIPTION=Remove Curse


[ITEMDEF i_scroll_sacred_journey]
ID=i_scroll_paladin
NAME=Sacred Journey
TYPE=t_scroll
RESOURCES=i_scroll_blank
SKILLMAKE=INSCRIPTION 35.0, CHIVALRY 25.0

ON=@Create
ATTR=ATTR_MAGIC
MOREX=s_sacred_journey
MOREY={50 80}

CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Paladin
DESCRIPTION=Sacred Journey


[ITEMDEF i_scroll_transmute_poison]
ID=i_scroll_paladin
NAME=Transmute Poison
TYPE=t_scroll
RESOURCES=i_scroll_blank
SKILLMAKE=INSCRIPTION 85.0, CHIVALRY 75.0

ON=@Create
ATTR=ATTR_MAGIC
MOREX=s_transmute_poison
MOREY={50 80}

CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Paladin
DESCRIPTION=Transmute Poison

[ITEMDEF i_scroll_destroy_undead]
ID=i_scroll_paladin
NAME=Destroy Undead
TYPE=t_scroll
RESOURCES=i_scroll_blank
SKILLMAKE=INSCRIPTION 85.0, CHIVALRY 75.0

ON=@Create
ATTR=ATTR_MAGIC
MOREX=s_destroy_undead
MOREY={50 80}

CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Paladin
DESCRIPTION=Destroy Undead


[spell 201]
defname=s_cleanse_by_fire
name=Cleanse by Fire
sound=
runes=
cast_time=3.0
resources=
rune_item=i_rune_cleanse_by_fire
scroll_item=i_scroll_cleanse_by_fire
flags=SPELLFLAG_TARG_CHAR | SPELLFLAG_FX_TARG | spellflag_good
effect_id=i_fx_smoke
effect=1
duration=0.0
manause=10
skillreq=Chivalry 5.0
interrupt=0.0

ON=@select
f_tithing_check 10

ON=@start
SRC.SAY Expor Flamus

ON=@effect
IF !(<SRC.NPC>)
	SRC.TITHING -= 10
ENDIF
src.act.spelleffect 11 <argn2> <src.uid> 0
SRC.DAMAGE 10 014 
//fire effect on caster
RETURN

[spell 202]
defname=s_close_wounds
name=Close Wounds
sound=01f2
runes=
cast_time=1.0
resources=
rune_item=i_rune_close_wounds
scroll_item=i_scroll_close_wounds
flags=SPELLFLAG_TARG_CHAR | SPELLFLAG_FX_TARG | spellflag_good
effect_id=i_fx_heal_effect
effect=1
duration=0.0
manause=10
skillreq=Chivalry 0.0
INTERRUPT=0.0

ON=@select
f_tithing_check 10

ON=@start
SRC.SAY Sanctu

ON=@effect
IF !(<SRC.NPC>)
	SRC.TITHING -= 10
ENDIF
act.hits += (<EVAL(RAND((<src.karma> / 640)))>+<EVAL(RAND((<argn2> / 50)))>+7)
IF (<act.HITS> > <act.MAXHITS>)
act.HITS=<act.MAXHITS>
ENDIF
RETURN

[spell 203]
defname=s_consecrate_weapon
name=Consecrate Weapon
sound=1482
runes=
cast_time=1.0
resources=
rune_item=i_rune_consecrate_weapon
scroll_item=i_scroll_consecrate_weapon
flags=spellflag_good
effect_id=i_fx_bless_effect
effect=1
duration=0.0
manause=10
skillreq=Chivalry 15.0
INTERRUPT=0.0

ON=@select
f_tithing_check 10
IF !(<ARGN3>=2)
IF (<SRC.tag0.consecrate> == 1)
SRC.SYSMESSAGE @0481 You are already fighting with a consecrated weapon.
RETURN 1
ENDIF
ENDIF


ON=@start
SRC.SAY Consecrus Arma

ON=@effect
SRC.SYSMESSAGE @0481 You imbue your attacks with divine energies.
tag0.consecrate=1
IF !(<SRC.NPC>)
	SRC.TITHING -= 10
ENDIF
src.newitem i_Consecrate_timer
SRC.ACT.equip


[itemdef i_consecrate_timer]
ID=i_memory
name=Consecrate Timer
TYPE=t_eq_script

on=@create
ATTR=attr_invis|attr_decay
LINK=<src.act.uid>

on=@equip
timer <EVAL(RAND(<src.karma> / 1920))>+<EVAL(RAND(<argn2> / 150))>+4

on=@timer
link.SYSMESSAGE @0481 You cease imbuing the weaponry.
link.tag.consecrate=
remove
return 1




[spell 204]
defname=s_dispel_evil
name=Dispel Evil
sound=0201
runes=
cast_time=2.0
resources=
rune_item=i_rune_dispel_evil
scroll_item=i_scroll_dispel_evil
flags=SPELLFLAG_FX_TARG|SPELLFLAG_TARG_CHAR|spellflag_dir_anim|spellflag_harm|spellflag_resist
effect_id=i_fx_smoke
effect=1
duration=0.0
manause=10
skillreq=Chivalry 35.0
INTERRUPT=0.0

ON=@select
f_tithing_check 10

ON=@start
SRC.SAY Dispiro Malas

ON=@effect
IF !(<SRC.NPC>)
	SRC.TITHING -= 10
ENDIF
IF (<ACT.KARMA> < 0 )
   IF ((<EVAL(RAND(<src.karma>/2))>+<EVAL(RAND(<argn2>*2))>) > <act.fame>)
      IF (<act.flags> & statf_conjured)
	act.remove
      else
        src.act.emote flee in terror
        src.act.events +e_dispel_evil_flee
        newitem i_dispel_evil_timer
        src.act.equip
      endif
   endif
endif


[itemdef i_dispel_evil_timer]
ID=i_memory
name=Dispel Evil Timer
TYPE=t_eq_script

on=@create
ATTR=attr_invis|attr_decay
LINK=<src.act.uid>

on=@equip
timer (<EVAL(RAND(45))>+<EVAL(RAND(45))>)

on=@timer
link.events -e_dispel_evil_flee
link.emote stop fleeing
remove
return 1


[events e_dispel_evil_flee]

ON=@NpcLookAtChar
FLEE
RETURN 0

ON=@HITTRY
FLEE
RETURN 1



[spell 205]
defname=s_Divine_Fury
name=Divine Fury
sound=1482
runes=
cast_time=2.0
resources=
rune_item=i_rune_Divine_Fury
scroll_item=i_scroll_Divine_Fury
flags=spellflag_good
effect_id=i_fx_bless_effect
effect=1
duration=0.0
manause=15
skillreq=Chivalry 25.0
INTERRUPT=0.0

ON=@select
f_tithing_check 10
IF !(<ARGN3>=2)
IF (<SRC.tag0.divine> == 1)
SRC.SYSMESSAGE @0481 You are already in a fury.
RETURN 1
ENDIF
ENDIF

ON=@start
SRC.SAY Divinum Furis

ON=@effect
SRC.SYSMESSAGE @0481 You fly into a righteous wrath.
tag0.divine=1
IF !(<SRC.NPC>)
	SRC.TITHING -= 10
ENDIF
src.stam = <src.maxstam>
src.modar = (<src.modar> -10)
src.rescold = (<src.rescold> -10)
src.resfire = (<src.resfire> -10)
src.resenergy = (<src.resenergy> -10)
src.respoison = (<src.respoison> -10)
src.tag.swingincrease += 30
src.tag.HITCHANCEINCREASE += 30
src.tag.damincrease += 30
src.newitem i_Divine_fury_timer
SRC.ACT.equip


[itemdef i_divine_fury_timer]
ID=i_memory
name=Divine fury Timer
TYPE=t_eq_script

on=@create
ATTR=attr_invis|attr_decay
LINK=<src.act.uid>

on=@equip
timer <EVAL(RAND(<src.karma> / 960))>+<EVAL(RAND(<argn2> / 100))>+8

on=@timer
link.modar = (<link.modar> +10)
link.rescold = (<link.rescold> +10)
link.resfire = (<link.resfire> +10)
link.resenergy = (<link.resenergy> +10)
link.respoison = (<link.respoison> +10)
src.tag.swingincrease -= 30
src.tag.HITCHANCEINCREASE -= 30
src.tag.damincrease -= 30
link.SYSMESSAGE @0481 You calm down.
link.tag.divine=
remove
return 1


[spell 206]
defname=s_enemy_of_one
name=Enemy of One
sound=1482
runes=
cast_time=1.0
resources=
rune_item=i_rune_enemy_of_one
scroll_item=i_scroll_enemy_of_one
flags=spellflag_good
effect_id=i_fx_bless_effect
effect=1
duration=0.0
manause=10
skillreq=Chivalry 45.0
INTERRUPT=0.0


ON=@select
f_tithing_check 10

ON=@start
SRC.SAY Forul Solum

ON=@effect
SRC.SYSMESSAGE @0481 You prepare to focus your efforts...
IF !(<SRC.NPC>)
	SRC.TITHING -= 10
ENDIF
SRC.events +e_enemy_of_one
src.newitem i_enemy_of_one_timer
SRC.ACT.equip


[itemdef i_enemy_of_one_timer]
ID=i_memory
name=enemy of one timer
TYPE=t_eq_script

on=@create
ATTR=attr_invis|attr_decay
LINK=<src.act.uid>

on=@equip
timer <EVAL(RAND(<src.karma> / 480))>+<EVAL(RAND(<argn2> / 25))>+30

on=@timer
link.events -e_enemy_of_one
link.SYSMESSAGE @0481 You cease your single-minded efforts.
link.tag.ENEMYOFONE=
remove
return 1


[events e_enemy_of_one]

ON=@Hit
	IF (<TAG0.ENEMYOFONE>==0)
		TAG.ENEMYOFONE=<eval <SRC.ID>>
		SYSMESSAGE @0481 You focus your efforts single-mindedly on <serv.chardef(<TAG.ENEMYOFONE>).name>.
	ENDIF



[spell 207]
defname=s_Holy_Light
name=Holy Light
sound=1425
runes=
cast_time=2.0
resources=
rune_item=i_rune_Holy_Light
scroll_item=i_Scroll_Holy_Light
flags=spellflag_harm
effect_id=
effect=0
duration=0.0
manause=10
skillreq=Chivalry 55.0
INTERRUPT=0.0

ON=@select
f_tithing_check 10

ON=@start
SRC.SAY Augus Luminos

ON=@effect
IF !(<SRC.NPC>)
	SRC.TITHING -= 10
ENDIF
forchars 6
	IF (<KARMA> <= 0) && !(<MEMORYFINDTYPE.MEMORY_IPET.link> == <src.uid>) && !(<ISINPARTY.SRC.PARTY>)
		effect = 3 037b9 9 16 0 0491 3
		LOCAL.LIGHTDMG=(<EVAL(RAND((<src.karma> / 320)))>+<EVAL(RAND((<argn2> / 40)))>+4)
		DAMAGE <LOCAL.LIGHTDMG> 02020
	ELSEIF <SRC.ATTACKER> > 0
		FOR x 0 <EVAL <SRC.ATTACKER>-1>
			IF (<SRC.ATTACKER.<LOCAL.X>.UID> == <UID>) && !(<SRC.ATTACKER.<LOCAL.X>.UID> == <SRC.UID>)
				effect = 3 037b9 9 16 0 0491 3
				LOCAL.LIGHTDMG=(<EVAL(RAND((<src.karma> / 320)))>+<EVAL(RAND((<argn2> / 40)))>+4)
				DAMAGE <LOCAL.LIGHTDMG> 02020
			ENDIF
		ENDFOR
	ENDIF
endfor

[spell 208]
defname=s_Noble_Sacrifice
name=Noble Sacrifice
sound=
runes=
cast_time=2.0
resources=
rune_item=i_rune_Noble_Sacrifice
scroll_item=i_scroll_Noble_Sacrifice
flags=spellflag_good
effect_id=i_fx_bless_effect
effect=1
duration=0.0
manause=20
skillreq=Chivalry 65.0
INTERRUPT=0.0

ON=@select
f_tithing_check 30

ON=@start
SRC.SAY Dium Prostra

ON=@effect

IF !(<SRC.NPC>)
	SRC.TITHING -= 30
ENDIF
LOCAL.SACRIFICE=0

foritems 6
	IF (<TYPE>==t_corpse) && !(<LINK>==04fffffff)
		IF (<LINK.ISINPARTY.SRC.PARTY>)
			spelleffect 59 <argn2> <src.uid> 0
			LOCAL.SACRIFICE=1
		ELSEIF (<LINK.KARMA> >= 0)
			LOCAL.ATTACKED=0
			IF <SRC.ATTACKER> > 0
				FOR x 0 <EVAL <SRC.ATTACKER>-1>
					IF (<SRC.ATTACKER.<LOCAL.X>.UID> == <LINK.UID>)
						LOCAL.ATTACKED=1
					ENDIF
				ENDFOR
			ENDIF
			IF !<LOCAL.ATTACKED>
				LOCAL.SACRIFICE=1
				spelleffect 59 <argn2> <src.uid> 0
			ENDIF
		ENDIF
	ENDIF
endfor

forchars 6
	IF (<MEMORYFINDTYPE.MEMORY_IPET.link> == <src.uid>) || (<ISINPARTY.SRC.PARTY>)
		IF !(<SRC.UID> == <UID>)
			IF (<HITS> < <MAXHITS>) || (<POISON> > 0) || (<flags> & statf_dead)
				spelleffect 59 <argn2> <src.uid> 0
				LOCAL.SACRIFICE=1
				spelleffect 11 <argn2> <src.uid> 0
				spelleffect 4 <argn2> <src.uid> 0
			ENDIF
		ENDIF
	ELSEIF (<KARMA> >= 0)
		LOCAL.ATTACKED=0
		IF <SRC.ATTACKER> > 0
			FOR x 0 <EVAL <SRC.ATTACKER>-1>
				IF (<SRC.ATTACKER.<LOCAL.X>.UID> == <UID>)
					LOCAL.ATTACKED=1
				ENDIF
			ENDFOR
		ENDIF
		IF !<LOCAL.ATTACKED>
			IF !(<SRC.UID> == <UID>)
				IF (<HITS> < <MAXHITS>) || (<POISON> > 0) || (<flags> & statf_dead)
					spelleffect 59 <argn2> <src.uid> 0
					LOCAL.SACRIFICE=1
					spelleffect 11 <argn2> <src.uid> 0
					spelleffect 4 <argn2> <src.uid> 0
				ENDIF
			ENDIF
		ENDIF
	ENDIF
endfor
IF <LOCAL.SACRIFICE>
SRC.HITS=1
ENDIF
RETURN 1

[Spell 209]
DEFNAME=s_Remove_Curse
NAME=Remove Curse
SOUND=
RUNES=
CAST_TIME=1.5
RESOURCES=
RUNE_ITEM=i_rune_Remove_Curse
SCROLL_ITEM=i_scroll_remove_curse
FLAGS=spellflag_targ_char|spellflag_nofreezeoncast
EFFECT_ID=
EFFECT=10
DURATION=0.0
MANAUSE=20
SKILLREQ=SKILL_CHIVALRY 5.0
INTERRUPT=0.0,0.0

ON=@select
f_tithing_check 10

ON=@start
SRC.SAY Extermo Vomica

ON=@Effect
IF !(<SRC.NPC>)
	SRC.TITHING -= 10
ENDIF
IF (<SRC.KARMA> < -5000)
LOCAL.CHANCE=0
ELIF (<SRC.KARMA> < 0)
LOCAL.CHANCE=<EVAL ((SQRT(20000+<SRC.KARMA>))-122)>
ELIF (<SRC.KARMA> < 5625)
LOCAL.CHANCE=<EVAL ((SQRT(<SRC.KARMA>))+25)>
ELSE
LOCAL.CHANCE=100
ENDIF
IF (<LOCAL.CHANCE> >= <R1,100>)
SOUND=0F6
SOUND=01F7
EFFECT 3,03709,1,30,,13
    IF (<FINDID.i_rune_weaken>)
    FINDID.i_rune_weaken.TIMERD=1
    ENDIF
    IF (<FINDID.i_rune_paralyze>)
    FINDID.i_rune_paralyze.TIMERD=1
    ENDIF
    IF (<FINDID.i_rune_clumsy>)
    FINDID.i_rune_clumsy.TIMERD=1
    ENDIF
    IF (<FINDID.i_rune_feeblemind>)
    FINDID.i_rune_feeblemind.TIMERD=1
    ENDIF
    IF (<FINDID.i_rune_curse>)
    FINDID.i_rune_curse.TIMERD=1
    ENDIF
ELSE
SOUND=01DF
ENDIF

[spell 210]
defname=s_sacred_journey
name=Sacred Journey
sound=
runes=
cast_time=3.0
resources=
rune_item=i_rune_sacred_journey
scroll_item=i_scroll_sacred_journey
flags=SPELLFLAG_TARG_OBJ
effect_id=i_fx_bless_effect
effect=1
duration=0.0
manause=10
skillreq=Chivalry 15.0
INTERRUPT=0.0

ON=@select
f_tithing_check 15

ON=@start
SRC.SAY Sanctum Viatas
IF (<FLAGS>&STATF_CRIMINAL)
SYSMESSAGELOC -1,1005561
RETURN 1
ELIF (<FLAGS>&STATF_WAR)
SYSMESSAGELOC -1,1061282
RETURN 1
ELIF (<WEIGHT> > <MAXWEIGHT>)
SYSMESSAGELOC 022,502359
RETURN 1
ENDIF

ON=@Effect
IF (<ISITEM>)
    IF (<TYPE> == T_RUNE) 
	    IF (!(<MOREX>))
	    SRC.MESSAGE @,,2 501805 //that rune is not yet marked
	    ELIF (<SERV.MAP(<MOREP>).REGION.FLAGS>&region_antimagic_recall_in)
	    SRC.SYSMESSAGELOC -1,1019004  // You are not allowed to travel there.
	    ELIF (<SRC.REGION.FLAGS>&region_antimagic_recall_out)
	    SRC.SYSMESSAGELOC -1,501035    //You cannot teleport from here to the destination.
	    ELIF (<SRC.FLAGS>&STATF_CRIMINAL)
	    SRC.SYSMESSAGELOC -1,1005561 // Thou'rt a criminal and cannot escape so easily.
	    ELIF (<SRC.FLAGS>&STATF_WAR)
	    SRC.SYSMESSAGELOC -1,1061282 // You cannot use the Sacred Journey ability to flee from combat.
	    ELIF (<SRC.WEIGHT> > <SRC.MAXWEIGHT>)
	    SRC.SYSMESSAGELOC 022,502359 // Thou art too encumbered to move.
	    ELSE
	    SRC.GO <MOREP>
		IF !(<SRC.NPC>)
			SRC.TITHING -= 10
		ENDIF
	    ENDIF
    ELIF (<TYPE> == T_KEY)
	    IF (<LINK.TYPE>!=t_ship)
	    SRC.MESSAGE That is not a ship key.
	    ELIF (<SERV.MAP(<LINK.P>).REGION.FLAGS>&region_antimagic_recall_in)
	    SRC.SYSMESSAGELOC -1,1019004  // You are not allowed to travel there.
	    ELIF (<SRC.REGION.FLAGS>&region_antimagic_recall_out
	    SRC.SYSMESSAGELOC -1,501035    //You cannot teleport from here to the destination.
	    ELIF (<SRC.FLAGS>&STATF_CRIMINAL)
	    SRC.SYSMESSAGELOC -1,1005561 // Thou'rt a criminal and cannot escape so easily.
	    ELIF (<SRC.FLAGS>&STATF_WAR)
	    SRC.SYSMESSAGELOC -1,1061282 // You cannot use the Sacred Journey ability to flee from combat.
	    ELIF (<SRC.WEIGHT> > <SRC.MAXWEIGHT>)
	    SRC.SYSMESSAGELOC 022,502359 // Thou art too encumbered to move.
	    ELSE
	    SRC.GO <LINK.P>
		IF !(<SRC.NPC>)
			SRC.TITHING -= 10
		ENDIF
	    ENDIF
    ELSE
	    SRC.MESSAGE @,,2 502357 //you can not recall from that object
    ENDIF
ENDIF


[spell 211]
defname=s_transmute_poison
name=Transmute Poison
sound=01e0
runes=
cast_time=1.0
resources=
rune_item=i_rune_transmute_poison
scroll_item=i_scroll_transmute_poison
flags=SPELLFLAG_TARG_obj | SPELLFLAG_FX_TARG | spellflag_good
effect_id=i_fx_heal_effect
effect=1
duration=0.0
manause=15
skillreq=Chivalry 75.0
INTERRUPT=0.0

ON=@select
f_tithing_check 10
 
ON=@start
SRC.SAY Alcort

ON=@effect
IF !(<SRC.NPC>)
	SRC.TITHING -= 10
ENDIF
IF (<SRC.ACT.ISCHAR>==1)
ELSEIF (<SRC.ACT.TYPE> == T_POTION)
ELSE
SRC.MESSAGE You must target a character or poison with this spell.
RETURN 1
ENDIF
IF (<SRC.ACT.ISCHAR>)
	IF (<src.findid.i_rune_poison>)
		IF (<src.findid.i_rune_poison.morey> > (<argn2>/2))
			SRC.ACT.HITS += (<argn2>/200)
			SRC.ACT.MANA += (<argn2>/200)
			src.findid.i_rune_poison.morey -= <argn2>/2
		ELSE
			SRC.ACT.HITS += (<src.findid.i_rune_poison.morey>/100)
			SRC.ACT.MANA += (<src.findid.i_rune_poison.morey>/100)
			src.findid.i_rune_poison.remove
		ENDIF
	ENDIF
ELSE
	IF (<SRC.ACT.TYPE> == T_POTION) && (<SRC.ACT.MORE1> == S_POISON)
		OBJ = <SRC.ACT>
		IF  (<obj.amount> >= 2)
			obj.amount -= 1
			obj.dupe
			new.amount = 1
			new.more = obj.more
			iF (<obj.cont>)
				NEW.CONT = <obj.cont>
			ELSE
				NEW.P = <obj.p>
			ENDIF
			obj.update
			obj = <new>
		ENDIF
		OBJ.MORE1 = S_restore
		LOCAL.POTENCY=Lesser
		IF (<OBJ.MORE2> > 100.0)
			LOCAL.POTENCY=Extremely Potent
		ELSEIF (<OBJ.MORE2> > 70.0)
			LOCAL.POTENCY=Greater
		ELSEIF (<OBJ.MORE2> > 50.0)
			LOCAL.POTENCY=Average
		ENDIF
		OBJ.NAME=<LOCAL.POTENCY> <SERV.SPELL.<OBJ.MORE1>.NAME> potion%s
		obj.update
	ENDIF
ENDIF
RETURN

[spell 212]
defname=s_destroy_undead
name=Destroy Undead
sound=1425
runes=
cast_time=2.0
resources=
rune_item=i_rune_destroy_undead
scroll_item=i_scroll_destroy_undead
flags=spellflag_harm
effect_id=
effect=0
duration=0.0
manause=10
skillreq=Chivalry 30.0
INTERRUPT=0.0

ON=@select
f_tithing_check 10

ON=@start
SRC.SAY Pontori

ON=@effect
IF !(<SRC.NPC>)
	SRC.TITHING -= 10
ENDIF
forchars 8
	IF (STRMATCH(*<BASEID>*,<DEF.UNDEAD_SLAYER>))
		effect = 3 0374a 9 16 0 0492 3
		LOCAL.DMG=(<EVAL(RAND((<src.karma> / 320)))>+<EVAL(RAND((<argn2> / 40)))>+4)
		DAMAGE <LOCAL.DMG> 08005
	ENDIF
endfor



[eof]