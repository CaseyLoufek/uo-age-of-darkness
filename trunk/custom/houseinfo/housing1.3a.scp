// Enhanced Housing v1.4 Scripted by Mordaunt aka Drogyn/Mordrogyn
//
// It's a housing script (duh!)
// Nothing in this script is supposed to be an exact copy of OSI. Anything that is is either happenstance, or I preferred their way to mine.
//
// If you find any bugs with this script please email me at drogyn@gmail.com
//
// Features:	NO MORE KEYS! (or locks to change), everything runs off the house lists, (or guild rosta if house has been declared a guildhouse)
//		Support for the new "custom" built houses & foundations.
//		Support for static buildings (w/ use of crius' static multi script, possibly others that may exist too)
//		Flexible Friend & Co-Owner limits based on house size.  (w/ defname cap)
//		Flexible no. of Secure Containers & Lockdowns. (w/ ability to buy more, up to 100% of original)
//		Decay timer (if activated in defs) resets when Owner uses any door on house (unless house is scheduled for demolition.)
//		Put house up for sale via sign, Ideal for static buildings.
//		Co-Owner can inherit house if owner no longer exists by dclicking sign w/ < 5 days before removal (w/ defname on/off toggle)
//		Castle/Keep owners can fill (some or all) of those obvious doorways with doors at their discretion.
//		Convert your classic house to a customizable one of equal size (not available for keep or castle)
//		House vendor limits, overages & persons not on co-owner/friends lists (or owner) will refuse to buy/sell,
//		ALL characters on the Owner's account have full access & privs within the house with def.account_ownership.
//		
// EXPERIMENTAL:	Added moving crate, which WILL shrink vendors in the house, and put ALL items (& shrunken vendor) into crate, when converting to customizable house.
//			I'm not sure what the shrink will do to the ownership of vendors when they are unshrunk, if not done by owner. (user vendor script may effect result)
//			Crate is accessible by anyone in the house as long as they are within 2 tiles and cannot be "secured" so if that causes a problem, put it in a locked room. 
//
// Installation:	This system uses the @step trigger to check players entering the house.
//			@Step will now work only with EF_New_Triggers on and EF_Minimize_Triggers off (sphere.ini) as per revisions.
//			This script uses Belgar's pay from bank function when buying a house/more storage via the sign, so make sure you grab it.
//
// House Vendor Setup:	This script has been designed to work with ANY custom vendor script (hopefully) or the sphere default.
//			If you are using a custom vendor script endure you turn it on and set the 2 defnames at the top of the script.
//			If you have a "redeed" option on your vendor system ensure that this option subtracts 1 from tag0.vendors on the sign
//			The sign can be found via region.tag0.sign
//
// IMPORTANT: 	If account ownership is turned on a player can still loose their house if they delete the character who name appears as house owner.
//		To retain the house the player should use another character on the account and use the transfer ownership option on that character 
//		(you can use this to transfer the house around the chars in account) BEFORE they delete the named character.
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

[DEFNAME housing_control]
account_ownership 0 	// set to 1 to grant ANY char on house owners account full ownership privs, set to 0 for owner char only 
buy_storage 1 		// if set to 1 allows players to increase secure storage by up to 100% via house sign, 0 to disable
can_bank 1 		// set to 1 to allow players to bank from home. (verbal command)(owner, co-owners & friends only)
can_decay 60 		// set to number of days for a house to decay, or set to 0 to disable
crate_decay 30 		// how long, in days the moving crate remains after a conversion (decays instantly when empty)
fallfix 0 		// Use fallfix event? 1=on/0=off (prevents falling/sinking through floor)
inheritance 1 		// set to 1 to allow co-owner #1 to inherit a property from it's owner if they no longer exist.
redeed_custom 0 	// 1 allows redeed of custom multi, 0 disables (IF ACTIVE YOU MUST HAVE ALL CUSTOM FOUNDATION DEEDS SCRIPTED)
wipe_switch_lists 1 	// if set to 1 access/ban lists are wiped when house is switched between private/public (helps reduce tag usage)

custom_vendor 1 		// set to 1 if you are using a custom vendor script 0 assumes sphere default & will hunt for pet memories in checks
vendordeed i_deed_vendor	// insert the DEFNAME of the vendor deed from your vendor system here if using custom script i.e i_deed_myvendor
vendor_owner_tag tag0.owner	// enter the tag that defines a vendors owner here if using custom script i.e. tag0.owner

//DO NOT ALTER THE FOLLOWING AFTER INITIAL SETUP!!!
account_house_limit 0	// Number of houses allowed PER ACCOUNT, set to 0 to disable limits
max_friends 20 		// DO NOT SET THIS ONE ABOVE 20 Dialog doesn't have extra pages for above that amount (actual number is set from building size)
max_co_owners 10 	// DO NOT SET THIS ONE ABOVE 10 Dialog is not set up for more than that amount (actual number is set from building size)
max_container_items 100 // Maximum items per secure container. (MOVING CRATE DOES NOT OBEY THIS RULE)
min_secure 4 		// Minimum secure containers will not be lower than this number (affects only small houses & foundations)
min_lockdowns 100 	// Minimum lockdowns will not be lower than this number (affects only small houses & foundations)
max_secure 100 		// Cap for secure containers, actual number is set from building size, but will not exceed this number
max_lockdowns 500 	// As above

//DO NOT ALTER THIS ONE (EVER!)
access_inhibit -1,1 

[ITEMDEF 0bd1] 
//brass sign 
DEFNAME=i_sign_brass 
TYPE=t_script 
CAN=can_i_dcignorelos
CATEGORY=Decoration - Signs 
SUBSECTION=Blank 
DESCRIPTION=Brass Blank 
DUPELIST=0bd2 

ON=@Create
attr=attr_move_never|attr_can_decay
timerf 1, f_initialise

ON=@Click
	if !<link.region.tag0.forsale>
		if <tag0.is_guild>
			message <name>
			message [<uid.<tag0.is_guild>.abbrev>]
			return 1
		else
			return 0
		endif
	else
		message For Sale
		message <link.dtag0.price>c
		return 1
	endif

ON=@ClientToolTip
if <obj.tag0.demolition>
	src.addcliloc 1070722, Demolition Scheduled in	<QVAL (<eval <timer>/86400> < 1)? <eval (<timer>%86400)/3600> hrs and <eval ((<timer>%86400)%3600)/60> mins:<eval <timer>/86400> days>
elif <link.region.tag0.forsale>
	src.addcliloc 1070722, FOR SALE <link.dtag0.price>c
endif
src.addcliloc 1061639,<name>
src.addcliloc 1061640,<uid.<more1>.name>
if <tag0.is_guild>
	src.addcliloc 1042971,[<uid.<tag0.is_guild>.abbrev>] Guild House
endif
src.addcliloc <QVAL (<link.tag0.private>)? 1061642:1061641>
return 1

ON=@DClick
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.isgm>)
	if !(<more1>) || !(<region.tag0.sign>)
		f_initialise
		return 1
	endif
endif
if <def.inheritance>
	if (<obj.link.tag0.coowner_01>==<src.uid>) && (<eval <timer>/86400> < 5) 
		obj.f_owner_check
		obj=<obj.link.more1>
		obj.try sysmessage @,,1 You have inherited this house from it's previous owner
	elif (<src.is_coowner>) || (<src.is_friend>) && (<obj.link.tag0.coowner_01>!=<src.uid>) && (<eval <timer>/86400> < 5)
		if !(<obj.f_existant_owner>)
			src.sysmessage @,,1 WARNING! The owner of this house no longer exists. Co-Owner <uid.<obj.link.tag0.coowner_01>.name> must dclick this sign within the next 5 days to inherit this house.
		endif
	endif
endif
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.is_friend>) || (<src.isgm>)
	if (<src.region.uid>==<link>)
		sdialog d_house_menu
		return 1		
	else
		dialog d_house_visitor
		src.sysmessage @,,1 You must be standing on your doorstep to access the house menu.
		return 1
	endif
else
	if !<link.region.tag0.forsale>
		dialog d_house_visitor
		return 1
	else
		dialog d_house_forsale
		return 1
	endif
endif

ON=@Timer
if (<def.can_decay> >0)
	if !(<link.region.tag0.noredeed>)
		if <def.account_house_limit>
			obj=<link.more1>
			if (<obj.account.dtag0.houses> > 0 )
				obj.account.tag0.houses=<eval <obj.account.tag0.houses>-1>
			endif
		endif
		link.f_empty
		link.remove
	else
		timer= -1
		link.tag0.price=<link.tag0.value>
		link.region.tag0.forsale=1
		tag0.boughtstorage=
		tag0.xtrastorage=
		link.f_wipe_contents
		link.f_open_house
		tag.friend=
		tag.co_owner=
		tag.bans=
		tag.access=
		link.tag0.private=
		newitem=i_realtor
		new.p=<p>
		more1=<new.uid>
		link.more1=<new.uid>
		resendtooltip
		return 1
	endif	
else
	return 1
endif

[ITEMDEF i_realtor]
NAME=Britannic Realtors
ID=0eec
TYPE=t_script

ON=@Create
attr=attr_invis|attr_can_decay|attr_move_never
color=0482
Timer= -1

ON=@Timer 
return 1

[FUNCTION f_initialise]
if !(<link>==04fffffff)
	if !<more1>
		more1=<link.more1>
		local.multi=<link.multiregion>
		call co_f_calc <local.multi>
		call lockdown_calc <local.multi>
		tag.Oname=<link.name>
		name=<link.name>
		link.tag0.private=1
		if (<link.baseid>==i_multi_castle)
			tag.doors=48
			tag.dused=4
		elif (<link.baseid>==i_multi_keep)
			tag.doors=22
			tag.dused=2
		endif
		if (<region.tag0.movingcrate>)
			tag0.secure +=1
		endif
		if !(<link.value>)
			link.region.tag0.noredeed=1
			local.topx=<strarg <link.multiregion>> 
			local.topy=<strarg <streat <link.multiregion>>>
			local.bottomx= <strarg <streat <streat <link.multiregion>>>> 
			local.bottomy= <strarg <streat <streat <streat <link.multiregion>>>>>
			local.x <eval (<local.bottomx>-<local.topx>)+1> 
			local.y <eval <local.bottomy>-<local.topy>>
			local.xy <dlocal.x>x<dlocal.y>
			link.tag0.footprint=<local.xy>
		endif
		obj=<link>
		try obj.region.events +r_houses
		try obj.region.tag0.sign=<uid>
		obj.f_door_setup
		obj=<region.tag0.sign>
		obj.attr=attr_move_never
		if <def.can_decay>
			timer=60*60*24*<def.can_decay>	
		else
			timer =-1
		endif
		if (<obj.link.more1.account.plevel> > 4) && (<obj.region.tag0.noredeed>) 
			timer=-1
		endif
		obj=<link.more1>
		obj.try f_remove_keys
		if (<def.account_house_limit>) 
			if !(<obj.isgm>)
				obj=<region.tag0.sign>
				if !(<obj.region.tag0.convert>)
					obj=<link.more1>
					if (<obj.account.dtag0.houses> < <def.account_house_limit>)
						obj.account.tag0.houses=<eval <obj.account.tag0.houses>+1>
					else
						obj=<region.tag0.sign>
						local.src=<obj.link.more1>
						serv.newitem i_deed_<STRSUB 8 30 <obj.link.baseid>>
						new.cont=<local.src>
						obj.link.remove
						obj=<local.src>
						obj.sysmessage @,,1 House redeeded, You already own the maximum number of houses permitable
					endif
				endif
			endif
		endif	
		resendtooltip
	endif
endif

[FUNCTION render_coowners]
local.y=170
for 1 <obj.dtag0.max_coowners>
	if (<obj.link.tag0.coowner_<local._for>>)
		if <def.inheritance>
			dtext 57 <local.y> <QVAL (<local._for>==01)? 54:90> <uid.<obj.link.tag0.coowner_<local._for>>.name>
		else
			dtext 57 <local.y> 90 <uid.<obj.link.tag0.coowner_<local._for>>.name>
		endif
	else
		return
	endif
local.y += 20
endfor

[FUNCTION render_coowner_remove]
local.y=170
for 1 <obj.dtag0.max_coowners>
	if (<obj.link.tag0.coowner_<local._for>>)
		if (<src.is_coowner>)
			if (<obj.link.tag0.coowner_<local._for>>==<src.uid>)
				dtext 57 <local.y> 90 <uid.<obj.link.tag0.coowner_<local._for>>.name>
				button 7 <local.y> 4017 4018 1 0 <eval (100+<local._for>)>
			else
				dtext 57 <local.y> 90 <uid.<obj.link.tag0.coowner_<local._for>>.name>
			endif
		elif <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)>
			dtext 57 <local.y> 90 <uid.<obj.link.tag0.coowner_<local._for>>.name>
			button 7 <local.y> 4017 4018 1 0 <eval (100+<local._for>)>
		endif		
	else
		return
	endif
local.y += 20
endfor

[FUNCTION render_friends]
obj=<src.region.tag0.sign>
local.y=170
for 1 <obj.dtag0.max_friends>
	if (<local._for> < 11)	
		local.txt = 57
	else
		local.txt = 250
	endif
	if (<local._for>==11)
		local.y=170
	endif
	if (<obj.link.tag0.friend_<local._for>>)
		dtext <local.txt> <local.y> 90 <uid.<obj.link.tag0.friend_<local._for>>.name>
	else
		return
	endif
local.y += 20
endfor

[FUNCTION render_friend_remove]
obj=<src.region.tag0.sign>
local.y=170
for  1 <obj.dtag0.max_friends>
	if (<local._for> < 11)	
		local.txt = 57
		local.btn = 7
	else
		local.txt = 250
		local.btn = 200
	endif
	if (<local._for>==11)
		local.y=170
	endif
	if (<obj.link.tag0.friend_<local._for>>)
		if (<src.is_friend>)
			if (<obj.link.tag0.friend_<local._for>>==<src.uid>)
				dtext <local.txt> <local.y> 90 <uid.<obj.link.tag0.friend_<local._for>>.name>
				button <local.btn> <local.y> 4017 4018 1 0 <eval (200+<local._for>)>
			else
				dtext <local.txt> <local.y> 90 <uid.<obj.link.tag0.friend_<local._for>>.name>
			endif
		elif <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>)
			dtext <local.txt> <local.y> 90 <uid.<obj.link.tag0.friend_<local._for>>.name>
			button <local.btn> <local.y> 4017 4018 1 0 <eval (200+<local._for>)>
		endif
	else
		return
	endif
local.y += 20
endfor

[FUNCTION render_access]
local.y=160
dtext 120 130 90 Access List
dtext 230 140 90 <dtag0.access> / 10
if (<obj.tag0.is_guild>)
	dtext 280 140 90 + [<uid.<obj.tag0.is_guild>.abbrev>] members
endif
for 1 10
	if (<obj.link.tag0.access_<local._for>>)
		dtext 57 <local.y> 367 <uid.<obj.link.tag0.access_<local._for>>.name>
		local.y += 20
	else
		return
	endif
endfor

[FUNCTION render_bans]
local.y=160
dtext 125 130 90 Bans List
dtext 230 140 90 <obj.dtag0.bans> / 10
for 01 10
	if (<obj.link.tag0.banned_<local._for>>)	
		dtext 57 <local.y> 32 <uid.<obj.link.tag0.banned_<local._for>>.name>
		local.y += 20
	else
		return
	endif
endfor

[FUNCTION render_access_remove]
local.y=160
dtext 120 130 90 Access List
dtext 230 140 90 <obj.dtag0.access> / 10
if (<obj.tag0.is_guild>)
	dtext 280 140 90 + [<uid.<obj.tag0.is_guild>.abbrev>] members
endif
for 01 10
	if (<obj.link.tag0.access_<local._for>>)
		button 7 <local.y> 4017 4018 1 0 <eval (300+<local._for>)>
		dtext 57 <local.y> 367 <uid.<obj.link.tag0.access_<local._for>>.name>
		local.y += 20
	else
		return
	endif
endfor

[FUNCTION render_ban_remove]
local.y=160
dtext 125 130 90 Bans List
dtext 230 140 90 <obj.dtag0.bans> / 10
for 01 10
	if (<obj.link.tag0.banned_<local._for>>)
		button 7 <local.y> 4017 4018 1 0 <eval (400+<local._for>)>	
		dtext 57 <local.y> 32 <uid.<obj.link.tag0.banned_<local._for>>.name>
		local.y += 20
	else
		return
	endif
endfor 

[function render_signs]
local.y=157
local.sign=2979
	button 7 132 4005 4007  1 0  2965
	tilepic	37 132 2965
	tilepic 77 132 2966
	button 117 132 4014 4015 1 0 2966
for 1 112
	if (<local._for> < 20)
		local.btn1 = 7
		local.pic1 = 37
		local.pic2 = 77
		local.btn2 = 117
	elif (<local._for> < 40)
		local.btn1 = 167
		local.pic1 = 197
		local.pic2 = 237
		local.btn2 = 277
	elif (<local._for> < 60)
		local.btn1 = 317
		local.pic1 = 357
		local.pic2 = 397
		local.btn2 = 447
	else
		local.btn1 = 487
		local.pic1 = 517
		local.pic2 = 557
		local.btn2 = 597
	endif

	if (<local._for>==20) || (<local._for>==40) || (<local._for>==60)
		local.y=132
	endif
	button <local.btn1> <local.y> 4005 4007  1 0 <local.sign>
	tilepic	<local.pic1> <local.y> <local.sign>
	local.sign +=1
	tilepic <local.pic2> <local.y> <local.sign>
	button <local.btn2> <local.y> 4014 4015 1 0 <local.sign>
	local.sign +=1
	local._for +=1
	local.y += 25
	if (<dlocal.sign> == 3087)
		local.sign = 3139
	elif (<dlocal.sign> > 3140)
		return 
	endif
endfor

//YOU MUST FOLLOW THE INSTALLATION INSTRUCTIONS FOR THE @STEP TRIGGER TO WORK!!
[REGIONTYPE r_houses]
ON=@Step
obj=<src.region.tag0.sign>
	if !<src.isgm>
		if !<obj.link.tag0.private>
			if <src.is_banned>
				src.move <def.access_inhibit>
				src.sysmessage @,,1 You are banned from this property
				return 1
			endif
		else
			if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.is_friend>) || (<src.can_access>)  
				return 0
			elif (<obj.tag0.is_guild>)
				if (<src.guild>==<obj.tag0.is_guild>)
					return 0
				else
					src.move <def.access_inhibit>
					src.sysmessage @,,1 This property has been deemed private.
					src.sysmessage @,,1 You may not trespass.
					return 1
				endif
			else
				src.move <def.access_inhibit>
				src.sysmessage @,,1 This property has been deemed private.
				src.sysmessage @,,1 You may not trespass.
				return 1
			endif
		endif
	endif	
	
ON=@Enter
	if (<src.isplayer>) && !(<region.tag0.noredeed>)
		if <def.fallfix>
			src.events +e_fallfix
		endif
		src.events +e_vendor_chk
		if !<src.isgm>
			obj=<src.region.tag0.sign>
			obj.more2 += 1
		endif
	else
		if (<src.brain>==brain_vendor)
			src.timerf 1, f_ven_chk
		endif
	endif

ON=@Exit
	if !<src.isplayer>
		if (<src.brain>==brain_vendor)
			if (<src.tag0.prohibit>)
				src.tag0.prohibit=
				src.speech -spk_nopermit
				src.speech -spk_nopermit2
			else			
				obj=<src.region.tag0.sign>
				obj.tag0.vendors=<eval <obj.tag0.vendors>-1>
			endif
		endif
	endif
	if <def.fallfix>
		src.events -e_fallfix
	endif
	src.events -e_vendor_chk

//Begin main menu		
[DIALOG d_house_menu]
100,100
page 0
tag0.inclocks=<eval ((<tag.maxlockdowns>*10)*<tag0.xtrastorage>)/1000>
tag0.incconts=<eval ((<tag.maxsecure>*10)*<tag0.xtrastorage>)/1000>
resizepic 0 0 2620 400 118 
checkertrans 5 5 390 108
resizepic 0 123 2620 400 267
checkertrans 5 128 390 257
resizepic 0 395 2620 400 60
checkertrans 5 400 390 50
gumppic 7 8 100
dhtmlgump 25 30 100 60 0 0 <def.center><name><def.centere>
button 150 8 4005 4007 0 1 1
dtext 200 8 90 Information
button 150 28 4005 4007 0 2 2
dtext 200 28 90 Security
button 150 48 4005 4007 0 3 3
dtext 200 48 90 Storage
button 150 68 4005 4007 0 4 4
dtext 200 68 90 Customize
button 150 88 4005 4007 0 5 5
dtext 200 88 90 Ownership
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
	button 7 405 4005 4007 1 0 <QVAL (<link.tag0.private>)? 14:15>
	dtext 57 405 <QVAL (<link.tag0.private>)? 367 Grant Access:32 Ban Person>
	button 7 425 4005 4007 0 11 6
	dtext 57 425 <QVAL (<link.tag0.private>)? 32 Revoke Access:367 Unban Person>
else
	dtext 57 405 992 <QVAL (<link.tag0.private>)? Grant Access:Ban Person>
	dtext 57 425 992 <QVAL (<link.tag0.private>)? Revoke Access:Unban Person>
endif
button 200 415 4005 4007 1 0 29
dtext 250 415 1509 Eject Person

page 1
dtext 57 140 90 Owner:
dtext 150 140 90 <uid.<more1>.name> 
if (<tag0.is_guild>)
	dtext 20 170 90 This house is designated as the [<uid.<tag0.is_guild>.abbrev>] guild house
endif
dtext 20 190 90 This house is <QVAL (<link.tag0.private>)? private property:public>
dtext 20 210 90 This house is of <QVAL (<obj.link.type>==t_multi_custom)? custom:prebuilt> design
dtext 20 230 <QVAL (<tag0.demolition>)? 32 This house is improperly placed:90 This house is properly placed>
if <obj.region.tag0.forsale>
	dtext 20 250 90 This house is currently for sale for <link.dtag0.price>c
endif
if <def.can_decay>
	if (<timer> == -1)
		dtext 20 270 90 This house is currently set to auto refresh
	else
		if (<link.region.tag0.noredeed>)
			dtext 20 270 90 This house repossessed in <QVAL (<eval <timer>/86400> < 1)? <eval (<timer>%86400)/3600> hours and <eval ((<timer>%86400)%3600)/60> minutes.:<eval <timer>/86400> days>
		else
			dtext 20 270 90 This house <QVAL (<tag0.demolition>)? will be demolished:will decay> in <QVAL (<eval <timer>/86400> < 1)? <eval (<timer>%86400)/3600> hours and <eval ((<timer>%86400)%3600)/60> minutes.:<eval <timer>/86400> days>
		endif
	endif
endif
dtext 20 300 90 House Value
dtext 150 300 90 <QVAL (<link.region.tag0.noredeed>)? <eval <link.tag0.value>+<obj.tag0.boughtstorage>>:<eval <link.value>+<tag0.boughtstorage>+<tag0.construction>>>c
dtext 20 350 90 Number of visits this house has had:
dtext 300 350 90 <dmore2>

page 2
button 7 140 4005 4007 0 6 8
dtext 57 140 90 View Co-Owner List
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.isgm>)
	button 7 160 4005 4007 1 0 17
	dtext 57 160 90 Add a Co-Owner
	button 7 180 4005 4007 0 9 9
	dtext 57 180 90 Remove a Co-Owner
	button 7 200 4005 4007 1 0 18
	dtext 57 200 90 Clear Co-Owner List
else
	dtext 57 160 992 Add a Co-Owner
	if (<src.is_coowner>)
		button 7 180 4005 4007 0 9 9
	endif
	dtext 57 180 <QVAL (<src.is_coowner>)? 90:992> Remove a Co-Owner
	dtext 57 200 992 Clear Co-Owner List
endif
button 7 240 4005 4007 0 7 10
dtext 57 240 90 View Friends List
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
	button 7 260 4005 4007 1 0 19
	dtext 57 260 90 Add a Friend 
	button 7 280 4005 4007 0 10 11
	dtext 57 280 90 Remove a Friend 
	button 7 300 4005 4007 1 0 20
	dtext 57 300 90 Clear Friends List
else
	dtext 57 260 992 Add a Friend 
	if (<src.is_friend>)
		button 7 280 4005 4007 0 10 11
	endif
	dtext 57 280 <QVAL (<src.is_friend>)? 90:992> Remove a Friend 
	dtext 57 300 992 Clear Friends List
endif
if <link.tag0.private>
	button 7 340 4005 4007 0 8 12
	dtext 57 340 90 View Access List
	if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
		button 7 360 4005 4007 1 0 21
		dtext 57 360 90 Clear Access List
	else	
		dtext 57 360 992 Clear Access List
	endif
else
	button 7 340 4005 4007 0 8 13
	dtext 57 340 90 View Ban List
	if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
		button 7 360 4005 4007 1 0 22
		dtext 57 360 90 Clear Ban List
	else
		dtext 57 360 992 Clear Ban List
	endif
endif
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.isgm>)
	dtext 250 190 90 <QVAL (<tag0.is_guild>)? Remove Guild Declaration:Declare Guildhouse>
	button 200 190 4005 4007 1 0 50
	button 200 250 4005 4007 1 0 23
	button 200 220 4005 4007 1 2 26
	dtext 250 220 90 Recode Doors
	dtext 250 250 90 Change to <QVAL (<link.tag0.private>)? Public:Private>
	if <def.wipe_switch_lists>
		dhtmlgump 230 280 150 60 0 0 <def.small><def.bfont_white>(Will also clear <QVAL (<link.tag0.private>)? access:bans> list, don't worry they won't needed in private config)<def.smalle>
	endif
else
	dtext 250 190 992 <QVAL (<tag0.is_guild>)? Remove Guild Declaration:Declare Guildhouse>
	dtext 250 220 992 Recode Doors
	dtext 250 250 992 Change to <QVAL (<link.tag0.private>)? Public:Private>
endif

page 3
dtext 100 140 90 House Storage
if <def.buy_storage>
	dtext 20 170 90 Increased Storage
	dtext 200 170 90 <dtag0.xtrastorage>%
	button 240 170 4005 4007 0 13 35
	dtext 280 170 90 Buy Storage
endif
dtext 20 200 90 Maximum Secure Containers
dtext 200 200 90 <eval (<tag0.maxsecure>+<tag0.incconts>)>
dtext 20 220 90 Available Secured Containers
dtext 200 220 90 <eval ((<tag0.maxsecure>+<tag0.incconts>)-<tag0.secure>)>
dtext 20 250 90 Maximum Secure Storage
dtext 200 250 90 <eval (<tag0.maxsecure>+<tag0.incconts>)*100>
dtext 20 270 90 Available Secure Storage
dtext 200 270 90 <eval (((<tag0.maxsecure>+<tag0.incconts>)*100)-<tag0.storage>)>
dtext 20 300 90 Maximum Lockdowns
dtext 200 300 90 <eval (<tag.maxlockdowns>+<tag0.inclocks>)>
dtext 20 320 90 Available Lockdowns
dtext 200 320 90 <eval ((<tag0.maxlockdowns>+<tag0.inclocks>)-<tag0.locked>)>
dtext 20 350 90 Vendors
dtext 200 350 90 <dtag0.vendors>/<dtag0.max_vendors>
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
	button 240 350 4005 4007 1 0 57
	dtext 280 350 90 Rmv Bad Vendors
endif

page 4
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.isgm>)
	if (<link.type>==t_multi_custom)
		dtext 57 140 992 Convert into customizable house
		button 7 160 4005 4007 1 0 35
		dtext 57 160 90 Customize this house
		if (<obj.region.tag0.movingcrate>)
			button 7 180 4005 4007 1 0 54
		endif
		dtext 57 180 <QVAL (<link.region.tag0.movingcrate>)? 90 Relocate moving crate:992 Relocate moving crate>
	else
		if (<link.baseid>==i_multi_keep) || (<link.baseid>==i_multi_castle) || (<link.region.tag0.noredeed>)
			dtext 57 140 992 Convert into customizable house
			dtext 57 160 992 Customize this house
			dtext 57 180 992 Relocate moving crate
		else
			button 7 140 4005 4007 1 0 53
			dtext 57 140 90 Convert into customizable house
			dtext 57 160 992 Customize this house	
			dtext 57 180 992 Relocate moving crate
		endif
	endif
	button 7 200 4005 4007 1 0 24
	dtext 57 200 90 Rename this house
	button 7 220 4005 4007 1 0 25
	dtext 57 220 90 Change house sign
	if (<link.type>==t_multi_custom) || (<link.region.tag0.noredeed>==2)
		button 7 240 4005 4007 0 14 37
	endif
	if (<link.type>==t_multi_custom)
		button 7 260 4005 4007 1 0 36
	endif
	dtext 57 240 <QVAL (<link.type>==t_multi_custom) || (<link.region.tag0.noredeed>==2)? 90 Change house sign hanger:992 Change house sign hanger>
	dtext 57 260 <QVAL (<link.type>==t_multi_custom)? 90 Change sign post:992 Change sign post>
	if (<link.region.tag0.noredeed>)
		button 7 280 4005 4007 1 0 58
		dtext 57 280 90 Flip house sign
		if (<link.region.tag0.noredeed>==1)	
			button 7 300 4005 4007 1 0 59
			dtext 57 300 90 Create house sign hanger
		elif (<link.region.tag0.noredeed>==2)
			button 7 300 4005 4007 1 0 59
			dtext 57 300 90 Remove house sign hanger
		endif	
	else
		dtext 57 280 992 Flip house sign
		dtext 57 300 992 Create house sign hanger
	endif
	if (<link.baseid>==i_multi_keep) || (<link.baseid>==i_multi_castle)
		button 7 320 4005 4007 1 0 52
		dtext 57 320 90 Place/Remove Door(s) (<eval <tag.doors>-<tag.dused>> available)
	else
		dtext 57 320 992 Place/Remove Door(s)  
	endif
else
	dtext 57 140 992 Convert into customizable house
	dtext 57 160 992 Customize this house
	dtext 57 180 992 Relocate moving crate
	dtext 57 200 992 Rename this house
	dtext 57 220 992 Change house sign
	dtext 57 240 992 Change house sign hanger
	dtext 57 260 992 Change sign post
	dtext 57 280 992 Flip house sign
	dtext 57 300 992 Create house sign hanger
	dtext 57 320 992 Place Door(s)
endif
dtext 57 350 90 Placed trashcan? (<QVAL <tag0.trashcan>? Yes:No>)

page 5
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.isgm>)
	button 7 140 4005 4007 1 0 27
	dtext 57 140 90 Transfer Ownership of this house
	button 7 170 4005 4007 0 12 30
	dtext 57 170 90 <QVAL (<link.region.tag0.forsale>)? Take this house off the market:Put this house up for sale>
	if !<obj.region.tag0.noredeed>
		button 7 200 4005 4007 1 0 28
	endif
	dtext 57 200 <QVAL (<link.region.tag0.noredeed>)? 992 Demolish this house:90 Demolish this house>
else
	dtext 57 140 992 Transfer Ownership of this house
	dtext 57 170 992 Put this house up for sale
	dtext 57 200 992 Demolish this house
endif
if (<src.isgm>)
	dtext 140 240 90 Administrative Options
	if (<link.region.tag0.noredeed>)
		dtext 57 290 90 Footprint	
		dtext 200 290 90 <link.tag0.footprint>
		button 7 310 4005 4007 1 0 55
		dtext 57 310 90 Set property value & place on market
	else
		if !(<tag0.demolition>)
			dtext 100 270 90 Schedule this property for demolition
			dtext 40 300 90 Minimum permitted time until demolition is 7 days
			dtext 40 320 90 Maximum permitted time until demolition is 30 days
			gumppic 210 350 2444
			dtextentrylimited 220 350 40 20 90 1 2
			dtext 290 350 90 days
		endif
		dtext 57 350 90 <QVAL (<tag0.demolition>)? Cancel scheduled demolition:Demolish this propery in> 
		button 7 350 4023 4025 1 0 75
	endif
endif

page 6
dtext 120 130 90 Co-Owner List
dtext 230 140 90 <dtag0.co_owner> / <dtag0.max_coowners>
render_coowners

page 7
dtext 130 130 90 Friends List
dtext 230 140 90 <dtag0.friend> / <dtag0.max_friends>
render_friends

page 8
if (<link.tag0.private>)
 	render_access
else
	render_bans
endif

page 9
dtext 120 130 90 Co-Owner List
dtext 230 140 90 <dtag0.co_owner> / <dtag0.max_coowners>
dtext 10 150 1152 Rmv
render_coowner_remove

page 10
dtext 130 130 90 Friends List
dtext 230 140 90 <dtag0.friend> / <dtag0.max_friends>
dtext 10 150 1152 Rmv
render_friend_remove

page 11
if (<link.tag0.private>)
 	render_access_remove
else
	render_ban_remove
endif

page 12
dtext 150 130 90 Sell This House
if !<link.region.tag0.forsale>
	gumppic 130 170 2501
	dtextentrylimited 135 170 100 20 90 0 10
	dhtmlgump 40 230 300 200 0 0 <def.bfont_white>Please ensure that your house is empty before you put it on the market.  All lists will be wiped and all doors will be unlocked.  Your house will be open to viewing, however you will retain control over it, until it is bought.<def.bfonte>
else
	dtext 160 250 90 <link.dtag0.price>c
endif	
dtext <QVAL (<link.region.tag0.forsale>)? 80 200 90 This house is currently for sale for:130 200 90 Enter your asking price>
button <QVAL (<link.region.tag0.forsale>)? 180 340 4023 4025 1 0 30:70 340 4023 4025 1 0 30>
dtext <QVAL (<link.region.tag0.forsale>)? 100 310 90 Take my house off the market:120 340 90 Put this house on the market>

page 13
dtext 150 130 90 Increase Storage
dhtmlgump 40 150 300 120 0 0 <def.bfont_white>You may buy up to a 100% increase in storage space. The value listed for this property will be adjusted to reflect your investment.  The price for a 100% upgrade should not cost more than 50% of the property price, however you buy it.<def.bfonte>
dhtmlgump 40 260 300 120 0 0 <def.bfont_white>The value of purchased storage will be refunded to your bankbox upon redeeding.<def.bfonte>
if !<tag0.xtrastorage>
	button 7 360 4023 4025 1 0 34
	dtext 57 360 90 Add 100% for: <QVAL (<link.value>)? <eval <link.value>/2>:<eval <link.tag0.value>/2>>c
else
	dtext 57 360 992 Add 100% 
endif
if (<eval <tag0.xtrastorage>> < 26)
	button 7 340 4023 4025 1 0 33
	dtext 57 340 90 Add 75% for: <QVAL (<link.value>)? <eval ((<link.value>/8)*3)>:<eval ((<link.tag0.value>/8)*3)>>c
else
	dtext 57 340 992 Add 75% 
endif
if (<eval <tag0.xtrastorage>> < 51)
	button 7 320 4023 4025 1 0 32
	dtext 57 320 90 Add 50% for: <QVAL (<link.value>)? <eval <link.value>/4>:<eval <link.tag0.value>/4>>c
else
	dtext 57 320 992 Add 50% 
endif
if (<eval <tag0.xtrastorage>> < 76)	
	button 7 300 4023 4025 1 0 31
	dtext 57 300 90 Add 25% for: <QVAL (<link.value>)? <eval (<link.value>/8)>:<eval (<link.tag0.value>/8)>>c
else
	dtext 57 300 992 Add 25% 
endif

page 14
local.number <uid.<src.region.tag0.sign>.dispid>
if (<eval <fval <local.number>/2>*2>==<local.number>)
	button 30 180 4005 4007 1 0 2968
	tilepic 70 180 2968
	button 150 180 4005 4007 1 0 2970
	tilepic 190 180 2970
	button 280 180 4005 4007 1 0 2972
	tilepic 320 180 2972
	button 30 280 4005 4007 1 0 2974
	tilepic 70 280 2974
	button 150 280 4005 4007 1 0 2976
	tilepic 190 280 2976
	button 280 280 4005 4007 1 0 2978
	tilepic 320 280 2978
else
	button 30 180 4005 4007 1 0 2967
	tilepic 70 180 2967
	button 150 180 4005 4007 1 0 2969
	tilepic 190 180 2969
	button 280 180 4005 4007 1 0 2971
	tilepic 320 180 2971
	button 30 280 4005 4007 1 0 2973
	tilepic 70 280 2973
	button 150 280 4005 4007 1 0 2975
	tilepic 190 280 2975
	button 280 280 4005 4007 1 0 2977
	tilepic 320 280 2977
endif


[DIALOG d_house_menu button]
obj=<src.region.tag.sign>
on=0,75
	if (<argn>==14) //grant access
			targetf, f_add_access
	elif (<argn>==15)//ban
			targetf, f_add_ban
	elif (<argn>==17) //add coowner
			targetf, f_add_coowner
	elif (<argn>==18) //clear coowner
			src.ctag0.list=CO-OWNER
			src.dialog d_clear_list
	elif (<argn>==19) //add friend
			targetf, f_add_friend
	elif (<argn>==20) //clear friend
			src.ctag0.list=FRIEND
			src.dialog d_clear_list
	elif (<argn>==21) //clear access
			src.ctag0.list=ACCESS
			src.dialog d_clear_list
	elif (<argn>==22) //clear bans
			src.ctag0.list=BANS
			src.dialog d_clear_list
	elif (<argn>==23) //private/public toggle
			if (<obj.link.tag0.private>)
				obj.link.tag0.private=
				obj.resendtooltip
				if <def.wipe_switch_lists>
					for 1 10
						obj.link.tag0.access_<local._for>=
					endfor
				endif
				src.sysmessage @,,1 This property is now to the public (shop setting), only those on the banned list may not enter.
				src.sysmessage @,,1 You should recode any doors you do not wish the general public to be able to open.
			else
				obj.link.tag0.private=1
				obj.resendtooltip
				if <def.wipe_switch_lists>
					for 1 10
						obj.link.tag0.banned_<local._for>=
					endfor
				endif
				src.sysmessage @,,1 This propery is now private & may only be accessed by co-owners/friends & those on the access list
			endif
			dialog d_house_menu 
	elif (<argn>==24)//rename house
			src.dialog d_rename_house
	elif (<argn>==25)//change sign
			src.dialog d_change_sign
			return 1
	elif (<argn>==26)//recode doors
			src.dialog d_secure
			return 1
	elif (<argn>==27)//transfer ownership
			obj=<src.region.tag0.sign>
			if (<obj.tag0.demolition>)
				src.sysmessage @,,1 This house is scheduled for demolition and cannot be traded.
				return 1
			endif
			targetf, f_transfer_house
			src.sysmessage @,,1 To whom do you wish to transfer full ownership of this property?
			return 1
	elif (<argn>==28)//demolish
			if (<dtag0.locked> >0) || (<dtag0.secure> >0)
				src.sysmessage @,,1 You still have Locked down items/Secure containers.
				src.sysmessage @,,1 You must remove these before demolishing your house.
				return 1
			else
				src.dialog d_demolish
				src.sysmessage @,,1 Any items left in your house will be permanently lost upon demolition.
				return 1
			endif
	elif (<argn>==29)// eject
			targetf, f_eject
			return 1
 	elif (<argn>==30)// price/take off market
			obj=<src.region.tag0.sign>
			if (<obj.tag0.demolition>)
				src.sysmessage @,,1 This house is scheduled for demolition and cannot be sold.
				return 1
			endif
			if (<obj.region.tag0.forsale>)
				obj.region.tag0.forsale=
				obj.link.tag0.price=	
				src.sysmessage @,,1 You have taken this house off the market
				src.sysmessage @,,1 Don't forget to reset the house settings to your preferences.
				resendtooltip
				return 1
			else
				if (<obj.tag0.lockdowns>) || (<obj.tag0.secure>)
					src.sysmessage @,,1 You still have lockdowns/secure containers within the house
					return 1
				else
					if (<isnum <argtxt[0]>>)
						obj.link.tag0.price=<argtxt[0]>
						obj.region.tag0.forsale=1
						obj.link.f_open_house
						obj.tag.friend=
						obj.tag.co_owner=
						obj.tag.bans=
						obj.tag.access=
						src.sysmessage @,,1 Your house is now on the market for <obj.link.dtag0.price>c
						resendtooltip
						return 1
					else
						src.sysmessage @,,1 Invalid price, your price must be numeric
						return 1
					endif
				endif
			endif
	elif (<argn>==31) // add 25% storage
			local.value=<QVAL (<obj.link.value>)? <eval (<obj.link.value>/8)>:<eval (<obj.link.tag0.value>8)>>
			call f_buy_storage <local.value>
			return 1
	elif (<argn>==32) //add 50% storage
			local.value=<QVAL (<obj.link.value>)? <eval <obj.link.value>/4>:<eval <obj.link.tag0.value>/4>>
			call f_buy_storage <local.value>
			return 1
	elif (<argn>==33) // add 75% storage
			local.value=<QVAL (<obj.link.value>)? <eval ((<obj.link.value>/8)*3)>:<eval ((<obj.link.tag0.value>/8)*3)>>
			call f_buy_storage <local.value>
			return 1
	elif (<argn>==34) //add 100% storage
			local.value=<QVAL (<obj.link.value>)? <eval <obj.link.value>/2>:<eval <obj.link.tag0.value>/2>>
			call f_buy_storage <local.value>
			return 1
	elif (<argn>==35) //custom builder
			obj=<src.region.tag0.sign>
			if (<obj.tag0.demolition>)
				src.sysmessage @,,1 This house is scheduled for demolition and cannot be customized.
				return 1
			endif
			if (<src.region.uid> != <region.uid>)
				return 2
			elif (<src.housedesign>)
				src.sysmessage @,,1 You are already designing <QVAL (<src.housedesign.uid> == <link.uid>)? this:another> building.
				return 1
			elif !(<src.isevent.e_house_customize>)
				src.events +e_house_customize
			endif
			link.customize
			return 1 
	elif (<argn>==36) //sign post menu
			src.dialog d_sign_post
			return 1
	elif (<argn>==50) //guild declaration
			obj=<src.region.tag0.sign>
			if (<obj.tag0.is_guild>)
				obj.tag0.is_guild=
				resendtooltip
				src.sysmessage @,,1 This building is no longer a guildhouse.
				return 1
			else
				src.targetf, f_declare_guild	
				src.sysmessage @,,1 Target your guildstone 
				return 1
			endif
	elif (<argn>==52) //door menu
			src.dialog d_door_menu	
			return 1
	elif (<argn>==53)// convert
			obj=<src.region.tag0.sign>
			if (<obj.tag0.demolition>)
				src.sysmessage @,,1 This house is scheduled for demolition and cannot be converted.
				return 1
			endif
			obj.link.f_swap
	elif (<argn>==54)//move crate
			obj=<region.tag0.movingcrate>
			if (<src.region.tag0.sign>==<obj.region.tag0.sign>)
				obj.try p=<src.p>
				obj.try update
			else
				src.sysmessage @,,1 You cannot relocate your moving crate outside of your house.
				return 1
			endif	
	elif (<argn>==55) //admin, value and market
			src.dialog d_static_pricing
			return 1
	elif (<argn>==57) //rmv bad vendors
			obj.link.f_vendor_recount
	elif (<argn>==58) //flip sign
			obj.flip
	elif (<argn>==59) //create/remove hanger
			if (<src.region.tag0.noredeed>==1)
				serv.newitem 0b98
				new.attr=04010
				new.timer= -1
				new.p=<uid.<src.region.tag0.sign>.p>
				src.region.tag0.hanger=<new.uid>
				src.region.tag0.noredeed=2
			else
				obj=<src.region.tag0.hanger>
				obj.remove
				src.region.tag0.hanger=
				src.region.tag0.noredeed=1
			endif
	elif (<argn>==75) //schedule demolition GM
			if !(<obj.tag0.demolition>)
				if (<obj.region.tag0.forsale>)
					obj.region.tag.forsale=
					obj.link.price=
					src.sysmessage @,,1 Property has been removed from housing market.
				endif
				if (<isnum <argtxt[1]>>)
					if (<eval <argtxt[1]>> < 31)
						if (<eval <argtxt[1]>> > 6)
							obj=<src.region.tag0.sign>
							obj.tag0.demolition=<eval <argtxt[1]>>
							obj.timer=60*60*24*<eval <argtxt[1]>>
							src.sysmessage @,,1 You have scheduled this house to be demolished in <eval <obj.tag0.demolition>> days.
							resendtooltip
							return 1
						else
							src.sysmessage @,,1 The minimum notice period before demolition can occur is 7 days.
							return 1
						endif
					else
						src.sysmessage @,,1 Demolition must occur within 30 days
						return 1
					endif
				else
					src.sysmessage @,,1 Invalid entry, days must be numeric & < 30
					return 1
				endif
			else
				obj.tag.demolition=
				resendtooltip
				src.sysmessage @,,1 The scheduled demolition has been cancelled.
			endif
	endif

on=100,499 // rmv person from (x) list
src.ctag0.button=<argn>
if (<argn><200)
	local.btn_correct=100
	call f_remove_from_list <local.btn_correct>
elif (<argn><300)
	local.btn_correct=200
	call f_remove_from_list <local.btn_correct>
elif (<argn><400)
	local.btn_correct=300
	call f_remove_from_list <local.btn_correct>
elif (<argn><500)
	local.btn_correct=400
	call f_remove_from_list <local.btn_correct>
endif

on=2967,2978
foritems 3
	if (<baseid>==0b98) || (<baseid>==0b97)
		obj=<uid>
		obj.try dispid=<hval <argn>>
		obj.update
	endif
endfor
//End main menu

[FUNCTION f_remove_from_list]
argn=<src.ctag0.button>
if (<dlocal.btn_correct>==100)	
	src.ctag0.list=coowner
elif (<dlocal.btn_correct>==200)	
	src.ctag0.list=friend
elif (<dlocal.btn_correct>==300)	
	src.ctag0.list=access
elif (<dlocal.btn_correct>==400)	
	src.ctag0.list=banned
endif

if (<obj.link.tag0.<src.ctag0.list>_<hval <argn>-<dlocal.btn_correct>>>==<src.uid>)
	src.sysmessage @,,1 You have removed yourself from the <src.ctag0.list> list.
	if (<obj.link.tag0.private>)
		src.go <uid.<obj.region.tag0.sign>.p>
		src.sysmessage @,,1 This is private property, you no longer have authority to enter.
	endif
else
	src.sysmessage @,,1 <uid.<obj.link.tag0.<src.ctag0.list>_<hval <argn>-<dlocal.btn_correct>>>.name> has been removed from the <src.ctag0.list> list.
endif
//bckwrd compatabilty
if (<dlocal.btn_correct>==100)
	obj.tag.co_owner -=1
elif (<dlocal.btn_correct>==400)
	obj.tag.bans -=1
else
	obj.tag.<src.ctag0.list> -=1
endif
//end
if <obj.link.tag0.<src.ctag0.list>_<hval <argn>-<eval (<local.btn_correct>-1)>>>
	local._for=<eval (<argn>-<dlocal.btn_correct>)>
	local.next=<eval (<argn>-(<dlocal.btn_correct>-1))>
	if (<dlocal.btn_correct>==100) || (<dlocal.btn_correct>==200)
		for <eval (<argn>-<dlocal.btn_correct>)> <obj.tag0.max_<src.ctag0.list>s>
			if <obj.link.tag0.<src.ctag0.list>_<local.next>>
				obj.try link.tag0.<src.ctag0.list>_<local._for>=<link.tag0.<src.ctag0.list>_<local.next>>
				local.next += 1
				local._for += 1
			else
				obj.try link.tag0.<src.ctag0.list>_<local._for>=
				return
			endif
		endfor
	else
		for 1 10
			if <obj.link.tag0.<src.ctag0.list>_<local.next>>
				obj.try link.tag0.<src.ctag0.list>_<local._for>=<link.tag0.<src.ctag0.list>_<local.next>>
				local.next += 1
				local._for += 1
			else
				obj.try link.tag0.<src.ctag0.list>_<local._for>=
				return
			endif
		endfor
	endif
else
	obj.try link.tag0.<src.ctag0.list>_<hval <argn>-<dlocal.btn_correct>> =
endif
src.ctag0.list=
src.ctag0.button=
dialog d_house_menu

[dialog d_change_sign]
0,0
page 0
resizepic 0 123 2620 485 525
checkertrans 5 128 475 515
render_signs
button 360 560 4017 4018 1 0 0
dtext 410 560 90 Cancel

[dialog d_change_sign button]
on=0
dialogclose

on=2965,3140
obj=<src.region.tag0.sign>
try obj.dispid=<hval <argn>>
try obj.update
return 1

[DIALOG d_demolish]
100,100
resizepic 0 0 2620 200 200 
checkertrans 5 5 190 190
dtext 50 10 32 !!! WARNING !!!
dtext 10 30 90 You are about to demolish
dtext 10 50 90 this property.
dtext 10 70 90 Do you wish to proceed?
button 7 100 4017 4018 1 0 0
dtext 47 100 90 Abort
button 7 130 4005 4007 1 0 1
dtext 47 130 90 Demolish w/o deed
if (<uid.<src.region.uid>.type>==t_multi_custom)
	if <def.redeed_custom>
		button 7 160 4005 4007 1 0 2
	endif
	dtext 47 160 <QVAL (<def.redeed_custom>)? 90 Redeed:992 Redeed>
else
	button 7 160 4005 4007 1 0 2
	dtext 47 160 90 Redeed
endif

[DIALOG d_demolish button]
on=0
src.sysmessage @,,1 Aborted
ON=1
obj=<src.region.tag0.sign>
local.total=<eval <obj.link.value>+<obj.tag0.boughtstorage>+<obj.tag0.construction>>
call f_refund <local.total>
if <def.account_house_limit>
	if (<src.account.dtag0.houses> > 0)
		src.account.tag0.houses=<eval <src.account.tag0.houses>-1>
	endif
endif
obj=<src.region.tag0.sign>
obj.link.f_empty
obj.link.remove

on=2
obj=<src.region.tag.sign>
obj.link.f_empty
local.total=<eval <obj.tag0.boughtstorage>+<obj.tag0.construction>>
call f_refund <local.total>
if <def.account_house_limit>
	if (<src.account.dtag0.houses> > 0)
		src.account.tag0.houses=<eval <src.account.tag0.houses>-1>
	endif
endif
obj=<src.region.tag0.sign>
serv.newitem i_deed_<STRSUB 8 30 <obj.link.baseid>>
new.p=<src.p>
new.bounce 
obj.link.remove

[DIALOG d_house_forsale]
10,10
obj=<region.tag0.sign>
page 0
resizepic 0 0 2620 400 118 
checkertrans 5 5 390 108
resizepic 0 123 2620 400 267
checkertrans 5 128 390 257
resizepic 0 395 2620 400 60
checkertrans 5 400 390 50
gumppic 7 8 100
dtext 50 50 0 FOR SALE
dtext 200 40 90 List Price <link.dtag.price>c
dtext 57 140 90 Owner:
dtext 150 140 90 <uid.<link.more1>.name> 
dtext 20 160 90 This house is for sale by owner
button 57 180 4005 4007 1 0 1
dtext 107 180 90 Buy this house 
dtext 20 200 90 Maximum Co-Owners  
dtext 250 200 90 <dtag.max_coowners>
dtext 20 220 90 Maximum Friends
dtext 250 220 90 <dtag.max_friends>
dtext 20 240 90 Maximum Lockdowns
dtext 250 240 90 <eval (<tag.maxlockdowns>+<tag0.inclocks>)>
dtext 20 260 90 Maximum Secure Containers
dtext 250 260 90  <eval (<tag0.maxsecure>+<tag0.incconts>)>
dtext 20 300 90 House Value
dtext 150 300 90 <QVAL (<obj.region.tag0.noredeed>)? <eval <obj.link.tag0.value>+<obj.tag0.boughtstorage>>:<eval <obj.link.value>+<obj.tag0.boughtstorage>+<obj.tag0.construction>>>c
dtext 20 340 90 Number of visits this house has had:
dtext 300 340 90 <dmore2>

[DIALOG d_house_forsale button]
obj=<src.region.tag0.sign>
on=1
			src.payup <eval <obj.link.tag0.price>>
			if (<var0.enough>)
				local.total=<eval <obj.link.tag0.price>>
				obj=<obj.link.more1>
				if (<uid.<argn>.more1>.isplayer)
					src.account.tag0.houses=<eval <src.account.tag0.houses>+1>
					if (<obj.account.dtag0.houses> > 0 )
						obj.account.tag0.houses=<eval <obj.account.tag0.houses>-1>
					endif
					call f_refund <local.total>
					if <obj.isonline>
						obj.sysmessage @,,1 Your house has been sold, <dlocal.total>c has been deposited in your bank box.
					endif
				else
					obj.remove
				endif
				obj=<src.region.tag0.sign>
				src.sysmessage @,,1 You are now the owner of this property.
				src.sysmessage @,,1 This house is currently set to public (shop setting)
				src.sysmessage @,,1 Do not forget to change the settings to your preferences.
				obj.region.tag0.forsale=
				obj.link.tag0.price=
				if <def.can_decay>
					obj.timer=60*60*24*<def.can_decay>
				endif
				obj.more1=<src.uid>
				obj.link.more1=<src.uid>
				obj.link.name=<tag.oname>
				obj.region.name=<tag.oname>
				obj.name=<tag.oname>
				obj.resendtooltip
			else
				src.sysmessage @,,1 You do not have the funds to purchase this property
				return 1
			endif
		else
			src.sysmessage @,,1 You already own the maximum number of houses permitable.
			return 1

[DIALOG d_house_visitor]
100,100
gumppic  27 27 100 
dhtmlgump 50 50 100 100 0 0 <def.center><name><def.centere>
if <tag0.is_guild>
	dtext 85 25 90 [<uid.<tag0.is_guild>.abbrev>]
endif

[DIALOG d_rename_house]
100,100
resizepic 0 0 2620 200 200 
checkertrans 5 5 190 190
gumppic 27 27 100
dtextentrylimited 50 40 95 90 0 1 60 
button 107 140 4017 4018 1 0 0
dtext 147 140 90 Abort
button 7 140 4005 4007 1 0 1
dtext 47 140 90 Confirm

[DIALOG d_rename_house BUTTON]
ON=0
src.sysmessage @,,1 House renaming cancelled.
return 1
ON=1
obj=<src.region.tag.sign>
obj.name=<argtxt[1]>
obj.link.region.name=<argtxt[1]>
return 1

[DIALOG d_clear_list]
100,100
obj=<src.region.tag.sign>
resizepic 0 0 2620 200 200 
checkertrans 5 5 190 190
dtext 50 10 32 !!! WARNING !!!
dtext 10 30 90 You are about to clear
dtext 10 50 90 your <src.ctag0.list> list
dtext 10 90 90 Do you wish to proceed?
button 107 140 4017 4018 1 0 0
dtext 147 140 90 Abort
if !(strcmp("<src.ctag0.list>","CO-OWNER"))
	button 7 140 4005 4007 1 0 1
elif !(strcmp("<src.ctag0.list>","FRIEND"))
	button 7 140 4005 4007 1 0 2
elif !(strcmp("<src.ctag0.list>","ACCESS"))
	button 7 140 4005 4007 1 0 3
elif !(strcmp("<src.ctag0.list>","BANS"))
	button 7 140 4005 4007 1 0 4
endif
dtext 47 140 90 Confirm

[DIALOG d_clear_list button]
ON=0
src.sysmessage @,,1 Aborted
src.ctag.list=
ON=1,4

if (<argn>==1)//co-owner
	for 1 <obj.dtag0.max_coowners>
		obj.link.tag0.coowner_<local._for>=
	endfor
	obj.tag0.co_owner=
	src.ctag.list=
	src.sysmessage @,,1 Co-Owners list has been cleared
	return 1
elif (<argn>==2)//friend
	for 1 <obj.dtag0.max_friends>
		obj.link.tag0.friend_<local._for>=
	endfor
	obj.tag0.friend=
	src.ctag.list=
	src.sysmessage @,,1 Friends list has been cleared
	return 1
elif (<argn>==3)//access
	for 1 10
		obj.link.tag0.access_<local._for>=
	endfor
	obj.tag0.access=
	src.ctag.list=
	src.sysmessage @,,1 Access list has been cleared
	return 1
elif (<argn>==4)//bans
	for 1 10
		obj.link.tag0.banned_<local._for>=
	endfor
	obj.tag0.bans=
	src.ctag.list=
	src.sysmessage @,,1 Bans list has been cleared
	return 1
endif

[DEFNAME signs]
sign_1 9
sign_2 29
sign_3 54
sign_4 90
sign_5 147
sign_6 169
sign_7 177
sign_8 204
sign_9 251
sign_10 257
sign_11 263
sign_12 298
sign_13 347
sign_14 424
sign_15 441
sign_16 466
sign_17 514
sign_18 600
sign_19 601
sign_20 602
sign_21 603
sign_22 660
sign_23 666
sign_24 672
sign_25 898
sign_26 970
sign_27 974
sign_28 982

[DIALOG d_sign_post]
0,0
page 0
resizepic 0 0 2620 600 620
checkertrans 5 5 590 610
local.y=10
for 1 28
	if (<local._for> < 6)
		local.btn=7
		local.pic=57
	elif (<local._for> < 11)
		local.btn=107
		local.pic=157		
	elif (<local._for> < 16)
		local.btn=207
		local.pic=257		
	elif (<local._for> < 21)
		local.btn=307
		local.pic=357
	elif (<local._for> < 26)
		local.btn=407
		local.pic=457
	else	
		local.btn=507
		local.pic=557
	endif
	if (<local._for>==6) || (<local._for>==11) || (<local._for>==16) || (<local._for>==21) || (<local._for>==26)
		local.y=10
	endif
	button <local.btn> <local.y> 4005 4007 1 0 <def.sign_<dlocal._for>>
	tilepic <local.pic> <local.y> <def.sign_<dlocal._for>>
	local.y += 125
endfor
button 510 520 4017 4019 1 0 0
dtext 550 520 90 Cancel

[DIALOG d_sign_post button]
on=0
dialogclose

on=1,1000
obj=<src.region.tag0.sign>
foritems 2
	if (<baseid>==i_wall_wood) && (<link>==<obj.link>)
		obj=<uid>
		obj.try dispid=<argn>
		obj.update
	endif
endfor

[DIALOG d_secure]
100,100
obj=<src.region.tag.sign>
resizepic 0 0 2620 280 250 
checkertrans 5 5 270 240
dtext 100 10 90 <QVAL (<src.ctag0.cont>)? Secure Containers:Door Recoding>
dtext 47 50 90 Restrict to Owner/Co-owners
dtext 47 80 90 Restrict to Owner/Co-owners/friends
dtext 47 140 <QVAL (<obj.tag0.is_guild>)? 90 Restrict to Guild members:992 Restrict to Guild members>
dtext 47 110 90 Restrict to Unique UID
button 7 50 4005 4007 1 0 <QVAL (<src.ctag0.cont>)? 6:1>
button 7 80 4005 4007 1 0 <QVAL (<src.ctag0.cont>)? 7:2>
button 7 110 4005 4007 1 0 <QVAL (<src.ctag0.cont>)? 8:3>
button 7 140 4005 4007 1 0 <QVAL (<src.ctag0.cont>)? 9:4>
if !(<src.ctag0.cont>)
	dtext 47 170 90 Reset
	button 7 170 4005 4007 1 0 5
endif
button 7 210 4017 4019 1 0 0
dtext 47 210 90 Exit

[DIALOG d_secure button]
ON=0
dialogclose
on=1,9
	if (<argn>==1)//door restrict 1
			src.targetf, f_restrict 1
			src.sysmessage @,,1 Target the door you wish to make Owner/Co-owner only
			return 1
	elif (<argn>==2)//door restrict 2
			src.targetf, f_restrict 2
			src.sysmessage @,,1 Target the door you wish to make Owner/Co-owner/Friend only
			return 1
	elif (<argn>==3)//door uid only
			src.targetf, f_uidonly
			src.sysmessage @,,1 Who are you making a unique access door for?
			return 1
	elif (<argn>==4)//door guild only
			src.targetf, f_guild
			src.sysmessage @,,1 Target the door you wish to make Guild access only
			return 1
	elif (<argn>==5)//door reset
			src.targetf, f_reset
			src.sysmessage @,,1 Target the door you wish to reset.
			return 1
	elif (<argn>==6)//cont owner/co-owner
			obj=<src.ctag0.cont>
			obj.events +t_coowner
			obj.resendtooltip
			src.sysmessage @,,1 Only the Owner & Co-owners may now open this <obj.name>
			return 1
	elif (<argn>==7)//cont owner/co-owner/friend
			obj=<src.ctag0.cont>
			obj.events +t_friend
			obj.resendtooltip
			src.sysmessage @,,1 Only the Owner, Co-owners & Friends of the house may now open this <obj.name>
			return 1
	elif (<argn>==8)//cont uid only
			targetf, f_uidonly_cont 
			obj.resendtooltip
			src.sysmessage @,,1 Whom are you making this unique access <uid.<src.ctag0.cont>.name> for?
			return 1
	elif (<argn>==9)//cont guild
			obj=<src.ctag0.cont>
			obj.events +t_guild
			obj.resendtooltip
			obj=<src.region.tag0.sign>
			src.sysmessage @,,1 Only <uid<obj.tag0.is_guild>.name> guild members may now open this <uid.<local.cont>.name>.
			return 1
	endif

[FUNCTION f_buy_storage]
obj=<src.region.tag0.sign>
if (<obj.tag0.demolition>)
	src.sysmessage @,,1 This house is scheduled for demolition and cannot be upgraded.
	return 1
endif
src.payup <dlocal.value>
if (<var0.enough>)
	obj.tag0.boughtstorage += <dlocal.value>
	if (<dlocal.value>==<QVAL (<obj.link.value>)? <eval (<obj.link.value>/8)>:<eval (<obj.link.tag0.value>8)>>)
		local.storage=25
	elif (<dlocal.value>==<QVAL (<obj.link.value>)? <eval <obj.link.value>/4>:<eval <obj.link.tag0.value>/4>>)
		local.storage=50
	elif (<dlocal.value>==<QVAL (<obj.link.value>)? <eval ((<obj.link.value>/8)*3)>:<eval ((<obj.link.tag0.value>/8)*3)>>)
		local.storage=75
	else
		local.storage=100
	endif
	obj.tag0.xtrastorage += <dlocal.storage>
	src.sysmessage @,,1 <dlocal.value>c has been deducted from your bank to pay for this upgrade.
else
	src.sysmessage @,,1 You do not have the funds to pay for this storage upgrade.
	return 1
endif

[FUNCTION f_refund]
obj=<src.region.tag0.sign>
obj=<obj.link.more1>
if (<dlocal.total> > 65000)
	local.remainder=<local.total>
	while (<dlocal.remainder> > 65000) 
		newitem i_gold
		new.amount=65000
		new.cont=<obj.findlayer.layer_bankbox.uid>
		local.remainder=<eval <local.remainder>-65000>
	endwhile
	newitem i_gold
	new.amount=<eval <local.remainder>>
	new.cont=<obj.findlayer.layer_bankbox.uid>
else
	newitem i_gold
	new.amount <dlocal.total>
	new.cont=<obj.findlayer.layer_bankbox.uid>
endif
if (<dlocal.total> > 0)
	obj.sysmessage @,,1 <dlocal.total>c has been refunded to your bankbox.
endif

[function f_ven_chk]
if <QVAL (<def.custom_vendor>)? (<uid.<src.<def.vendor_owner_tag>>.is_owner>) || (<uid.<src.<def.vendor_owner_tag>>.is_coowner>) || (<uid.<src.<def.vendor_owner_tag>>.is_friend>):(<uid.<src.memoryfindtype.memory_ipet.link>.is_owner>) || (<uid.<src.memoryfindtype.memory_ipet.link>.is_coowner>) || (<uid.<src.memoryfindtype.memory_ipet.link>.is_friend>)>
	obj=<src.region.tag0.sign>
	if (<obj.dtag0.vendors> < <obj.dtag0.max_vendors>)
		obj.tag0.vendors=<eval <obj.tag0.vendors>+1>
		return 0
	else
		src.tag0.prohibit=1
		src.speech +spk_nopermit
		src.say These premises do not have room for me to work.
		return 1
	endif
else
	src.tag0.prohibit=1
	src.speech +spk_nopermit2
	src.say I cannot work from these premises, you are not on the house list.
	return 1
endif

[FUNCTION f_add_coowner]
obj=<src.region.tag.sign>
if <argo.isplayer>
	if (<argo>==<src>)
		src.sysmessage @,,1 You cannot add yourself to the co-owner list.
		return 1
	endif
	if (<argo.is_owner>)
		src.sysmessage @,,1 You are the owner of this property
		return 1
	elif (<argo.is_coowner>)
		src.sysmessage @,,1 <argo.name> is already a co-owner of this property.
		return 1
	elif (<argo.is_friend>)
		src.sysmessage @,,1 <argo.name> is a friend of this property.
		return 1
	elif (<argo.can_access>)
		src.sysmessage @,,1 <argo.name> is on the access list.
		return 1
	elif (<argo.is_banned>)
		src.sysmessage @,,1 <argo.name> is on the bans list.
		return 1
	else
		local._for=1
		FOR 1 <obj.dtag0.max_coowners>
			if !(<obj.link.tag0.coowner_<local._for>>)
				obj.link.tag0.coowner_<local._for>=<argo.uid>
				obj.tag0.co_owner +=1
				src.sysmessage @,,1 <argo.name> has been added as a co-owner for this property
				argo.sysmessage @,,1 You have been made a co-owner for this property	
				return 1
			endif
		endfor
		src.sysmessage @,,1 You already have the maximum number of co-owners permitted for this property.
		return 1
	endif
endif

[FUNCTION f_add_friend]
obj=<src.region.tag.sign>
if <argo.isplayer>
	if (<argo>==<src>)
		src.sysmessage @,,1 You cannot add yourself to the friend list.
		return 1
	endif
	if (<argo.is_owner>)
		src.sysmessage @,,1 You are the owner of this property
		return 1
	elif (<argo.is_coowner>)
		src.sysmessage @,,1 <argo.name> is a co-owner of this property.
		return 1
	elif (<argo.is_friend>)
		src.sysmessage @,,1 <argo.name> is already a friend of this property.
		return 1
	elif (<argo.can_access>)
		src.sysmessage @,,1 <argo.name> is on the access list.
		return 1
	elif (<argo.is_banned>)
		src.sysmessage @,,1 <argo.name> is on the bans list.
		return 1
	else
		FOR 1 <obj.dtag0.max_friends>
			if !(<obj.link.tag0.friend_<local._for>>)
				obj.link.tag0.friend_<local._for>=<argo.uid>
				obj.tag0.friend +=1
				src.sysmessage @,,1 <argo.name> has been added as a friend of this property
				argo.sysmessage @,,1 You have been made a friend of this property	
				return 1
			endif
		endfor
		src.sysmessage @,,1 You already have the maximum number of friends permitted for this property.
		return 1
	endif
endif

[FUNCTION f_add_access]
obj=<src.region.tag.sign>
if <argo.isplayer>
	if (<argo>==<src>)
		src.sysmessage @,,1 You cannot add yourself to the access list.
		return 1
	endif
	if (<argo.is_owner>)
		src.sysmessage @,,1 You are the owner of this property
		return 1
	elif (<argo.is_coowner>)
		src.sysmessage @,,1 <argo.name> is a co-owner of this property.
		return 1
	elif (<argo.is_friend>)
		src.sysmessage @,,1 <argo.name> is a friend of this property.
		return 1
	elif (<argo.can_access>)
		src.sysmessage @,,1 <argo.name> is already on the access list.
		return 1
	elif (<argo.is_banned>)
		src.sysmessage @,,1 <argo.name> is on the bans list.
		return 1
	else
		FOR 1 10
			if !(<obj.link.tag0.access_<local._for>>)
				obj.link.tag0.access_<local._for>=<argo.uid>
				obj.tag0.access +=1
				src.sysmessage @,,1 <argo.name> has been granted access to this property
				argo.sysmessage @,,1 You have been access to this property	
				return 1
			endif
		endfor
		src.sysmessage @,,1 You already have the maximum number of persons permitted to access this property.
		return 1
	endif
endif

[FUNCTION f_add_ban]
if <argo.isplayer>
	if (<argo>==<src>)
		src.sysmessage @,,1 You cannot ban yourself.
		return 1
	endif
	if (<argo.is_owner>)
		src.sysmessage @,,1 You are the owner of this property
		return 1
	elif (<argo.is_coowner>)
		src.sysmessage @,,1 <argo.name> is a co-owner of this property.
		return 1
	elif (<argo.is_friend>)
		src.sysmessage @,,1 <argo.name> is a friend of this property.
		return 1
	elif (<argo.can_access>)
		src.sysmessage @,,1 <argo.name> is on the access list.
		return 1
	elif (<argo.is_banned>)
		src.sysmessage @,,1 <argo.name> is already on the bans list.
		return 1
	else	
		FOR 1 10
			if !(<obj.link.tag0.banned_<local._for>>)
				try obj.link.tag0.banned_<local._for>=<argo.uid>
				obj.tag0.bans +=1
				if (<argo.region.uid>==<obj.link.region.uid>)
					argo.go <uid.<obj.region.tag0.sign>.p>
				endif
				src.sysmessage @,,1 <argo.name> has been banned from this property
				argo.sysmessage @,,1 You have been banned from this property	
				return 1
			endif
		endfor
		src.sysmessage @,,1 You already have the maximum number of bans permitted for this property.
		return 1
	endif
endif


[FUNCTION f_eject]
obj=<src.region.tag0.sign>
if <argo.isplayer>
	if (<argo>==<src>)
		src.sysmessage @,,1 You cannot eject yourself from this property.
		return 1
	endif
	if (<argo.is_owner>) || (<argo.is_coowner>) || (<argo.is_friend>)
		src.sysmessage @,,1 You cannot eject this person
	else
		argo.go <uid.<obj.region.tag0.sign>.p>
	endif
return 1
endif

[FUNCTION f_transfer_house]
obj=<src.region.tag.sign>
if (<argo.isplayer>)
	if (<argo.uid>==<obj.link.more1>)
		src.sysmessage @,,1 You already own this property
		return 1
	endif
	if (<argo.uid>==<src.uid>)
		src.sysmessage @,,1 <src.name> is now the named owner of this house.
		obj.link.more1=<argo.uid>
		obj.more1=<obj.link.more1>
		return 1
	endif
	if (<def.account_house_limit>) 
		if !(<argo.isgm>)
			if (<argo.account.dtag0.houses> < <def.account_house_limit>)
				if (<src.account.dtag0.houses> > 0 )	
					src.account.tag0.houses=<eval <src.account.tag0.houses>-1>
				endif
				argo.account.tag0.houses=<eval <argo.account.tag0.houses>+1>
				obj.link.more1=<argo>
				obj.more1=<obj.link.more1>
				src.sysmessage @,,1 You transfer the property to <argo.name>
				argo.sysmessage @,,1 <src.name> has transferred ownership of this property to you.
				resendtooltip
				return 1
			else
				src.sysmessage @,,1 <argo.name> already owns the maximum number of houses permitable.
				argo.sysmessage @,,1 You already own the maximum number of houses permitable.
				return 1
			endif
		endif
	endif
endif

[FUNCTION is_owner]
obj=<src.region.tag0.sign>
if (<obj.link.more1>==<uid>)
	return 1
endif
return 0

[FUNCTION acc_is_owner]
for <src.account.chars>
	if (<account.char.<eval <dlocal._for>-1>.is_owner>)
		return 1
	endif
endfor
return 0

[FUNCTION is_coowner]
obj=<src.region.tag0.sign>
FOR 1 <obj.dtag0.max_coowners> 
	if (<obj.link.tag0.coowner_<local._for>>==<uid>)
		return 1
	endif
endfor
return 0

[FUNCTION is_friend]
obj=<src.region.tag0.sign>
FOR 1 <obj.dtag0.max_friends>
	if (<obj.link.tag0.friend_<local._for>>==<uid>)
		return 1
	endif
endfor
return 0

[FUNCTION can_access]
obj=<src.region.tag0.sign>
for 1 10
	if (<obj.link.tag0.access_<local._for>>==<uid>)
		return 1
	endif
endfor
return 0

[FUNCTION is_banned]
obj=<src.region.tag0.sign>
for 1 10
	if (<obj.link.tag0.banned_<local._for>>==<uid>)
		return 1
	endif
endfor
return 0

[FUNCTION co_f_calc]	
local.srx=<eval (<argv[2]>) *2>
local.sry=<eval (<argv[3]>) *2>
local.size=<eval <local.srx>* <local.sry>>
tag.max_coowners=<eval (<local.size>/40)>
if (<dtag.max_coowners> > <def.max_co_owners>)
	tag.max_coowners=<def.max_co_owners>
endif
tag.max_friends=<eval (<local.size>/20)>
if (<dtag.max_friends> > <def.max_friends>)
	tag.max_friends=<def.max_friends>
endif
tag0.max_vendors=<eval <tag.max_coowners>+<tag.max_friends>+2>

[FUNCTION lockdown_calc]
local.mrx=<eval (<argv[2]>) *2>
local.mry=<eval (<argv[3]>) *2>
local.space=<eval <local.mrx>* <local.mry>>
tag.maxlockdowns=<eval <local.space>*4>
if (<dtag.maxlockdowns> < <def.min_lockdowns>)
	tag.maxlockdowns=<def.min_lockdowns>
elif (<dtag.maxlockdowns> > <def.max_lockdowns>)
	tag.maxlockdowns=<def.max_lockdowns>
endif
tag.maxsecure=<eval (<local.space>/20)>
if (<dtag.maxsecure> < <def.min_secure>)
	tag.maxsecure=<def.min_secure>)
elif (<dtag.maxsecure> > <def.max_secure>)
	tag.maxsecure=<def.max_secure>
endif

[FUNCTION f_vendor_recount]
forchars <eval <strarg <streat <streat <obj.link.multiregion>>>>+<strarg <streat <streat <streat <obj.link.multiregion>>>>>>
	if !<isplayer>
		if (<brain>==brain_vendor)
			if <def.custom_vendor>
				if !(<def.vendor_owner_tag>)
					local.bad_vendors +=1
					obj=<uid>
					obj.remove
				endif
				if (<uid.<def.vendor_owner_tag>.is_owner>) || (<uid.<def.vendor_owner_tag>.is_coowner>) || (<uid.<def.vendor_owner_tag>.is_friend>)
					local.vendors +=1
				endif
				if (<tag0.prohibit>)
					local.bad_vendors +=1
					obj=<uid>
					obj.remove
				endif
			else
				if !(<memoryfindtype.memory_ipet.link>) 
					local.bad_vendors +=1
					obj=<uid>
					obj.remove
				endif
				if (<tag0.prohibit>)
					local.bad_vendors +=1
					obj=<uid>
					obj.remove
				endif
				if (<uid.<memoryfindtype.memory_ipet.link>.is_owner>) || (<uid.<memoryfindtype.memory_ipet.link>.is_coowner>) || (<uid.<memoryfindtype.memory_ipet.link>.is_friend>) 
					local.vendors +=1
				endif
			endif
		endif
	endif
endfor
obj=<src.region.tag0.sign>
obj.tag0.vendors=<dlocal.vendors>
src.sysmessage @,,1 <dlocal.bad_vendors> invalid vendor(s) were found and removed
src.sysmessage @,,1 You have <obj.dtag0.vendors> registered vendor(s)

[FUNCTION f_wipe_contents]
foritems <eval (<strarg <streat <streat <multiregion>>>>+<strarg <streat <streat <streat <multiregion>>>>>)>
	local.sign=<region.tag0.sign>
	if (<type>==t_container) || (<type>==t_container_locked) 
		obj=<uid>
		obj.remove
	elif (<link>==04fffffff) && (<region.tag0.sign>==<local.sign>) && !(<type>==t_multi)
		obj=<uid>
		obj.remove
	endif
endfor

[FUNCTION f_open_house]
foritems <eval (<strarg <streat <streat <multiregion>>>>+<strarg <streat <streat <streat <multiregion>>>>>)>
	local.sign=<region.tag0.sign>
	if (<type>==t_door) && (<link>==<uid.<local.sign>.link>) 
		obj=<uid>
		if <obj.tag0.restricted>
			obj.tag0.restricted=
		endif
		if <obj.tag0.uidonly>
			obj.tag0.uidonly=
		endif
		if <obj.tag0.guild>
			obj.tag0.guild=
		endif
	elif (<type>==t_door_locked) && (<link>==<uid.<local.sign>.link>) 
		obj=<uid>
		obj.type=t_door
		if <obj.tag0.restricted>
			obj.tag0.restricted=
		endif
		if <obj.tag0.uidonly>
			obj.tag0.uidonly=
		endif
		if <obj.tag0.guild>
			obj.tag0.guild=
		endif
	endif
endfor
for 1 10
	tag0.access_<local._for>=
endfor
local._for=1
for 1 10
	tag0.banned_<local._for>=
endfor
local._for=1
for 1 <eval <obj.tag.max_coowners>>
	tag0.coowner_<local._for>=
endfor
local._for=1
for 1 <eval <obj.tag.max_friends>>
	tag0.friend_<local._for>=
endfor
if <link.tag0.private>
	link.tag0.private=
endif

[FUNCTION f_remove_keys]
FOR 0 <obj.findlayer.layer_pack.rescount>
	if (<obj.findlayer.layer_pack.findcont(<local._for>).dispid>==i_key_copper)
 		 if (<obj.findlayer.layer_pack.findcont(<local._for>).link.type>==t_multi)
  			try uid.<obj.findlayer.layer_pack.findcont(<local._for>).uid>.remove
  		endif
	endif
	if (<obj.findlayer.layer_bankbox.findcont(<local._for>).dispid>==i_key_copper)
 		 if (<obj.findlayer.layer_bankbox.findcont(<local._for>).link.type>==t_multi)
  			try uid.<obj.findlayer.layer_bankbox.findcont(<local._for>).uid>.remove
  		endif
	endif
ENDFOR
return 0

[FUNCTION f_door_setup]
foritems <eval <strarg <streat <streat <obj.multiregion>>>>+<strarg <streat <streat <streat <obj.multiregion>>>>>>
	if (<type>==t_door_locked)
		obj=<uid>
		obj.type=t_door
	endif
endfor
return 0

[FUNCTION f_empty]
obj=<region.tag0.sign>
obj=<obj.link>
foritems <eval <strarg <streat <streat <obj.multiregion>>>>+<strarg <streat <streat <streat <obj.multiregion>>>>>>
	if (<link>==04fffffff)
		obj=<uid>
		obj.try remove
	endif
endfor
return 0

[FUNCTION f_existant_owner]
forplayers 7168
	if (<obj.link.more1>==<uid>)
		return 1
	endif
endfor
return 0

[FUNCTION f_owner_check]
forplayers 7168
		if (<obj.link.more1>==<uid>)
			return 1
		endif
endfor
obj.link.more1=<obj.link.tag.coowner_01>
obj.tag.co_owner -=1
if <obj.link.tag0.coowner_02>
	local._for=01
	local.next=02
	for 01 <obj.tag0.max_coowners>
		if <obj.link.tag0.coowner_<local.next>>
			obj.try link.tag0.coowner_<local._for>=<link.tag0.coowner_<local.next>>
			local.next += 1
			local._for += 1
		else
			obj.try link.tag0.coowner_<local._for>=
			return
		endif
	endfor
else
	obj.link.tag0.coowner_01 =
endif

[ITEMDEF i_trashcan] 
NAME=trashcan 
ID=i_barrel_open 
TYPE=t_container 

ON=@CREATE 
COLOR=0835 

ON=@Dropon_Self
obj=<argo> 
src.sound=011e
obj.remove 
return 1  

[TYPEDEF t_door]
ON=@DClick
if !(<link>==04fffffff) 
	if <def.can_decay>
		if <QVAL (<def.account_ownership>) ? ((<src.is_owner>) || (<src.acc_is_owner>)) : (<src.is_owner>)>
			obj=<src.region.tag0.sign>
			if !(<obj.tag0.demolition>)
				obj.timer=60*60*24*<def.can_decay>
			else
				src.sysmessage @,,1 This house is improperly placed and has been scheduled for demolition in  <QVAL (<eval <obj.timer>/86400> < 1)? <eval (<timer>%86400)/3600> hours and <eval ((<timer>%86400)%3600)/60> minutes.:<eval <obj.timer>/86400> days.>
				src.sysmessage @,,1 Unless you redeed it yourself before that time the house and all items within will be lost without refund.
			endif
		endif
	endif
	if !(<link.tag0.private>)
		if (<src.is_banned>)
			src.sysmessage @,,1 This door is locked.
			return 1
		elif (<tag0.uidonly>)
			if (<tag0.uidonly>==<src.uid>)
				return 0
			else
				src.sysmessage @,,1 This door is locked.
				return 1	
			endif
		elif (<tag0.guild>)
			if (<tag0.guild>==<src.guild>)
				return 0
			else
				src.sysmessage @,,1 This door is locked.
				return 1	
			endif
		elif (<tag0.is_guild>)
			obj=<src.region.tag0.sign>
			if (<obj.tag0.is_guild>==<src.guild>)
				return 0
			else
				src.sysmessage @,,1 This door is locked.
				return 1
			endif
		elif (<tag0.restricted>)
			if (<tag0.restricted>==1) 
				if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>)
					return 0
				else
					src.sysmessage @,,1 This door is locked.
					return 1
				endif
			elif (<tag0.restricted>==2) 	
				if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.is_friend>)
					return 0
				else
					src.sysmessage @,,1 This door is locked.
					return 1
				endif
			endif
		else
			return 0
		endif

	else
		if (<tag0.uidonly>)
			if (<tag0.uidonly>==<src.uid>)
				return 0
			else
				src.sysmessage @,,1 This door is locked.
				return 1	
			endif
		elif (<tag0.guild>)
			if (<tag0.guild>==<src.guild>)
				return 0
			else
				src.sysmessage @,,1 This door is locked.
				return 1	
			endif
		elif (<tag0.is_guild>)
			obj=<src.region.tag0.sign>
			if (<obj.tag0.is_guild>)
				if (<obj.tag0.is_guild>==<src.guild>)
					return 0
				else
					src.sysmessage @,,1 This door is locked.
					return 1
				endif
			endif
		elif (<tag0.restricted>) 
			if (<tag0.restricted>==1)  
				if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>)
					return 0
				else
					src.sysmessage @,,1 This door is locked.
					return 1
				endif
			elif (<tag0.restricted>==2) 	
				if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.is_friend>)
					return 0
				else
					src.sysmessage @,,1 This door is locked.
					return 1
				endif
			endif
		else
			if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.is_friend>) || (<src.can_access>)
				return 0
			else
				return 1
			endif
		endif
	endif
endif

ON=@Click	
	if (<tag0.guild>)
		message [<uid.<tag0.guild>.abbrev>] Guild Members Only.
		return 1
	elif (<tag0.uidonly>)
		message Private
		message <uid.<tag0.uidonly>.name>
		return 1
	elif (<tag0.restricted>)
		message Restricted Access
		return 1
	endif
			
ON=@ClientToolTip
	if (<tag0.guild>)
		src.addcliloc 1042971, [<uid.<tag0.guild>.abbrev>] Guild Members Only
		return 1
	elif (<tag0.uidonly>)
		src.addcliloc 1070722, Private
		src.addcliloc 1042971, <uid.<tag.uidonly>.name> Only
		return 1
	elif (<tag0.restricted>)
		if (<tag0.restricted>==1)
			src.addcliloc 1042971, Restricted Access
			src.addcliloc 1070722, Owner & Co-owners Only
			return 1
		elif (<tag0.restricted>==2)
			src.addcliloc 1042971, Restricted Access
			src.addcliloc 1070722, Owner, Co-owners & Friends Only
			return 1
		endif
	endif

[FUNCTION f_restrict]
obj=<argo>
if (<obj.tag0.uidonly>) || (<obj.tag0.guild>)
	src.sysmessage @,,1 You must reset this door before recoding.
	return 1
endif
if (<obj.type>==t_door) && (<obj.region.uid>==<src.region.uid>)
	obj.tag0.restricted=<args>
	obj.resendtooltip
	if (<args>==1)
		src.sysmessage @,,1 Only the owner & co-owners can open this door.
	elif (<args>==2)
		src.sysmessage @,,1 Only the owner, co-owners & friends of the house can open this door.
	endif
endif
src.dialog d_secure

[FUNCTION f_guild]
obj=<src.region.tag0.sign>
local.guild=<obj.tag0.is_guild>
obj=<argo>
if (<obj.tag0.uidonly>) || (<obj.tag0.restricted>)
	src.sysmessage @,,1 You must reset this door before recoding.
	return 1
endif
if (<obj.type>==t_door) && (<obj.region.uid>==<src.region.uid>)
	obj.tag0.guild=<local.guild>
	obj.resendtooltip
	return 1
endif
src.dialog d_secure

[FUNCTION f_uidonly]
if <argo.isplayer>
	src.ctag0.key=<argo>
	targetf, f_uidonly_door
	src.sysmessage @,,1 Target the door.
endif

[FUNCTION f_uidonly_door]
obj=<argo>
if (<obj.tag0.restricted>) || (<obj.tag0.guild>)
	src.sysmessage @,,1 You must reset this door before recoding.
	return 1
endif
if (<obj.type>==t_door) && (<obj.region.uid>==<src.region.uid>)
	obj.tag0.uidonly=<src.ctag0.key>
	obj.resendtooltip
	src.sysmessage @,,1 Door coded, only <uid.<src.ctag0.key>.name> can open this door
endif
src.ctag0.key=
src.dialog d_secure

[FUNCTION f_reset]	
obj=<argo>
if (<obj.type>==t_door_locked)
	obj.type=t_door
endif
if (<obj.type>==t_door) && (<obj.region.uid>==<src.region.uid>)
	obj.tag0.restricted=
	obj.tag0.uidonly=
	obj.tag0.guild=
	obj.resendtooltip
	src.sysmessage @,,1 Door Reset
	src.sysmessage @,,1 Warning, If your house is set to public, anyone will be able to open this door.
endif
src.dialog d_secure

[FUNCTION f_declare_guild]
if (<argo.type>==t_stone_guild) && (<argo.region.uid>==<src.region.uid>)
	obj.tag0.is_guild=<argo.uid>
	obj.resendtooltip
	src.sysmessage @,,1 All <uid.<argo.uid>.name> guild members now have access to this building.
	src.sysmessage @,,1 Don't forget to set your door codes if areas are/need to be restricted.
endif

//house speak
[SPEECH spk_nopermit]
on=*buy*
	say I am unable to work on these premises, there are no vendor slots available.
	return 1

on=*sell*
	say I am unable to work on these premises, there are no vendor slots available.
	return 1

[SPEECH spk_nopermit2]
on=*buy*
	say You do not have permission for me to work on these premises.
	return 1

on=*sell*
	say You do not have permission for me to work on these premises.
	return 1

[SPEECH spk_player]
ON=*lock this*
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
	src.targetf, f_lock
	src.sysmessage @,,1 What do you wish to lockdown?
	return 1
else
	src.sysmessage @,,1 You must be in your house to do that.
	return 1
endif

ON=*unlock this*
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
	src.targetf, f_lock
	src.sysmessage @,,1 What do you wish to release?
	return 1
else
	src.sysmessage @,,1 You must be in your house to do that.
	return 1
endif

ON=*secure this*
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
	src.targetf, f_secure
	src.sysmessage @,,1 What do you wish to secure?
	return 1
else
	src.sysmessage @,,1 You must be in your house to do that.
	return 1
endif

ON=*unsecure this*
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
	src.targetf, f_secure
	src.sysmessage @,,1 What do you wish to release?
	return 1
else
	src.sysmessage @,,1 You must be in your house to do that.
	return 1
endif

ON=*raise this*
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
	src.targetf, f_raise
	src.sysmessage @,,1 What do you wish to raise?
	return 1
else
	src.sysmessage @,,1 You must be in your house to do that.
	return 1
endif

ON=*lower this*
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
	src.targetf, f_lower
	src.sysmessage @,,1 What do you wish to lower?
	return 1
else
	src.sysmessage @,,1 You must be in your house to do that.
	return 1
endif

ON=*flip this*
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
	src.targetf, f_flip
	src.sysmessage @,,1 What do you wish to flip?
	return 1
else
	src.sysmessage @,,1 You must be in your house to do that.
	return 1
endif

ON=*bank*
if <def.can_bank>
	if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.is_friend>) || (<src.isgm>)
		src.bankself
		src.sysmessage You have <src.bankbalance> gold in your Bank Box
	else
		return 1
	endif
endif

ON=*eject*
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.is_friend>) || (<src.isgm>) 
	src.targetf, f_eject
else
	return 1
endif

ON=Ban Person
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
	src.targetf, f_add_ban
else
	return 1
endif

ON=Place trashcan
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
	obj=<src.region.tag0.sign>
	if (<obj.tag0.trashcan>)
		src.sysmessage @,,1 You already have a trashcan.
		return 1
	else
		serv.newitem i_trashcan
		new.p=<src.p>
		new.attr=attr_move_never
		obj.tag0.trashcan=<new.uid>
	endif
else
	return 1
endif


ON=Remove trashcan
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.isgm>)
	obj=<src.region.tag0.sign>
	if (<obj.tag0.trashcan>)
		try uid.<obj.tag0.trashcan>.remove 
		obj.tag.trashcan=
	else
		src.sysmessage @,,1 You have no trashcan to remove.
		return 1
	endif
else
	return 1
endif


ON=*house commands*
src.sysmessage @,,1 Valid verbal commands within the house are:
src.sysmessage @,,1 Lock this, Unlock this, Secure This, Unsecure this. Raise this, Lower this, flip this.
src.sysmessage @,,1 Eject, Ban Person, Place trashcan, Remove trashcan
if <def.can_bank>
	src.sysmessage @,,1 Bank
endif

[FUNCTION f_lock]
obj=<src.region.tag0.sign>
if (<argo.link>==<obj.link>)
	src.sysmessage @,,1 You cannot lockdown that.
	return 1
endif
if (<argo.region.tag0.sign>==<src.region.tag0.sign>)
	if (<argo.type>!=t_container) || (<argo.type>!=t_container_locked)
		if (<argo.attr> & attr_move_never)
			obj=<argo>
			obj.attr &= ~attr_move_never
			obj.message [Released]
			obj.events -t_locked
			obj.resendtooltip
			obj=<src.region.tag0.sign>
			obj.tag.locked=<eval <obj.tag0.locked>-1> 
			return 1
		else
			obj=<src.region.tag0.sign>
			if (<eval <obj.tag.locked>> < <eval (<obj.tag0.maxlockdowns>+<obj.tag0.inclocks>)>)
				obj=<argo>
				obj.attr |= attr_move_never
				obj.message [Locked]
				obj.events +t_locked
				obj.resendtooltip
				obj=<region.tag0.sign>
				obj.tag.locked=<eval <obj.tag0.locked>+1> 
				return 1
			else
				src.sysmessage @,,1 You have no more lockdown slots available.
				return 1
			endif
		endif
	else
		src.sysmessage @,,1 You must use secure for containers.
		return 1
	endif
else
	src.sysmessage @,,1 Nice try, but that's NOT in your house!
	return 1
endif

[FUNCTION f_secure]
obj=<src.region.tag0.sign>
if (<argo.link>==<obj.link>)
	src.sysmessage @,,1 You cannot lockdown that.
	return 1
endif
if (<argo.region.tag0.sign>==<src.region.tag0.sign>) 
	if (<argo.type>==t_container) || (<argo.type>==t_container_locked)
		if (<argo.tag0.no_unlock>)
			src.sysmessage @,,1 You cannot release a moving crate
			return 1
		endif
		if (<argo.attr> & attr_move_never)
			obj=<argo>
			obj.attr &= ~attr_move_never
			obj.message [Released]
			obj.events -t_coowner
			obj.events -t_friend
			obj.events -t_uidonly
			obj.events -t_guild
			obj.resendtooltip
			obj.tag0.uidonly=
			src.sysmessage @,,1 This <obj.name> is now unsecured, anyone may access or move it.
			obj=<src.region.tag0.sign>
			obj.tag.secure=<eval <obj.tag0.secure>-1> 
			obj.tag0.storage=<eval <obj.tag0.storage>-<argo.rescount>>
			return 1
		else
			obj=<src.region.tag0.sign>
			if (<eval <obj.tag.secure>> < <eval (<obj.tag0.maxsecure>+<obj.tag0.incconts>)>)
				if (<argo.rescount> < 1) || (<argo.rescount> < <eval <eval <eval (<tag0.maxsecure> + <tag0.incconts>)>*100>-<eval <tag.storage>>>)
					obj=<argo>
					obj.attr |= attr_move_never
					obj.message [Secured]
					obj.resendtooltip
					obj=<region.tag0.sign>
					obj.tag0.secure=<eval <obj.tag0.secure>+1>
					obj.tag0.storage=<eval <obj.tag0.storage>+<argo.rescount>>
					src.ctag0.cont=<argo.uid>
					src.dialog d_secure 
					src.sysmessage @,,1 Who do you wish to have access to this <argo.name>.
				else
					src.sysmessage @,,1 You cannot secure this <argo.name> it contains <argo.rescount> items, you only have <eval <eval <eval (<tag0.maxsecure> + <tag0.incconts>)>*100>-<eval <tag.storage>>> secure storage slots left.
					return 1
				endif
			else
				src.sysmessage @,,1 You have no more secure container slots available.
				return 1
			endif
		endif
	else
		src.sysmessage @,,1 You must use lockdowns for non containers.		
		return 1
	endif
endif

[FUNCTION f_raise]
obj=<src.region.tag0.sign>
if (<argo.link>==<obj.link>)
	src.sysmessage @,,1 You cannot lockdown that.
	return 1
endif
if (<obj.region.tag0.sign>==<argo.region.tag0.sign>)
	obj=<argo>
	if (<obj.p.z> < 77)
		obj.move 0,0,1
		obj.update
		return 1
	else
		src.sysmessage @,,1 You cannot raise that any further
		return 1
	endif
else
	src.sysmessage @,,1 Nice try, but that's NOT in your house!
	return 1
endif

[FUNCTION f_lower]
obj=<src.region.tag0.sign>
if (<argo.link>==<obj.link>)
	src.sysmessage @,,1 You cannot lockdown that.
	return 1
endif
if (<obj.region.tag0.sign>==<argo.region.tag0.sign>) 
	obj=<argo>
	if (<obj.p.z> > 0)
		obj.move 0,0,-1
		obj.update
		return 1
	else
		src.sysmessage @,,1 You cannot lower this any further.
		return 1
	endif
else
	src.sysmessage @,,1 Nice try, but that's NOT in your house!
	return 1
endif

[FUNCTION f_flip]
obj=<src.region.tag0.sign>
if (<argo.link>==<obj.link>)
	src.sysmessage @,,1 You cannot lockdown that.
	return 1
endif
if (<obj.region.tag0.sign>==<argo.region.tag0.sign>)
	obj=<argo>
	obj.flip
	return 1
else
	src.sysmessage @,,1 Nice try, but that's NOT in your house!
	return 1
endif

[EVENTS e_fallfix]
ON=@ItemDClick
	if (<src.p.z><5) 
		src.move=0,0,5
		src.update
	elif (<src.p.z>>23) && (<src.p.z><28)
		src.move=0,0,5
		src.update
	elif (<src.p.z>>43) && (<src.p.z><48)
		src.move=0,0,5
		src.update
	elif (<src.p.z>>63) && (<src.p.z><68)
		src.move=0,0,5
		src.update
	endif

ON=@ItemClick
	if (<src.p.z><5) 
		src.move=0,0,5
		src.update
	elif (<src.p.z>>23) && (<src.p.z><28)
		src.move=0,0,5
		src.update
	elif (<src.p.z>>43) && (<src.p.z><48)
		src.move=0,0,5
		src.update
	elif (<src.p.z>>63) && (<src.p.z><68)
		src.move=0,0,5
		src.update
	endif

[EVENTS e_vendor_chk]
ON=@ItemDClick
	if !(strcmpi("<act.defname>","<QVAL (<def.custom_vendor>)? <def.vendordeed>:i_deed_vendor>"))
		if (<is_owner>) || (<is_coowner>) || (<is_friend>)
			obj=<src.region.tag0.sign>
			if (<obj.dtag0.vendors> < <obj.dtag0.max_vendors>)
				obj.tag0.vendors=<eval <obj.tag0.vendors>+1>
				return 0
			else
				sysmessage @,,1 This property has no more open vendor slots, another vendor must be removed before your vendor may work here.
				return 1
			endif
		else
			sysmessage @,,1 You cannot establish a vendor here, you must be at least on the friends list to do so.
			return 1
		endif
	endif

[DIALOG d_door_menu]
100,100
page 0
obj=<src.region.tag0.sign>
resizepic 0 123 2620 400 267
checkertrans 5 128 390 257
dhtmlgump 20 135 370 40 0 0 <def.bfont_white>Stand where you wish to place the door, then press the button that matches the orientation of the door you require.
gumppic 30 180 05780
gumppic 75 180 05781
gumppic 95 300 05782
gumppic 135 300 05783
gumppic 255 300 05784
gumppic 295 300 05785 
gumppic 320 180 05786
gumppic 360 180 05787
tilepic 15 200 0679 //door 1
tilepic 55 200 067b //door 2
tilepic 95 200 0675 //door 3
tilepic 135 200 0677 //door 4
tilepic 220 200 067d //door 5
tilepic 260 200 067f //door 6
tilepic 300 200 0681 //door 7
tilepic 340 200 0683 //door 8
button 10 340 4023 4025 1 0 1657
button 50 340 4023 4025 1 0 1659
button 90 340 4023 4025 1 0 1653
button 130 340 4023 4025 1 0 1655
button 240 340 4023 4025 1 0 1661
button 280 340 4023 4025 1 0 1663
button 320 340 4023 4025 1 0 1665
button 360 340 4023 4025 1 0 1667
button 190 200 4017 4018 1 0 0
dtext 185 220 90 Cancel
button 190 260 4020 4022 1 0 1
dtext 180 280 90 Remove
dtext 190 300 90 Door
dtext 180 340 90 <obj.dtag0.dused>/<obj.dtag0.doors>

[DIALOG d_door_menu button]
on=1
src.targetf, f_remove_door
src.sysmessage @,,1 Target the door to remove
src.sysmessage @,,1 It is not advised to remove your front doors...
return 1

on=1653,1667
obj=<src.region.tag0.sign>
if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)>
	if (<dtag0.dused> < <dtag0.doors>)
		serv.newitem <hval <argn>>
		new.p <src.p>
		new.attr=attr_move_never
		new.link=<obj.link>
		tag0.dused +=1
	else
		src.sysmessage @,,1 You have no more doors available.
		return 1
	endif
else
	src.sysmessage @,,1 You may only create doors within your house.
	return 1
endif
src.dialog d_door_menu

[FUNCTION f_remove_door]
obj=<src.region.tag0.sign>
if (<argo.link>==<obj.link>) && <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)>
	tag0.dused -=1
	obj=<argo>
	obj.remove
	src.sysmessage @,,1 You have removed the door.
	return 1
else
	src.sysmessage @,,1 You cannot remove that door.
	return 1
endif
dialog d_door_menu

//Begin MrSugarCube	
[EVENTS e_house_customize]
ON=@SkillStart
	IF !(<HOUSEDESIGN>)
		RETURN 2
	ENDIF
	SYSMESSAGE @,,1 You cannot do this whilst designing a house.
	RETURN 1

ON=@SkillUseQuick
	IF !(<HOUSEDESIGN>)
		RETURN 2
	ENDIF
	RETURN 1

ON=@HouseDesignCommit
	IF (<ISGM>)
		SYSMESSAGE @,,1 Your new house design has been committed.
		RETURN 2
	ENDIF
	LOCAL.OLDCOST = <EVAL (<ARGN1> * 500)>
	LOCAL.NEWCOST = <EVAL (<ARGN2> * 500)>
	LOCAL.CURCOST = <EVAL (<LOCAL.NEWCOST> - <LOCAL.OLDCOST>)>
	IF (<GOLD> < <LOCAL.CURCOST>)
		SYSMESSAGE @,,1 You cannot afford this house design.
		RETURN 1
	ENDIF
	obj=<src.region.tag0.sign>
	obj.tag0.construction=<eval <local.newcost>-<obj.link.value>>
	SYSMESSAGE @,,1 Your new house design has been committed.
	IF (<LOCAL.CURCOST> == 0)
		SYSMESSAGE @,,1 As the new design costs the same as the previous one, no gold has been taken out of your account.
	ELSEIF (<LOCAL.CURCOST> < 0)
		LOCAL.CURCOST = <EVAL (0 - <LOCAL.CURCOST>)>
		GOLD += <LOCAL.CURCOST>
		SYSMESSAGE @,,1 As the new design is cheaper than the previous one, <dLOCAL.CURCOST> gold has been returned to you.
	ELSE
		GOLD -= <LOCAL.CURCOST>
		SYSMESSAGE @,,1 <dLOCAL.CURCOST> gold has been taken out of your account to pay for the construction.
	ENDIF
	RETURN 2

ON=@HouseDesignExit
	SYSMESSAGE @,,1 You have left house design mode.
	timerf 1,f_fixpc
	RETURN 2
//End Mr SugarCube

[FUNCTION f_fixpc]
p=<uid.<src.region.tag0.sign>.p>
update

[DIALOG d_static_pricing]
100,100
page 0
obj=<src.region.tag0.sign>
resizepic 0 123 2620 400 267
checkertrans 5 128 390 257
dtext 140 140 90 Static Building Pricing 
dtext 100 180 90 Building footprint <obj.link.tag0.footprint> 
gumppic 130 220 2501
dtextentrylimited 135 220 100 20 90 0 10
dtext 120 240 90 Enter this property's value
button 180 300 4023 4025 1 0 1
dtext 120 330 90 Put this house on the market

[DIALOG d_static_pricing button]
on=1
obj=<src.region.tag0.sign>
obj.link.tag0.value=<argtxt[0]>
obj.link.tag0.price=<argtxt[0]>
obj.region.tag0.forsale=1
obj.link.tag0.private=	

[FUNCTION f_uidonly_cont]
if <argo.isplayer>
	obj=<src.ctag0.cont>
	obj.tag0.uidonly=<argo.uid>
	obj.events +t_uidonly
	src.sysmessage @,,1 Only <uid.<argo.uid>.name> may now open this <obj.name>.
	return 1
endif

[TYPEDEF t_uidonly]
ON=@DClick
	if !<src.isgm>
		if (<src.uid>==<tag0.uidonly>)
			return 0
		else
			src.sysmessage @,,1 This <name> belongs to <uid.<tag0.uidonly>.name> you cannot open it.
			return 1
		endif
	endif

ON=@Dropon_Self
	if (<rescount> < <def.max_container_items>) 
		obj=<src.region.tag0.sign>
		if (<obj.tag0.storage> < <eval ((<obj.tag0.maxsecure>+<obj.tag0.incconts>)*<def.max_container_items>)>)
			obj.try tag.storage=<eval <obj.tag0.storage>+1>
		else
			src.sysmessage @,,1 You have reached your maximum storage capacity for this property.
			return 1
		endif
	else
		src.sysmessage @,,1 This <name> is full
		return 1
	endif

ON=@PickUp_Self
	obj=<src.region.tag0.sign>
	obj.try tag.storage=<eval <obj.tag0.storage>-1>

ON=@ClientToolTip
	src.addcliloc 1042971,<uid.<tag0.uidonly>.name>'s
	src.addcliloc 501644 

[TYPEDEF t_coowner]
ON=@DClick
	if !<src.isgm>
		if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>)
			return 0
		else
			src.sysmessage @,,1 Only the owner & co-owners can open this <name>.
			return 1
		endif
	endif

ON=@Dropon_Self
obj=<src.region.tag0.sign>
	if (<rescount> < <def.max_container_items>) 
		if (<obj.tag0.storage> < <eval ((<obj.tag0.maxsecure>+<obj.tag0.incconts>)*<def.max_container_items>)>)
			obj.try tag.storage=<eval <obj.tag0.storage>+1>
		else
			src.sysmessage @,,1 You have reached your maximum storage capacity for this property.
			return 1
		endif
	else
		src.sysmessage @,,1 This <name> is full
		return 1
	endif

ON=@PickUp_Self
	obj=<src.region.tag0.sign>
	obj.try tag.storage=<eval <obj.tag0.storage>-1>

ON=@ClientToolTip
	src.addcliloc 1042971,Owner & Co-owners Only
	src.addcliloc 501644 

[TYPEDEF t_friend]
ON=@DClick
	if !<src.isgm>
		if <QVAL (<def.account_ownership>)? (<src.is_owner>) || (<src.acc_is_owner>):(<src.is_owner>)> || (<src.is_coowner>) || (<src.is_friend>)
			return 0
		else
			src.sysmessage @,,1 Only The owner, co-owners & friends of the house can open this <name>.
			return 1
		endif
	endif

ON=@Dropon_Self
obj=<src.region.tag0.sign>
	if (<rescount> < <def.max_container_items>) 
		if (<obj.tag0.storage> < <eval ((<obj.tag0.maxsecure>+<obj.tag0.incconts>)*<def.max_container_items>)>)
			obj.try tag.storage=<eval <obj.tag0.storage>+1>
		else
			src.sysmessage @,,1 You have reached your maximum storage capacity for this property.
			return 1
		endif
	else
		src.sysmessage @,,1 This <name> is full
		return 1
	endif

ON=@PickUp_Self
	obj=<src.region.tag0.sign>
	obj.try tag.storage=<eval <obj.tag0.storage>-1>

ON=@ClientToolTip
	src.addcliloc 1042971,Owner, Co-Owners & Friends Only
	src.addcliloc 501644 

[TYPEDEF t_guild]
ON=@DClick
	if !<src.isgm>
		obj=<src.region.tag0.sign>	
			if (<src.guild>==<obj.tag0.guild>)
				return 0
			else
				src.sysmessage @,,1 Only <uid.<obj.tag0.guild>.name> guild members can open this <name>.
				return 1
			endif
	endif

ON=@Dropon_Self
obj=<src.region.tag0.sign>
	if (<rescount> < <def.max_container_items>) 
		if (<obj.tag0.storage> < <eval ((<obj.tag0.maxsecure>+<obj.tag0.incconts>)*<def.max_container_items>)>)
			obj.try tag.storage=<eval <obj.tag0.storage>+1>
		else
			src.sysmessage @,,1 You have reached your maximum storage capacity for this property.
			return 1
		endif
	else
		src.sysmessage @,,1 This <name> is full
		return 1
	endif

ON=@PickUp_Self
	obj=<src.region.tag0.sign>
	obj.try tag.storage=<eval <obj.tag0.storage>-1>

ON=@ClientToolTip
	src.addcliloc 1042971,[<uid.<tag0.is_guild>.abbrev>]
	src.addcliloc 501644 

[TYPEDEF t_locked]
ON=@ClientToolTip
src.addcliloc 501643

[ITEMDEF i_moving_crate]
ID=i_crate_lg
NAME=Moving Crate
TYPE=t_container

ON=@Create
attr=attr_move_never|attr_decay
tag.no_unlock=1
timer=60*60*24*<def.crate_decay>

ON=@DClick
if (<src.distance> > 2)
	src.sysmessage @,,1 Your too far away
	return 1
endif

ON=@DropOn_Self
src.sysmessage @,,1 You cannot add or return things to the moving crate.
return 1

ON=@PickUp_Self
argo.events -e_no_usage
if (<rescount>==1)
	src.sysmessage @,,1 Your moving crate is now empty, and is being removed.
	obj=<src.region.tag0.sign>
	obj.try tag.secure=<eval <obj.tag0.secure>-1>
	obj.region.tag.movingcrate=
	remove
endif

ON=@Timer
obj=<region.tag0.sign>
obj.tag.secure=<eval <obj.tag0.secure>-1>
obj.region.tag.movingcrate=
remove

ON=@ClientToolTip
src.addcliloc 1042971, This crate will decay in:
if (<eval <timer>/86400> > 1)
	src.addcliloc 1070722, <eval <timer>/86400> days
else
	src.addcliloc 1070722, <eval (<timer>%86400)/3600> hrs and <eval ((<timer>%86400)%3600)/60> mins.
endif

[EVENTS e_no_usage]
ON=@DClick
	src.sysmessage @,,1 You cannot use that where it is.
	return 1


//I don't like these defnames here, but until I can find a way of recovering a value from an item not yet created....
//classic houses only
[DEFNAME price_recovery]
7x7 30500 
8x7 34500
8x8 39500
8x13 61500
11x11 70500
12x8 57000
14x14 111000
15x14 118500
16x14 126000

[FUNCTION f_swap]
obj=<src.region.tag0.sign>
if (<obj.link.baseid>==i_multi_tower)
	local.xy=16x14
else
	local.topx=<strarg <obj.link.multiregion>> 
	local.topy=<strarg <streat <obj.link.multiregion>>>
	local.bottomx= <strarg <streat <streat <obj.link.multiregion>>>> 
	local.bottomy= <strarg <streat <streat <streat <obj.link.multiregion>>>>>
	local.x <eval (<local.bottomx>-<local.topx>)+1> 
	local.y <eval <local.bottomy>-<local.topy>>
	local.xy <dlocal.x>x<dlocal.y>
endif
obj=<src.region.tag0.sign>
if (<eval <obj.link.value>> < <eval <def.<local.xy>>>)
	local.extra=<eval <eval <def.<local.xy>>>-<eval <obj.link.value>>>
	src.payup <eval <local.extra>>
	if (<var0.enough>)
		obj=<src.region.tag0.sign>
		local.p=<obj.link.p>
		forchars <eval (((<strarg <streat <streat <obj.link.multiregion>>>>+<strarg <streat <streat <streat <obj.link.multiregion>>>>>)/2)+2)>
			if (<region.tag0.sign>==<obj.region.tag0.sign>)
				if (<brain>==brain_vendor) || (<typedef.tspeech>==vendors) 
					if !(<var0.crate>)
						serv.newitem i_moving_crate
						var0.crate <new>
						new.p <obj.link.p>
						new.move 0,0,5
					endif
				shrink
				endif
			endif
		endfor
		foritems <eval <strarg <streat <streat <obj.link.multiregion>>>>+<strarg <streat <streat <streat <obj.link.multiregion>>>>>>
			if !(<link>==<obj.link>) && (<region.tag0.sign>==<obj.region.tag0.sign>) && !(<type>==t_multi)
				local.counter +=1
				if (<eval <local.counter>> > 0)
					if !(<var0.crate>)
						serv.newitem i_moving_crate
						var0.crate <new>
						new.p <obj.link.p>
						new.move 0,0,5
					endif
					if (<attr> & attr_move_never)
						if (<type>==t_container)
							attr &= ~attr_move_never
							obj=<src.region.tag0.sign>
							events +e_no_usage
							cont=<var0.crate>
						else
							attr &= ~attr_move_never
							events -t_locked
							events +e_no_usage
							cont=<var0.crate>
						endif
					else
						events +e_no_usage
						cont=<var0.crate>
					endif
				endif
			endif
		endfor
		obj.link.remove
		src.newitem i_multi_foundation_<local.xy>
		new.more1 <src>
		new.p <local.p>
		new.multicreate 
		new.region.tag0.convert=1
		src.sysmessage @32,,1 You MUST Double Click your house sign to ensure correct initialisation
		if (<var0.crate>)
			src.sysmessage @,,1 Your items have been stored in your moving crate, you have <def.crate_decay> days before the crate decays
			src.sysmessage @,,1 So don't forget to unpack.
			new.region.tag0.movingcrate=<var0.crate>
			var.crate=
		endif
	else
		src.sysmessage @,,1 You lack the funds to convert this house.
		return 1
	endif
else
	local.total=<eval (<obj.link.value>-<def.<local.xy>>)>
	obj=<src.region.tag0.sign>
	obj=<obj.link.more1>
	call f_refund <local.total>
	obj=<src.region.tag0.sign>
	local.p=<obj.link.p>
	forchars <eval (((<strarg <streat <streat <obj.link.multiregion>>>>+<strarg <streat <streat <streat <obj.link.multiregion>>>>>)/2)+2)>
		if (<region.tag0.sign>==<obj.region.tag0.sign>)
			if !(<isplayer>) && (<brain>==brain_vendor) || (<typedef.tspeech>==vendors) 
				if !(<var0.crate>)
					serv.newitem i_moving_crate
					var0.crate <new>
					new.p <obj.link.p>
					new.move 0,0,5
				endif
				shrink
			endif
		endif
	endfor
	foritems <eval <strarg <streat <streat <obj.link.multiregion>>>>+<strarg <streat <streat <streat <obj.link.multiregion>>>>>>
		if !(<link>==<obj.link>) && (<region.tag0.sign>==<obj.region.tag0.sign>) && !(<type>==t_multi)
			local.counter +=1
			if (<eval <local.counter>> > 0)
				if !(<var0.crate>)
					serv.newitem i_moving_crate
					var0.crate <new>
					new.p <obj.link.p>
					new.move 0,0,5
				endif
				if (<attr> & attr_move_never)
					if (<type>==t_container)
						attr &= ~attr_move_never
						obj=<src.region.tag0.sign>
						events +e_no_usage
						cont=<var0.crate>
					else
						attr &= ~attr_move_never
						events -t_locked
						events +e_no_usage
						cont=<var0.crate>
					endif
				else
					events +e_no_usage
					cont=<var0.crate>
				endif				
			endif
		endif
	endfor
	obj.link.remove
	src.newitem i_multi_foundation_<local.xy>
	new.more1 <src>
	new.p <local.p>
	new.multicreate
	new.region.tag0.convert=1
	src.sysmessage @32,,1 You MUST Double Click your house sign to ensure correct initialisation
	if (<var0.crate>)
		src.sysmessage @,,1 Your items have been stored in your moving crate, you have <def.crate_decay> days before the crate decays
		src.sysmessage @,,1 So don't forget to unpack.
		new.region.tag0.movingcrate=<var0.crate>
		var.crate=
	endif
endif

[EOF]
