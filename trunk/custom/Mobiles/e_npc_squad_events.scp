//*****************************************************************
//** NPC Squad Events
//** By Lerkur
//** Last Edited 07/07/02
//**
//** This script contains:
//** -i_squad_ld_mem
//** -i_squad_member_mem
//** -SquadTest Function
//** -FormSquad Function
//** -e_squad_member Events
//*****************************************************************

[ITEMDEF i_squad_ld_mem]
ID=i_memory
TYPE=t_eq_script
NAME=Squad Leader Memory

ON=@Equip
SRC.TAG.HOME=<SRC.HOME>
SRC.TAG.HOMEDIST=<SRC.HOMEDIST>
IF !(<EVAL npcai_opt_stationarysquads>)
	SRC.HOMEDIST=(<SRC.HOMEDIST>*4)
endif
SRC.EVENTS +e_squad_member
TIMER=3

ON=@Timer
TIMER=10
IF (<MORE2>)
	LOCAL.LEADER=<CONT.UID>
	LOCAL.TARGET=<MORE2>
	TRYSRC <LOCAL.TARGET> CONT.ATTACK
	FORCHARS 16
		IF ( (<FINDID(i_squad_member_mem).LINK> == <LOCAL.LEADER>) && !(<FLAGS>&statf_war) )
			TRYSRC <LOCAL.TARGET> ATTACK
		ENDIF
	ENDFOR
	IF !(<CONT.FLAGS>&statf_war)
		TRYSRC <LOCAL.TARGET> CONT.ATTACK
	ENDIF
	MORE2=0
	CONT.f_leadershout
ENDIF


RETURN 1

ON=@UnEquip
SRC.EVENTS -e_squad_member
SRC.HOMEDIST=<SRC.TAG.HOMEDIST>
SRC.HOME=<SRC.TAG.HOME>


[ITEMDEF i_squad_member_mem]
ID=i_memory
TYPE=t_eq_script
NAME=Squad Member Memory

ON=@Equip
cont.f_join
SRC.EVENTS +e_squad_member
SRC.TAG.HOME=<SRC.HOME>
SRC.TAG.HOMEDIST=<SRC.HOMEDIST>
ATTR=04002
TIMER=1

ON=@Timer
TIMER={5 15} //Randomized so that healers wouldn't double up on spells as much
IF ( !(<LINK.HITS>) || !(<cont.HITS>) || !(<LINK.FINDID(i_squad_ld_mem)>) )
	REMOVE
ELSE
	CONT.HOME=<LINK.P>
	CONT.HOMEDIST=8
ENDIF

IF !(<CONT.FLAGS>&statf_war)
	return 1
ELIF !(<EVAL <CONT.TAG0.LASTCASTING>> < <EVAL <SERV.TIME>>) || (<CONT.FLAGS>&statf_freeze)
	return 1
ELIF ((<CONT.TAG0.NPCAI.FighterType>)==1) || ((<CONT.TAG0.NPCAI.FighterType>)==2)
	TAG.NEARBY=0
	CONT.say ft
	LOCAL.LEADER=<LINK.UID>
	LOCAL.HEAL=0
	LOCAL.HITS=0
	IF (<CONT.ATTACKER> > 0)
		local.i = 0
		While ( <dlocal.i> < <CONT.ATTACKER> )
        		OBJ=<CONT.ATTACKER.<dlocal.i>.UID>
	        	LOCAL.DISTANCE = <obj.distance>
			IF (<LOCAL.DISTANCE> < 2)
				TAG.NEARBY=1
			ENDIF
		        Local.i ++
		EndWhile
	ENDIF
	IF (<TAG0.NEARBY>==1)
		IF !(<EVAL {1 100}>>(<TAG0.RANDCAST>+10))
			IF ((<CONT.TAG0.NPCAI.FighterType>)==1)
				CONT.HostileSpellCheck
				CONT.say fth
			ELSE
				FORCHARS 16
					IF (<FINDID(i_squad_member_mem).LINK> == <LOCAL.LEADER>)
						IF (<HITS> < <MAXHITS>) 
							IF (<LOCAL.HITS>)
								IF (<LOCAL.HITS> > <HITS>)
									OBJ=<UID>
									LOCAL.HEAL=1	
								ENDIF
							ELSE
								OBJ=<UID>
								LOCAL.HEAL=1
							ENDIF
						ENDIF
					ENDIF
				ENDFOR
				IF (<LOCAL.HEAL>)
					HealerSpellCheck
					CONT.say fth
				ENDIF
			ENDIF
		ELSE
			CONT.FLEE 5
			CONT.say fl
		ENDIF
	ELIF (<TAG0.NEARBY>==0) && !(<EVAL {1 100}>>(<TAG0.RANDCAST>+50))
		IF ((<CONT.TAG0.NPCAI.FighterType>)==1)
			CONT.HostileSpellCheck
			CONT.say fth
		ELSE
			FORCHARS 16
				IF (<FINDID(i_squad_member_mem).LINK> == <LOCAL.LEADER>)
					IF (<HITS> < <MAXHITS>) 
						IF (<LOCAL.HITS>)
							IF (<LOCAL.HITS> > <HITS>)
								OBJ=<UID>
								LOCAL.HEAL=1	
							ENDIF
						ELSE
							OBJ=<UID>
							LOCAL.HEAL=1
						ENDIF
					ENDIF
				ENDIF
			ENDFOR
			IF (<LOCAL.HEAL>)
				HealerSpellCheck
				CONT.say fth
			ENDIF
		ENDIF
	ENDIF
ENDIF

RETURN 1


ON=@UnEquip
SRC.EVENTS -e_squad_member
SRC.HOME=<SRC.TAG.HOME>
SRC.HOMEDIST=<SRC.TAG.HOMEDIST>



[FUNCTION SquadTest]
IF !( (<EVAL npcai_opt_formsquads>) )
	return 0
endif
IF (<TAG0.NPCAI.RANK> < 0) || (<SRC.TAG0.NPCAI.RANK> < 0) 
	return 0
endif
IF !( <SRC.TAG0.NPCAI.Race> == <TAG0.NPCAI.Race> )
	return 0
endif
IF !( <ISEVENT.e_squad_member> ) //If I am not in a squad
	IF !( <SRC.ISEVENT.e_squad_member> ) //If target is not in a squad
		IF ( (<SRC.TAG0.NPCAI.Rank>) > (<TAG0.NPCAI.RANK>) ) //If target rank is more than SRC rank	
			SRC.NEWITEM i_squad_ld_mem
			SRC.NEW.LINK=<UID>
			SRC.NEW.EQUIP
			NEWITEM i_squad_member_mem
			NEW.LINK=<SRC.UID>
			TRYSRC <UID> NEW.EQUIP
		ENDIF
	ENDIF
ELSE //If I am in a squad
	IF ( <FINDID(i_squad_ld_mem)> ) //If I'm the leader
		IF !( <SRC.ISEVENT.e_squad_member> ) //If target is not in a squad
			IF ( (<TAG0.NPCAI.Rank>) >= (<SRC.TAG0.NPCAI.Rank>) ) //If target's rank is lower than or equal to mine
				NEWITEM i_squad_member_mem
				SRC.NEW.LINK=<UID>
				SRC.NEW.EQUIP
			ENDIF
		ENDIF
	ENDIF
ENDIF


[FUNCTION AlertSquad]
	IF ( <FINDID(i_squad_ld_mem)> ) //If I'm the leader, assign it as my order target 
		FINDID(i_squad_ld_mem).MORE2=<SRC.UID>
	ELIF ( <FINDID(i_squad_member_mem)> )//If I'm not the leader, assign it thier order target
		FINDID(i_squad_member_mem).LINK.FINDID(i_squad_ld_mem).MORE2=<SRC.UID> 
	ELSE //I don't seem to be in a squad anymore?
		EVENTS -e_squad_member
		HOME=<TAG.HOME>
		HOMEDIST=<TAG.HOMEDIST>
	ENDIF


[EVENTS e_squad_member]
ON=@GetHit
IF ( (<TAG0.NPCAI.Race>==(<SRC.TAG0.NPCAI.Race>)) ) //If the attacker is of the same race
	RETURN 0
ENDIF
	AlertSquad
return 0


[eof]