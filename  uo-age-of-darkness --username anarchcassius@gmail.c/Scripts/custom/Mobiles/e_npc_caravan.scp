//*****************************************************************
//
//	Monster Events
//
//*****************************************************************

[ITEMDEF i_minoc_vesper_caravan_leader_control]
ID=i_plant_cactus_potted
TYPE=T_EQ_SCRIPT
NAME=Caravan Leader Controller
WEIGHT=0

ON=@CREATE
ATTR=attr_invis|attr_decay
TIMER=1
MOREZ=0

ON=@TIMER
if (<MOREZ>==0) //stopping at destinations but generally keeping moving
	TIMER=60*60
	CONT.HOMEDIST=6
	MOREZ=1
	MOREM=0
	IF (<MOREX>==0) //initialize caravan
		CONT.NEWNPC { c_horse_pack_caravan 3 c_llama_pack_caravan 2 }
		CONT.act.p=<cont.p>
		CONT.ACT.FIX
		NEWITEM { Mining_crate 2 Smith_crate 1 Supply_keg 1 Provision_keg 1 }
		TRYSRC <CONT.ACT> NEW.BOUNCE
		NEWITEM { Mining_crate 2 Smith_crate 1 Supply_keg 1 Provision_keg 1 }
		TRYSRC <CONT.ACT> NEW.BOUNCE
		NEWITEM { Mining_crate 2 Smith_crate 1 Supply_keg 1 Provision_keg 1 }
		TRYSRC <CONT.ACT> NEW.BOUNCE
		IF (<R2>)
			CONT.NEWNPC { c_horse_pack_caravan 3 c_llama_pack_caravan 2 }
			CONT.act.p=<cont.p>
			CONT.ACT.FIX
			NEWITEM { Mining_crate 2 Smith_crate 1 Supply_keg 1 Provision_keg 1 }
			TRYSRC <CONT.ACT> NEW.BOUNCE
			NEWITEM { Mining_crate 2 Smith_crate 1 Supply_keg 1 Provision_keg 1 }
			TRYSRC <CONT.ACT> NEW.BOUNCE
			NEWITEM { Mining_crate 2 Smith_crate 1 Supply_keg 1 Provision_keg 1 }
			TRYSRC <CONT.ACT> NEW.BOUNCE
		ENDIF	
		IF (<R2>)
			CONT.NEWNPC c_dog_caravan
			CONT.act.p=<cont.p>
			CONT.ACT.FIX
		ENDIF
		TIMER=1
		MOREX=1
	ENDIF
	return 1
ELIF (<MOREZ>==20)
	TIMER=60*60
	CONT.HOMEDIST=6
	MOREZ=19
	MOREM=1
	return 1
ELSE
	CONT.HOMEDIST=1
	TIMER=20
ENDIF

if (<cont.p.x> == <cont.home>) //waypoint tracking
	if (<cont.p.y> == <STREAT <cont.home>>)
		IF (<MOREM>) 
			MOREZ = <MOREZ>-1
		ELSE
			MOREZ = <MOREZ>+1
		ENDIF
		//cont.say heading to waypoint <morez>
		if (<MOREZ>==1)
			CONT.HOME=2489,570,5
		Elif (<MOREZ>==2)
			CONT.HOME=2489,518
		Elif (<MOREZ>==3)
			CONT.HOME=2526,516
		Elif (<MOREZ>==4)
			CONT.HOME=2527,500
		elif (<MOREZ>==5)
			CONT.HOME=2552,499
		elif (<MOREZ>==6)
			CONT.HOME=2565,509
		elif (<MOREZ>==7)
			CONT.HOME=2608,505
		elif (<MOREZ>==8)
			CONT.HOME=2624,488
		elif (<MOREZ>==9)
			CONT.HOME=2630,469
		elif (<MOREZ>==10)
			CONT.HOME=2662,469
		elif (<MOREZ>==11)
			CONT.HOME=2662,513
		elif (<MOREZ>==12)
			CONT.HOME=2668,520
		elif (<MOREZ>==13)
			CONT.HOME=2740,507
		elif (<MOREZ>==14)
			CONT.HOME=2774,502
		elif (<MOREZ>==15)
			CONT.HOME=2850,580
		elif (<MOREZ>==16)
			CONT.HOME=2907,589
		elif (<MOREZ>==17)
			CONT.HOME=2907,683
		elif (<MOREZ>==18)
			CONT.HOME=2886,710
		elif (<MOREZ>==19)
			CONT.HOME=2893,729
		endif
	endif
endif

return 1


[ITEMDEF i_caravan_member_control]
ID=i_plant_cactus_potted
TYPE=T_EQ_SCRIPT
NAME=Caravan Member Controller
WEIGHT=0

ON=@CREATE
 ATTR=attr_invis|attr_decay
 TIMER=1

ON=@TIMER
 if (<link> == 04fffffff)   //we have no leader
	local.leader = 0
	FORCHARS 16
		IF <FINDID(i_minoc_vesper_caravan_leader_control)>
			local.leader = <UID>
		ENDIF
	ENDFOR
	IF (<local.leader>)
		link = <local.leader>
	else
		cont.home = <cont.p>
		cont.homedist = 6
		timer = 30
		return 1
	endif
 endif

 timer=2

 cont.home = <link.p>
 cont.homedist = 2

return 1


[CHARDEF c_h_caravan_leader]
NAME=caravan leader
ID=C_MAN
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff
moverate=15 // This helps keep the caravan together really. The guy's a little slow, he's no fighter and walks all the time.

TSPEECH=spk_human_prime
TSPEECH=spk_needs
TSPEECH=spk_rehello
TSPEECH=spk_human_default

TEVENTS=e_Human_HearUnk
TEVENTS=e_Human_Needs
TEVENTS=e_Human_ConvInit
TEVENTS=e_Human_Greet
TEVENTS=e_Human_Space
TEVENTS=e_Human_Refuse
TEVENTS=e_Human_Environ

ON=@Create
     NAME="#NAMES_HUMANMALE"
     COLOR=colors_skin
     STR={66 75}
     DEX={76 85}
     INT={41 55}

     ARMSLORE={65.0 85.0}
     ANATOMY={65.0 85.0}
     FENCING={53.0 72.0}
     PARRYING={54.0 73.0}
     WRESTLING={49.0 70.0}
     TACTICS={51.0 70.0}
     MAGICRESISTANCE={35.0 40.0}

     NPC=brain_human

     ITEMNEWBIE=random_male_hair
     COLOR=colors_hair
     ITEMNEWBIE=random_facial_hair
     COLOR=match_hair
     ITEMNEWBIE=i_minoc_vesper_caravan_leader_control

ON=@NPCRestock
     ITEM={ Supply_backpack 2 Treasure_backpack 1 }
     ITEM=i_mt_horse_tan
     ITEM=random_LIGHT
     ITEM=random_food,{1 5}
     ITEM={ i_silver_coin 3 i_gold_coin 1 0 2 },{1 9}

     ITEM=i_shirt_fancy
     COLOR=colors_all
     ITEM=RANDOM_OVER_ARMOR
     COLOR=colors_all
     ITEM=RANDOM_hats
     COLOR=colors_all

     ITEM=i_chainmail_tunic
     ITEM=i_chainmail_leggings
     ITEM=random_gloves
     ITEM=random_shield
     ITEM=i_boots_calf
     ITEM={ i_dagger 2 i_kryss 1 i_sword_assassin_spike 1 }

////////////////////////////////////////Custom Pack horses to follow

[CHARDEF c_horse_pack_caravan]
ID=0123
NAME="pack horse"
SOUND=snd_ANIMALS_HORSE1
ANIM=01fff
ICON=i_pet_horse_pack
CAN=MT_WALK|MT_RUN|mt_nonhum
DAM=4,6
ARMOR=14
moverate=0
resources=8 i_ribs_raw, 6 i_hide, 2 i_backpack
foodtype=70 t_crops,t_fruit t_grain, t_grass
desires=e_horses,c_man,c_woman,t_crops, t_fruit, t_grain, i_hay, t_grass
TEVENTS=e_Level_up_NPC
aversions=e_carnivores3
tevents=e_horses
tag.slots_needed=2
tag.wrestlingspeed=44
Tag.BeastClass=Beast_Heavy
TAG.MANUEVER1=3
TAG.MANUEVER2=4
TAG.MANUEVER3=1


category=AoD Animals
subsection=Caravan
CATEGORY=Animals

ON=@Create
     NPC=brain_animal
     STR={148 190}
     DEX={46 65}
     INT={6 12}

     PARRYING={45.0 55.0}
     MAGICRESISTANCE={35.0 40.0}
     TACTICS={49.0 54.0}
     WRESTLING={49.0 64.0}

     FAME={100 199}
     KARMA={1 99}

     ITEMNEWBIE=i_caravan_member_control


[chardef 0124]
defname=c_llama_pack_caravan
defname=packllamas
name="pack llama"
sound=snd_animals_llama3
icon=i_pet_llama_pack
anim=01fff
dam=4,7
armor=13
moverate=0
resources=5 i_ribs_raw, 5 i_fur_light, 2 i_backpack
can=mt_walk|mt_run|mt_nonhum
foodtype=60 t_crops, t_fruit, t_grain, i_hay, t_grass
TEVENTS=e_Level_up_NPC
desires=c_llama,c_llama_pack,t_crops, t_fruit, t_grain, i_hay, t_grass
tag.slots_needed=2
tag.wrestlingspeed=44
Tag.BeastClass=Beast_Heavy
TAG.MANUEVER1=3
TAG.MANUEVER2=4
TAG.MANUEVER3=1

category=AoD Animals
subsection=Caravan
description=Pack Llama

on=@create
     npc=brain_animal

     str={157 177}
     dex={71 84}
     int={19 32}

     parrying={45.0 55.0}
     magicresistance={26.0 29.9}
     tactics={42.8 57.6}
     wrestling={48.4 61.3}


     FAME={100 199}
     KARMA={1 99}

     ITEMNEWBIE=i_caravan_member_control

[chardef c_dog_caravan]
id=c_dog
name="dog"
sound=snd_animals_dog1
icon=i_pet_dog
can=mt_walk|mt_run|mt_nonhum
anim=017ff
dam=4,7
armor=6
moverate=0
resources=3 i_ribs_raw, 2 i_fur_light
foodtype=40 t_meat_raw
TEVENTS=e_Level_up_NPC
desires=c_man,c_woman,c_dog,t_meat_raw
tag.wrestlingspeed=44
Tag.BeastClass=Beast_Heavy
TAG.MANUEVER1=2
TAG.MANUEVER2=11
TAG.MANUEVER3=6

category=AoD Animals
subsection=Caravan
description=Dog

on=@create
     npc=brain_animal

     str={95 111}
     dex={77 87}
     int={15 26}

     anatomy={68.0 93.0}
     parrying={68.0 93.0}
     magicresistance={12.8 36.4}
     tactics={61.0 77.7}
     wrestling={63.5 80.7}

     FAME={140 299}
     KARMA={1 99}

     ITEMNEWBIE=i_caravan_member_control


[eof]
